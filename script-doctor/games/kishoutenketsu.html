title "My first Kishoutenketsu"
author HeskHwis
homepage heskhwis.tumblr.com
noaction
background_color #287992

========
OBJECTS
========

Background
#268aa7

Transitionwatersand1
#268aa7 #3fbc97 #dedb27
00000
00000
11111
12221
22222

Transitionwatersand2
#268aa7 #3fbc97 #dedb27
00000
00000
00000
11011
21112

Transitionsandgrass
#dedb27 #a9ce19 #51ce19
00000
00000
11010
11111
22121

Grass
#51ce19 #41b224
00000
00100
01010
00100
00000

Bridge1
#5e3b1a
.....
.....
.....
.....
....0

Bridge2
#7c4f22 #5e3b1a
.....
.....
.....
00000
11111

Bridge3
#5e3b1a #287992
.....
.....
.....
.....
01...

Bridge4
#7c4f22 #5e3b1a
00000
11111
00000
11111
00000

Bridge5
#287992 #268aa7
000..
000..
000..
110..
.11..

Bridge6
#7c4f22 #5e3b1a
11111
00000
11111
.....
.....

Character1
#562d25
.....
.....
.....
.....
0....

Character2
#562d25 #f0aa83 #c32c24 #c3ae24
..1.0
.222.
.222.
.333.
.3.3.

Character3
#626262 #3fbc97
.0...
..0..
...0.
....1
.....

PlayerWater
#ffffff #c32c24 #3fbc97
..0..
..1..
.111.
20002
22222

PlayerCharacter
#562d25 #f0aa83 #c32c24 #c3ae24
..1..
.222.
12221
13331
.3.3.

Fishingrod
#562d25
....0
...0.
..0..
.0...
.....


Bucket0
#626262 #4e4e4e #7c4f22
.000.
21110
02000
00222
00000

Bucket1
#626262 #4e4e4e #d45227 #7c4f22
.000.
32110
03000
00333
00000

Bucket2
#626262 #4e4e4e #d45227 #7c4f22
.000.
32210
03000
00333
00000

Bucket3
#626262 #4e4e4e #d45227 #7c4f22
.000.
32220
03000
00333
00000

Fishrightshadow
#287992
.....
0.00.
00000
0.000
.....

Fishright
#d47027 #d45227
.....
0.00.
00010
0.100
.....

Fishleftshadow
#287992
.....
.00.0
00000
000.0
.....

Fishleft
#d47027 #d45227
.....
.00.0
01000
001.0
.....

Frog
#a9ce19 #ce9e19
.....
.101.
00000
.000.
00.00

Bigfish1
#d47027 #d45227
...01
....0
.....
.....
.....

Bigfish2
#d47027 #d45227
00100
10101
01010
.000.
00000

Bigfish3
#d47027 #d45227
10...
0....
.....
.....
.....

Bigfish4
#d47027 #d45227
....0
.1.00
.1100
.1100
..100

Bigfish5
#d47027 #c32c24
00000
00000
00000
00000
10001

Bigfish6
#d47027 #d45227
0....
00.1.
0011.
0011.
001..

Bigfish7
#d47027 #d45227
...00
...00
...01
....1
....1

Bigfish8
#d47027 #d45227 #c32c24
00000
11111
22222
22222
11111

Bigfish9
#d47027 #d45227
00...
00...
10...
1....
1....

Bigfishshadow1
#287992
.....
0.00.
00000
0.000
.....

Bigfishshadow2
#287992
.....
.00.0
00000
000.0
.....

Bigfishshadow3
#287992
.....
.00.0
00000
000.0
.....

Bigfishshadow4
#287992
.....
0.00.
00000
0.000
.....

Bigfishshadow5
#287992
.....
0.00.
00000
0.000
.....

Bigfishshadow6
#287992
.....
.00.0
00000
000.0
.....

Bigfishshadow7
#287992
.....
.00.0
00000
000.0
.....

Bigfishshadow8
#287992
.....
0.00.
00000
0.000
.....

Bigfishshadow9
#287992
.....
.00.0
00000
000.0
.....
Waterlily
#399d20 #318d3e
.000.
00110
01110
01110
.000.

Lilyflower
#ffffff #f6afcb #dedb27
.001.
00001
00201
01011
.111.

Worm
#f6afcb #f289b1
.....
.....
....0
.01.0
0..00

Target
transparent

Playercharacterwall
transparent

=======
LEGEND
=======

Player = PlayerWater or PlayerCharacter
Character = Character1 or Character2 or Character3
Bucket = Bucket0 or Bucket1 or Bucket2 or Bucket3
Lily = Waterlily or Lilyflower
Fish = Fishleftshadow or Fishrightshadow or Fishleft or Fishright
Bigfishshadow = Bigfishshadow1 or Bigfishshadow2 or Bigfishshadow3 or Bigfishshadow4 or Bigfishshadow5 or Bigfishshadow6 or Bigfishshadow7 or Bigfishshadow8 or Bigfishshadow9
Bigfish = Bigfish1 or Bigfish2 or Bigfish3 or Bigfish4 or Bigfish5 or Bigfish6 or Bigfish7 or Bigfish8 or Bigfish9
Transitionwatersand = Transitionwatersand1 or Transitionwatersand2
. = Background
P = PlayerWater
π = PlayerCharacter and Bridge2 and Fishingrod
B = Transitionwatersand1 and Bucket0
R = Fishrightshadow
L = Fishleftshadow
¬ = Frog
1 = Bigfishshadow1
2 = Bigfishshadow2
3 = Bigfishshadow3
4 = Bigfishshadow4
5 = Bigfishshadow5
6 = Bigfishshadow6
7 = Bigfishshadow7
8 = Bigfishshadow8
9 = Bigfishshadow9
W = Waterlily
X = Lilyflower
‹ = Waterlily and Character1
` = Transitionwatersand1
¨ = Transitionwatersand2
' = Transitionsandgrass
" = Grass
« = Character1
{ = Bridge1
å = Bridge1 and Playercharacterwall
[ = Bridge2 and Character2
§ = Bridge3 and Character3 and Target
Û = Bridge3 and Character3
¶ = Bridge4
¡ = Bridge4 and Target
] = Transitionwatersand2 and Bridge5
} = Transitionsandgrass and Bridge6
/ = Playercharacterwall
~ = Worm and Transitionsandgrass

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
Background
Transitionsandgrass, Grass
Target
Bridge2, Transitionwatersand, PlayerWater, Fish, Lily, Frog, Bigfishshadow, Bigfish
Bridge1, Bridge3, Bridge4, Bridge5, Bridge6
Fishingrod
Character, Bucket, PlayerCharacter, Playercharacterwall, Worm


======
RULES
======

[ Moving Player | Fishleftshadow ] -> [ Moving Player | < Fishleft ]
[ Moving Player | Fishrightshadow ] -> [ Moving Player | < Fishright ]
[ > Player |...| Fishleftshadow ] -> [ > Player |...| > Fishleftshadow ]
[ > Player |...| Fishrightshadow ] -> [ > Player |...| > Fishrightshadow ]
[ ^ Player |...| Fishleftshadow ] -> [ ^ Player |...| > Fishleftshadow ]
[ ^ Player |...| Fishrightshadow ] -> [ ^ Player |...| > Fishrightshadow ]
[ v Player |...| Fishleftshadow ] -> [ v Player |...| > Fishleftshadow ]
[ v Player |...| Fishrightshadow ] -> [ v Player |...| > Fishrightshadow ]
[ < Player || Fishleftshadow ] -> [ < Player || < Fishleftshadow ]
[ < Player || Fishrightshadow ] -> [ < Player || < Fishrightshadow ]
[ Moving Player | Fishleft ] -> [ Moving Player | < Fishleft ]
[ Moving Player | Fishright ] -> [ Moving Player | < Fishright ]
[ ^ Player | Frog ] -> [ ^ Player | < Frog ]
[ v Player | Frog ] -> [ v Player | < Frog ]
[ Stationary Worm ] -> [ randomDir Worm ]
[ Moving Player | Worm ] -> [ Moving Player | < Worm ]

[ Fishright | Target ] [ Bucket0 ]-> [ | Target ] [ Bucket1 ]
[ Fishleft | Target ] [ Bucket0 ] -> [ | Target ] [ Bucket1 ]
[ Fishright | Target ] [ Bucket1 ]-> [ | Target ] [ Bucket2 ]
[ Fishleft | Target ] [ Bucket1 ] -> [ | Target ] [ Bucket2 ]
[ Fishright | Target ] [ Bucket2 ]-> [ | Target ] [ Bucket3 ]
[ Fishleft | Target ] [ Bucket2 ] -> [ | Target ] [ Bucket3 ]
[ > Worm | Target ] -> [ | Target ]

[ Stationary Bigfishshadow2 ] -> [ DOWN Bigfishshadow2 ]
[ Stationary Bigfishshadow4 ] -> [ RIGHT Bigfishshadow4 ]
[ Stationary Bigfishshadow9 ] -> [ LEFT Bigfishshadow9 ]
[ Moving Player | Bigfishshadow8 ] [ Bigfishshadow1 ] [ Bigfishshadow2 ] [ Bigfishshadow3 ] [ Bigfishshadow4 ] [ Bigfishshadow5 ] [ Bigfishshadow6 ] [ Bigfishshadow7 ] [ Bigfishshadow9 ] -> [ Player | Bigfish8 ] [ Bigfish1 ] [ Bigfish2 ] [ Bigfish3 ] [ Bigfish4 ] [ Bigfish5 ] [ Bigfish6 ] [ Bigfish7 ] [ Bigfish9 ]

rigid [ Moving Player | Bigfish ] ->  [Moving Player | < Bigfish ]
+ rigid [ Moving Bigfish | Bigfish ] -> [ Moving Bigfish | Moving Bigfish ]

[ Bigfish7 | Target ] [ Bucket0 ] [ Bigfish1 ] [ Bigfish2 ] [ Bigfish3 ] [ Bigfish4 ] [ Bigfish5 ] [ Bigfish6 ] [ Bigfish8 ] [ Bigfish9 ] -> [ | Target ] [ Bucket3 ] [] [] [] [] [] [] [] []
[ Bigfish8 | Target ] [ Bucket0 ] [ Bigfish1 ] [ Bigfish2 ] [ Bigfish3 ] [ Bigfish4 ] [ Bigfish5 ] [ Bigfish6 ] [ Bigfish7 ] [ Bigfish9 ] -> [ | Target ] [ Bucket3 ] [] [] [] [] [] [] [] []
[ Bigfish9 | Target ] [ Bucket0 ] [ Bigfish1 ] [ Bigfish2 ] [ Bigfish3 ] [ Bigfish4 ] [ Bigfish5 ] [ Bigfish6 ] [ Bigfish7 ] [ Bigfish8 ] -> [ | Target ] [ Bucket3 ] [] [] [] [] [] [] [] []



==============
WINCONDITIONS
==============

No Fishleftshadow
No Fishrightshadow
No Fishleft
No Fishright
No Worm
No Bigfishshadow
No Bigfish

=======
LEVELS
=======

message What a beautiful day to go fishing
message Tonight, he'll eat yummy fishes

x.wwwwwwx.w
xw.xw..ww.w
...w.w...w.
...w.w.....
..........w
.........wx
...wl.....w
...........
xw.......ww
...r..«.xl.
....{[§p...
`¨`¨b¶]`¨`¨
'''''}'''''
"""""""""""

message Frogs like to play around with him
message But they're disturbing his fishing

w..wxwwwx.¬
.wwww..ww..
w.r..w..ww.
w.wx.w.w...
w.xw¬w....w
w..........
¬wwww......
........wwl
.w.....x.¬.
rw....‹w.x.
w...{[§p...
`¨`¨b¶]`¨`¨
'''''}'''''
"""""""""""

message Just a minute...
message He needs a special something

www.xww..w.
w.wxww....w
...w.......
........www
.........x.
....w......
...........
w.x....w...
w.....wxw..
...../«ww..
////åπÛ///w
`¨`¨b¡]`¨`¨
''~''}'''''
"""""""""""

message "Got you !"
message He puts the worm on his fishing rod

xxw..2.xww¬
xw..1.3...w
w4...56....
w...78..9..
w........w.
........wxw
......ww.w.
x..........
¬ww¬.......
w.....«..w.
.ww.{[§p...
`¨`¨b¶]`¨`¨
'''''}'''''
"""""""""""

message The king of the pond has been caught
message His bucket is full now
message Let's go home !
message The supper will be unforgettable
message THE END