title Extended Body Example: 1 Player, Multiple Extended Objects
author Scott Hughes

(

Usable in the case where there's only a single player that's pushing stuff around.

Example for
https://www.puzzlescript.net/Documentation/rigidbodies.html#one_many

)

=======
OBJECTS
=======

Background
white

Player
pink

Box1
red

Box2
green

Box3
blue

Box4
orange

Wall
grey

======
LEGEND
======

. = Background
@ = Player
1 = Box1
2 = Box2
3 = Box3
4 = Box4
# = Wall

Box = Box1 or Box2 or Box3 or Box4

======
SOUNDS
======

===============
COLLISIONLAYERS
===============

Background
Player, Box, Wall

=====
RULES
=====

[ > Player | Box ] -> [ > Player | > Box ]

startloop

(Propagate movements through crates of different types.)
[ moving Box1 | Box1 ] -> [ moving Box1 | moving Box1 ]
[ moving Box2 | Box2 ] -> [ moving Box2 | moving Box2 ]
[ moving Box3 | Box3 ] -> [ moving Box3 | moving Box3 ]
[ moving Box4 | Box4 ] -> [ moving Box4 | moving Box4 ]

(Extend the movements between crates of different types.)
[ > Box | Box ] -> [ > Box | > Box ]

endloop

(Just cancel the whole turn if something can't move.)
[ > Box | Wall ] -> cancel

=============
WINCONDITIONS
=============

======
LEVELS
======

################
#....###.......#
#....#.....##..#
#...........#..#
#...@..222..#..#
##.....2.......#
#...3.....1....#
#..33.....1....#
#..3.....11....#
#....4........##
#...444...##...#
#..#.....##....#
################
