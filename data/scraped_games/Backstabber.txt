title Backstabber
author Dennis Ranke

========
OBJECTS
========

Background 
GREEN

Wall 
BROWN DARKBROWN LIGHTBROWN
02222
10000
00000
10000
11111

Player 
BLACK BLUE RED
.111.
.000.
00200
.000.
.0.0.

SoldierU
Orange BLUE DARKBROWN 
..2..
.111.
.111.
..1..
.2.2.

SoldierL
Orange BLUE DARKBROWN
..0..
.111.
.101.
..1..
.22..

SoldierR
Orange BLUE DARKBROWN
..0..
.111.
.101.
..1..
..22.

SoldierD
Orange BLUE DARKBROWN 
..0..
.111.
.010.
..1..
.2.2.

Tombstone
DARKGREY LIGHTGREEN GREY
..2..
.002.
.002.
.002.
.101.

Outside
BLACK

Hole
BLACK GREEN
10001
00000
00000
00000
10001

Mark
RED
.....
.0.0.
..0..
.0.0.
.....

Crate
DARKBLUE BLUE DARKRED
11111
01012
00102
01010
20202

ClosedDoor
BROWN DARKBROWN
00000
01010
01010
01010
01010

OpenDoor
BROWN DARKBROWN
00000
01.10
0...0
0...0
0...0

TempDoor
BLACK

Switch1
GREY LIGHTBLUE DARKBLUE
.....
.1...
.0...
..0..
.222.

Switch2
GREY LIGHTBLUE DARKBLUE
.....
...1.
...0.
..0..
.222.

Sentinel
RED LIGHTRED BROWN
.1...
.0001
.020.
1000.
...1.

=======
LEGEND
=======

. = Background
~ = Outside
O = Hole
# = Wall
P = Player
^ = SoldierU
< = SoldierL
> = SoldierR
Y = SoldierD
+ = Crate
H = ClosedDoor
" = OpenDoor
S = Switch1
B = Sentinel
Soldier = SoldierU or SoldierL or SoldierR or SoldierD
Opaque = Wall or ClosedDoor
Switch = Switch1 or Switch2
Pusher = Player or Soldier
BlockDoor = Crate or Soldier

=======
SOUNDS
=======

SFX0 41274901
SFX1 15270104
SFX2 18868907

================
COLLISIONLAYERS
================

Background
Tombstone
Outside, OpenDoor, TempDoor
Player, Wall, Hole, Soldier, Crate, ClosedDoor, Switch1, Switch2, Sentinel
Mark

======
RULES     
======    

[ Player | No Opaque ] -> [ Player | Mark ]
[ Player | ... | Mark | No Opaque ] -> [ Player | ... | Mark | Mark ]

[ Action Player | Switch ] [ OpenDoor No BlockDoor ] -> [ Action Player | Switch ] [ TempDoor ]
[ Action Player | Switch ] [ ClosedDoor ] -> [ Action Player | Switch ] [ OpenDoor ]
[ TempDoor ] -> [ ClosedDoor ]

[ Action Player | Switch1 ] -> [ Action Player | TempDoor ] again
[ Action Player | Switch2 ] -> [ Action Player | Switch1 ] again
[ TempDoor ] -> [ Switch2 ]

[ Sentinel Mark | ... | MOVING Player ] -> [ > Sentinel | ... | MOVING Player ] SFX2
late [ Sentinel | Player ] -> [ Sentinel | Tombstone ] SFX1

UP [ SoldierU Mark | ... | Player ] -> [ > SoldierU | ... | Player ] again SFX2
UP [ Action Player | SoldierU ] -> [ Player | Tombstone ] SFX0
late UP [ SoldierU | Player ] -> [ SoldierU | Tombstone ] SFX1

LEFT [ SoldierL Mark | ... | Player ] -> [ > SoldierL | ... | Player ] again SFX2
LEFT [ Action Player | SoldierL ] -> [ Player | Tombstone ] SFX0
late LEFT [ SoldierL | Player ] -> [ SoldierL | Tombstone ] SFX1

RIGHT [ SoldierR Mark | ... | Player ] -> [ > SoldierR | ... | Player ] again SFX2
RIGHT [ Action Player | SoldierR ] -> [ Player | Tombstone ] SFX0
late RIGHT [ SoldierR | Player ] -> [ SoldierR | Tombstone ] SFX1

DOWN [ SoldierD Mark | ... | Player ] -> [ > SoldierD | ... | Player ] again SFX2
DOWN [ Action Player | SoldierD ] -> [ Player | Tombstone ] SFX0
late DOWN [ SoldierD | Player ] -> [ SoldierD | Tombstone ] SFX1

[ > Pusher | Crate ] -> [ > Pusher | > Crate ]

[ > Player ] -> again
late [ Mark ] -> []

==============
WINCONDITIONS
==============

No Soldier

=======     
LEVELS
=======

MESSAGE Without much hesitation, he would press 'x' to give a friendly stab.

~#####~~~~~~~
~#y#y###~~~~~
~#.....######
~#.#....<...#
##O#O#.##.#O#
#p....>##.#.#
###O#O.##...#
~~#.#..##.#^#
~~#^.^....###
~~#########~~

MESSAGE When he saw crates, he knew what to do.

###~########
#y#~#>.....#
#.###.#p+.##
#..y..#.++.#
#.o.+.#...##
#.o...######
#...o.#~~~~~
###...#~~~~~
~~#####~~~~~

MESSAGE Doors! Somewhere there has to be a switch to open/close them. Because that's how modern buildings work.

~~~~~~#####~~
~~~~~~#...###
#####~#h#+".#
#..y###...#.#
#.#h#s#.....#
#p.......####
#.#o##...h<#~
#.......####~
#########~~~~

Message Unlike the doors back home, these don't crush the inattentive foot. He likes these doors!

####~~~~~~~
#..###~~~~~
#..#y#~~~~~
#....#~#~~~
##h#.#~####
~#.#"###..#
~#+#.#s#..#
~#........#
~#p#h#.##.#
~#.....##^#
~##########

Message Those guards sure aren't all that strong. Or Intelligent.

~~~###~~~~~
####y#####~
#.......S#~
#..#.##+###
##..h..."<#
~#.#.######
~#+....p.#~
~#.....+.#~
~####s#..#~
~~~~######~

MESSAGE The sentinel bot: Though slow, it is irritatingly invincible.

~~~~~~~~###~~~~
~~~~~~###y#~~~~
~~~~###s#h##~~~
~~###......#~~~
~~#>h......####
~~###........s#
~~#p#b.....#.##
###.####.#...#~
#b.......#b..#~
##############~

MESSAGE And that's how Mr. Smith became employee of the month.


