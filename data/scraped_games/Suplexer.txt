title Suplexer
author Mark Diehr
homepage www.spielmatrix.com
again_interval 0.08
color_palette arnecolors
youtube guG9cVs3ms4
text_color red
background_color black

========
OBJECTS
========

Background
BLACK

Scaffold
DARKGRAY GRAY WHITE 
1...0
.1.0.
..0..
.1.0.
0...1

LightR
DARKGRAY GRAY YELLOW WHITE
0....
.01..
.111.
.0111
0..12

LightL
DARKGRAY GRAY YELLOW WHITE
....0
..10.
.111.
1110.
21..0

RaysR
YELLOW WHITE
1....
.....
..0..
.....
....0

RaysL
YELLOW WHITE
....1
.....
..0..
.....
0....

CameraFlashSpawn
YELLOW WHITE
.....
.....
.....
.....
.....

CameraFlash
YELLOW WHITE
.....
.....
.....
.....
.....

CameraFlash0
YELLOW WHITE
.....
.....
..0..
.....
.....

CameraFlash1
YELLOW WHITE
.....
..0..
.010.
..0..
.....

CameraFlash2
YELLOW WHITE
..0..
..1..
01110
..1..
..0..

Ground
BLACK DARKGRAY GRAY RED BLUE
22221
11110
22221
11110
00000

TurnbuckleL
WHITE GRAY DARKGRAY RED BLUE
.3301
.33..
.4011
.4...
.4...

TurnbuckleR
WHITE GRAY DARKGRAY RED BLUE
1033.
..33.
1040.
..4..
..4..

RopesBG
DARKGRAY
00000
.....
00000
.....
.....

Desk
DARKGRAY GRAY LIGHTGRAY RED BLUE
00000
01010
01210
01110
00000

Mat
WHITE GRAY DARKGRAY
00000
00000
11111
11111
11111

Player
YELLOW Orange Red White
.000.
.313.
03030
02220
.2.2.

PlayerUp
YELLOW

PlayerFall
YELLOW Orange Red White
.000.
03130
03030
.222.
.2.2.

Wrestler
GREEN YELLOW WHITE
.....
.111.
01110
.000.
.2.2.

WrestlerFall
GREEN YELLOW WHITE
2...2
.000.
.111.
01110
.....

WrestlerRight
GREEN YELLOW WHITE
2..0.
.011.
.011.
.011.
2..0.

WrestlerLeft
GREEN YELLOW WHITE
.0..2
.110.
.110.
.110.
.0..2

Ladder
DARKGRAY GRAY
.1.1.
.101.
.1.1.
.101.
.1.1.

LadderRope
DARKGRAY GRAY
01.10
.101.
01.10
.101.
.1.1.

Chair
DARKGRAY GRAY
....1
....0
.111.
.0.0.
0...0

TempWrestler
Orange

TempWrestlerRight
Orange

TempWrestlerLeft
Orange

Target
Red White Blue
.....
.....
.....
.....
01210


=======
LEGEND
=======

. = Background
# = Mat
% = Ground
P = Player

* = Wrestler
& = Wrestler and RopesBG
O = Target
x = Scaffold
- = RopesBG
[ = TurnbuckleL
] = TurnbuckleR
D = Desk
< = LightR
> = LightL
\ = RaysR
/ = RaysL
+ = CameraFlashSpawn
| = Ladder
L = LadderRope
h = Chair

( Cosmetic )
Lights = LightR or LightL
Rays = RaysR or RaysL
Flashes = CameraFlashSpawn or CameraFlash or CameraFlash0 or CameraFlash1 or CameraFlash2

TempWrestlers = TempWrestler or TempWrestlerRight or TempWrestlerLeft

( Solid walls )
Wall = TurnbuckleL or TurnbuckleR or Ground or Mat or Desk or Scaffold

( Wrestler types )
Wrestlers = Wrestler or WrestlerLeft or WrestlerRight or WrestlerFall
HWrestler = WrestlerLeft or WrestlerRight

( Classifications )
Obstacle = Wall or Wrestlers or Lights
Climbable = Ladder or Chair or LadderRope
Stand = Obstacle or Climbable
Gravity = Player or WrestlerFall


=======
SOUNDS
=======

sfx0 37682902 (Impact)
sfx1 41655502 (Lift)
sfx2 45516907 (Push)
Startlevel 16082708
TitleScreen 16082708

================
COLLISIONLAYERS
================

Background,
Flashes
RopesBG, Rays, Climbable
Target
TempWrestlers, PlayerUp
Player, PlayerFall, Wall, Wrestlers, Lights

======
RULES
======

( Pull crate into player )
Right [ action Player | Wrestler ] -> [ Player TempWrestlerLeft | ] sfx1
Left [ action Player | Wrestler ] -> [ Player TempWrestlerRight | ] sfx1

( Move crates sideways )
Right [ WrestlerRight ] -> [ > WrestlerRight ] again sfx2
Left [ WrestlerLeft ] -> [ > WrestlerLeft ] again sfx2

( Collide with walls )
Horizontal [ > HWrestler | Obstacle ] -> [ Wrestler | Obstacle ]

( Wrestlers fall down )
Down [ Wrestler | no Obstacle ] -> [ WrestlerFall | ]
Down [ WrestlerFall ] -> [ > WrestlerFall ] again
Down [ WrestlerFall | Obstacle ] -> [ Wrestler | Obstacle ] sfx0

( Lift crate up and suplex it if clear )
Up [ Player TempWrestlerRight | no Obstacle ] -> [ Player | WrestlerRight ] again
Up [ Player TempWrestlerLeft | no Obstacle ] -> [ Player | WrestlerLeft ] again

( Cancel crate suplex if ceiling is blocked )
Left [ Player TempWrestlerRight | no Obstacle ] -> [ Player | Wrestler ]
Right [ Player TempWrestlerLeft | no Obstacle ] -> [ Player | Wrestler ]

(Spawn initial cameras)
[ CameraFlashSpawn ] -> [ CameraFlash ]
( Camera Flash process )
[ CameraFlash2 ] -> [  ]
[ CameraFlash1 ] -> [ CameraFlash2 ]
[ CameraFlash0 ] -> [ CameraFlash1 ]

( Spawn cameras near where the wrestlers are )
VERTICAL [ HWrestler | no CameraFlash ] -> [HWrestler | CameraFlash]

( Move cameras )
random  [ CameraFlash ] -> [ randomdir CameraFlash ]
random  [ CameraFlash ] -> [ randomdir CameraFlash ]
random  [ CameraFlash ] -> [ randomdir CameraFlash ]
random  [ CameraFlash ] -> [ randomdir CameraFlash ]
( Say cheese! )
random [ CameraFlash ] -> [ CameraFlash0 ]

( Only allow vertical movement if the player is on a Climbable object )
[ DOWN Player | Climbable no Obstacle ] -> [  | Player Climbable ]
[ VERTICAL Player no Climbable ] -> [ Player ]

( Player movement )
HORIZONTAL [ > Player | Wrestler ] -> [ > Player | > Wrestler ]

HORIZONTAL [ > Player | Wall ] -> [ No Player | Wall < PlayerUp ]

HORIZONTAL [ > Player | Wrestler | Obstacle ] -> [ No Player | Wrestler < PlayerUp | Obstacle ]

UP [ Obstacle PlayerUp | No Obstacle ] -> [ Obstacle No PlayerUp | Player ]

( Gravity )
DOWN [ Gravity | No Stand ] -> [ DOWN Gravity | No Mat ]

( Player pops up )
late [ PlayerUp ] -> [ Player ]

( Gravity )
late DOWN [ Gravity | No Stand ] -> [ Gravity | No Mat ] again
late DOWN [ Wrestler | No Stand ] -> [ WrestlerFall | No Mat ] again


==============
WINCONDITIONS
==============

All Target on Wrestler

=======
LEVELS
=======

message Suplex those wrestlers out of the ring!

xxxxxxxxxxxxxx
x<<<......>>>x
x.\\\....///.x
x..\\\..///..x
x....\\//....x
x...../\.....x
x..p.//\\....x
xo.[&--&--].ox
xd|########|dx
x%%%%%%%%%%%%%

message Use the ladders to get a great angle for a throw!

xxxxxxxxxxxxxxxx
x<<<<......>>>>x
x.\\\\....////.x
x...\\\..///...x
x.....\\//.....x
x<...../\......x
x.\.p.|/|\....>x
x.o.[-l&l&-]|o.x
x.d.########|d.x
%%%%%%%%%%%%%%%%

message It's a Royal Rumble!

xxxxxxxxxxxxxxxxx
x.<<<<......>>>>x
x..\\\\....////.x
x....\\\..///...x
x<.....\\//.....x
x<\...../\......x
x.\\.p.//\*...|%x
xooo\[&--&&-]|%%x
xddd|########%%%x
%%%%%%%%%%%%%%%%%

message Two-Ring Battle!

xxxxxxxxxxxxxxxxxxxxx
x<......>x<.x<x<x<..x
x<\..../>x<\..\.\.\.x
x<\\..//>x<\\..\.\.ox
x.\.\/./.x<\\\..\.odx
x.../\.....\**.*.od%x
x..p...*...[&&-].d%%x
x|.|[-&&]..#####%%%%x
x|h|#####h.%%%%%%%%%x
%%%%%%%%%%%%%%%%%%%%%
