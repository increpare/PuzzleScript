title Reset
author Pichusuperlover
homepage twitter.com/pichusuperlover
noaction

========
OBJECTS
========
Reset
transparent

Timer0
Brown DarkRed
01110
10001
10001
10001
01110

Timer1
Brown DarkRed
00100
01100
10100
00100
11111

Timer2
Brown DarkRed
01110
10001
00110
01000
11111

Timer3
Brown DarkRed
01110
10001
00010
10001
01110

Timer4
Brown DarkRed
00010
00110
01010
11111
00010

Timer5
Brown DarkRed
11111
10000
11110
00001
11110

Timer6
Brown DarkRed
01110
10000
11110
10001
01110

Timer7
Brown DarkRed
11111
00010
00100
00100
00100

Timer8
Brown DarkRed
01110
10001
01110
10001
01110

Timer9
Brown DarkRed
01110
10001
01111
00001
01110

TimerMarker1
Transparent

TimerMarker2
Transparent

TimerMarker3
Transparent

TimerMarker4
Transparent

TimerMarker5
Transparent

TimerMarker6
Transparent

TimerMarker7
Transparent

TimerMarker8
Transparent

TimerMarker9
Transparent

Background
White

Wall
Brown

Player
Blue

PlayerMarker
Transparent

TempPlayer
Transparent

Box
Orange

BoxMarker
Transparent

DestroyPower
Red
.....
.000.
.000.
.000.
.....

Destroy
Brown Red
00000
01110
01110
01110
00000

DestroyMarker
Transparent

SpeedPower
Green
.....
.000.
.000.
.000.
.....

Speed
Brown Green
00000
01110
01110
01110
00000

SpeedMarker
Transparent

Goal
Green

TimeUp
darkblue
.....
.000.
.000.
.000.
.....

=======
LEGEND
=======

. = Background
# = Wall
$ = Wall and DestroyMarker
% = Wall and SpeedMarker
@ = Box and BoxMarker
P = Player and PlayerMarker
G = Goal
T = TimeUp
D = DestroyPower
S = SpeedPower
0 = Timer0 
1 = Timer1 and TimerMarker1
2 = Timer2 and TimerMarker2
3 = Timer3 and TimerMarker3
4 = Timer4 and TimerMarker4
5 = Timer5 and TimerMarker5
6 = Timer6 and TimerMarker6
7 = Timer7 and TimerMarker7
8 = Timer8 and TimerMarker8
9 = Timer9 and TimerMarker9

TimerMarker = TimerMarker1 or TimerMarker2 or TimerMarker3 or TimerMarker4 or TimerMarker5 or TimerMarker6 or TimerMarker7 or TimerMarker8 or TimerMarker9
Timer = Timer0 or Timer1 or Timer2 or Timer3 or Timer4 or Timer5 or Timer6 or Timer7 or Timer8 or Timer9 or Reset
PlayerLayer = Timer or Wall or TempPlayer or Box or Destroy or Speed

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
PlayerMarker, Timermarker, BoxMarker, DestroyMarker, SpeedMarker
Goal, TimeUp, DestroyPower, SpeedPower
Timer, Wall, Player, TempPlayer, Box, Destroy, Speed

======
RULES
======

(Timer)
late [ Timer0 ] -> [ Reset ]
late [ Timer1 ] -> [ Timer0 ]
late [ Timer2 ] -> [ Timer1 ]
late [ Timer3 ] -> [ Timer2 ]
late [ Timer4 ] -> [ Timer3 ]
late [ Timer5 ] -> [ Timer4 ]
late [ Timer6 ] -> [ Timer5 ]
late [ Timer7 ] -> [ Timer6 ]
late [ Timer8 ] -> [ Timer7 ]
late [ Timer9 ] -> [ Timer8 ]



(Boxes)
[ > Player | Box ] -> [ > Player | > Box ]



(Extra Time)
(Current Time)
(late [ Player TimeUp ] [ Timer8 ] -> [ Player TimeUp ] [ Timer9 ]
late [ Player TimeUp ] [ Timer7 ] -> [ Player TimeUp ] [ Timer8 ]
late [ Player TimeUp ] [ Timer6 ] -> [ Player TimeUp ] [ Timer7 ]
late [ Player TimeUp ] [ Timer5 ] -> [ Player TimeUp ] [ Timer6 ]
late [ Player TimeUp ] [ Timer4 ] -> [ Player TimeUp ] [ Timer5 ]
late [ Player TimeUp ] [ Timer3 ] -> [ Player TimeUp ] [ Timer4 ]
late [ Player TimeUp ] [ Timer2 ] -> [ Player TimeUp ] [ Timer3 ]
late [ Player TimeUp ] [ Timer1 ] -> [ Player TimeUp ] [ Timer2 ]
late [ Player TimeUp ] [ Timer0 ] -> [ Player TimeUp ] [ Timer1 ])

(Future Time)
late [ Player TimeUp ] [ TimerMarker8 ] -> [ Player TimeUp ] [ TimerMarker9 ]
late [ Player TimeUp ] [ TimerMarker7 ] -> [ Player TimeUp ] [ TimerMarker8 ]
late [ Player TimeUp ] [ TimerMarker6 ] -> [ Player TimeUp ] [ TimerMarker7 ]
late [ Player TimeUp ] [ TimerMarker5 ] -> [ Player TimeUp ] [ TimerMarker6 ]
late [ Player TimeUp ] [ TimerMarker4 ] -> [ Player TimeUp ] [ TimerMarker5 ]
late [ Player TimeUp ] [ TimerMarker3 ] -> [ Player TimeUp ] [ TimerMarker4 ]
late [ Player TimeUp ] [ TimerMarker2 ] -> [ Player TimeUp ] [ TimerMarker3 ]
late [ Player TimeUp ] [ TimerMarker1 ] -> [ Player TimeUp ] [ TimerMarker2 ]

(Cleanup)
late [ Player TimeUp ] -> [ Player ]



(Box Destruction)
late [ DestroyPower Player ] [ DestroyMarker ] -> [ Player ] [ DestroyMarker Destroy ]
[ Destroy ] [ > Player | Box ] -> [ Destroy ] [ > Player | ]



(Speed)
late [ SpeedPower Player ] [ SpeedMarker ] -> [ Player ] [ SpeedMarker Speed ]
[ Speed ] [ > Player | no PlayerLayer ] -> [ Speed ] [ no PlayerLayer | > TempPlayer ]
[ Speed ] [ TempPlayer ] -> [ Speed ] [ Player ]



(Reset)
(Last Move)
late [ Timer0 ] -> again

(Reset Player)
late [ Reset ] [ Player ] -> [ Reset ] [ ]
late [ Reset ] [ PlayerMarker ] -> [ Reset ] [ Player PlayerMarker ]

(Reset Boxes)
late [ Reset ] [ Box ] -> [ Reset ] [ ]
late [ Reset ] [ BoxMarker ] -> [ Reset ] [ Box BoxMarker ]

(Reset Timer)
late [ Reset ] [ TimerMarker1 ] -> [ Timer1 ] [ TimerMarker1 ]
late [ Reset ] [ TimerMarker2 ] -> [ Timer2 ] [ TimerMarker2 ]
late [ Reset ] [ TimerMarker3 ] -> [ Timer3 ] [ TimerMarker3 ]
late [ Reset ] [ TimerMarker4 ] -> [ Timer4 ] [ TimerMarker4 ]
late [ Reset ] [ TimerMarker5 ] -> [ Timer5 ] [ TimerMarker5 ]
late [ Reset ] [ TimerMarker6 ] -> [ Timer6 ] [ TimerMarker6 ]
late [ Reset ] [ TimerMarker7 ] -> [ Timer7 ] [ TimerMarker7 ]
late [ Reset ] [ TimerMarker8 ] -> [ Timer8 ] [ TimerMarker8 ]
late [ Reset ] [ TimerMarker9 ] -> [ Timer9 ] [ TimerMarker9 ]

=============
WINCONDITIONS
==============

All Goal on Player

=======
LEVELS
=======

4#######%$
#........#
#...P...G#
#........#
##########

3#######%$
#........#
#T..P...G#
#........#
##########

2#######%$
#........#
#T..P.T.G#
#........#
##########

6####%$
#...###
#P.@.G#
#...###
#######

9###%$
#.@.##
#P@.G#
#.@.##
######

5####%$
##..T##
##..@.#
#T@.@G#
#.@..##
##..P##
#######

4######%$
####D####
#...@.###
#..P@..G#
#...@.###
#########

5########%$
#G..###.T##
###@T##.###
###.###@.##
###..P.@.D#
########.##
###########

4########%$
#.........#
#P...S...G#
#.........#
###########

5######%$
###....##
#G......#
###.S.P.#
#########

5#####%$
##.....#
#G.....#
##.S.P.#
########

Message Thanks to Patashu for the idea
