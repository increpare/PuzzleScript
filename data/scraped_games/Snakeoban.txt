title Snakeoban
author Jack Lance


require_player_movement

========
OBJECTS
========

Background1
black
00000
00000
00000
00000
00000

Target
purple
.....
.000.
.0.0.
.000.
.....

GhostTarget
#720018
.....
.000.
.0.0.
.000.
.....

Wall
Grey DarkGrey
00010
11111
01000
11111
00010

Player
orange #880000  darkBlue #880000 #880000 #550000 #DD0000
.555.
53316
53246
51446
.666.

PlayerBody
orange #880000  darkBlue #880000 #880000 #550000 #DD0000
.555.
53316
53146
51446
.666.

PlayerTail
orange #880000  darkBlue #880000 #880000 #550000 #DD0000
.555.
53316
53146
51446
.666.

PlayerUp
orange #880000  darkBlue #880000 #880000 #550000 #DD0000
53146
.314.
.....
.....
.....

PlayerDown
orange #880000  darkBlue #880000 #880000 #550000 #DD0000
.....
.....
.....
.314.
53146

PlayerLeft
orange #880000  darkBlue #880000 #880000 #550000 #DD0000
5....
33...
11...
44...
6....

PlayerRight
orange #880000  darkBlue #880000 #880000 #550000 #DD0000
....5
...33
...11
...44
....6

Crate1
Brown darkbrown lightbrown
.111.
10002
10002
10002
.222.

Crate2
Brown darkbrown lightbrown blue
.111.
13332
13032
13332
.222.

Apple
Red Green LightRed
..1..
.000.
00020
00000
.000.


UpMarker
purple
..0..
.000.
0.0.0
..0..
..0..

LeftMarker
purple
..0..
.0...
00000
.0...
..0..

RightMarker
purple
..0..
...0.
00000
...0.
..0..

DownMarker
purple
..0..
..0..
0.0.0
.000.
..0..

=======
LEGEND
=======
(If you are reading this, sorry. I just used the "Level editor" because I didn't want to try and find suitable symbols for everything.)

1 = Background1
3 = Wall
4 = Player and Background1 and PlayerRight
g = Player and Background1 and PlayerLeft
h = Player and Background1 and PlayerUp
j = Player and Background1 and PlayerDown
k = Player and Background1

5 = PlayerTail and RightMarker and PlayerRight and Background1
6 = PlayerTail and LeftMarker and PlayerLeft and Background1
7 = PlayerTail and UpMarker and PlayerUp and Background1
8 = PlayerTail and DownMarker and PlayerDown and Background1

9 = PlayerBody and RightMarker and PlayerLeft and PlayerRight and Background1
0 = PlayerBody and LeftMarker and PlayerLeft and PlayerRight and Background1
q = PlayerBody and UpMarker and PlayerUp and PlayerDown and Background1
w = PlayerBody and DownMarker and PlayerDown and PlayerUp and Background1

e = PlayerBody and RightMarker and PlayerRight and PlayerUp and Background1
r = PlayerBody and LeftMarker and PlayerLeft and PlayerUp and Background1
t = PlayerBody and UpMarker and PlayerUp and PlayerLeft and Background1
y = PlayerBody and DownMarker and PlayerDown and PlayerLeft and Background1

u = PlayerBody and RightMarker and PlayerRight and PlayerDown and Background1
i = PlayerBody and LeftMarker and PlayerLeft and PlayerDown and Background1
o = PlayerBody and UpMarker and PlayerUp and PlayerRight and Background1
p = PlayerBody and DownMarker and PlayerDown and PlayerRight and Background1

a = Crate1 and Background1
s = Crate2 and Target and Background1
d = Target and Background1
f = Apple and Background1

Marker = UpMarker or LeftMarker or RightMarker or DownMarker
Body = PlayerUp or PlayerDown or PlayerLeft or PlayerRight
PlayerPart = Player or PlayerBody or PlayerTail
Crate = Crate1 or Crate2
Background = Background1

=======
SOUNDS
=======

Undo 50721302
Restart 31950301
Player MOVE 71013707
SFX0 88208701  (Eat apple)
endlevel 90267708  (Win, but where to bind it?)
SFX2 23251107
SFX3 27612707

================
COLLISIONLAYERS
================
Marker
Background
Target
Player, PlayerBody, PlayerTail, Wall, Crate, Apple
PlayerLeft
PlayerRight
PlayerUp
PlayerDown
GhostTarget




======
RULES
======
[Body] -> []
[ > Player | Crate ] -> [ > Player | > Crate ]
[ > Crate | Crate] -> [> Crate | > Crate]
left [ > Player | Apple | ] -> [ PlayerBody Leftmarker | Player | SFX0]
right [ > Player | Apple | ] -> [ PlayerBody Rightmarker | Player | SFX0 ]
up [ > Player | Apple | ] -> [ PlayerBody Upmarker | Player | SFX0 ]
down [ > Player | Apple | ] -> [ PlayerBody Downmarker | Player | SFX0 ]

[ left Player ] -> [ left Player Leftmarker ]
[ right Player ] -> [ right Player Rightmarker ]
[ up Player ] -> [ up Player Upmarker ]
[ down Player ] -> [ down Player Downmarker ]

startloop
late left [ PlayerBody Leftmarker | No PlayerPart ] -> [ Leftmarker | PlayerBody ]
late right [ PlayerBody Rightmarker | No PlayerPart ] -> [ Rightmarker | PlayerBody ]
late up [ PlayerBody Upmarker | No PlayerPart ] -> [ Upmarker | PlayerBody ]
late down [ PlayerBody Downmarker | No PlayerPart ] -> [ Downmarker | PlayerBody ]
endloop

late left [ PlayerTail Leftmarker | No PlayerPart ] -> [ | PlayerTail ]
late right [ PlayerTail Rightmarker | No PlayerPart ] -> [ | PlayerTail ]
late up [ PlayerTail Upmarker | No PlayerPart ] -> [ | PlayerTail ]
late down [ PlayerTail Downmarker | No PlayerPart ] -> [ | PlayerTail ]

late [ Crate1 Target ] -> [ Crate2 Target] SFX2
late [ Crate2 No Target ] -> [ Crate1 ] SFX3
late [ Playerpart Target ] -> [ playerpart ghosttarget Target]
late [ ghosttarget No playerpart ] -> [ ]

late down [Downmarker|no PlayerUp] -> [Downmarker PlayerDown|PlayerUp]
late left [Leftmarker|no PlayerRight] -> [Leftmarker PlayerLeft|PlayerRight]
late up [Upmarker|no PlayerDown] -> [Upmarker PlayerUp|PlayerDown]
late right [Rightmarker|no PlayerLeft] -> [Rightmarker PlayerRight|PlayerLeft]

==============
WINCONDITIONS
==============

All Crate on Target
No Apple

=======
LEVELS
=======

message Thanks to Wayne Myers and Aidan Sun

(eat an apple)

3333333
359g1f3
3333333

(don't get trapped)

33333333333
31131113113
3f1f15gf1f3
31131113113
33333333333

(one is first one is last)

33333333
35g131f3
311f1113
31111f33
33f11113
3111f113
3f131113
33333333


(push some boxes)

3333333333333
359g1aaa1ddd3
3333333333333

(cute cross)

3333333
3311133
311s113
31sfs13
311s113
3340633
3333333

(push two boxes)

333333333
3111d5y13
311a1ah13
313131313
3111d1113
333333333

333333333
35gfffff3
3f3f3f3f3
3ff111ff3
3f31313f3
3ff111ff3
3f3f3f3f3
3fffffff3
333333333

333333333
31f111d13
3da133af3
313j11113
313q11313
311o61313
3fa331ad3
31d111f13
333333333

33333333
38131113
3hafd113
31131113
31afdf33
31131113
31afd113
31131113
33333333

333333333
35gfffff3
3fdfffff3
31fffff13
3fffffff3
3fffffff3
3fffffff3
31fffff13
3fffffaf3
3fffffff3
333333333









