title Goblin Hooblob
author Evan Kuhn and Michael Franklin
homepage www.puzzlescript.net

========
OBJECTS
========

(Brick pattern from 'Count Mover' by Jonah Ostroff: https://www.puzzlescript.net/play.html?p=ed8845d930c8940bef1236cf18ca5bd2)
Background 
GRAY DARKGRAY
00010
11111
01000
11111
00010

Target 
Yellow    

Wall 
DARKBLUE

Player 
GREEN BROWN BLACK PINK
00.00
.202.
00000
.000.
11.11

HoomanUp
PINK BROWN
.000.
.000.
01110
.111.
11.11

HoomanDown
PINK BROWN
.000.
.000.
01110
.111.
11.11

HoomanLeft
PINK BROWN
.000.
.000.
01110
.111.
11.11

HoomanRight
PINK BROWN
.000.
.000.
01110
.111.
11.11

HoomanStationary
PINK BROWN BLACK
.000.
.202.
01110
.111.
11.11

Splatter
Red Brown
..0.0
1001.
..0.0
0.0..
1....

CratePush
BROWN BLACK
00000
11111
00000
11111
00000

CratePull
BROWN LightGreen
00000
11111
00000
11111
00000


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = CratePush
' = CratePull
O = Target
U = HoomanUp
D = HoomanDown
L = HoomanLeft
R = HoomanRight
S = Splatter
$ = HoomanStationary

Crate = CratePush or CratePull
Hooman = HoomanUp or HoomanDown or HoomanStationary or HoomanLEft or HoomanRight
Object = Crate or Wall

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Target
Splatter, Player, Wall, Crate, Hooman

======
RULES     
======     
(Crate movement)
[ >  Player | CratePush ] -> [  >  Player | > CratePush  ]
[ <  Player | CratePull ] -> [  <  Player | < CratePull  ]

(HoomanUp movement)
[ > Player | No Object ][ Stationary HoomanUp ] -> [ > Player | ][ Up HoomanUp ]
[ > Player | CratePush | No Object ][ Stationary HoomanUp ] -> [ > Player | CratePush | ][ Up HoomanUp ]

(HoomanDown movement)
[ > Player | No Object ][ Stationary HoomanDown ] -> [ > Player | ][ Down HoomanDown ]
[ > Player | CratePush | No Object ][ Stationary HoomanDown ] -> [ > Player | > CratePush | ][ Down HoomanDown ]

(HoomanLeft movement)
[ > Player | No Object ][ Stationary HoomanLeft ] -> [ > Player | ][ Left HoomanLeft ]
[ > Player | CratePush | No Object ][ Stationary HoomanLeft ] -> [ > Player | > CratePush | ][ Left HoomanLeft ]

(HoomanRight movement)
[ > Player | No Object ][ Stationary HoomanRight ] -> [ > Player | ][ Right HoomanRight ]
[ > Player | CratePush | No Object ][ Stationary HoomanRight ] -> [ > Player | > CratePush | No Object ][ Right HoomanRight ]

(Player killed by Hooman)
[ > Hooman | Player ] -> [ Hooman | Splatter ]
[ > Player | Hooman ] -> [ Splatter | Hooman ]

(Change in Hooman direction)
[ > HoomanUp | Object ] -> [ HoomanDown | Object ]
[ > HoomanDown | Object ] -> [ HoomanUp | Object ]
[ > HoomanLeft | Object ] -> [ HoomanRight | Object ]
[ > HoomanRight | Object ] -> [ HoomanLeft | Object ]

==============
WINCONDITIONS
==============

all Player on Target
no Splatter on Background

=======     
LEVELS
=======
Message Just another day of movin' boxes for ol' Hooblob
##########
#....###.#
#....*.#.#
#.p..*...o
#....*.#.#
#....###.#
##########

Message I sure hope there aren't any of those ~sticky~ boxes lyin' around
###########
#p.'...####
#...#.#####
#####*#####
####...#..o
####...#..#
####..*...#
####.**...#
###########

Message At least none of those pesky humies are getting between me and my go- .....
###############
#....d........#
#.............o
#p............#
#.........u...#
###############

###############
#p.....#......o
#......#......#
#......#####'##
#......#......#
#...*.*#......#
#....*.#......#
#...*.*...u...#
###############

#######
#.....#
#.lp..#
##.'.##
##***##
##.*.##
##.'*##
###.###
##.o.##


Message Now to collect the last of my gold!


###############
########d'.'.'#
########..*.*.o
#P######.'.'.'#
#.#.......*.*l#
#..*.....'.'.'#
####d##...*.*r#
#.....#..'d'.'#
###############



Message I did it! Silly humies can't keep up with me!

