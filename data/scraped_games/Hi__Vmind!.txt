title Hi, Vmind!
author Denis Prause

realtime_interval 0.3
throttle_movement
norepeat_action
noundo

========
OBJECTS
========

Background
DARKGREEN 


Target1
blue darkblue
..0..
.....
.1.1.
..1..
.....

Target2
blue darkblue 
.....
..0..
.1.1.
..1..
.....

FrozenTarget1
yellow darkblue
.....
.....
.101.
..1..
.....

FrozenTarget2
yellow darkblue
.....
.....
.101.
..1..
.....


Tmp
transparent

Tree 
BROWN GREEN blue
..1..
.111.
.111.
..0..
.....

Shroom
lightbrown red
.....
..1..
.111.
..0..
.....

Dead
red
.....
....0
.00..
0000.
.000.

endingblack
black
endingblue
blue
endinggreen
green
endingred
red
endingred2
red
endingwhite
white
endingFinished
transparent


Loss1
transparent
Loss2
transparent
Loss3
transparent
Loss4
transparent
Loss5
transparent
Loss6
transparent

Red1
red darkred
1.0.1
.111.
11011
.101.
1...1

Red2
red darkred
..0..
11111
.101.
11011
.1.1.

Silver1
lightgrey grey darkgrey
1.2.1
.000.
10001
.000.
1...1

Silver2
lightgrey grey darkgrey
..2..
10001
.000.
10001
.1.1.

Green1
lightgreen orange 
.....
.....
.....
0001.
.....

Green2
lightgreen orange green
.....
.....
..00.
.221.
.....

GreenB1
lightgreen orange 
.....
.1...
..0..
..0..
.....

GreenB2
lightgreen orange 
.....
...1.
..0..
..0..
.....

YellowOff2
yellow  orange
1.0.1
.000.
10001
.000.
1...1

YellowOff1
yellow  orange
..0..
10001
.000.
10001
.1.1.

YellowOn2
yellow orange pink
1.0.1
.020.
12221
.020.
1...1

YellowOn1
yellow orange pink
..0..
10201
.222.
10201
.1.1.

Pheromones
pink
..0..
0...0
..0..
0....
...0.

BearRight
darkbrown brown 
...0.
10000
00100
0011.
00.0.

BearLeft
darkbrown brown 
.0...
00001
00100
.1100
.0.00

Trap
lightgrey grey
.....
.....
.1.1.
1.0.1
.111.

UsedTrap
grey
.....
.0.0.
.000.
.000.
..0..

UsedTrap2
grey
.....
.0.0.
.000.
.000.
..0..

Honey
orange Yellow
.010.
01110
.010.
01110
.010.

CollectedHoney
orange Yellow
.010.
01110
.010.
01110
.010.

MutatingYellow
Yellow Orange Pink
.000.
10201
12221
02220
01010

Hive
Yellow Orange Pink
.000.
10201
12221
02220
01010

CreepL
Yellow
.....
00...
...00
.0000
00..0

CreepR
Yellow
.....
.....
000..
0..00
0000.

CreepD
Yellow
00000
.000.
0..00
....0
...0.

AudioDummy
transparent

=======
LEGEND
=======

ActiveTarget = Target1 or Target2
FrozenTarget = FrozenTarget1 or FrozenTarget2
Target = ActiveTarget or FrozenTarget
Red = Red1 or Red2
Silver = Silver1 or Silver2
Green = Green1 or Green2 or GreenB1 or GreenB2
Locked = GreenB1 or GreenB2
Yellow = YellowOn1 or YellowOn2 or YellowOff1 or YellowOff2
YellowOn = YellowOn1 or YellowOn2
Player = Red or Silver or Green or Yellow
Obstacle = Tree or Shroom
Bear = BearRight or BearLeft
Enemy = Bear
EnemyBlocker = Obstacle or Enemy or Silver
Loss = Loss1 or Loss2 or Loss3 or Loss4 or Loss5 or Loss6
EndingEarth = endingblue or endinggreen or endingwhite
AnyHoney = Honey or CollectedHoney
Ending = EndingEarth or endingblack or endingred or endingred2

. = Background
# = Tree
m = Shroom
R = Red1
1 = Red2
S = Silver1
G = Green1
Y = YellowOff1
O = Target1
B = BearRight
ß = BearLeft
X = Trap
+ = endingblack
4 = endingblue
5 = endinggreen
6 = endingwhite
7 = endingred
H = Honey
! = CollectedHoney and MutatingYellow
? = Hive
I = CreepL
P = CreepR
L = CreepD
D = Dead
A = AudioDummy
~ = AudioDummy and endingblack

=======
SOUNDS
=======
Player move  53174907
Endlevel 64555908
Titlescreen 45324108
EndGame 45324108
Shroom destroy 72700102
UsedTrap Create 19593507
Dead Create 52537708
CloseMessage 80097500
SFX0 91500705
SFX2 71683507
SFX3 79803108
SFX4 24411903

================
COLLISIONLAYERS
================

Background
Tmp
Dead
Trap, UsedTrap, UsedTrap2, CollectedHoney, CreepD, CreepL, CreepR
MutatingYellow, Hive
Player, Obstacle, Bear, Honey
Pheromones, 
Target
Loss
Ending
AudioDummy
endingfinished

======
RULES     
======     

    [ stationary Player ] [ Target2 ] -> [ stationary Player ] [ Tmp ] 
    [ stationary Player ] [ Target1 ] -> [ stationary Player ] [ Target2 ]
    [ Tmp ] -> [ Target1 ]    
	[ Target2 ] [ FrozenTarget1 ] -> [ Target2 ] [ FrozenTarget2 ]
	[ Target1 ] [ FrozenTarget2 ] -> [ Target1 ] [ FrozenTarget1 ]

    [ stationary Red2 ] -> [ stationary Tmp ] 
    [ stationary Red1 ] -> [ stationary Red2 ] 
    [ Tmp ] -> [ stationary Red1 ]

    [ stationary Silver2 ] -> [ stationary Tmp ] 
    [ stationary Silver1 ] -> [ stationary Silver2 ] 
    [ Tmp ] -> [ stationary Silver1 ]

    [ stationary Green2 ] -> [ stationary Tmp ] 
    [ stationary Green1 ] -> [ stationary Green2 ] 
    [ Tmp ] -> [ stationary Green1 ]
    [ stationary GreenB2 ] -> [ stationary Tmp ] 
    [ stationary GreenB1 ] -> [ stationary GreenB2 ] 
    [ Tmp ] -> [ stationary GreenB1 ]

	[ stationary YellowOff2 ] -> [ stationary Tmp ] 
    [ stationary YellowOff1 ] -> [ stationary YellowOff2 ] 
    [ Tmp ] -> [ stationary YellowOff1 ]
	[ stationary YellowOn2 ] -> [ stationary Tmp ] 
    [ stationary YellowOn1 ] -> [ stationary YellowOn2 ] 
    [ Tmp ] -> [ stationary YellowOn1 ]
    
	[ AudioDummy ] -> [ ] SFX3

    late [ ActiveTarget ] [ no Player FrozenTarget ] -> [ ActiveTarget ] [ no Player ActiveTarget ]
    late [ Player ActiveTarget ] -> [ Player FrozenTarget1 ]

late [ Dead no Loss ] -> [ Dead Loss1 ]
late [ AnyHoney ] [ Yellow ] [ Loss1 ] -> [ AnyHoney ] [ Yellow ] [ ]
late [ Hive ] [ Loss1 ] -> [ Hive ] [ ]
late [ Loss6 ] -> restart
late [ Loss5 ] -> [ Loss6 ]
late [ Loss4 ] -> [ Loss5 ]
late [ Loss3 ] -> [ Loss4 ]
late [ Loss2 ] -> [ Loss3 ]
late [ Loss1 ] -> [ Loss2 ]


	
[ stationary Player ] [ BearRight ] -> [ stationary Player] [ RIGHT BearRight ]
[ stationary Player ] [ BearLeft ] -> [ stationary Player] [ LEFT BearLeft ]
[ Silver | < Bear ] -> [ Silver | Bear ]
[ Player | < Bear ] -> [ Dead | < Bear ]


[ Loss ] [ moving Player ] -> [Loss] [ Player ]

[ > Locked ] -> [ Locked ]
[ Player Pheromones | moving Yellow ] -> [ > Player Pheromones | moving Yellow ]
[ moving Player ] [ Pheromones ] -> [ moving Player ] [ ]
[ > Silver | Enemy ] -> [ Silver | Enemy ]
[ > Player | Hive ] -> [ Player | Hive ]
[ > Yellow ][ Pheromones ] -> [ Yellow ][ ]
[ > YellowOn ] -> [ > YellowOn Pheromones]
late [ Yellow Pheromones ] -> [ Yellow ]
[ > Player | Enemy ] -> [  | Dead Enemy ]
[ > Yellow | Honey ] -> [ > Yellow | CollectedHoney ]
late [ Yellow CollectedHoney ] -> win
late [ MutatingYellow CollectedHoney ] -> win

[ Action Red | Shroom ] -> [ Action Red | ] 
[ Action Green1 ] -> [ Action Tmp ] sfx2
[ Action GreenB1 ] -> [ Action Green1 ] sfx2
[ Action Tmp ] -> [ Action GreenB1 ]
[ Action Green2 ] -> [ Action Tmp ] sfx2
[ Action GreenB2 ] -> [ Action Green2 ] sfx2
[ Action Tmp ] -> [ Action GreenB2 ]
[ Action YellowOff1 ] -> [ Action Tmp ] 
[ Action YellowOn1 ] -> [ Action YellowOff1 ] 
[ Action Tmp ] -> [ Action YellowOn1 ]
[ Action YellowOff2 ] -> [ Action Tmp ] 
[ Action YellowOn2 ] -> [ Action YellowOff2 ] 
[ Action Tmp ] -> [ Action YellowOn2 ]


late right [ NO EnemyBlocker | BearRight | EnemyBlocker ] -> [ NO Enemy | BearLeft | EnemyBlocker ]
late left [ NO EnemyBlocker | BearLeft | EnemyBlocker ] -> [ NO EnemyBlocker | BearRight | EnemyBlocker ]

late [ UsedTrap2 ] -> [ ]
late [ UsedTrap ] -> [ UsedTrap2 ]
late [ Silver Trap ] -> [ Silver UsedTrap ]
late [ Player Trap ] -> [ Dead UsedTrap]
late [ Enemy Trap ] -> [ UsedTrap ]

[ Hive no Player ] -> [ Hive random Red ]


random [ Ending ] -> [ Ending endingfinished ] again
[ endingEarth ] [ endingfinished ] -> [ endingEarth ] [ ]
[ endingred ] -> [ endingred2 ]
[ EndingEarth | endingred2 ] -> [ endingred | endingred2 ]
[ endingfinished ] ->  win sfx4


==============
WINCONDITIONS
==============
All Target on Player 
No Ending


=======     
LEVELS
=======

MESSAGE "We are the Vmind. We have finally arrived on Earth. Now we must gather! This one moves north..."
#########
####O####
#.#..#..#
#..#....#
#..####.#
#.#.....#
#....#..#
####R####

MESSAGE "Yes... Yeeees! We are not only one anymore, and yet we are one."
#########
#.O...O##
#.......#
#####...#
#.......#
#...#..##
#...#.R.#
##1######

MESSAGE "These puny lifeforms are no match for our drones! Assist us, or be eradicated!"
MESSAGE Hint: Command red drones to destroy adjacent mushrooms by pressing X.
#########
#.......#
#.#....O#
#.#..mO.#
#.#....O#
#.......#
##R#1R###

MESSAGE "The furry giants are Earth's fighting forces for sure. We must avoid them at all costs! For now..."
#########
#.O.#.OO#
#....#..#
#....mB.#
###m....#
#m....m.#
##R##1R##

MESSAGE "We are almost at the rendezvous. Only a little further."
########
#.....OO
#.....OO
#.B.####
#...####
#.#.####
#..R####
#..1####
#1.R####

MESSAGE "Yeeeees - beetles! We can protect ourselves again."
MESSAGE Hint: Silver beetles are invulnerable and block enemy movement.
##########
##..#.#O.#
######Om.#
##.OOOß..#
R1..##.#.#
1R..##...#
######S###

MESSAGE "Ha! Noone will stop our advance!"
########S#
R...#m...#
###m##m###
#OO..B.OO#
###m##m###
S...m#...1
##########

MESSAGE "Just a bit further and we will catch up with the rooters. Eeeeexcellent!"
#R##########S#
#B..........ß#
########O#.#O#

MESSAGE "We are glad to have met up. This will help assuming formation. Advance!"
MESSAGE Hint: Lock and unlock green rooters in place by pressing X.
##########
R........O
1...G....O
R........O
S.....O..O
1....O....
##########

MESSAGE "We are making good progress here. We can feeeel it."
#O##.##
#O#####
#B.m.X#
#.#mX##
#.X#O##
#R.#OS#
#G.#R##

MESSAGE "Security is tight around here! We must be getting close."
#########
#...GX#SO
#RX..X#mO
#Bm.m.mX#
.R###..#O
.......XO
#B.######

MESSAGE "At last, our swarm is compleeeete again!"
MESSAGE Hint: Yellow queens can leave behind a pheromon cloud that compells other Vmind to follow them. Toggle with X.
########
#..m..XO
#...X..O
#.X#####
#......#
#......#
#.YR...#

MESSAGE "We can smeeeell our reward. We are almost there.
#########
##....mO.
#...X...O
#....####
#M.....m.
XY#..#m#.
#.X.##.R.

MESSAGE "Let us round up us who are still on their own and then claim what is meant to be ours!"
#.XOO.OOOO...S#
#.#############
#.....#B.##mm.1
###...X..XmMm.R
##X##.X..######
#X...#..ß#X...#
#XmX...XY...Xm#
#XGX..m.m###XR#

MESSAGE "THERE IT IS!! Earth bee honey! No matter how many of us die, if our queen feeds on it, victory will be guaranteed!
######O######
......H......
...#B...#....
...##...ß#...
...#...B#.#..
.#..#m.m#....
...#..X..#...
..##.#X#.##..
..#..#G#..#..
.#...#.#...#.
.#.###R###.#.
#SßmRRYRRmBS#

MESSAGE "Our intelligence was correct! Earth invented a substance that would speed up our metabolism by thousandfold. Our queen immediately began to... grow, change, optimize."
MESSAGE "The queen was no more. In her place... the birthing pit.
######O######
.A....!......
...#....#....
...##....#...
...#D...#.#..
.#..#..D#....
...#.....#...
..##.#D#.##..
..#..#.#..#..
.#...#.#...#.
.#.###.###.#.
#..D...D....#

######O######
.....I!P.....
...#..L.#....
...##....#...
...#D...#.#..
.#..#..D#....
...#.....#...
..##.#D#.##..
..#..#.#..#..
.#...#.#...#.
.#.###.###.#.
#..D...D....#

######O######
O....I?P....O
O..#..L.#...O
O..##....#..O
O..#D...#.#.O
O#..#..D#...O
O..#.....#..O
O.##.#D#.##.O
O.#..#.#..#OO
O#...#.#...#O
O#.###.###.#O
#.OOOOOOOOO.#


MESSAGE "And thus, there was no stopping us anymore. We later learned that a  species far more advanced than what we initially faced inhabitated the planet but we were already undefeatable when they even realized we were there!"
++++++++++++++++++
+~++++466664++++++
++++4444444444++++
+++555544444444+++
+++554444544445+++
++55444454455555++
++54444445557555++
++55444555455555++
++55544455445544++
++55444554454455++
+++444455544444+++
+++444445544444+++
++++4444444444++++
++++++446664++++++
++++++++++++++++++
MESSAGE Congratulations! Earth has been overrun! 
MESSAGE Credits
MESSAGE A game by Denis Prause
MESSAGE Made in PuzzleScript by Stephen Lavelle
MESSAGE #1 playtester: Matthias Weimann
MESSAGE Global Game Jame 2015