title Keys and Doors 0.1.0
author Timo Dreyer
homepage www.puzzlescript.net

========
OBJECTS
========

Background
lightgreen GREEN
11011
10111
10111
01110
11101

Target
black
.....
.0.0.
..0..
.0.0.
.....

Wall
BROWN darkbrown
11111
11011
10111
10101
11111

Player
purple
..0..
00000
.000.
.0.0.
00.00

Crate
Orange Brown
11111
10101
11111
10101
11111

redKey
Red
.....
00...
0.000
00.0.
.....

blueKey
blue
.....
00...
0.000
00.0.
.....

redDoor
Red brown
00000
01110
01010
01010
01110

blueDoor
blue brown
00000
01110
01010
01010
01110

=======
LEGEND
=======

. = Background
# = Wall
P = Player
1 = redKey
2 = blueKey
A = redDoor
B = blueDoor
* = Crate
@ = Crate and Target
O = Target
Key = redKey or blueKey
Door = redDoor or blueDoor

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate, redKey, blueKey, redDoor, blueDoor

======
RULES
======

[ >  Player | Crate ] -> [  >  Player | > Crate  ]
[ >  Player | Key ] -> [  >  Player | > Key  ]
[ >  redKey | redDoor ] -> [ |  ]
[ >  blueKey | blueDoor ] -> [ |  ]


==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

message TUTORIAL

message Push a box on every X!

#############
#...........#
#...........#
#..p..*..o..#
#...........#
#...........#
#############

message Use keys to open matching doors!

#############
#.....#.....#
#...2.A.*.o.#
#.p...#.....#
#...1.B.o.*.#
#.....#.....#
#############

message Level 1

##############
#.......#....#
#..1..@.#..O.#
#.P.*.O.A..*.#
#.......#....#
#.......#....#
##############

message Level 2

##############
#.p.....#....#
#.o...*.#....#
#...*...b....#
#.1#..*.#.*#o#
#..#....#..#o#
####a#########
..#..2.#......
..#.o..#......
..######......

message Level 3

##############
###.......####
#.##*##.*...##
#.p....*2*#.##
#*......*.#..#
#..#a##......#
#.1#.##.###.##
#..#o#...##..#
####o#....#*.#
#oooob..@.#..#
##############

message All done!