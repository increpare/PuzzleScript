title Tic-Fa-No
author Ben Erlebach

text_color black
background_color white

run_rules_on_level_start
noundo

( Alright, so here's the deal. This game takes place on the Fano plane, which has
  the property that every pair of points has exactly one line that goes through it
  ( so for any two points, there is another point that turns them into a line ).
  This is very useful, and allows us to come up with a simple strategy to win as
  the first player:
  
  ( Call the first point we place an X on a1, the second a2, etc.
    Similarly call the first point the second player places an O on b1, etc. )
  
  > Turn 1 (X): Place an X anywhere (it doesn't matter).
  
  > Turn 2 (O): The second player places an O. It doesn't matter where.
  
  > Turn 3 (X): Place an X anywhere, but not on the line with a1 and b1 on it.
  Now, we have that a1 and a2 lie on a line, and it's not the line that b1 is 
  on, so this forces the hand of the second player.
  
  > Turn 4 (O): There are effectively two possiblities here: either the second
  player places their second O on the a1-a2 line, or they don't.
  
  - If they don't:
  
    > Turn 5 (X): Place a point on the a1-a2 line - then we've won!
  
  - If they do:
  
    > Turn 5 (X): Although they blocked our attack, the die has been cast.
    Place our third X on the b1-b2 line. This stops them from winning next turn,
    and it also means they can't stop us winning: we now have two points on both
    the a1-a3 line and the a2-a3 line, so:
    
    Turn 6 (O): Either they try and block us on the a1-a3 line, or the a2-a3 line.
    
    Turn 7 (X): Place the final X on the remaing empty point - we've won!
  
)

========
OBJECTS
========

Background .
White

( == Won flags == )

( The 'Won' object is created when the game is over, to stop the game from continuing. )

Won
Transparent

( The 'WonN' objects keep track of which points were in the winning line, so they can be highlighted.
  Here and henceforth, the points are named like so:
    1   
   2 3
    4
  5 6 7 ) 

Won1
Transparent

Won2
Transparent

Won3
Transparent

Won4
Transparent

Won5
Transparent

Won6
Transparent

Won7
Transparent

( == Players == )

( Because actually moving the cursor around the board would be difficult, the 'PlayerN' objects 
  keep track of which point the cursor should be on, so the cursor can be removed and a new one
  placed on the new point. Thus moving the cursor just amounts to changing 'PlayerN' to 'PlayerM'. )

Player1
Transparent

Player2
Transparent

Player3
Transparent

Player4
Transparent

Player5
Transparent

Player6
Transparent

Player7
Transparent

( == Xs & Os == )

( Again, instead of actually trying to keep track of where the Xs and Os are placed on the board,
  each 'XN' and 'ON' object represents where the Xs and Os should be placed, so the relevant
  symbols can be drawn on the board, and so the AI can tell where the pieces have been placed. )

X1
Transparent

X2
Transparent

X3
Transparent

X4
Transparent

X5
Transparent

X6
Transparent

X7
Transparent

O1
Transparent

O2
Transparent

O3
Transparent

O4
Transparent

O5
Transparent

O6
Transparent

O7
Transparent

( == Turn counter == )

( Keeps track of what turn it is - at the start of the game, the 'Turn1' object is created, and 
  every time a move is made, the 'TurnN' object is replaced with a 'TurnN+1' object. This allows
  the AI to know easily what turn it is, and for the game to allow player input only on the
  relevant turns. )

Turn1
Transparent

Turn2
Transparent

Turn3
Transparent

Turn4
Transparent

Turn5
Transparent

Turn6
Transparent

Turn7
Transparent

( == Board pieces == )

( The drawing of the board. The numbering starts from 11 in the top-left corner to 67 in the bottom
  right, although many of those squares are not actually used.
  The unicode symbols are used so the board can easily be placed in the level without having to
  scrounge around the alphabet/etc. for symbols to use! Shame it ruins the monospacing... )

Board14 ☀
Black LightBlue
.....
.111.
.111.
.111.
.000.

Board23 ☁
Black
.....
....0
....0
...0.
...0.

Board24 ☂
Black
0.0.0
..0..
..0..
..0..
..0..

Board25 ☃
Black 
.....
0....
0....
.0...
.0...

Board32 ☄
Black LightBlue
.....
.....
.....
....1
....1

Board33 ★
Black LightBlue
..0..
.0...
.0.00
110..
11...

Board34 ☆
Black
..0..
00000
..0..
..0..
..0..

Board35 ☇
Black LightBlue
..0..
...0.
00.0.
..011
...11

Board36 ☈
Black LightBlue
.....
.....
.....
1....
1....

Board42 ☉
Black LightBlue
....1
....0
...00
..0.0
..0.0

Board43 ☊
Black LightBlue
110..
0..0.
....0
.....
.....

Board44 ☋
Black LightBlue
..0..
..0..
0.0.0
.111.
.111.

Board45 ☌
Black LightBlue
..011
.0..0
0....
.....
.....

Board46 ☍
Black LightBlue
1....
0....
00...
0.0..
0.0..

Board51 ☎
Black
.....
.....
.....
.....
....0

Board52 ☏
Black
.0..0
.0..0
0....
0...0
..00.

Board53 ☐
Black
....0
..00.
00...
0....
.0...

Board54 ☑
Black LightBlue
01110
..0..
..0..
..0..
..0..

Board55 ☒
Black
0....
.00..
...00
....0
...0.

Board56 ☓
Black
0..0.
0..0.
....0
0...0
.00..

Board57 ☔
Black
.....
.....
.....
.....
0....

Board61 ☕
Black LightBlue
...0.
.1110
.1110
.111.
.....

Board62 ☖
Black
00...
.....
00000
.....
.....

Board63 ☗
Black
.00..
...00
00000
.....
.....

Board64 ☘
Black LightBlue
..0..
.111.
01110
.111.
.....

Board65 ☙
Black
..00.
00...
00000
.....
.....

Board66 ☚
Black
...00
.....
00000
.....
.....

Board67 ☛
Black LightBlue
.0...
0111.
0111.
.111.
.....

( == Cursor pieces == )

( Because some of the points are not just on one 5x5 square, various objects are required to
  represent the cursors / Xs / Os / highlights. The number signifies where on the board the
  object is to be placed.
  Although points 1, 5, 6 and 7 do actually lie on one 5x5 square, so the objects for point
  1 could be reused for points 5, 6 and 7, I made them seperate objects. Partially I think
  this makes the code more consistent, but mainly it's because at this point I can't be
  bothered to go through and change all the code just to save some lines here... )

( 1 )

Cursor14
Red
00.00
0...0
.....
0...0
00.00

( 2 )

Cursor32
Red
.....
.....
...00
...0.
.....

Cursor33
Red
.....
.....
.00..
..0..
.....

Cursor42
Red
...0.
...00
.....
.....
.....

Cursor43
Red
..0..
.00..
.....
.....
.....

( 3 )

Cursor35
Red
.....
.....
..00.
..0..
.....

Cursor36
Red
.....
.....
00...
.0...
.....

Cursor45
Red
..0..
..00.
.....
.....
.....

Cursor46
Red
.0...
00...
.....
.....
.....

( 4 )

Cursor44
Red
.....
.....
00.00
0...0
.....

Cursor54
Red
0...0
00.00
.....
.....
.....

( 5 )

Cursor61
Red
00.00
0...0
.....
0...0
00.00

( 6 )

Cursor64
Red
00.00
0...0
.....
0...0
00.00

( 7 )

Cursor67
Red
00.00
0...0
.....
0...0
00.00

( == X & O sprites == )

( 1 )

XSprite14
Green
0...0
.0.0.
..0..
.0.0.
0...0

OSprite14
Green
.000.
0...0
0...0
0...0
.000.

( 2 )

XSprite32
Green
.....
.....
...0.
....0
.....

OSprite32
Green
.....
.....
....0
...0.
...0.

XSprite33
Green
.....
.....
..0..
.0...
0....

OSprite33
Green
.....
.....
00...
..0..
..0..

XSprite42
Green
....0
...0.
.....
.....
.....

OSprite42
Green
...0.
....0
.....
.....
.....

XSprite43
Green
.0...
..0..
.....
.....
.....

OSprite43
Green
..0..
00...
.....
.....
.....

( 3 )

XSprite35
Green
.....
.....
..0..
...0.
....0

OSprite35
Green
.....
.....
...00
..0..
..0..

XSprite36
Green
.....
.....
.0...
0....
.....

OSprite36
Green
.....
.....
0....
.0...
.0...

XSprite45
Green
...0.
..0..
.....
.....
.....

OSprite45
Green
..0..
...00
.....
.....
.....

XSprite46
Green
0....
.0...
.....
.....
.....

OSprite46
Green
.0...
0....
.....
.....
.....

( 4 )

XSprite44
Green
.....
.....
0...0
.0.0.
..0..

OSprite44
Green
.....
.....
.000.
0...0
0...0

XSprite54
Green
.0.0.
0...0
.....
.....
.....

OSprite54
Green
0...0
.000.
.....
.....
.....

( 5 )

XSprite61
Green
0...0
.0.0.
..0..
.0.0.
0...0

OSprite61
Green
.000.
0...0
0...0
0...0
.000.

( 6 )

XSprite64
Green
0...0
.0.0.
..0..
.0.0.
0...0

OSprite64
Green
.000.
0...0
0...0
0...0
.000.

( 7 )

XSprite67
Green
0...0
.0.0.
..0..
.0.0.
0...0

OSprite67
Green
.000.
0...0
0...0
0...0
.000.

( == Highlights == )

( 1 )

Highlight14
Pink
00000
00000
00000
00000
00000

( 2 )

Highlight32
Pink
.....
.....
...00
...00
...00

Highlight33
Pink
.....
.....
000..
000..
000..

Highlight42
Pink
...00
...00
.....
.....
.....

Highlight43
Pink
000..
000..
.....
.....
.....

( 3 )

Highlight35
Pink
.....
.....
..000
..000
..000

Highlight36
Pink
.....
.....
00...
00...
00...

Highlight45
Pink
..000
..000
.....
.....
.....

Highlight46
Pink
00...
00...
.....
.....
.....

( 4 )

Highlight44
Pink
.....
.....
00000
00000
00000

Highlight54
Pink
00000
00000
.....
.....
.....

( 5 )

Highlight61
Pink
00000
00000
00000
00000
00000

( 6 )

Highlight64
Pink
00000
00000
00000
00000
00000

( 7 )

Highlight67
Pink
00000
00000
00000
00000
00000

=======
LEGEND
=======

WonNumber = Won1 or Won2 or Won3 or Won4 or Won5 or Won6 or Won7

Player = Player1 or Player2 or Player3 or Player4 or Player5 or Player6 or Player7

X = X1 or X2 or X3 or X4 or X5 or X6 or X7
O = O1 or O2 or O3 or O4 or O5 or O6 or O7

XO = X or O

XO1 = X1 or O1
XO2 = X2 or O2
XO3 = X3 or O3
XO4 = X4 or O4
XO5 = X5 or O5
XO6 = X6 or O6
XO7 = X7 or O7

Turn = Turn1 or Turn2 or Turn3 or Turn4 or Turn5 or Turn6 or Turn7

Board = Board51 or Board61 or Board32 or Board42 or Board52 or Board62 or Board23 or Board33 or Board43 or Board53 or Board63 or Board14 or Board24 or Board34 or Board44 or Board54 or Board64 or Board25 or Board35 or Board45 or Board55 or Board65 or Board36 or Board46 or Board56 or Board66 or Board57 or Board67

Cursor = Cursor61 or Cursor32 or Cursor42 or Cursor33 or Cursor43 or Cursor14 or Cursor44 or Cursor54 or Cursor64 or Cursor35 or Cursor45 or Cursor36 or Cursor46 or Cursor67

XSprite = XSprite61 or XSprite32 or XSprite42 or XSprite33 or XSprite43 or XSprite14 or XSprite44 or XSprite54 or XSprite64 or XSprite35 or XSprite45 or XSprite36 or XSprite46 or XSprite67

OSprite = OSprite61 or OSprite32 or OSprite42 or OSprite33 or OSprite43 or OSprite14 or OSprite44 or OSprite54 or OSprite64 or OSprite35 or OSprite45 or OSprite36 or OSprite46 or OSprite67

Highlight = Highlight61 or Highlight32 or Highlight42 or Highlight33 or Highlight43 or Highlight14 or Highlight44 or Highlight54 or Highlight64 or Highlight35 or Highlight45 or Highlight36 or Highlight46 or Highlight67

=======
SOUNDS
=======

XO create 51169100

Won create 40059902

================
COLLISIONLAYERS
================

Background
Board
Highlight
XSprite, OSprite
Cursor
Player
Turn
Won
WonNumber
XO1
XO2
XO3
XO4
XO5
XO6
XO7

======
RULES
======

( ================================ )

( ==== Logic ==== )

( ================================ )

( == Initialise game == )

[ Board14 no Player ] -> [ Board14 Player1 Turn1 ]

( == If the game's over, the game's over. == )

[ Won ] -> cancel

( ================ )

( === Player === )

( ================ )

( == Movement == )

( Instead of being able to use the natural movement capabilities of Puzzlescript,
  I have hard-coded where pressing each direction on each point takes you. I can
  see it being possible to be clever about this, but it wouldn't be much shorter
  and it'd be much less readable. But it did mean I had to work out on paper where
  I wanted each direction to go... )

[ left  Player1 ] -> [ Player2 ]
[ down  Player1 ] -> [ Player4 ]
[ right Player1 ] -> [ Player3 ]

[ up    Player2 ] -> [ Player1 ]
[ left  Player2 ] -> [ Player5 ]
[ down  Player2 ] -> [ Player5 ]
[ right Player2 ] -> [ Player4 ]

[ up    Player3 ] -> [ Player1 ]
[ left  Player3 ] -> [ Player4 ]
[ down  Player3 ] -> [ Player7 ]
[ right Player3 ] -> [ Player7 ]

[ up    Player4 ] -> [ Player1 ]
[ left  Player4 ] -> [ Player2 ]
[ down  Player4 ] -> [ Player6 ]
[ right Player4 ] -> [ Player3 ]

[ up    Player5 ] -> [ Player2 ]
[ right Player5 ] -> [ Player6 ]

[ up    Player6 ] -> [ Player4 ]
[ left  Player6 ] -> [ Player5 ]
[ right Player6 ] -> [ Player7 ]

[ up    Player7 ] -> [ Player3 ]
[ left  Player7 ] -> [ Player6 ]

( == Action == )

( This is a slightly ugly solution, largely because this is a holdover from when
  the code was much more complicated. But it works fine, and I can't be bothered to
  change it now. It is important (for the AI) that the 'O' objects are placed in
  different locations, so I suspect at best you could get this down to about eight
  lines. )

( = Turn 2 = )

( Place b1 )

down [ Turn2 action Player1 no XO1 ] -> [ Turn3 Player1 O1 ]
down [ Turn2 action Player2 no XO2 ] -> [ Turn3 Player2 O2 ]
down [ Turn2 action Player3 no XO3 ] -> [ Turn3 Player3 O3 ]
down [ Turn2 action Player4 no XO4 ] -> [ Turn3 Player4 O4 ]
down [ Turn2 action Player5 no XO5 ] -> [ Turn3 Player5 O5 ]
down [ Turn2 action Player6 no XO6 ] -> [ Turn3 Player6 O6 ]
down [ Turn2 action Player7 no XO7 ] -> [ Turn3 Player7 O7 ]

( = Turn 4 = )

( Place b2 )

down [ Turn4 action Player1 no XO1 | no XO1 ] -> [ Turn5 Player1 | O1 ]
down [ Turn4 action Player2 no XO2 | no XO2 ] -> [ Turn5 Player2 | O2 ]
down [ Turn4 action Player3 no XO3 | no XO3 ] -> [ Turn5 Player3 | O3 ]
down [ Turn4 action Player4 no XO4 | no XO4 ] -> [ Turn5 Player4 | O4 ]
down [ Turn4 action Player5 no XO5 | no XO5 ] -> [ Turn5 Player5 | O5 ]
down [ Turn4 action Player6 no XO6 | no XO6 ] -> [ Turn5 Player6 | O6 ]
down [ Turn4 action Player7 no XO7 | no XO7 ] -> [ Turn5 Player7 | O7 ]

( = Turn 6 = )

( Place b3 )

down [ Turn6 action Player1 no XO1 | no XO1 | no XO1 ] -> [ Turn7 Player1 | | O1 ]
down [ Turn6 action Player2 no XO2 | no XO2 | no XO2 ] -> [ Turn7 Player2 | | O2 ]
down [ Turn6 action Player3 no XO3 | no XO3 | no XO3 ] -> [ Turn7 Player3 | | O3 ]
down [ Turn6 action Player4 no XO4 | no XO4 | no XO4 ] -> [ Turn7 Player4 | | O4 ]
down [ Turn6 action Player5 no XO5 | no XO5 | no XO5 ] -> [ Turn7 Player5 | | O5 ]
down [ Turn6 action Player6 no XO6 | no XO6 | no XO6 ] -> [ Turn7 Player6 | | O6 ]
down [ Turn6 action Player7 no XO7 | no XO7 | no XO7 ] -> [ Turn7 Player7 | | O7 ]

( ================ )

( === AI === )

( ================ )

( == Turn 1 == )

( Place a1 randomly )

[ Turn1 ] -> [ Turn2 random X ]

( == Turn 3 == )

( Place a2 randomly (not on the a1-b1 line) )

( It's here where most of the baggage lies - because Puzzlescript has no functions,
  to calculate what the third point on a line is, given two points, it takes 42 rules,
  as there's 7 choices for the first point and 6 for the second! Sometimes I can be
  clever and reduce it to 21 lines, by not worrying about the order the points are in. )

down [ Turn3 XO1 XO2 | ] -> [ Turn4 XO1 XO2 | random X3 random X4 random X6 random X7 ]
down [ Turn3 XO1 XO3 | ] -> [ Turn4 XO1 XO3 | random X2 random X4 random X5 random X6 ]
down [ Turn3 XO1 XO4 | ] -> [ Turn4 XO1 XO4 | random X2 random X3 random X5 random X7 ]
down [ Turn3 XO1 XO5 | ] -> [ Turn4 XO1 XO5 | random X3 random X4 random X6 random X7 ]
down [ Turn3 XO1 XO6 | ] -> [ Turn4 XO1 XO6 | random X2 random X3 random X5 random X7 ]
down [ Turn3 XO1 XO7 | ] -> [ Turn4 XO1 XO7 | random X2 random X4 random X5 random X6 ]
down [ Turn3 XO2 XO3 | ] -> [ Turn4 XO2 XO3 | random X1 random X4 random X5 random X7 ]
down [ Turn3 XO2 XO4 | ] -> [ Turn4 XO2 XO4 | random X1 random X3 random X5 random X6 ]
down [ Turn3 XO2 XO5 | ] -> [ Turn4 XO2 XO5 | random X3 random X4 random X6 random X7 ]
down [ Turn3 XO2 XO6 | ] -> [ Turn4 XO2 XO6 | random X1 random X4 random X5 random X7 ]
down [ Turn3 XO2 XO7 | ] -> [ Turn4 XO2 XO7 | random X1 random X3 random X5 random X6 ]
down [ Turn3 XO3 XO4 | ] -> [ Turn4 XO3 XO4 | random X1 random X2 random X6 random X7 ]
down [ Turn3 XO3 XO5 | ] -> [ Turn4 XO3 XO5 | random X1 random X2 random X6 random X7 ]
down [ Turn3 XO3 XO6 | ] -> [ Turn4 XO3 XO6 | random X1 random X4 random X5 random X7 ]
down [ Turn3 XO3 XO7 | ] -> [ Turn4 XO3 XO7 | random X2 random X4 random X5 random X6 ]
down [ Turn3 XO4 XO5 | ] -> [ Turn4 XO4 XO5 | random X1 random X2 random X6 random X7 ]
down [ Turn3 XO4 XO6 | ] -> [ Turn4 XO4 XO6 | random X2 random X3 random X5 random X7 ]
down [ Turn3 XO4 XO7 | ] -> [ Turn4 XO4 XO7 | random X1 random X3 random X5 random X6 ]
down [ Turn3 XO5 XO6 | ] -> [ Turn4 XO5 XO6 | random X1 random X2 random X3 random X4 ]
down [ Turn3 XO5 XO7 | ] -> [ Turn4 XO5 XO7 | random X1 random X2 random X3 random X4 ]
down [ Turn3 XO6 XO7 | ] -> [ Turn4 XO6 XO7 | random X1 random X2 random X3 random X4 ]

( == Turn 5 == )

( If winnable, win! )

down [ Turn5 X1 | X2 no O5 | ] -> [ Turn6 X5 Won Won1 | X2 Won2 | Won5 ]
down [ Turn5 X1 | X3 no O7 | ] -> [ Turn6 X7 Won Won1 | X3 Won3 | Won7 ]
down [ Turn5 X1 | X4 no O6 | ] -> [ Turn6 X6 Won Won1 | X4 Won4 | Won6 ]
down [ Turn5 X1 | X5 no O2 | ] -> [ Turn6 X2 Won Won1 | X5 Won5 | Won2 ]
down [ Turn5 X1 | X6 no O4 | ] -> [ Turn6 X4 Won Won1 | X6 Won6 | Won4 ]
down [ Turn5 X1 | X7 no O3 | ] -> [ Turn6 X3 Won Won1 | X7 Won7 | Won3 ]
down [ Turn5 X2 | X1 no O5 | ] -> [ Turn6 X5 Won Won2 | X1 Won1 | Won5 ]
down [ Turn5 X2 | X3 no O6 | ] -> [ Turn6 X6 Won Won2 | X3 Won3 | Won6 ]
down [ Turn5 X2 | X4 no O7 | ] -> [ Turn6 X7 Won Won2 | X4 Won4 | Won7 ]
down [ Turn5 X2 | X5 no O1 | ] -> [ Turn6 X1 Won Won2 | X5 Won5 | Won1 ]
down [ Turn5 X2 | X6 no O3 | ] -> [ Turn6 X3 Won Won2 | X6 Won6 | Won3 ]
down [ Turn5 X2 | X7 no O4 | ] -> [ Turn6 X4 Won Won2 | X7 Won7 | Won4 ]
down [ Turn5 X3 | X1 no O7 | ] -> [ Turn6 X7 Won Won3 | X1 Won1 | Won7 ]
down [ Turn5 X3 | X2 no O6 | ] -> [ Turn6 X6 Won Won3 | X2 Won2 | Won6 ]
down [ Turn5 X3 | X4 no O5 | ] -> [ Turn6 X5 Won Won3 | X4 Won4 | Won5 ]
down [ Turn5 X3 | X5 no O4 | ] -> [ Turn6 X4 Won Won3 | X5 Won5 | Won4 ]
down [ Turn5 X3 | X6 no O2 | ] -> [ Turn6 X2 Won Won3 | X6 Won6 | Won2 ]
down [ Turn5 X3 | X7 no O1 | ] -> [ Turn6 X1 Won Won3 | X7 Won7 | Won1 ]
down [ Turn5 X4 | X1 no O6 | ] -> [ Turn6 X6 Won Won4 | X1 Won1 | Won6 ]
down [ Turn5 X4 | X2 no O7 | ] -> [ Turn6 X7 Won Won4 | X2 Won2 | Won7 ]
down [ Turn5 X4 | X3 no O5 | ] -> [ Turn6 X5 Won Won4 | X3 Won3 | Won5 ]
down [ Turn5 X4 | X5 no O3 | ] -> [ Turn6 X3 Won Won4 | X5 Won5 | Won3 ]
down [ Turn5 X4 | X6 no O1 | ] -> [ Turn6 X1 Won Won4 | X6 Won6 | Won1 ]
down [ Turn5 X4 | X7 no O2 | ] -> [ Turn6 X2 Won Won4 | X7 Won7 | Won2 ]
down [ Turn5 X5 | X1 no O2 | ] -> [ Turn6 X2 Won Won5 | X1 Won1 | Won2 ]
down [ Turn5 X5 | X2 no O1 | ] -> [ Turn6 X1 Won Won5 | X2 Won2 | Won1 ]
down [ Turn5 X5 | X3 no O4 | ] -> [ Turn6 X4 Won Won5 | X3 Won3 | Won4 ]
down [ Turn5 X5 | X4 no O3 | ] -> [ Turn6 X3 Won Won5 | X4 Won4 | Won3 ]
down [ Turn5 X5 | X6 no O7 | ] -> [ Turn6 X7 Won Won5 | X6 Won6 | Won7 ]
down [ Turn5 X5 | X7 no O6 | ] -> [ Turn6 X6 Won Won5 | X7 Won7 | Won6 ]
down [ Turn5 X6 | X1 no O4 | ] -> [ Turn6 X4 Won Won6 | X1 Won1 | Won4 ]
down [ Turn5 X6 | X2 no O3 | ] -> [ Turn6 X3 Won Won6 | X2 Won2 | Won3 ]
down [ Turn5 X6 | X3 no O2 | ] -> [ Turn6 X2 Won Won6 | X3 Won3 | Won2 ]
down [ Turn5 X6 | X4 no O1 | ] -> [ Turn6 X1 Won Won6 | X4 Won4 | Won1 ]
down [ Turn5 X6 | X5 no O7 | ] -> [ Turn6 X7 Won Won6 | X5 Won5 | Won7 ]
down [ Turn5 X6 | X7 no O5 | ] -> [ Turn6 X5 Won Won6 | X7 Won7 | Won5 ]
down [ Turn5 X7 | X1 no O3 | ] -> [ Turn6 X3 Won Won7 | X1 Won1 | Won3 ]
down [ Turn5 X7 | X2 no O4 | ] -> [ Turn6 X4 Won Won7 | X2 Won2 | Won4 ]
down [ Turn5 X7 | X3 no O1 | ] -> [ Turn6 X1 Won Won7 | X3 Won3 | Won1 ]
down [ Turn5 X7 | X4 no O2 | ] -> [ Turn6 X2 Won Won7 | X4 Won4 | Won2 ]
down [ Turn5 X7 | X5 no O6 | ] -> [ Turn6 X6 Won Won7 | X5 Won5 | Won6 ]
down [ Turn5 X7 | X6 no O5 | ] -> [ Turn6 X5 Won Won7 | X6 Won6 | Won5 ]

( If not winnable, don't lose! Place a3 on the b1-b2 line )

down [ Turn5 O1 | O2 | ] -> [ Turn6 O1 | O2 | X5 ]
down [ Turn5 O1 | O3 | ] -> [ Turn6 O1 | O3 | X7 ]
down [ Turn5 O1 | O4 | ] -> [ Turn6 O1 | O4 | X6 ]
down [ Turn5 O1 | O5 | ] -> [ Turn6 O1 | O5 | X2 ]
down [ Turn5 O1 | O6 | ] -> [ Turn6 O1 | O6 | X4 ]
down [ Turn5 O1 | O7 | ] -> [ Turn6 O1 | O7 | X3 ]
down [ Turn5 O2 | O1 | ] -> [ Turn6 O2 | O1 | X5 ]
down [ Turn5 O2 | O3 | ] -> [ Turn6 O2 | O3 | X6 ]
down [ Turn5 O2 | O4 | ] -> [ Turn6 O2 | O4 | X7 ]
down [ Turn5 O2 | O5 | ] -> [ Turn6 O2 | O5 | X1 ]
down [ Turn5 O2 | O6 | ] -> [ Turn6 O2 | O6 | X3 ]
down [ Turn5 O2 | O7 | ] -> [ Turn6 O2 | O7 | X4 ]
down [ Turn5 O3 | O1 | ] -> [ Turn6 O3 | O1 | X7 ]
down [ Turn5 O3 | O2 | ] -> [ Turn6 O3 | O2 | X6 ]
down [ Turn5 O3 | O4 | ] -> [ Turn6 O3 | O4 | X5 ]
down [ Turn5 O3 | O5 | ] -> [ Turn6 O3 | O5 | X4 ]
down [ Turn5 O3 | O6 | ] -> [ Turn6 O3 | O6 | X2 ]
down [ Turn5 O3 | O7 | ] -> [ Turn6 O3 | O7 | X1 ]
down [ Turn5 O4 | O1 | ] -> [ Turn6 O4 | O1 | X6 ]
down [ Turn5 O4 | O2 | ] -> [ Turn6 O4 | O2 | X7 ]
down [ Turn5 O4 | O3 | ] -> [ Turn6 O4 | O3 | X5 ]
down [ Turn5 O4 | O5 | ] -> [ Turn6 O4 | O5 | X3 ]
down [ Turn5 O4 | O6 | ] -> [ Turn6 O4 | O6 | X1 ]
down [ Turn5 O4 | O7 | ] -> [ Turn6 O4 | O7 | X2 ]
down [ Turn5 O5 | O1 | ] -> [ Turn6 O5 | O1 | X2 ]
down [ Turn5 O5 | O2 | ] -> [ Turn6 O5 | O2 | X1 ]
down [ Turn5 O5 | O3 | ] -> [ Turn6 O5 | O3 | X4 ]
down [ Turn5 O5 | O4 | ] -> [ Turn6 O5 | O4 | X3 ]
down [ Turn5 O5 | O6 | ] -> [ Turn6 O5 | O6 | X7 ]
down [ Turn5 O5 | O7 | ] -> [ Turn6 O5 | O7 | X6 ]
down [ Turn5 O6 | O1 | ] -> [ Turn6 O6 | O1 | X4 ]
down [ Turn5 O6 | O2 | ] -> [ Turn6 O6 | O2 | X3 ]
down [ Turn5 O6 | O3 | ] -> [ Turn6 O6 | O3 | X2 ]
down [ Turn5 O6 | O4 | ] -> [ Turn6 O6 | O4 | X1 ]
down [ Turn5 O6 | O5 | ] -> [ Turn6 O6 | O5 | X7 ]
down [ Turn5 O6 | O7 | ] -> [ Turn6 O6 | O7 | X5 ]
down [ Turn5 O7 | O1 | ] -> [ Turn6 O7 | O1 | X3 ]
down [ Turn5 O7 | O2 | ] -> [ Turn6 O7 | O2 | X4 ]
down [ Turn5 O7 | O3 | ] -> [ Turn6 O7 | O3 | X1 ]
down [ Turn5 O7 | O4 | ] -> [ Turn6 O7 | O4 | X2 ]
down [ Turn5 O7 | O5 | ] -> [ Turn6 O7 | O5 | X6 ]
down [ Turn5 O7 | O6 | ] -> [ Turn6 O7 | O6 | X5 ]

( == Turn 7 == )

( Win! Place a4 in the only possible place )

down [ Turn7 no XO1 | no XO1 | no XO1 | ] -> [ Turn7 Won | | | X1 Won1 ]
down [ Turn7 no XO2 | no XO2 | no XO2 | ] -> [ Turn7 Won | | | X2 Won2 ]
down [ Turn7 no XO3 | no XO3 | no XO3 | ] -> [ Turn7 Won | | | X3 Won3 ]
down [ Turn7 no XO4 | no XO4 | no XO4 | ] -> [ Turn7 Won | | | X4 Won4 ]
down [ Turn7 no XO5 | no XO5 | no XO5 | ] -> [ Turn7 Won | | | X5 Won5 ]
down [ Turn7 no XO6 | no XO6 | no XO6 | ] -> [ Turn7 Won | | | X6 Won6 ]
down [ Turn7 no XO7 | no XO7 | no XO7 | ] -> [ Turn7 Won | | | X7 Won7 ]

( Highlight a3 )

down [ Turn7 | | X1 ] -> [ Turn7 | | X1 Won1 ]
down [ Turn7 | | X2 ] -> [ Turn7 | | X2 Won2 ]
down [ Turn7 | | X3 ] -> [ Turn7 | | X3 Won3 ]
down [ Turn7 | | X4 ] -> [ Turn7 | | X4 Won4 ]
down [ Turn7 | | X5 ] -> [ Turn7 | | X5 Won5 ]
down [ Turn7 | | X6 ] -> [ Turn7 | | X6 Won6 ]
down [ Turn7 | | X7 ] -> [ Turn7 | | X7 Won7 ]

( Finally, highlight whichever of a1 or a2 was used! )

down [ Turn7 | X ] -> [ Turn7 X | ]

down [ Turn7 X5 | | Won1 | Won2 ] -> [ Turn7 X5 Won5 | | Won1 | Won2 ]
down [ Turn7 X7 | | Won1 | Won3 ] -> [ Turn7 X7 Won7 | | Won1 | Won3 ]
down [ Turn7 X6 | | Won1 | Won4 ] -> [ Turn7 X6 Won6 | | Won1 | Won4 ]
down [ Turn7 X2 | | Won1 | Won5 ] -> [ Turn7 X2 Won2 | | Won1 | Won5 ]
down [ Turn7 X4 | | Won1 | Won6 ] -> [ Turn7 X4 Won4 | | Won1 | Won6 ]
down [ Turn7 X3 | | Won1 | Won7 ] -> [ Turn7 X3 Won3 | | Won1 | Won7 ]
down [ Turn7 X5 | | Won2 | Won1 ] -> [ Turn7 X5 Won5 | | Won2 | Won1 ]
down [ Turn7 X6 | | Won2 | Won3 ] -> [ Turn7 X6 Won6 | | Won2 | Won3 ]
down [ Turn7 X7 | | Won2 | Won4 ] -> [ Turn7 X7 Won7 | | Won2 | Won4 ]
down [ Turn7 X1 | | Won2 | Won5 ] -> [ Turn7 X1 Won1 | | Won2 | Won5 ]
down [ Turn7 X3 | | Won2 | Won6 ] -> [ Turn7 X3 Won3 | | Won2 | Won6 ]
down [ Turn7 X4 | | Won2 | Won7 ] -> [ Turn7 X4 Won4 | | Won2 | Won7 ]
down [ Turn7 X7 | | Won3 | Won1 ] -> [ Turn7 X7 Won7 | | Won3 | Won1 ]
down [ Turn7 X6 | | Won3 | Won2 ] -> [ Turn7 X6 Won6 | | Won3 | Won2 ]
down [ Turn7 X5 | | Won3 | Won4 ] -> [ Turn7 X5 Won5 | | Won3 | Won4 ]
down [ Turn7 X4 | | Won3 | Won5 ] -> [ Turn7 X4 Won4 | | Won3 | Won5 ]
down [ Turn7 X2 | | Won3 | Won6 ] -> [ Turn7 X2 Won2 | | Won3 | Won6 ]
down [ Turn7 X1 | | Won3 | Won7 ] -> [ Turn7 X1 Won1 | | Won3 | Won7 ]
down [ Turn7 X6 | | Won4 | Won1 ] -> [ Turn7 X6 Won6 | | Won4 | Won1 ]
down [ Turn7 X7 | | Won4 | Won2 ] -> [ Turn7 X7 Won7 | | Won4 | Won2 ]
down [ Turn7 X5 | | Won4 | Won3 ] -> [ Turn7 X5 Won5 | | Won4 | Won3 ]
down [ Turn7 X3 | | Won4 | Won5 ] -> [ Turn7 X3 Won3 | | Won4 | Won5 ]
down [ Turn7 X1 | | Won4 | Won6 ] -> [ Turn7 X1 Won1 | | Won4 | Won6 ]
down [ Turn7 X2 | | Won4 | Won7 ] -> [ Turn7 X2 Won2 | | Won4 | Won7 ]
down [ Turn7 X2 | | Won5 | Won1 ] -> [ Turn7 X2 Won2 | | Won5 | Won1 ]
down [ Turn7 X1 | | Won5 | Won2 ] -> [ Turn7 X1 Won1 | | Won5 | Won2 ]
down [ Turn7 X4 | | Won5 | Won3 ] -> [ Turn7 X4 Won4 | | Won5 | Won3 ]
down [ Turn7 X3 | | Won5 | Won4 ] -> [ Turn7 X3 Won3 | | Won5 | Won4 ]
down [ Turn7 X7 | | Won5 | Won6 ] -> [ Turn7 X7 Won7 | | Won5 | Won6 ]
down [ Turn7 X6 | | Won5 | Won7 ] -> [ Turn7 X6 Won6 | | Won5 | Won7 ]
down [ Turn7 X4 | | Won6 | Won1 ] -> [ Turn7 X4 Won4 | | Won6 | Won1 ]
down [ Turn7 X3 | | Won6 | Won2 ] -> [ Turn7 X3 Won3 | | Won6 | Won2 ]
down [ Turn7 X2 | | Won6 | Won3 ] -> [ Turn7 X2 Won2 | | Won6 | Won3 ]
down [ Turn7 X1 | | Won6 | Won4 ] -> [ Turn7 X1 Won1 | | Won6 | Won4 ]
down [ Turn7 X7 | | Won6 | Won5 ] -> [ Turn7 X7 Won7 | | Won6 | Won5 ]
down [ Turn7 X5 | | Won6 | Won7 ] -> [ Turn7 X5 Won5 | | Won6 | Won7 ]
down [ Turn7 X3 | | Won7 | Won1 ] -> [ Turn7 X3 Won3 | | Won7 | Won1 ]
down [ Turn7 X4 | | Won7 | Won2 ] -> [ Turn7 X4 Won4 | | Won7 | Won2 ]
down [ Turn7 X1 | | Won7 | Won3 ] -> [ Turn7 X1 Won1 | | Won7 | Won3 ]
down [ Turn7 X2 | | Won7 | Won4 ] -> [ Turn7 X2 Won2 | | Won7 | Won4 ]
down [ Turn7 X6 | | Won7 | Won5 ] -> [ Turn7 X6 Won6 | | Won7 | Won5 ]
down [ Turn7 X5 | | Won7 | Won6 ] -> [ Turn7 X5 Won5 | | Won7 | Won6 ]

( ================================ )

( ==== Rendering ==== )

( ================================ )

( Most of the rendering works like this - look at the various flags (e.g. Player objects,
  X/O objects ) and then draw the relevant sprites on the relevant points. This does result
  in a lot of lines of code, but at least it's simple. )

( == Place Cursors == )

[ Cursor ] -> []

[ Board14 ] [ Player1 ] -> [ Board14 Cursor14 ] [ Player1 ]

[ Board32 ] [ Player2 ] -> [ Board32 Cursor32 ] [ Player2 ]
[ Board33 ] [ Player2 ] -> [ Board33 Cursor33 ] [ Player2 ]
[ Board42 ] [ Player2 ] -> [ Board42 Cursor42 ] [ Player2 ]
[ Board43 ] [ Player2 ] -> [ Board43 Cursor43 ] [ Player2 ]

[ Board35 ] [ Player3 ] -> [ Board35 Cursor35 ] [ Player3 ]
[ Board36 ] [ Player3 ] -> [ Board36 Cursor36 ] [ Player3 ]
[ Board45 ] [ Player3 ] -> [ Board45 Cursor45 ] [ Player3 ]
[ Board46 ] [ Player3 ] -> [ Board46 Cursor46 ] [ Player3 ]

[ Board44 ] [ Player4 ] -> [ Board44 Cursor44 ] [ Player4 ]
[ Board54 ] [ Player4 ] -> [ Board54 Cursor54 ] [ Player4 ]

[ Board61 ] [ Player5 ] -> [ Board61 Cursor61 ] [ Player5 ]

[ Board64 ] [ Player6 ] -> [ Board64 Cursor64 ] [ Player6 ]

[ Board67 ] [ Player7 ] -> [ Board67 Cursor67 ] [ Player7 ]

( == Draw Xs & Os == )

( = X = )

[ Board14 ] [ X1 ] -> [ Board14 XSprite14 ] [ X1 ]

[ Board32 ] [ X2 ] -> [ Board32 XSprite32 ] [ X2 ]
[ Board33 ] [ X2 ] -> [ Board33 XSprite33 ] [ X2 ]
[ Board42 ] [ X2 ] -> [ Board42 XSprite42 ] [ X2 ]
[ Board43 ] [ X2 ] -> [ Board43 XSprite43 ] [ X2 ]

[ Board35 ] [ X3 ] -> [ Board35 XSprite35 ] [ X3 ]
[ Board36 ] [ X3 ] -> [ Board36 XSprite36 ] [ X3 ]
[ Board45 ] [ X3 ] -> [ Board45 XSprite45 ] [ X3 ]
[ Board46 ] [ X3 ] -> [ Board46 XSprite46 ] [ X3 ]

[ Board44 ] [ X4 ] -> [ Board44 XSprite44 ] [ X4 ]
[ Board54 ] [ X4 ] -> [ Board54 XSprite54 ] [ X4 ]

[ Board61 ] [ X5 ] -> [ Board61 XSprite61 ] [ X5 ]

[ Board64 ] [ X6 ] -> [ Board64 XSprite64 ] [ X6 ]

[ Board67 ] [ X7 ] -> [ Board67 XSprite67 ] [ X7 ]

( = O = )

[ Board14 ] [ O1 ] -> [ Board14 OSprite14 ] [ O1 ]

[ Board32 ] [ O2 ] -> [ Board32 OSprite32 ] [ O2 ]
[ Board33 ] [ O2 ] -> [ Board33 OSprite33 ] [ O2 ]
[ Board42 ] [ O2 ] -> [ Board42 OSprite42 ] [ O2 ]
[ Board43 ] [ O2 ] -> [ Board43 OSprite43 ] [ O2 ]

[ Board35 ] [ O3 ] -> [ Board35 OSprite35 ] [ O3 ]
[ Board36 ] [ O3 ] -> [ Board36 OSprite36 ] [ O3 ]
[ Board45 ] [ O3 ] -> [ Board45 OSprite45 ] [ O3 ]
[ Board46 ] [ O3 ] -> [ Board46 OSprite46 ] [ O3 ]

[ Board44 ] [ O4 ] -> [ Board44 OSprite44 ] [ O4 ]
[ Board54 ] [ O4 ] -> [ Board54 OSprite54 ] [ O4 ]

[ Board61 ] [ O5 ] -> [ Board61 OSprite61 ] [ O5 ]

[ Board64 ] [ O6 ] -> [ Board64 OSprite64 ] [ O6 ]

[ Board67 ] [ O7 ] -> [ Board67 OSprite67 ] [ O7 ]

( ================ )

( === Winning === )

( ================ )

[ Won ] -> message X won!
[ Won ] [ Cursor ] -> [ Won ] []

( == Draw highlights == )

[ Board14 ] [ Won1 ] -> [ Board14 Highlight14 ] [ Won1 ]

[ Board32 ] [ Won2 ] -> [ Board32 Highlight32 ] [ Won2 ]
[ Board33 ] [ Won2 ] -> [ Board33 Highlight33 ] [ Won2 ]
[ Board42 ] [ Won2 ] -> [ Board42 Highlight42 ] [ Won2 ]
[ Board43 ] [ Won2 ] -> [ Board43 Highlight43 ] [ Won2 ]

[ Board35 ] [ Won3 ] -> [ Board35 Highlight35 ] [ Won3 ]
[ Board36 ] [ Won3 ] -> [ Board36 Highlight36 ] [ Won3 ]
[ Board45 ] [ Won3 ] -> [ Board45 Highlight45 ] [ Won3 ]
[ Board46 ] [ Won3 ] -> [ Board46 Highlight46 ] [ Won3 ]

[ Board44 ] [ Won4 ] -> [ Board44 Highlight44 ] [ Won4 ]
[ Board54 ] [ Won4 ] -> [ Board54 Highlight54 ] [ Won4 ]

[ Board61 ] [ Won5 ] -> [ Board61 Highlight61 ] [ Won5 ]

[ Board64 ] [ Won6 ] -> [ Board64 Highlight64 ] [ Won6 ]

[ Board67 ] [ Won7 ] -> [ Board67 Highlight67 ] [ Won7 ]

( ================================ )

( ==== Cleanup ==== )

( ================================ )

( I don't think this should ever matter, but better safe than sorry. )

[ moving Player ] -> cancel


==============
WINCONDITIONS
==============

( Maybe you should try going first if you want to win! )

=======
LEVELS
=======

.........
....☀....
...☁☂☃...
..☄★☆☇☈..
..☉☊☋☌☍..
.☎☏☐☑☒☓☔.
.☕☖☗☘☙☚☛.
.........
