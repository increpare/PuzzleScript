title Elements
author TheGreatEscaper

========
OBJECTS
========

Background
White

Temp
White

Target
Black
00.00
0...0
.....
0...0
00.00

Wall
Black Darkgrey
00000
01110
01110
01110
00000

Player
Black Pink
.....
.000.
.010.
.000.
.....

Rock
Brown
.....
.000.
.000.
.000.
.....

Sand
Yellow
.....
.000.
.000.
.000.
.....

Water
Blue
.....
.000.
.000.
.000.
.....

Ice
Lightblue
.....
.000.
.000.
.000.
.....

ILeft
Lightblue
.....
0....
0....
0....
.....

IUp
Lightblue
.000.
.....
.....
.....
.....

IRight
Lightblue
.....
....0
....0
....0
.....

IDown
Lightblue
.....
.....
.....
.....
.000.

Fire
Red
.....
.000.
.000.
.000.
.....

Air
LightGrey
0...0
.000.
.000.
.000.
0...0

Glass
LightGrey
.000.
00000
00000
00000
.000.

Grass
LightGreen
.....
.000.
.000.
.000.
.....




=======
LEGEND
=======

. = Background
# = Wall
P = Player
O = Target
R = Rock
S = Sand
W = Water
I = Ice
F = Fire
A = Air
G = Glass
border = iup or idown or ileft or iright
L = Grass

Moveable = Rock or Sand or Player
Obstacle = Wall or Rock 
Element = Rock or Sand or Water or Ice or Fire or Air or Glass or grass
=======
SOUNDS
=======



================
COLLISIONLAYERS
================

Background
Air, Glass
ileft
iup
iright
idown
Target, Temp
Fire
Player, Wall, Rock, Sand, Water, Ice, Grass

======
RULES
======

startloop
late [ice|water] -> [ice|ice]
endloop

late left [ice no ileft| ice] -> [ice ileft| ice]
late right [ice no iright| ice] -> [ice iright| ice]
late up [ice no iup|ice] -> [ice iup|ice]
late down [ice no idown|ice] -> [ice idown| ice]

late left [ileft| no ice] -> [no ileft| ]
late right [iright| no ice] -> [no iright| ]
late up [iup|no ice] -> [no iup|]
late down [idown|no ice] -> [no idown| ]

late [no ice border] -> [no ice no border]

right[right ice ]->[stationary ice no ileft]
left[left ice ]->[stationary ice no iright]
down[down ice ]->[stationary ice no iup]
up[up ice ]->[stationary ice no idown]

startloop

left [moving ice | stationary ice ] -> [moving ice | moving ice]
up [moving ice | stationary ice ] -> [moving ice | moving ice]
down [moving ice | stationary ice ] -> [moving ice | moving ice]
right [moving ice | stationary ice ] -> [moving ice | moving ice]

[> moveable | ice] -> [> moveable | > ice]
+ rigid [ moving ice| ice ] -> [ moving ice | moving ice ]
endloop

[ > ice | moveable no rock] -> [> ice|> moveable ]
[ >  Moveable no Water| Moveable ] -> [  >  Moveable | > Moveable ]
[ >  Moveable no Sand | Water ]    -> [  >  Moveable | > Water ]
[ >  Water | Moveable no Sand no Obstacle]    -> [  >  Water    | > Moveable ]
[ >  Water | Water ] -> [ > Water | > Water ]

[ > Moveable no Player | Obstacle] -> cancel

startloop

left [moving ice | stationary ice ] -> [moving ice | moving ice]
up [moving ice | stationary ice ] -> [moving ice | moving ice]
down [moving ice | stationary ice ] -> [moving ice | moving ice]
right [moving ice | stationary ice ] -> [moving ice | moving ice]

[> moveable | ice] -> [> moveable | > ice]
+ rigid [ moving ice| ice ] -> [ moving ice | moving ice ]
endloop

[ action player no temp ] -> [temp player]
[ action player temp ] -> [ player target no temp]
late [temp no player] -> [no temp]

[ > Sand | Water ] -> [ > Rock |]
[ > Water | Sand ] -> [ > Rock |]

[> Player | Fire] -> cancel
late [Rock Fire] -> []
late [Sand Fire] -> [Glass]
late [Water Fire] -> [Air]
late [Ice Fire] -> [Air]

[> Player | Glass] -> cancel
[> Element | Air ] -> cancel

late [grass|fire] -> [fire|fire]

[> moveable| grass] -> [> moveable| > grass]
[> water | grass] -> [|grass]
[> grass | water] -> [|grass]

==============
WINCONDITIONS
==============

All Player on Target

=======
LEVELS
=======

message ELEMENTS
message Press X twice to skip a level


message Level 1: Blank Space

###########
#.........#
#.........#
#.........#
#.........#
#..P...O..#
#.........#
#.........#
#.........#
#.........#
###########


message Level 2: Rocky Rooms

###########
#.R...R.R.#
#.R..#RR.R#
#.RRR#.RR.#
#P...#R...#
#########.#
#O.R.#R.R.#
#..RR#..R.#
#R.R.#RR..#
#.R.....RR#
###########


message Level 3: Sand Dunes

###########
#PSS.R.S..#
#S.SS.....#
########..#
#..S...RR.#
#.R...SR..#
#.#########
#.S.S.S.S.#
#S.S.S.S.O#
#.S.S.S.S.#
###########


message Level 4: A Rude Sandstorm

###########
#RRR.O..RR#
#........R#
#SSSSSSSSS#
#S.SRSSS.S#
#SSS.RSSSS#
#SSSSSSSSS#
#SSSSSSSSS#
#R........#
#RR..P.RRR#
###########


message Level 5: Waterfall

###########
#P..RW....#
#####W#.###
#....WS...#
##.##W#####
#.WWWW....#
#.W.#W###.#
#.W..RSSS.#
#.WSWWW##.#
#WS..W..O.#
###########


message Level 6: Mosaic I

###########
#P........#
#.RSSSSSR.#
#.SRWRWRS.#
#.SWSRSWS.#
#.SRRORRS.#
#.SWSRSWS.#
#.SRWRWRS.#
#.RSSSSSR.#
#.........#
###########


message Level 7: Mosaic II

###########
#P........#
#.RSSSSSR.#
#.SWWWWWS.#
#.SWRRRWS.#
#.SWRORWS.#
#.SWSRSWS.#
#.SWWWWWS.#
#.RSSSSSR.#
#.........#
###########


message Level 8: Ice Cavern

###########
#PI.I.IRRR#
#I.I.I.RRR#
#.I.I.IRRR#
#I.I.I.RRR#
#.I.I.IRRR#
#I.I.I.R.I#
#RRRRRRRI.#
#RRRRRI.RI#
#RRRRR...O#
###########


message Level 9: Barricade

###########
#P..SWRWS.#
#...SWRWS.#
#.I.SWRWS.#
#...SWRWS.#
#.I.SWRWS.#
#...SWRWS.#
#.I.SWRWS.#
#...SWRWSO#
#...SWRWS.#
###########


message Level 10: Smoke and Mirrors

###########
#P........#
#S#S#S#.#.#
#S#S#S#.#.#
#.#.#.#.#.#
#A#G#G#A#G#
#.G.A.A.G.#
#.#.#.#.#.#
#G#A#G#G#A#
#O#O#O#O#O#
###########

message Level 11: FireWall
###########
#P.....FF.#
#.I.S..FF.#
#..S...FF.#
#.I.S..FF.#
#..I...FF.#
#.I.S..FF.#
#..S...FF.#
#.I.S..FF.#
#......FFO#
###########
