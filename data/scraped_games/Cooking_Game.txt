title Cooking Game
author Nathan Pinsker
color_palette 11
run_rules_on_level_Start

========
OBJECTS
========

Background 
GREEN  

Oven
red yellow
10101
01010
10101
01010
10101

OvenAlt
yellow red
10101
01010
10101
01010
10101

Ocean
Blue

PlayerSprite
Orange Brown
.111.
10001
10001
10001
.111.
PlayerInit
Orange Brown
.111.
10001
10001
10001
.111.
PlayerMarker
Black
PlayerMarkerVisible
LightBlue
.000.
00000
00000
00000
.000.
PlayerMarkerVisibleN
DarkGreen
..0..
..0..
0.0.0
.000.
.000.
PlayerMarkerVisibleS
DarkGreen
.000.
.000.
0.0.0
..0..
..0..
PlayerMarkerVisibleE
DarkGreen
..0..
00...
00000
00...
..0..
PlayerMarkerVisibleW
DarkGreen
..0..
...00
00000
...00
..0..

PlayerForkN
Black
..0..
..0..
0.0.0
0.0.0
00000
PlayerForkE
Black
000..
0....
00000
0....
000..
PlayerForkS
Black
00000
0.0.0
0.0.0
..0..
..0..
PlayerForkW
Black
..000
....0
00000
....0
..000
PlayerForkMarkerW
Black
PlayerForkMarkerE
Black
PlayerForkMarkerS
Black
PlayerForkMarkerN
Black
PlayerForkMarkerVisibleN
LightBlue
..0..
..0..
0.0.0
0.0.0
00000
PlayerForkMarkerVisibleE
LightBlue
000..
0....
00000
0....
000..
PlayerForkMarkerVisibleS
LightBlue
00000
0.0.0
0.0.0
..0..
..0..
PlayerForkMarkerVisibleW
LightBlue
..000
....0
00000
....0
..000

PlayerRotateLeftUp
Yellow
PlayerRotateRightUp
Yellow
PlayerRotateLeftDown
Yellow
PlayerRotateRightDown
Yellow
PlayerRotateUpLeft
Yellow
PlayerRotateUpRight
Yellow
PlayerRotateDownLeft
Yellow
PlayerRotateDownRight
Yellow
Impetus
Black
ImpetusTemp
Black

CrateToCook
Black
CrateBurned
Black
PermanentLoss
Blue
WinCheck
Blue

( Binders -- for connecting two parts of a sausage)
BinderRBothUncooked
Pink Red
....1
....1
....1
....1
....1
BinderLBothUncooked
Pink Red
1....
1....
1....
1....
1....
BinderUBothUncooked
Pink Red
11111
.....
.....
.....
.....
BinderDBothUncooked
Pink Red
.....
.....
.....
.....
11111
BinderRBothCooked
Pink Red
....0
....0
....0
....0
....0
BinderLBothCooked
Pink Red
0....
0....
0....
0....
0....
BinderUBothCooked
Pink Red
00000
.....
.....
.....
.....
BinderDBothCooked
Pink Red
.....
.....
.....
.....
00000
BinderRTopUncooked
Pink Red
....0
....1
....1
....1
....0
BinderLTopUncooked
Pink Red
0....
1....
1....
1....
0....
BinderUTopUncooked
Pink Red
01110
.....
.....
.....
.....
BinderDTopUncooked
Pink Red
.....
.....
.....
.....
01110
BinderRTopCooked
Pink Red
....1
....0
....0
....0
....1
BinderLTopCooked
Pink Red
1....
0....
0....
0....
1....
BinderUTopCooked
Pink Red
10001
.....
.....
.....
.....
BinderDTopCooked
Pink Red
.....
.....
.....
.....
10001

( Individual sausage tile sprites )
CrateHoriz1BothUncooked
Pink Red
.111.
11111
10001
11111
.111.
CrateHoriz1BothCooked
Pink Red
.000.
00000
01110
00000
.000.
CrateHoriz1TopUncooked
Pink Red
.000.
01110
01110
01110
.000.
CrateHoriz1TopCooked
Red Pink
.000.
01110
01110
01110
.000.
CrateHoriz1OceanInit
Pink Red Blue
21112
11111
10001
11111
21112
CrateHoriz1TopTempRoll
Red

CrateHoriz2BothUncooked
Pink Red
.111.
11111
10001
11111
.111.
CrateHoriz2BothCooked
Pink Red
.000.
00000
01110
00000
.000.
CrateHoriz2TopUncooked
Pink Red
.000.
01110
01110
01110
.000.
CrateHoriz2TopCooked
Red Pink
.000.
01110
01110
01110
.000.
CrateHoriz2OceanInit
Pink Red Blue
21112
11111
10001
11111
21112
CrateHoriz2TopTempRoll
Red

CrateVert1BothUncooked
Pink Red
.111.
11011
11011
11011
.111.
CrateVert1BothCooked
Pink Red
.000.
00100
00100
00100
.000.
CrateVert1TopUncooked
Pink Red
.000.
01110
01110
01110
.000.
CrateVert1TopCooked
Red Pink
.000.
01110
01110
01110
.000.
CrateVert1OceanInit
Pink Red Blue
21112
11011
11011
11011
21112
CrateVert1TopTempRoll
Red

CrateVert2BothUncooked
Pink Red
.111.
11011
11011
11011
.111.
CrateVert2BothCooked
Pink Red
.000.
00100
00100
00100
.000.
CrateVert2TopUncooked
Pink Red
.000.
01110
01110
01110
.000.
CrateVert2TopCooked
Red Pink
.000.
01110
01110
01110
.000.
CrateVert2OceanInit
Pink Red Blue
21112
11011
11011
11011
21112
CrateVert2TopTempRoll
Red

=======
LEGEND
=======

. = Background
~ = Ocean
# = Oven
£ = Oven and Ovenalt
p = PlayerInit
1 = PlayerForkN
2 = PlayerForkE
3 = PlayerForkS
4 = PlayerForkW

h = CrateHoriz1BothUncooked
j = CrateHoriz2BothUncooked
m = CrateVert1BothUncooked
n = CrateVert2BothUncooked

a = CrateHoriz1OceanInit
b = CrateHoriz2OceanInit
c = CrateVert1OceanInit
d = CrateVert2OceanInit

CrateHoriz1 = CrateHoriz1BothUncooked or CrateHoriz1BothCooked or CrateHoriz1TopUncooked or CrateHoriz1TopCooked or CrateHoriz1TopTempRoll
CrateHoriz2 = CrateHoriz2BothUncooked or CrateHoriz2BothCooked or CrateHoriz2TopUncooked or CrateHoriz2TopCooked or CrateHoriz2TopTempRoll
CrateVert1 = CrateVert1BothUncooked or CrateVert1BothCooked or CrateVert1TopUncooked or CrateVert1TopCooked or CrateVert1TopTempRoll
CrateVert2 = CrateVert2BothUncooked or CrateVert2BothCooked or CrateVert2TopUncooked or CrateVert2TopCooked or CrateVert2TopTempRoll

Binder = BinderRTopUncooked or BinderLTopUncooked or BinderUTopUncooked or BinderDTopUncooked or BinderRBothCooked or BinderLBothCooked or BinderUBothCooked or BinderDBothCooked or BinderRBothUncooked or BinderLBothUncooked or BinderUBothUncooked or BinderDBothUncooked or BinderRTopCooked or BinderLTopCooked or BinderUTopCooked or BinderDTopCooked


CrateInit = CrateHoriz1OceanInit or CrateHoriz2OceanInit or CrateVert1OceanInit or CrateVert2OceanInit
Crate = CrateHoriz1 or CrateHoriz2 or CrateVert1 or CrateVert2 or CrateBurned

CrateUncooked = CrateHoriz1BothUncooked or CrateHoriz1TopUncooked or CrateHoriz1TopCooked or CrateHoriz2BothUncooked or CrateHoriz2TopUncooked or CrateHoriz2TopCooked or CrateVert1BothUncooked or CrateVert1TopUncooked or CrateVert1TopCooked or CrateVert2BothUncooked or CrateVert2TopUncooked or CrateVert2TopCooked


PlayerRotate = PlayerRotateLeftUp or PlayerRotateRightUp or PlayerRotateLeftDown or PlayerRotateRightDown or PlayerRotateUpLeft or PlayerRotateUpRight or PlayerRotateDownLeft or PlayerRotateDownRight
PlayerFork = PlayerForkN or PlayerForkS or PlayerForkW or PlayerForkE
PlayerForkMarker = PlayerForkMarkerN or PlayerForkMarkerS or PlayerForkMarkerW or PlayerForkMarkerE
PlayerForkMarkerVisible = PlayerForkMarkerVisibleN or PlayerForkMarkerVisibleS or PlayerForkMarkerVisibleW or PlayerForkMarkerVisibleE

Player = PlayerInit or PlayerSprite

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

WinCheck
PermanentLoss
PlayerMarker, PlayerForkMarker
Background
Oven, Ocean
Ovenalt
PlayerMarkerVisibleN, PlayerMarkerVisibleS, PlayerMarkerVisibleE, PlayerMarkerVisibleW
PlayerForkMarkerVisible, PlayerMarkerVisible
Player, PlayerRotate, Crate, CrateInit
Binder
PlayerFork
Impetus, CrateToCook
ImpetusTemp

======
RULES
======

[ PlayerInit | PlayerForkN ] -> [ PlayerSprite PlayerMarker PlayerMarkerVisibleN | PlayerForkN PlayerForkMarkerN ]
[ PlayerInit | PlayerForkS ] -> [ PlayerSprite PlayerMarker PlayerMarkerVisibleS | PlayerForkS PlayerForkMarkerS ]
[ PlayerInit | PlayerForkE ] -> [ PlayerSprite PlayerMarker PlayerMarkerVisibleE | PlayerForkE PlayerForkMarkerE ]
[ PlayerInit | PlayerForkW ] -> [ PlayerSprite PlayerMarker PlayerMarkerVisibleW | PlayerForkW PlayerForkMarkerW ]

[ CrateHoriz1OceanInit ] -> [ CrateHoriz1BothUncooked Ocean]
[ CrateHoriz2OceanInit ] -> [ CrateHoriz2BothUncooked Ocean]
[ CrateVert1OceanInit ] -> [ CrateVert1BothUncooked Ocean]
[ CrateVert2OceanInit ] -> [ CrateVert2BothUncooked Ocean]

[ > PlayerSprite | no WinCheck ] -> [ > PlayerSprite | WinCheck ]
[ > PlayerSprite | PlayerFork ] -> [ > PlayerSprite | > PlayerFork ]
[ < PlayerSprite | PlayerFork ] -> [ < PlayerSprite | < PlayerFork ]
left [up PlayerSprite | PlayerFork ] -> [ PlayerRotateLeftUp | up Impetus ]
left [down PlayerSprite | PlayerFork ] -> [ PlayerRotateLeftDown | down Impetus ]
right [up PlayerSprite | PlayerFork ] -> [ PlayerRotateRightUp | up Impetus ]
right [down PlayerSprite | PlayerFork ] -> [ PlayerRotateRightDown | down Impetus ]
up [left PlayerSprite | PlayerFork ] -> [ PlayerRotateUpLeft | left Impetus ]
up [right PlayerSprite | PlayerFork ] -> [ PlayerRotateUpRight | right Impetus ]
down [left PlayerSprite | PlayerFork ] -> [ PlayerRotateDownLeft | left Impetus ]
down [right PlayerSprite | PlayerFork ] -> [ PlayerRotateDownRight | right Impetus ]
[ > PlayerSprite | Ocean ] -> cancel

[ > Impetus no ImpetusTemp | ] -> [ | > Impetus ImpetusTemp ]
[ ImpetusTemp ] -> [ ]

up [PlayerRotateLeftUp | ] -> [PlayerSprite | PlayerForkN ]
up [PlayerRotateRightUp | ] -> [PlayerSprite | PlayerForkN ]
down [PlayerRotateLeftDown | ] -> [PlayerSprite | PlayerForkS ]
down [PlayerRotateRightDown | ] -> [PlayerSprite | PlayerForkS ]
right [PlayerRotateUpRight | ] -> [PlayerSprite | PlayerForkE ]
right [PlayerRotateDownRight | ] -> [PlayerSprite | PlayerForkE ]
left [PlayerRotateUpLeft | ] -> [PlayerSprite | PlayerForkW ]
left [PlayerRotateDownLeft | ] -> [PlayerSprite | PlayerForkW ]

[ > Impetus Crate ] -> [ > Impetus > Crate ]

startloop

[ moving CrateHoriz1 | CrateHoriz1 ] -> [ moving CrateHoriz1 | moving CrateHoriz1 ]
[ moving CrateHoriz2 | CrateHoriz2 ] -> [ moving CrateHoriz2 | moving CrateHoriz2 ]
[ moving CrateVert1 | CrateVert1 ] -> [ moving CrateVert1 | moving CrateVert1 ]
[ moving CrateVert2 | CrateVert2 ] -> [ moving CrateVert2 | moving CrateVert2 ]

[ > Crate | Crate ] -> [ > Crate | > Crate ]

endloop

left [Perpendicular Impetus | PlayerFork ] -> [ Perpendicular Impetus | left Impetus PlayerFork ]
right [Perpendicular Impetus | PlayerFork ] -> [ Perpendicular Impetus | right Impetus PlayerFork ]
up [Perpendicular Impetus | PlayerFork ] -> [ Perpendicular Impetus | up Impetus PlayerFork ]
down [Perpendicular Impetus | PlayerFork ] -> [ Perpendicular Impetus | down Impetus PlayerFork ]

[ > Impetus STATIONARY Crate ] -> [ > Impetus > Crate ]
[ Impetus ] -> [ ]
[ > PlayerFork | STATIONARY Crate ] -> [ > PlayerFork | > Crate ]
[ > Player | STATIONARY Crate ] -> [ > PlayerSprite | > Crate ]

startloop

[ moving CrateHoriz1 | CrateHoriz1 ] -> [ moving CrateHoriz1 | moving CrateHoriz1 ]
[ moving CrateHoriz2 | CrateHoriz2 ] -> [ moving CrateHoriz2 | moving CrateHoriz2 ]
[ moving CrateVert1 | CrateVert1 ] -> [ moving CrateVert1 | moving CrateVert1 ]
[ moving CrateVert2 | CrateVert2 ] -> [ moving CrateVert2 | moving CrateVert2 ]

[ > Crate | Crate ] -> [ > Crate | > Crate ]

endloop

[ UP CrateHoriz1TopCooked ] -> [ UP CrateHoriz1TopTempRoll ]
[ UP CrateHoriz1TopUncooked ] -> [ UP CrateHoriz1TopCooked ]
[ UP CrateHoriz1TopTempRoll ] -> [ UP CrateHoriz1TopUncooked ]
[ DOWN CrateHoriz1TopCooked ] -> [ DOWN CrateHoriz1TopTempRoll ]
[ DOWN CrateHoriz1TopUncooked ] -> [ DOWN CrateHoriz1TopCooked ]
[ DOWN CrateHoriz1TopTempRoll ] -> [ DOWN CrateHoriz1TopUncooked ]

[ UP CrateHoriz2TopCooked ] -> [ UP CrateHoriz2TopTempRoll ]
[ UP CrateHoriz2TopUncooked ] -> [ UP CrateHoriz2TopCooked ]
[ UP CrateHoriz2TopTempRoll ] -> [ UP CrateHoriz2TopUncooked ]
[ DOWN CrateHoriz2TopCooked ] -> [ DOWN CrateHoriz2TopTempRoll ]
[ DOWN CrateHoriz2TopUncooked ] -> [ DOWN CrateHoriz2TopCooked ]
[ DOWN CrateHoriz2TopTempRoll ] -> [ DOWN CrateHoriz2TopUncooked ]

[ LEFT CrateVert1TopCooked ] -> [ LEFT CrateVert1TopTempRoll ]
[ LEFT CrateVert1TopUncooked ] -> [ LEFT CrateVert1TopCooked ]
[ LEFT CrateVert1TopTempRoll ] -> [ LEFT CrateVert1TopUncooked ]
[ RIGHT CrateVert1TopCooked ] -> [ RIGHT CrateVert1TopTempRoll ]
[ RIGHT CrateVert1TopUncooked ] -> [ RIGHT CrateVert1TopCooked ]
[ RIGHT CrateVert1TopTempRoll ] -> [ RIGHT CrateVert1TopUncooked ]

[ LEFT CrateVert2TopCooked ] -> [ LEFT CrateVert2TopTempRoll ]
[ LEFT CrateVert2TopUncooked ] -> [ LEFT CrateVert2TopCooked ]
[ LEFT CrateVert2TopTempRoll ] -> [ LEFT CrateVert2TopUncooked ]
[ RIGHT CrateVert2TopCooked ] -> [ RIGHT CrateVert2TopTempRoll ]
[ RIGHT CrateVert2TopUncooked ] -> [ RIGHT CrateVert2TopCooked ]
[ RIGHT CrateVert2TopTempRoll ] -> [ RIGHT CrateVert2TopUncooked ]

[ > PlayerSprite | > PlayerFork Oven ] -> [ PlayerSprite | PlayerFork Oven ]
[ > PlayerFork | > PlayerSprite | Oven ] -> [ PlayerFork | PlayerSprite | Oven ]
[ > Crate | ] -> [ > Crate | CrateToCook ]

late [CrateHoriz1 Ocean | CrateHoriz1 Ocean] -> [ Ocean PermanentLoss | Ocean ]
late [CrateHoriz2 Ocean | CrateHoriz2 Ocean] -> [ Ocean PermanentLoss | Ocean ]
late [CrateVert1 Ocean | CrateVert1 Ocean] -> [ Ocean PermanentLoss | Ocean ]
late [CrateVert2 Ocean | CrateVert2 Ocean] -> [ Ocean PermanentLoss | Ocean ]
late [CrateHoriz1TopUncooked CrateToCook Oven] -> [CrateBurned PermanentLoss Oven] message 'Burned'
late [CrateHoriz1BothCooked CrateToCook Oven] -> [CrateBurned PermanentLoss Oven] message 'Burned'
late [CrateHoriz1BothUncooked CrateToCook Oven] -> [CrateHoriz1TopUncooked Oven]
late [CrateHoriz1TopCooked CrateToCook Oven] -> [CrateHoriz1BothCooked Oven]
late [CrateHoriz2TopUncooked CrateToCook Oven] -> [CrateBurned PermanentLoss Oven] message 'Burned'
late [CrateHoriz2BothCooked CrateToCook Oven] -> [CrateBurned PermanentLoss Oven] message 'Burned'
late [CrateHoriz2BothUncooked CrateToCook Oven] -> [CrateHoriz2TopUncooked Oven]
late [CrateHoriz2TopCooked CrateToCook Oven] -> [CrateHoriz2BothCooked Oven]
late [CrateVert1TopUncooked CrateToCook Oven] -> [CrateBurned PermanentLoss Oven] message 'Burned'
late [CrateVert1BothCooked CrateToCook Oven] -> [CrateBurned PermanentLoss Oven] message 'Burned'
late [CrateVert1BothUncooked CrateToCook Oven] -> [CrateVert1TopUncooked Oven]
late [CrateVert1TopCooked CrateToCook Oven] -> [CrateVert1BothCooked Oven]
late [CrateVert2TopUncooked CrateToCook Oven] -> [CrateBurned PermanentLoss Oven] message 'Burned'
late [CrateVert2BothCooked CrateToCook Oven] -> [CrateBurned PermanentLoss Oven] message 'Burned'
late [CrateVert2BothUncooked CrateToCook Oven] -> [CrateVert2TopUncooked Oven]
late [CrateVert2TopCooked CrateToCook Oven] -> [CrateVert2BothCooked Oven]
late [CrateToCook] -> [ ]

late [WinCheck] [CrateUncooked] -> [] [CrateUncooked]
late [WinCheck] [CrateBurned] -> [] [CrateBurned]
late [WinCheck] [PermanentLoss] -> [] [PermanentLoss]

late [WinCheck] [PlayerMarker] -> [WinCheck] [PlayerMarkerVisible]
late [WinCheck] [PlayerForkMarkerN] -> [WinCheck] [PlayerForkMarkerVisibleN]
late [WinCheck] [PlayerForkMarkerS] -> [WinCheck] [PlayerForkMarkerVisibleS]
late [WinCheck] [PlayerForkMarkerE] -> [WinCheck] [PlayerForkMarkerVisibleE]
late [WinCheck] [PlayerForkMarkerW] -> [WinCheck] [PlayerForkMarkerVisibleW]

late [PlayerMarkerVisible] [PermanentLoss] -> [] [PermanentLoss]
late [PlayerForkMarkerVisible] [PermanentLoss] -> [] [PermanentLoss]

( reshade between sausages )

late [Binder] -> []

late right [CrateHoriz1BothUncooked | CrateHoriz1 ] -> [ CrateHoriz1BothUncooked BinderRBothUncooked | CrateHoriz1 ]
late right [CrateHoriz1BothCooked | CrateHoriz1 ] -> [ CrateHoriz1BothCooked BinderRBothCooked | CrateHoriz1 ]
late right [CrateHoriz1TopUncooked | CrateHoriz1 ] -> [ CrateHoriz1TopUncooked BinderRTopUncooked | CrateHoriz1 ]
late right [CrateHoriz1TopCooked | CrateHoriz1 ] -> [ CrateHoriz1TopCooked BinderRTopCooked | CrateHoriz1 ]

late right [CrateHoriz2BothUncooked | CrateHoriz2 ] -> [ CrateHoriz2BothUncooked BinderRBothUncooked | CrateHoriz2 ]
late right [CrateHoriz2BothCooked | CrateHoriz2 ] -> [ CrateHoriz2BothCooked BinderRBothCooked | CrateHoriz2 ]
late right [CrateHoriz2TopUncooked | CrateHoriz2 ] -> [ CrateHoriz2TopUncooked BinderRTopUncooked | CrateHoriz2 ]
late right [CrateHoriz2TopCooked | CrateHoriz2 ] -> [ CrateHoriz2TopCooked BinderRTopCooked | CrateHoriz2 ]

late down [CrateVert1BothUncooked | CrateVert1 ] -> [ CrateVert1BothUncooked BinderDBothUncooked | CrateVert1 ]
late down [CrateVert1BothCooked | CrateVert1 ] -> [ CrateVert1BothCooked BinderDBothCooked | CrateVert1 ]
late down [CrateVert1TopUncooked | CrateVert1 ] -> [ CrateVert1TopUncooked BinderDTopUncooked | CrateVert1 ]
late down [CrateVert1TopCooked | CrateVert1 ] -> [ CrateVert1TopCooked BinderDTopCooked | CrateVert1 ]

late down [CrateVert2BothUncooked | CrateVert2 ] -> [ CrateVert2BothUncooked BinderDBothUncooked | CrateVert2 ]
late down [CrateVert2BothCooked | CrateVert2 ] -> [ CrateVert2BothCooked BinderDBothCooked | CrateVert2 ]
late down [CrateVert2TopUncooked | CrateVert2 ] -> [ CrateVert2TopUncooked BinderDTopUncooked | CrateVert2 ]
late down [CrateVert2TopCooked | CrateVert2 ] -> [ CrateVert2TopCooked BinderDTopCooked | CrateVert2 ]

late left [CrateHoriz1BothUncooked | CrateHoriz1 ] -> [ CrateHoriz1BothUncooked BinderLBothUncooked | CrateHoriz1 ]
late left [CrateHoriz1BothCooked | CrateHoriz1 ] -> [ CrateHoriz1BothCooked BinderLBothCooked | CrateHoriz1 ]
late left [CrateHoriz1TopUncooked | CrateHoriz1 ] -> [ CrateHoriz1TopUncooked BinderLTopUncooked | CrateHoriz1 ]
late left [CrateHoriz1TopCooked | CrateHoriz1 ] -> [ CrateHoriz1TopCooked BinderLTopCooked | CrateHoriz1 ]

late left [CrateHoriz2BothUncooked | CrateHoriz2 ] -> [ CrateHoriz2BothUncooked BinderLBothUncooked | CrateHoriz2 ]
late left [CrateHoriz2BothCooked | CrateHoriz2 ] -> [ CrateHoriz2BothCooked BinderLBothCooked | CrateHoriz2 ]
late left [CrateHoriz2TopUncooked | CrateHoriz2 ] -> [ CrateHoriz2TopUncooked BinderLTopUncooked | CrateHoriz2 ]
late left [CrateHoriz2TopCooked | CrateHoriz2 ] -> [ CrateHoriz2TopCooked BinderLTopCooked | CrateHoriz2 ]

late up [CrateVert1BothUncooked | CrateVert1 ] -> [ CrateVert1BothUncooked BinderUBothUncooked | CrateVert1 ]
late up [CrateVert1BothCooked | CrateVert1 ] -> [ CrateVert1BothCooked BinderUBothCooked | CrateVert1 ]
late up [CrateVert1TopUncooked | CrateVert1 ] -> [ CrateVert1TopUncooked BinderUTopUncooked | CrateVert1 ]
late up [CrateVert1TopCooked | CrateVert1 ] -> [ CrateVert1TopCooked BinderUTopCooked | CrateVert1 ]

late up [CrateVert2BothUncooked | CrateVert2 ] -> [ CrateVert2BothUncooked BinderUBothUncooked | CrateVert2 ]
late up [CrateVert2BothCooked | CrateVert2 ] -> [ CrateVert2BothCooked BinderUBothCooked | CrateVert2 ]
late up [CrateVert2TopUncooked | CrateVert2 ] -> [ CrateVert2TopUncooked BinderUTopUncooked | CrateVert2 ]
late up [CrateVert2TopCooked | CrateVert2 ] -> [ CrateVert2TopCooked BinderUTopCooked | CrateVert2 ]

==============
WINCONDITIONS
==============

All PlayerFork on PlayerForkMarkerVisible
All Player on PlayerMarkerVisible

=======     
LEVELS
=======

message 'Demake of Stephen's Sausage Roll for the PC.'

~~~~~~~~
~~~1.~~~
~~.p..~~
~~m#£n~~
~~m£#n~~
~~....~~
~~~~~~~~
~~~~~~~~

~~~~~~~~
~~.hh..~
~..~~~.~
~.~~#~.~
~.~#~~.~
~p~~~..~
~3....~~
~~~~~~~~

~~~~~~~~
~~..#.~~
~~~.~.~~
~~m#4p~~
~~m..~~~
~~~~~~~~

~~~~~~~~~~~
~~~~~~~~~~~
~~~~...~~~~
~~~~hhm~~~~
~~#£4pm#~~~
~~£#~~~£#~~
~~~~~~~~~~~
~~~~~~~~~~~

~~~~~~~~~~
~~~~~~~~~~
~~p2hh..~~
~~.m#£~.~~
~~~c£#~.~~
~~~~~~~~~~
~~~~~~~~~~

~~~~~~~~~~~
~~~~~~~~~~~
~~~m..#~~~~
~~~mhh£#~~~
~~~4p.~~~~~
~~~.#£~~~~~
~~~~£#~~~~~
~~~~~~~~~~~

~~~~~~~~~~~
~~~~...~~~~
~~~....~~~~
~~..~m..~~~
~~.hac#.~~~
~~.1nbj.~~~
~~~pn..~~~~
~~~..#~~~~~
~~~~~~~~~~~
~~~~~~~~~~~