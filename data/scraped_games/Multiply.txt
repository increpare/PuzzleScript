title Multiply
author Austyn Burgess

========
OBJECTS
========

Background
LightBlue

Wall
DarkGrey

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

PlayerOff1
Black Orange White Blue
.000.
.000.
00000
.000.
.0.0.

PlayerOff2
Black Orange White Blue
.000.
.000.
00000
.000.
.0.0.

Duplicator
Pink


Button1
Red
.....
.000.
.000.
.000.
.....

DoorFrameV1
Red DarkGrey
10001
.....
.....
.....
10001

DoorFrameH1
Red DarkGrey
1...1
0...0
0...0
0...0
1...1

DoorClosed1
Red DarkGrey
.....
.000.
.000.
.000.
.....

DoorOpen1
Red DarkGrey
.....
.....
.....
.....
.....

Button2
Blue
.....
.000.
.000.
.000.
.....

DoorFrameV2
Blue DarkGrey
10001
.....
.....
.....
10001

DoorFrameH2
Blue DarkGrey
1...1
0...0
0...0
0...0
1...1

DoorClosed2
Blue DarkGrey
.....
.000.
.000.
.000.
.....

DoorOpen2
Blue DarkGrey
.....
.....
.....
.....
.....

Button3
Green
.....
.000.
.000.
.000.
.....

DoorFrameV3
Green DarkGrey
10001
.....
.....
.....
10001

DoorFrameH3
Green DarkGrey
1...1
0...0
0...0
0...0
1...1

DoorClosed3
Green DarkGrey
.....
.000.
.000.
.000.
.....

DoorOpen3
Green DarkGrey
.....
.....
.....
.....
.....

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000


Goal
Brown

TutDuplicator
Pink

DoorTest1
Black

DoorTest2
Black

DoorTest3
Black

=======
LEGEND
=======

Off = PlayerOff1 or PlayerOff2
Pusher = Crate or Player or Off
Goaler = Player or Off
Button = Button1 or Button2 or Button3
DoorFrame = DoorFrameV1 or DoorFrameH1 or DoorFrameV2 or DoorFrameH2 or DoorFrameV3 or DoorFrameH3
DoorClosed = DoorClosed1 or DoorClosed2 or DoorClosed3
DoorOpen = DoorOpen1 or DoorOpen2 or DoorOpen3
DoorTest = DoorTest1 or DoorTest2 or DoorTest3

. = Background
# = Wall
P = Player
* = Duplicator
, = TutDuplicator

1 = Button1
Q = DoorFrameV1 and DoorClosed1
A = DoorFrameH1 and DoorClosed1
2 = Button2
W = DoorFrameV2 and DoorClosed2
S = DoorFrameH2 and DoorClosed2
3 = Button3
E = DoorFrameV3 and DoorClosed3
D = DoorFrameH3 and DoorClosed3


C = Crate
G = Goal


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

DoorTest
Background
DoorFrame
Goal, DoorOpen, Button, DoorClosed
Player, Off, Wall, Crate, Duplicator, TutDuplicator

======
RULES
======


(Movement)
[> Player | Crate] -> [> Player | > Crate]
[> Pusher | DoorClosed] -> [Pusher | DoorClosed]



(Activate duplicator)
[> Player | TutDuplicator] -> [Player | PlayerOff1] message Press X to change body
[> Player | Duplicator][PlayerOff1] -> [Player | PlayerOff2][PlayerOff1]
[> Player | Duplicator] -> [Player | PlayerOff1]



(Player change)
[action Player][PlayerOff1][PlayerOff2] -> [PlayerOff2][Player][PlayerOff1]
[action Player][PlayerOff1] -> [PlayerOff1][Player]
[action Player][PlayerOff2] -> [PlayerOff2][Player]



(Buttons)
Late [DoorOpen1] -> [DoorOpen1 DoorTest1]
Late [Button1 Pusher][DoorTest1] -> [Button1 Pusher][]
Late [DoorOpen1 DoorTest1] -> [DoorClosed1]
Late [Button1 Pusher][DoorClosed1] -> [Button1 Pusher][DoorOpen1]

Late [DoorOpen2] -> [DoorOpen2 DoorTest2]
Late [Button2 Pusher][DoorTest2] -> [Button2 Pusher][]
Late [DoorOpen2 DoorTest2] -> [DoorClosed2]
Late [Button2 Pusher][DoorClosed2] -> [Button2 Pusher][DoorOpen2]

Late [DoorOpen3] -> [DoorOpen3 DoorTest3]
Late [Button3 Pusher][DoorTest3] -> [Button3 Pusher][]
Late [DoorOpen3 DoorTest3] -> [DoorClosed3]
Late [Button3 Pusher][DoorClosed3] -> [Button3 Pusher][DoorOpen3]


(Door Crushing)
Late [Pusher DoorClosed] -> [DoorClosed]


==============
WINCONDITIONS
==============

All Goal on Goaler

=======
LEVELS
=======

#############(Basic Crate)
#p....#.....#
#.....#.....#
#.....q...g.#
#.c.1.#.....#
#.....#.....#
#############

#############(Duplicator Tutorial)
#p....#.....#
#.....#.....#
#.....w...g.#
#.,.2.#.....#
#.....#.....#
#############

####################(Access)
#.....##..p..#.....#
#.....##.....#.....#
#..g..wq..c..q..*..#
#.....##.2.1.#.....#
#.....##.....#.....#
####################

#############(Double)
#.....#....p#
#.....#.....#
#..*..q...c.#
#.....#..1..#
#.....#.....#
#.....#.....#
###a#####s###
#.....###d###
#.....#.....#
#.....#.....#
#.....#..g..#
#.2.3.#.....#
#.....#.....#
#############

####################(Follow)
#p....#.....##.....#
#.....#..*..##.....#
#...........##..g..#
#.....#.123.##.....#
#.....#.....##.....#
###a############s###
#.....#.....####d###
#.....#.....##.....#
#.....e..c..qw.....#
#.....#.....##.....#
#.....#.....##.....#
####################

#############(???)
#p..#g#.....#
#...#d#.....#
#..1..cc....#
#.c.c.#.....#
#.....#.....#
###a#########
#.....#.....#
#.....#.3...#
#..1..#.....#
#.....#.....#
#.....#.....#
#############





















