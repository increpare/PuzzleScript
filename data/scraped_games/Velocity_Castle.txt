title Velocity Castle
author Tobin Mollett
homepage www.puzzlescript.net

require_player_movement
again_interval 0.06
key_repeat_interval 0.5
flickscreen 16x16
noaction
youtube tOKqIfwuXIc

========
OBJECTS
========

Background
Black

Floor
#31221a #32263d
00011
00011
00011
11100
11100

Exit
#6e1927 #782737 #5e1416
.222.
20002
00012
00112
01112
 

Gate
#e6d5c4 #c0b1a3
0.0.0
00001
0.1.1
00101
1.1.1

Entrance
#e6d5c4 #c0b1a3
0.0.0
00001
0.1.1
00101
1.1.1

Wall
#9fb092 #909f84 #86947b 
01000
22111
00010
11221
01000

Block
#9fb092 #909f84 #86947b
20002
01112
01112
01112
22220

SwitchUp
#9fb092 #909f84 #86947b #06247e #0d0f0a #f7372a
01000
23531
03430
13331
01000

SwitchDown
#9fb092 #909f84 #86947b #06247e #0d0f0a #6e130d
01000
23331
03430
13531
01000

PlayerRest
#fcf695 #fea1e7 #ff3637 #2d29d3 #faf9f3 
.000.
.010.
.222.
13331
.4.4.

PlayerUp
#fcf695 #ff3637 #fea1e7 #2d29d3 #faf9f3 
.000.
.000.
21112
.333.
.4.4.

PlayerDown
#fcf695 #ff3637 #fea1e7 #2d29d3 #faf9f3 
.000.
.020.
21112
.333.
.4.4.

PlayerLeft
#fcf695 #ff3637 #fea1e7 #2d29d3 #faf9f3 
..00.
..20.
2111.
..33.
...44

PlayerRight
#fcf695 #ff3637 #fea1e7 #2d29d3 #faf9f3 
.00..
.02..
.1112
.33..
44...

Statue
#525055 #413f48 #8e3438
0.0.0
02021
.001.
.001.
00.11

StatueEyes
#ff0000
.....
.0.0.
.....
.....
.....

BoulderRest
#485e4a #3e4d44 #654e40
.220.
20000
00001
00011
.111.

BoulderUp
#485e4a #3e4d44 #654e40
.220.
20000
00001
00011
.111.

BoulderDown
#485e4a #3e4d44 #654e40
.220.
20000
00001
00011
.111.

BoulderRight
#485e4a #3e4d44 #654e40
.220.
20000
00001
00011
.111.

BoulderLeft
#485e4a #3e4d44 #654e40
.220.
20000
00001
00011
.111.

BoulderLights
#54e859 
.....
.0.0.
.....
.0.0.
.....

FloorPanel
#298fe2 #0761b3 #d769ca
.....
..0..
.021.
..1..
.....

ForceField
#747df2 #d769ca
.0.1.
1.1.0
.....
.1.0.
0.1.1

OpenForceField
#8848c2
.....
.....
.0.0.
.....
.....

SpikeTrap
#e9fca6 #e6d5c4 #c0b1a3
.....
.....
.....
..0..
0.1.0

Spikes
#e9fca6 #e6d5c4 #c0b1a3
..0..
0.1.0
1.1.1
1.2.2
2...2

TriggerSpikes
#ede0d9 #e6d5c4 #c0b1a3
.....
.....
.....
..0..
0.1.0

Treasure
#884700 #fde557 #7a4009 #5e2c03
.....
.000.
01110
00223
33333

ShadowGate
White
.....
.....
.....
.....
.....

ShadowField
White
.....
.....
.....
.....
.....

=======
LEGEND
=======

- = Background
. = Floor
+ = Block
# = Wall
* = Entrance
P = PlayerRest and Floor
X = Gate and Exit
U = SwitchUp
D = SwitchDown
S = Statue and Floor
Z = StatueEyes and Statue and FloorPanel and Floor
O = BoulderRest and Floor
Q = BoulderRest and BoulderLights and FloorPanel and Floor
F = FloorPanel and Floor
% = ForceField and Floor
: = OpenForceField and Floor
K = SpikeTrap and Floor
C = Treasure and Floor
T = Treasure

Player = PlayerRest or PlayerUp or PlayerDown or PlayerLeft or PlayerRight
Boulder = BoulderRest or BoulderUp or BoulderLeft or BoulderRight or BoulderDown
Immovable = Wall or Block or SwitchUp or Switchdown or Gate or Forcefield  or Entrance or Spikes or Treasure
Movable = Statue or BoulderRest or BoulderUp or BoulderDown or BoulderLeft or BoulderRight
Weight = PlayerUp or PlayerDown or PlayerLeft or PlayerRight or Player or Statue or Boulder or BoulderUp or BoulderDown or BoulderRight or BoulderLeft

=======
SOUNDS
=======

( Gate opening )
Sfx0 94191702

( Switch flipping )
Sfx1 99768904

( Statue moving )
Sfx2 80097301

( Forcefield opening )
Sfx3 17645103 

( ForceField closing )
Sfx4 55900709

( Boulder hitting a wall )
Sfx5 14620302

( SpikeTrap Springing )
Sfx6 9748102

( Collecting Treasure )
Sfx7 99718300

( Hitting a ForceField )
Sfx8 32267702

( Player hitting a wall )
Sfx10 75256507

CloseMessage 67736505
EndLevel 29086103

================
COLLISIONLAYERS
================

Background
ShadowGate, ShadowField
Floor
Exit, FloorPanel, OpenForceField, SpikeTrap, TriggerSpikes
PlayerRest, PlayerUp, PlayerDown, PlayerLeft, PlayerRight, Wall, Block, SwitchUp, SwitchDown, Gate, Statue, Forcefield, BoulderRest, BoulderUp, BoulderDown, BoulderLeft, BoulderRight, Entrance, Spikes, Treasure
StatueEyes, BoulderLights

======
RULES
======

( Player movement )

[ UP PlayerRest ] -> [ UP PlayerUp ]
[ RIGHT PlayerRest ] -> [ RIGHT PlayerRight ]
[ DOWN PlayerRest ] -> [ DOWN PlayerDown ]
[ LEFT PlayerRest ] -> [ LEFT PlayerLeft ]

[ PlayerUp ] -> [ UP PlayerUp ] again
[ PlayerRight ] -> [ RIGHT PlayerRight ] again
[ PlayerDown ] -> [ DOWN PlayerDown ] again
[ PlayerLeft ] -> [ LEFT PlayerLeft ] again

[ > Player | SwitchUp ] -> [ PlayerRest | SwitchDown Sfx1 ]
[ > Player | Movable ] -> [ PlayerRest | > Movable Sfx2 ]
[ > Player | Boulder ] -> [ PlayerRest | > Boulder Sfx2 ]
[ > Player | ForceField ] -> [ PlayerRest | ForceField Sfx8 ]
[ > Player | Treasure ] -> [ PlayerRest | no Treasure Sfx7 ]
[ > Player | Immovable ] -> [ PlayerRest | Immovable Sfx10 ]


( Spiketraps )

[ Weight SpikeTrap no TriggerSpikes ] -> [ Weight TriggerSpikes Sfx1 ]
[ TriggerSpikes no Weight ] -> [ Spikes Sfx6 ]


( Boulders roll )

[ BoulderUp ] -> [ UP BoulderUp ] again
[ BoulderRight ] -> [ RIGHT BoulderRight ] again
[ BoulderLeft ] -> [ LEFT BoulderLeft ] again
[ BoulderDown ] -> [ DOWN BoulderDown ] again

[ > Boulder | SwitchUp ] -> [ BoulderRest | SwitchDown Sfx1 ]
[ > Boulder | ForceField ] -> [ BoulderRest | ForceField Sfx8 ]
[ > Boulder | Treasure ] -> [ BoulderRest | no Treasure Sfx5 ]
[ > Boulder | Immovable ] -> [ BoulderRest | Immovable Sfx5 ]
[ > Boulder | Movable ] -> [ BoulderRest | > Movable Sfx2 ]

[ UP Boulder ] -> [ UP BoulderUp ]
[ RIGHT Boulder ] -> [ RIGHT BoulderRight ]
[ DOWN Boulder ] -> [ DOWN BoulderDown ]
[ LEFT Boulder ] -> [ LEFT BoulderLeft ]


( Switches and Gates )

late [ SwitchDown ] [ Gate ] -> [ SwitchDown ] [ Gate ShadowGate ]
late [ SwitchUp ] [ ShadowGate ] -> [ SwitchUp ] [ no ShadowGate ]
late [ Gate ShadowGate ] -> [ no Gate no ShadowGate Sfx0 ]


( Floor Panels and Forcefields )

late [ Weight FloorPanel ] [ Forcefield ] -> [ Weight FloorPanel ] [ ForceField ShadowField ]
late [ FloorPanel no Weight ] [ ShadowField ] -> [ FloorPanel ] [ no ShadowField ]
late [ ForceField ShadowField ] -> [ OpenForceField no ShadowField Sfx3 ]

late [ FloorPanel no Weight ] [ OpenForceField ] -> [ FloorPanel] [ ForceField Sfx4 ]


( Statues light up on Panels )

[ StatueEyes ] -> [ no StatueEyes ]
late [ Statue FloorPanel ] -> [ Statue StatueEyes FloorPanel ]


( So do Boulders )

[ BoulderLights ] -> [ no BoulderLights ]
late [ Boulder FloorPanel ] -> [ Boulder BoulderLights FloorPanel ]


==============
WINCONDITIONS
==============

Some Player on Exit

=======
LEVELS
=======

( Template

################
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
#..............#
################

)

message You found a pair of magical shoes that never stop moving! Go to the old castle and look for treasure!

----------------
----------------
----------------
---#u######+----
---#.#.....#----
---#....+..#----
---x.......#----
---#......##----
---#.......#----
---##......#----
---#...p..+#----
---+##***###----
----------------
----------------
----------------
----------------

----------------
----------------
----------------
---+#####u#x##--
---#...##.#.+#--
---#.uk#.....#--
---#.........#--
---#...++....#--
---##.......k#--
---#...#######--
---#...k....p*--
---+##########--
----------------
----------------
----------------
----------------

-----+#X#+------
-----#...#------
-----#.C.#------
-----#...#------
-+###+%%%+####+-
-#............#-
-#.+..f.f...+.#-
-#......s.....#-
-#.+..s.......#-
-#.......+..+.#-
-+####........#-
-----#..######+-
-----##..####---
-----#.....u#---
-----#.p.###+---
-----+#*##------

message Remember, you can press "R" to restart a level or press "Z" to undo the last move.

----------------
----------------
----------------
----#x########+-
--+##.#.......#-
--#......+....#-
--#.#.#.....+.#-
--#...#...+...#-
+####.#.+.....u-
#.....k....s..#-
#k###s#.......#-
#.....####p###+-
+######--#*#----
----------------
----------------
----------------

--------------------------------
--------------------------------
--------------------------------
--------------------------------
--------------------------------
--------------------------------
---------------------#u#--------
--------------------##.#######+-
--------------------#.........#-
--------------------#......C..#-
--------------------#..C......#-
--------------------#C........#-
--------------------#...C.....#-
--------------------#.......C.#-
--------------------+######.##+-
--------------------------#.#---
--------------------------#.#---
--------------------------#.#---
--------------------------#.#---
--------------------------#.#---
--------------------------#.#---
--------------------------#.#---
--------------------------#.#---
-------####x###+--+#######+:+##+
-------##......#--#............#
-------##.+....#--#..........k.#
-------#....+..+##+k...........#
-------#.......:..:...k..z.....#
-------#k......+##+.........k..#
-------#.......#--#............#
-------u...p...u--#..k.........#
-------####*####--#############+

------########------------------
------#......#------------------
----##+%####%+########X###------
--###....................##-----
--#.....+.f...............##----
--#.+......................##---
--#...fs..s.................##--
-####................k......%u--
-#.%........k...............##--
-#.#.....+.................##---
-#.#.+....................##----
-#.#..................p..##-----
-#.####%+#############*###------
-#......#-----------------------
-+#######-----------------------
--------------------------------

############----
#.....######----
#.###...##U#----
#.###....#.#----
#.#....f.#.#----
#.#...+..#.#----
#....s...#.#----
##.........#----
###.....##.#----
#####.####.#----
---#..####.#----
---#...###.##x##
---#.......##.##
---#...######.##
---#.p..%%%...##
---##*##########

-------#X#-------------#u#------
-#u#---#.#-------------#.#------
-#.#---#.#-------------#o#------
-#.#---#.###############.#####--
-#.#####.#C..................u--
##.......#...C########.#o###u#--
#...######.C.C.........#.#-#.#--
#...#++++#############...###.#--
#.o.####################.....#--
#....................o...o...u--
#...################.###.....#--
##.##--------------#.#-#.###.#--
-#.#---------------#.###.....#--
-#.#---------------#.....#####--
-#p#---------------###########--
-#*#----------------------------

#X#############+
#.###u##...#.u.#
#.#u#.##.+k#...#
#..........#...#
#......+.#.##k##
###.+o.+.#.#...#
#.k......#.#...#
#####.##o#.#..##
#u..o..o...k.o.#
###.+.+..#.##..#
###......#.#...#
#u#.+.++.#.#..##
#.....k..#.##..#
#.+.####k#.#...#
#...#..#.#u#.p.#
+############*#+

----#X#---------
#u###%#########-
#.............#-
#.+.+.+.+o+o+.#-
#f.o.........ou-
#.+.+.+.+.+.+.#-
#.............#-
#.+.+.+.+.+.+o#-
#...o.fo..o...#-
#o+.+o+.+.+.+.#-
u.........o...#-
#.+o+.+.+.+.+.#-
#.o..o......of#-
#.+.+.+.+o+.+.#-
#p............#-
#*#############-

################--##############---------#x#----
#..............####.........C..#---------#.#----
#.....+........##.k........C...#---------#.#----
#...+........+.##.#......C...C.#---------#.#----
#......+..+....##.#.........C..#########u#.###--
#..+...........##.k..C..o............%%%...k.+--
#...........+..#######################u#.#####--
#......s.................................k.+----
#..+...........#######k#############u#.#####----
#...f......+...##....k.................k.+------
#.....+.....+..##..........#########.#####------
#........+.....##k...#.....k.+-----#.#----------
#...+..........##.....o.o..k.+-----#.#----------
#..........+...##..........###-----#.#----------
#..............##......k...k.+-----#p#----------
##############################-----#*#----------

----------------
--#x###########+
--#...#........#
--#.+.#...:....#
--#.#.k.....:..#
###.+.#........#
#.....#..:.....#
#.+.+.#....:...#
#......z.......#
###.+.#......:.#
--#...#...:....#
###%#%#.:...:..#
u.....#...p....#
##########*####+
----------------
----------------

---##########################---
---##########################---
--##........................##--
####........................####
##......++....................##
##......++....................##
##....................++......##
##....................++......##
##..........::................##
##..........::................##
##................++..........##
##................++..........##
XX............................##
##............................##
##............u##u............##
##............####............##
##............####............##
##............u##u............##
##........++..............++..##
##.........+..............++..##
##....++......................##
##....++......................##
##......................++....##
##......................++....##
##......++....................##
##......++....................##
##........q...................##
##............................##
####........................####
--##................p.......##--
---#######.#########*########---
---#################*########---

----------------
----------------
----------------
----------------
----------------
----------------
-----##x##------
-----u...u------
-----#...#------
-----#k.k#------
-----##k##------
------#.#-------
------#.#-------
----------------
----------------
----------------
----------------
--T-------------
------------T---
-----T----------
-----------T----
---T------------
----------------
--------------T-
---------T------
----T-----------
----------------
------------T---
-T--------------
----------T-----
---T------------
----------------
+######.#######+
#..............#
#..............#
#.....C........#
#.C........C...#
#...C..........#
#..............#
#..............#
#....C........p*
#..............#
#............C.#
#..C...........#
#.......C.C....#
#....C......C..#
#..............#
+##############+

message Congratulations! You got treasure! Buy some regular shoes and take a break!
