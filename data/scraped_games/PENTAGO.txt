title PENTAGO
homepage by cubestudio
realtime_interval 1
background_color #212121
text_color #69f0ae
run_rules_on_level_start

========
OBJECTS
========

( orange
  #FF7D66 #FFABA8
  blue
  #5A6ED2 #8DABFB )

background `
transparent

cursorwall .
transparent

bgSettings "
transparent #424242
00000
00000
00100
00000
00000

bgSettingsBlocked
#212121 #252525
00000
00000
00100
00000
00000

bg1 (1)
#212121 #1a1a1a
00000
00000
00100
00000
00000

bg2 (2)
#212121 #1a1a1a
00000
00000
00100
00000
00000

bg3 (2)
#212121 #1a1a1a
00000
00000
00100
00000
00000

bg4 (1)
#212121 #1a1a1a
00000
00000
00100
00000
00000

bgU
transparent #212121 #1a1a1a #19e683 #0d0d0d
22222
44444
33333
22222
11111

bgUL
transparent #212121 #1a1a1a #19e683 #0d0d0d 
02222
02444
02333
02322
02321

bgUR
transparent #212121 #1a1a1a #19e683 #0d0d0d
22220
44420
33320
22320
12320

bgD
transparent #212121 #0d0d0d #69f0ae #1a1a1a
11111
44444
22222
33333
44444

bgDL
transparent #212121 #0d0d0d #69f0ae #1a1a1a
04341
04344
04322
04333
04444

bgDR
transparent #212121 #0d0d0d #69f0ae #1a1a1a
14340
44340
22340
33340
44440

bgL
transparent #212121 #1a1a1a #19e683
02321
02321
02321
02321
02321

bgR
transparent #212121 #1a1a1a #19e683
12320
12320
12320
12320
12320

bgX
#212121

pentago0
#1a1a1a #161616
.....
111.1
..0.0
110.0
0...0

pentago1
#1a1a1a #161616
.....
11.11
0..0.
...0.
11.0.

pentago2
#1a1a1a #161616
.....
11.11
.0..0
.0..0
.0..0

pentago3
#1a1a1a #161616
.....
1.111
..0.0
..010
..0.0

pentago4
#1a1a1a #161616
.....
.1111
.0...
.0..1
.0110

pentago5
#1a1a1a #161616
.....
.1111
.0..0
.0..0
.0110


m1 +
#fafafa #E8E8E8
.....
.000.
.000.
.111.
.....

m2 -
#00e676 #00c853
.....
.000.
.000.
.111.
.....

g1
#fafafa #E8E8E8
00000
00000
00000
00000
11111

g2
#00e676 #00c853
00000
00000
00000
00000
11111

cursor1 °
#ffffff
00.00
0...0
.....
0...0
00.00

cursor2
#69f0ae
00.00
0...0
.....
0...0
00.00

box1
transparent

box2
transparent

box3
transparent

box4
transparent

box5
transparent

box6
transparent

box7
transparent

box8
transparent

box9
transparent

rotaR
transparent

rotaL
transparent

pEmpty
transparent
.....
.000.
.0.0.
.000.
.0...

pPlaced
transparent #f06292
.....
.0.0.
..0..
.0.0.
.....

pRota
transparent #f06292
.....
.0.0.
..0..
.0.0.
.....

pFinish
transparent

arrow1 (1)
#1a1a1a
0...0
00.00
.000.
..0..
.....

arrow2 (2)
#212121 
0...0
00.00
.000.
..0..
.....

arrow3 (2)
#212121 
0...0
00.00
.000.
..0..
.....

arrow4 (1)
#1a1a1a
0...0
00.00
.000.
..0..
.....

highlight1
#212121 

highlight2
#1a1a1a

markClose
transparent

markCopy
transparent

markPaste
transparent

save1
transparent

save2
transparent

saveE
transparent

win11
red

win21
blue

win12
green

win22
yellow

win13
orange

win23
purple

win14
white

win24
brown

win15
black

win25
#555555

guiR (C)
transparent transparent
00000
00000
11000
10000
11000

guiL (CC)
transparent transparent
00000
00000
11011
10010
11011

(guiR
#aed581 #212121 #698b4b
11111
10121
10211
10001
11111

guiL
#aed581 #212121 #698b4b
11111
12101
11201
10001
11111)

tpSettings
#616161
.....
..000
.....
...00
.....

tpGame
#616161
.....
...00
.....
..000
.....

guiHider0
transparent

guiHider1
#212121 #252525
00000
00000
00100
00000
00000

setHidden0 (visible)
#5A6ED2 #212121
11111
10001
00100
10001
11111

setHidden1 (invisible)
#616161 #212121
11111
10001
00100
10001
11111

buttonNew0 (off)
#616161 #212121
11111
10101
11011
10101
11111

buttonNew1 (on)
#FF7D66 #212121
11111
10101
11011
10101
11111




=======
LEGEND
=======
(." ` + - °)
1 = bg1 and box1 and cursor1
2 = bg1 and box2
3 = bg1 and box3
4 = bg1 and box4
5 = bg1 and box5
6 = bg1 and box6
7 = bg1 and box7
8 = bg1 and box8
9 = bg1 and box9

a = bg2 and box1
b = bg2 and box2
c = bg2 and box3
d = bg2 and box4
e = bg2 and box5
f = bg2 and box6
g = bg2 and box7
h = bg2 and box8
i = bg2 and box9
 
j = bg3 and box1
k = bg3 and box2
l = bg3 and box3
m = bg3 and box4
n = bg3 and box5
o = bg3 and box6
p = bg3 and box7
q = bg3 and box8
r = bg3 and box9
 
s = bg4 and box1
t = bg4 and box2
u = bg4 and box3
v = bg4 and box4
w = bg4 and box5
x = bg4 and box6
y = bg4 and box7
z = bg4 and box8
ä = bg4 and box9

~ = bgSettingsBlocked and cursorwall

ü = bgU and cursorwall
ö = bgD and cursorwall
< = bgR and cursorwall
> = bgL and cursorwall

{ = bgUR and cursorwall
} = bgUL and cursorwall
: = bgDL and cursorwall
; = bgDR and cursorwall

/ = pentago1 and bgX
\ = pentago2 and bgX
& = pentago3 and bgX
% = pentago4 and bgX
| = pentago5 and bgX


? = guiHider0 and pEmpty and pentago0 and bgX
* = guiHider0 and tpSettings
! = guiHider0 and tpGame
§ = setHidden0
$ = buttonNew0 

boxR = box6
boxM = box5
boxL = box4
player = cursor1 or cursor2

(#groups)
#bg = bg1 or bg2 or bg3 or bg4
#box = box1 or box2 or box3 or box4 or box5 or box6 or box7 or box8 or box9
#arrows = arrow1 or arrow2 or arrow3 or arrow4
#highlights = highlight1 or highlight2
#cursor = cursor1 or cursor2
#win1 = win11 or win12 or win13 or win14 or win15
#win2 = win21 or win22 or win23 or win24 or win25
#win = win11 or win12 or win13 or win14 or win15 or win21 or win22 or win23 or win24 or win25
#gui = guiR or guiL
#tp = tpSettings or tpGame
#button = setHidden0 or setHidden1 or buttonNew0 or buttonNew1
#guiHider = guiHider0 or guiHider1

#phases = pPlaced or pRota or pFinish
#rota = rotaR or rotaL
#boxDir = boxR or boxL
#boxClose = box2 or box4 or box6 or box8
#save = save1 or save2 or saveE

#marble = m1 or m2
#goldMarble = g1 or g2
#anyMarble = m1 or m2 or g1 or g2

#clearObj = m1 or m2 or arrow1 or arrow2 or arrow3 or arrow4 or g1 or g2

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
background
(bgs)
bgU, bgD, bgR, bgL, bgX, bgUL, bgUR, bgDL, bgDR
bg1, bg2, bg3, bg4, bgSettings, bgSettingsBlocked
(on bg)
pentago0, pentago1, pentago2, pentago3, pentago4, pentago5
highlight1, highlight2
arrow1, arrow2, arrow3, arrow4
(GUI)
pEmpty, pPlaced, pRota, pFinish
guiR, guiL
tpSettings, tpGame
setHidden0, setHidden1, buttonNew0, buttonNew1
(mask GUI)
guiHider0, guiHider1
(invisible)
box1, box2, box3, box4, box5, box6, box7, box8, box9
rotaR, rotaL
markClose
markCopy, markPaste
save1, save2, saveE
win11, win12, win13, win14, win15, win21, win22, win23, win24, win25
(cursor)
player, cursorWall
(obj)
m1, m2, g1, g2


======
RULES     
======     

(
(SETTINGS)

(tp)
[ action cursor1 tpSettings ] [ tpGame ] -> [ tpSettings ] [ cursor1 tpGame ]
[ action cursor2 tpSettings ] [ tpGame ] -> [ tpSettings ] [ cursor2 tpGame ]
[ action cursor1 tpGame ] [ tpSettings ] -> [ tpGame ] [ cursor1 tpSettings ]
[ action cursor2 tpGame ] [ tpSettings ] -> [ tpGame ] [ cursor2 tpSettings ]
[ action cursor1 bgSettings ] [ bg1 ] -> [ bgSettings ] [ cursor1 bg1 ]
[ action cursor2 bgSettings ] [ bg1 ] -> [ bgSettings ] [ cursor2 bg1 ]

(hide GUI)
[ action #cursor setHidden0 ] -> [ #cursor setHidden1 ]
[ action #cursor setHidden1 ] -> [ #cursor setHidden0 ]
late [ setHidden1 ] [ guiHider0 ] -> [ setHidden1 ] [ guiHider1 ]
late [ setHidden0 ] [ guiHider1 ] -> [ setHidden0 ] [ guiHider0 ]

(new game)
[ action #cursor buttonNew0 ] -> [ #cursor buttonNew1 ]
[ #cursor buttonNew1 ] [ pRota ] -> [ #cursor buttonNew1 ] [ pEmpty ]
[ #cursor buttonNew1 ] [ #clearObj ] -> [ #cursor buttonNew1 ] [ ]
late [ buttonNew1 #cursor ] -> checkpoint
late [ buttonNew1 no #cursor ] -> [ buttonNew0 ]
)

(GAME)
(place)
[ action cursor1 no #marble #bg ] [ pEmpty ] -> [ cursor1 m1 #bg ] [ pPlaced ]
[ action cursor2 no #marble #bg ] [ pEmpty ] -> [ cursor2 m2 #bg ] [ pPlaced ]


(ROTA PREP)

(start rota or end turn)
[ > #cursor ] [ pPlaced ] -> [ > #cursor ] [ pRota ]
[ action #cursor #bg ] [ pPlaced ] -> [ #cursor #bg ] [ pFinish ]

(rotation arrows)
late [ pRota ] [ boxL bg1 ] -> [ pRota ] [ arrow1 boxL bg1 ]
late [ pRota ] [ boxR bg1 ] -> [ pRota ] [ arrow1 boxR bg1 ]
late [ pRota ] [ boxL bg2 ] -> [ pRota ] [ arrow2 boxL bg2 ]
late [ pRota ] [ boxR bg2 ] -> [ pRota ] [ arrow2 boxR bg2 ]
late [ pRota ] [ boxL bg3 ] -> [ pRota ] [ arrow3 boxL bg3 ]
late [ pRota ] [ boxR bg3 ] -> [ pRota ] [ arrow3 boxR bg3 ]
late [ pRota ] [ boxL bg4 ] -> [ pRota ] [ arrow4 boxL bg4 ]
late [ pRota ] [ boxR bg4 ] -> [ pRota ] [ arrow4 boxR bg4 ]
late [ pFinish ] [ #arrows ] -> [ pFinish ] [ ]

(rotation highlights)
late [ pRota ] [ bg1 ] -> [ pRota ] [ highlight1 bg1 ]
late [ pRota ] [ bg2 ] -> [ pRota ] [ highlight2 bg2 ]
late [ pRota ] [ bg3 ] -> [ pRota ] [ highlight2 bg3 ]
late [ pRota ] [ bg4 ] -> [ pRota ] [ highlight1 bg4 ]
late [ pFinish ] [ #highlights ] -> [ pFinish ] [ ]

(GUI arrow)
late [ pRota ] [ #cursor boxR ] -> [ pRota guiR ] [ #cursor boxR ]
late [ pRota ] [ #cursor boxL ] -> [ pRota guiL ] [ #cursor boxL ]
late [ #gui ] [ #cursor no #boxDir ] -> [  ] [ #cursor ]

(start rotate and end turn)
[ action #cursor boxR | boxM ] [ pRota ] -> [ #cursor boxR | rotaR boxM markCopy ] [ pFinish ]
[ action #cursor boxL | boxM ] [ pRota ] -> [ #cursor boxL | rotaL boxM markCopy ] [ pFinish ]
[ action #cursor no #boxDir ] [ pRota ] -> [ #cursor ] [ pFinish ]

(ROTATION)

(copy near area)
[ markCopy ] [ box2 m1 | #rota ] -> [ markCopy ] [ box2 save1 markClose | #rota ]
[ markCopy ] [ box2 m2 | #rota ] -> [ markCopy ] [ box2 save2 markClose | #rota ]
[ markCopy ] [ box2 no #marble no #save | #rota ] -> [ markCopy ] [ box2 saveE markClose | #rota ]
[ markCopy ] [ box6 m1 | #rota ] -> [ markCopy ] [ box6 save1 markClose | #rota ]
[ markCopy ] [ box6 m2 | #rota ] -> [ markCopy ] [ box6 save2 markClose | #rota ]
[ markCopy ] [ box6 no #marble no #save | #rota ] -> [ markCopy ] [ box6 saveE markClose | #rota ]
[ markCopy ] [ box8 m1 | #rota ] -> [ markCopy ] [ box8 save1 markClose | #rota ]
[ markCopy ] [ box8 m2 | #rota ] -> [ markCopy ] [ box8 save2 markClose | #rota ]
[ markCopy ] [ box8 no #marble no #save | #rota ] -> [ markCopy ] [ box8 saveE markClose | #rota ]
[ markCopy ] [ box4 m1 | #rota ] -> [ markCopy ] [ box4 save1 markClose | #rota ]
[ markCopy ] [ box4 m2 | #rota ] -> [ markCopy ] [ box4 save2 markClose | #rota ]
[ markCopy ] [ box4 no #marble no #save | #rota ] -> [ markCopy ] [ box4 saveE markClose | #rota ]
(copy corners)
[ markCopy ] [ box1 m1 | markClose ] -> [ markCopy ] [ box1 save1 | markClose ]
[ markCopy ] [ box1 m2 | markClose ] -> [ markCopy ] [ box1 save2 | markClose ]
[ markCopy ] [ box1 no #marble no #save | markClose ] -> [ markCopy ] [ box1 saveE | markClose ]
[ markCopy ] [ box3 m1 | markClose ] -> [ markCopy ] [ box3 save1 | markClose ]
[ markCopy ] [ box3 m2 | markClose ] -> [ markCopy ] [ box3 save2 | markClose ]
[ markCopy ] [ box3 no #marble no #save | markClose ] -> [ markCopy ] [ box3 saveE | markClose ]
[ markCopy ] [ box9 m1 | markClose ] -> [ markCopy ] [ box9 save1 | markClose ]
[ markCopy ] [ box9 m2 | markClose ] -> [ markCopy ] [ box9 save2 | markClose ]
[ markCopy ] [ box9 no #marble no #save | markClose ] -> [ markCopy ] [ box9 saveE | markClose ]
[ markCopy ] [ box7 m1 | markClose ] -> [ markCopy ] [ box7 save1 | markClose ]
[ markCopy ] [ box7 m2 | markClose ] -> [ markCopy ] [ box7 save2 | markClose ]
[ markCopy ] [ box7 no #marble no #save | markClose ] -> [ markCopy ] [ box7 saveE | markClose ]
(switch mode)
[ box1 #save ] [ box2 #save ] [ box3 #save ] [ box4 #save ] [ box6 #save ] [ box7 #save ] [ box8 #save ] [ box9 #save ] [ markCopy ] -> [ box1 #save ] [ box2 #save ] [ box3 #save ] [ box4 #save ] [ box6 #save ] [ box7 #save ] [ box8 #save ] [ box9 #save ] [ markPaste ]
(paste near area R)
[ rotaR markPaste ] [ box2 save1 ] [ box6 | #rota ] -> [ rotaR markPaste ] [ box2 ] [ box6 m1 | #rota ]
[ rotaR markPaste ] [ box2 save2 ] [ box6 | #rota ] -> [ rotaR markPaste ] [ box2 ] [ box6 m2 | #rota ]
[ rotaR markPaste ] [ box2 saveE ] [ box6 | #rota ] -> [ rotaR markPaste ] [ box2 ] [ box6 | #rota ]
[ rotaR markPaste ] [ box6 save1 ] [ box8 | #rota ] -> [ rotaR markPaste ] [ box6 ] [ box8 m1 | #rota ]
[ rotaR markPaste ] [ box6 save2 ] [ box8 | #rota ] -> [ rotaR markPaste ] [ box6 ] [ box8 m2 | #rota ]
[ rotaR markPaste ] [ box6 saveE ] [ box8 | #rota ] -> [ rotaR markPaste ] [ box6 ] [ box8 | #rota ]
[ rotaR markPaste ] [ box8 save1 ] [ box4 | #rota ] -> [ rotaR markPaste ] [ box8 ] [ box4 m1 | #rota ]
[ rotaR markPaste ] [ box8 save2 ] [ box4 | #rota ] -> [ rotaR markPaste ] [ box8 ] [ box4 m2 | #rota ]
[ rotaR markPaste ] [ box8 saveE ] [ box4 | #rota ] -> [ rotaR markPaste ] [ box8 ] [ box4 | #rota ]
[ rotaR markPaste ] [ box4 save1 ] [ box2 | #rota ] -> [ rotaR markPaste ] [ box4 ] [ box2 m1 | #rota ]
[ rotaR markPaste ] [ box4 save2 ] [ box2 | #rota ] -> [ rotaR markPaste ] [ box4 ] [ box2 m2 | #rota ]
[ rotaR markPaste ] [ box4 saveE ] [ box2 | #rota ] -> [ rotaR markPaste ] [ box4 ] [ box2 | #rota ]
(paste corners R)
[ rotaR markPaste ] [ box1 save1 ] [ box3 | markClose ] -> [ rotaR markPaste ] [ box1 ] [ box3 m1 | markClose ]
[ rotaR markPaste ] [ box1 save2 ] [ box3 | markClose ] -> [ rotaR markPaste ] [ box1 ] [ box3 m2 | markClose ]
[ rotaR markPaste ] [ box1 saveE ] [ box3 | markClose ] -> [ rotaR markPaste ] [ box1 ] [ box3 | markClose ]
[ rotaR markPaste ] [ box3 save1 ] [ box9 | markClose ] -> [ rotaR markPaste ] [ box3 ] [ box9 m1 | markClose ]
[ rotaR markPaste ] [ box3 save2 ] [ box9 | markClose ] -> [ rotaR markPaste ] [ box3 ] [ box9 m2 | markClose ]
[ rotaR markPaste ] [ box3 saveE ] [ box9 | markClose ] -> [ rotaR markPaste ] [ box3 ] [ box9 | markClose ]
[ rotaR markPaste ] [ box7 save1 ] [ box1 | markClose ] -> [ rotaR markPaste ] [ box7 ] [ box1 m1 | markClose ]
[ rotaR markPaste ] [ box7 save2 ] [ box1 | markClose ] -> [ rotaR markPaste ] [ box7 ] [ box1 m2 | markClose ]
[ rotaR markPaste ] [ box7 saveE ] [ box1 | markClose ] -> [ rotaR markPaste ] [ box7 ] [ box1 | markClose ]
[ rotaR markPaste ] [ box9 save1 ] [ box7 | markClose ] -> [ rotaR markPaste ] [ box9 ] [ box7 m1 | markClose ]
[ rotaR markPaste ] [ box9 save2 ] [ box7 | markClose ] -> [ rotaR markPaste ] [ box9 ] [ box7 m2 | markClose ]
[ rotaR markPaste ] [ box9 saveE ] [ box7 | markClose ] -> [ rotaR markPaste ] [ box9 ] [ box7 | markClose ]
(paste near area L)
[ rotaL markPaste ] [ box2 save1 ] [ box4 | #rota ] -> [ rotaL markPaste ] [ box2 ] [ box4 m1 | #rota ]
[ rotaL markPaste ] [ box2 save2 ] [ box4 | #rota ] -> [ rotaL markPaste ] [ box2 ] [ box4 m2 | #rota ]
[ rotaL markPaste ] [ box2 saveE ] [ box4 | #rota ] -> [ rotaL markPaste ] [ box2 ] [ box4 | #rota ]
[ rotaL markPaste ] [ box6 save1 ] [ box2 | #rota ] -> [ rotaL markPaste ] [ box6 ] [ box2 m1 | #rota ]
[ rotaL markPaste ] [ box6 save2 ] [ box2 | #rota ] -> [ rotaL markPaste ] [ box6 ] [ box2 m2 | #rota ]
[ rotaL markPaste ] [ box6 saveE ] [ box2 | #rota ] -> [ rotaL markPaste ] [ box6 ] [ box2 | #rota ]
[ rotaL markPaste ] [ box8 save1 ] [ box6 | #rota ] -> [ rotaL markPaste ] [ box8 ] [ box6 m1 | #rota ]
[ rotaL markPaste ] [ box8 save2 ] [ box6 | #rota ] -> [ rotaL markPaste ] [ box8 ] [ box6 m2 | #rota ]
[ rotaL markPaste ] [ box8 saveE ] [ box6 | #rota ] -> [ rotaL markPaste ] [ box8 ] [ box6 | #rota ]
[ rotaL markPaste ] [ box4 save1 ] [ box8 | #rota ] -> [ rotaL markPaste ] [ box4 ] [ box8 m1 | #rota ]
[ rotaL markPaste ] [ box4 save2 ] [ box8 | #rota ] -> [ rotaL markPaste ] [ box4 ] [ box8 m2 | #rota ]
[ rotaL markPaste ] [ box4 saveE ] [ box8 | #rota ] -> [ rotaL markPaste ] [ box4 ] [ box8 | #rota ]
(paste corners L)
[ rotaL markPaste ] [ box1 save1 ] [ box7 | markClose ] -> [ rotaL markPaste ] [ box1 ] [ box7 m1 | markClose ]
[ rotaL markPaste ] [ box1 save2 ] [ box7 | markClose ] -> [ rotaL markPaste ] [ box1 ] [ box7 m2 | markClose ]
[ rotaL markPaste ] [ box1 saveE ] [ box7 | markClose ] -> [ rotaL markPaste ] [ box1 ] [ box7 | markClose ]
[ rotaL markPaste ] [ box3 save1 ] [ box1 | markClose ] -> [ rotaL markPaste ] [ box3 ] [ box1 m1 | markClose ]
[ rotaL markPaste ] [ box3 save2 ] [ box1 | markClose ] -> [ rotaL markPaste ] [ box3 ] [ box1 m2 | markClose ]
[ rotaL markPaste ] [ box3 saveE ] [ box1 | markClose ] -> [ rotaL markPaste ] [ box3 ] [ box1 | markClose ]
[ rotaL markPaste ] [ box7 save1 ] [ box9 | markClose ] -> [ rotaL markPaste ] [ box7 ] [ box9 m1 | markClose ]
[ rotaL markPaste ] [ box7 save2 ] [ box9 | markClose ] -> [ rotaL markPaste ] [ box7 ] [ box9 m2 | markClose ]
[ rotaL markPaste ] [ box7 saveE ] [ box9 | markClose ] -> [ rotaL markPaste ] [ box7 ] [ box9 | markClose ]
[ rotaL markPaste ] [ box9 save1 ] [ box3 | markClose ] -> [ rotaL markPaste ] [ box9 ] [ box3 m1 | markClose ]
[ rotaL markPaste ] [ box9 save2 ] [ box3 | markClose ] -> [ rotaL markPaste ] [ box9 ] [ box3 m2 | markClose ]
[ rotaL markPaste ] [ box9 saveE ] [ box3 | markClose ] -> [ rotaL markPaste ] [ box9 ] [ box3 | markClose ]
(end mode)
[ box1 no #save | box2 no #save markClose | box3 no #save ] [ box4 no #save markClose | #rota | box6 no #save markClose ] [ box7 no #save | box8 no #save markClose | box9 no #save ] [ markPaste ] -> [ box1 | box2 | box3 ] [ box4 | | box6 ] [ box7 | box8 | box9 ] [ ]


(WIN CHECK)
late [ m1 | m1 | m1 | m1 | m1 ] -> [ g1 | g1 | g1 | g1 | g1 ]
late [ m2 | m2 | m2 | m2 | m2 ] -> [ g2 | g2 | g2 | g2 | g2 ]

(END)
(switch player)
late [ cursor1 ] [ pFinish ] [ #box no #anyMarble ] -> [ ] [ pEmpty ] [ cursor2 #box ]
late [ cursor2 ] [ pFinish ] [ #box no #anyMarble ] -> [ ] [ pEmpty ] [ cursor1 #box ]

(
(NEW GAME)
[ #goldMarble ] [ > cursor1 #box ] [ buttonNew0 ] -> [ #goldMarble ] [ ] [ cursor1 buttonNew0 ]
[ #goldMarble ] [ > cursor2 #box ] [ buttonNew0 ] -> [ #goldMarble ] [ ] [ cursor2 buttonNew0 ]

(fix?)
[ #goldMarble ] [ action cursor1 #box ] [ buttonNew0 ] -> [ #goldMarble ] [ ] [ cursor1 buttonNew0 ]
[ #goldMarble ] [ action cursor2 #box ] [ buttonNew0 ] -> [ #goldMarble ] [ ] [ cursor2 buttonNew0 ]
)

==============
WINCONDITIONS
==============

=======     
LEVELS
=======

}üüüüüü{
>123abc<
>456def<
>789ghi<
>jklstu<
>mnovwx<
>pqryzä<
>?/\&%|<
:öööööö;

