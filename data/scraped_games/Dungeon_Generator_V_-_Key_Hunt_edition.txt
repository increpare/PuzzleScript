title Dungeon Generator V - Key Hunt edition
author Connorses
homepage connorspuzzles.tumblr.com
run_rules_on_level_start
again_interval 0
zoomscreen 7x7
========
OBJECTS
========

floor
darkGREEN

footprints
darkgreen darkgreen
11111
10111
11111
11101
11111

Target 
Blue    
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0
Wall 
darkbrown

Player 
Blue   
.000.
.000.
00000
.000.
.0.0.
solid 
darkblue darkbrown 
00000
01010
00000
01010 
00000
hori
darkblue
0...0
0...0
0...0
0...0
0...0

vert
darkblue
00000
.....
.....
.....
00000

key
yellow
.....
00...
0.000
00.00
.....
gotkey
transparent
gen_player
transparent

gen_target
transparent

flood
orange
new
red
=======
LEGEND
=======
background = floor or footprints
. = floor
# = Wall
P = gen_player and gen_target and wall
O = Target and wall
% = solid and wall
deleteflood = flood or new or gotkey or key
=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
solid
Player, Wall, key
hori
vert
gen_player
gen_target
Target, gotkey
flood, new

======
RULES     
======
[> player|target] [key] -> [> player|target] [key] message Find the key!

random [gen_player] [wall no player no solid] -> [ ] [player]
random [gen_target] [wall no target no solid] -> [ ] [target wall]

startloop
horizontal [wall no target|no wall] -> [wall hori|  ]
vertical   [wall no target|no wall] -> [wall vert|  ]

horizontal random [no wall no solid|wall no vert|wall] -> [  |  |wall]
vertical   random [no wall no solid|wall no hori|wall] -> [  |  |wall]

[hori no wall] -> [  ]
[vert no wall] -> [  ]
endloop


[player no flood] -> [player flood]
[target no flood] -> [target flood]
[flood] [deleteflood] -> [  ] [deleteflood]
[new  ] [deleteflood] -> [  ] [deleteflood]

startloop
[flood|no flood no new no wall] -> [flood|new]
[new|no wall no flood] [new] -> [new|  ] [flood]
[new|no wall no flood] -> [flood|  ]
endloop
random [new] -> [key]
[new] [deleteflood] -> [  ] [deleteflood]
[flood] [deleteflood] -> [  ] [deleteflood]

[target wall] -> [target]

[> player|key] -> [> player|gotkey] message Key get!

[player no footprints] -> [player footprints]
==============
WINCONDITIONS
==============

some player on target  
no key

=======     
LEVELS
=======
%%%%%%%%%%%%%%%%%%%%
%p#################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%##################%
%%%%%%%%%%%%%%%%%%%%