title The Block Tower
author Max
run_rules_on_level_start
flickscreen 10x10

========
OBJECTS
========

Background
#493929 #432
11111
01111
11101
11111
10111

Wall
#8a7 #787
00010
11111
01000
11111
00010

Block
#888 #555 #444
11111
10002
10002
10002
12222


BlockTop
#444 #333 #222
11111
10002
10002
10002
12222

Player
black orange red blue
.000.
.111.
22222
.333.
.3.3.

Empty
#131313 #111
11111
11111
10111
11111
11110

StairsUp
#666 #999 #ccc #fff
00230
01230
01210
01110
00000


StairsUp2
#ccc #fff
.....
.....
.....
.....
...1.

StairsDown
#bbb #999 #666 #333 #000
00000
01440
01240
01230
00000

temp
Transparent

save
Transparent

saveused
Transparent

justsaved
Transparent

Flag
yellow white
01111
01111
01111
0....
0....

=======
LEGEND
=======

. = Background
x = Block
t = BlockTop
p = Player
# = Wall
e = Empty
/ = StairsUp
\ = StairsDown
* = Flag
s = save
& = save and StairsUp

Stairs = StairsUp or StairsDown
stopblock = Wall or Stairs or Block

=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Background
save
justsaved
saveused
temp
Empty
Wall
StairsUp
StairsDown
BlockTop
Block
Flag
Player
StairsUp2

======
RULES
======

[ moving player justsaved ] -> [ moving player ]

[ > player | Wall ] -> [ player | Wall ]
[ > player | Empty no BlockTop ] -> [ player | Empty ]
[ > player | Block ] -> [ > player | > Block ]

late up [ no Block |||||||||| Block Empty ] -> [ Block |||||||||| Empty ]
late [ Block Empty no BlockTop ] -> [ BlockTop ]

late up [ Block |||||||||| empty no BlockTop ] -> [ Block |||||||||| empty BlockTop ]
late up [ Block |||||||||| no empty BlockTop ] -> [ Block |||||||||| ]
late up [ no Block |||||||||| BlockTop ] -> [ |||||||||| ]

up [ moving Block |||||||||| BlockTop ] -> [ moving Block |||||||||| moving BlockTop ]
up [ moving BlockTop Block ] -> [ moving BlockTop moving Block ]

[ > Block | StopBlock ] -> cancel

late [temp] -> []
late [ Player justsaved ] -> [ Player temp ]
late [ Player save no temp no saveused ] -> [ Player save justsaved saveused ] Checkpoint

late up [ Player no temp StairsUp |||||||||| no temp ] -> [StairsUp |||||||||| Player temp]
late down [ Player no temp StairsDown |||||||||| no temp ] -> [StairsDown |||||||||| Player temp]

late [ justsaved ][ player no justsaved ] -> [][ player justsaved ]

late up [ no Player StairsUp | no StairsUp2 ] -> [ StairsUp | StairsUp2 ]

( Get rid of that dumb looking pixel lol )
late up [ Player | StairsUp2 ] -> [ Player | ]



==============
WINCONDITIONS
==============

Some Player
some Flag
all Player on Flag

=======
LEVELS
=======

..........
.\........
..........
..........
....*.....
..........
..........
..........
..........
..........
##########
#&#......#
#.#......#
#.#......#
#.#......#
#e#......#
#.#......#
#.#......#
#\#......#
##########
##########
#..eeee.\#
#.xeeeeee#
#eeeeeeee#
#eeeeeeee#
#eeeeeeee#
#..eeeeee#
#..eeeeee#
#/.eeeeee#
##########
##########
#....x../#
#...#x...#
#....x...#
#...######
#...#.x..#
#....xxx.#
#...#.x..#
#...#...\#
##########
##########
##########
##########
##########
##########
#######..#
#######.\#
#######..#
#######.&#
##########
##########
#\..e...\#
#...e...##
#...eeeee#
#...e.eee#
####eeeee#
#ee...ee/#
#ee.eeeee#
#ee.e....#
##########
##########
#/...#../#
#....#...#
#....#...#
####x#...#
#....#xxx#
#..x.#.x.#
#....#...#
#\...#...#
##########
##########
#.......\#
#..eeee..#
#.xeeeee.#
#..eeeee.#
#eeeeeee.#
###......#
#eeeeee.e#
#&e......#
##########
##########
#\....../#
#........#
#..x.....#
#........#
#........#
#........#
###......#
###......#
##########
##########
#&\#######
##########
#eeeee.###
#e..x..###
#...x....#
#..####e.#
#......e.#
#......e\#
##########
##########
#./.e....#
#...e..#.#
####eee..#
#e..e....#
#...###..#
##x...#..#
##..x.#..#
#\..###./#
##########
##########
#.xe..e..#
#.xe.xex##
#eeee.e.xp
#.xe.xe.##
#..e..e..#
#eeeeeeee#
#..e#x.x.#
#&.e#....#
##########

Message You made it!