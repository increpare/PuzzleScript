Title ColorLift
Author Connorses [Loneship Games]
realtime_interval 0.15
(color_palette)
background_color #090cd8
noundo
========
OBJECTS
========
background
black darkblue
10001
00000
00000
00000
10001
player
white blue
00000
..0..
110.0
1100.
.0.0.

rope
grey
..0..
..0..
..0..
..0..
..0..
cut
transparent
A
lightbrown
00000
0.0.0
00000
0.0.0
00000
B
green
00000
0.0.0
00.00
0.0.0
00000
C
#2386e1
00000
.0.0.
00000
.0.0.
00000
D
red
00.00
00000
.000.
00000
00.00
E
purple
.0.0.
00000
00000
00000
.0.0.
F
orange
00000
0...0
.000.
0...0
00000

rando
white

wall
darkgrey

counter
yellow orange
.....
10000
10000
10000
.....
counter1
yellow orange
.....
100..
100..
100..
.....
tip
pink
0...0
.000.
.000.
.000.
0...0

spawn
yellow
.....
.....
.....
0...0
00.00

c1
white
0....
.....
.....
.....
.....
c2
pink
..0..
.....
.....
.....
.....
=======
LEGEND
=======
# = wall
H = player
. = background
? = rando
! = wall and tip
* = spawn
block = A or B or C or D or E or F
notA = B or C or D or E or F
notB = A or C or D or E or F
notC = A or B or D or E or F
notD = A or B or C or E or F
notE = A or B or C or D or F
notF = A or B or C or D or E
solid = wall or block
cutter = wall

=======
SOUNDS
=======
endlevel 48217508
================
COLLISIONLAYERS
================
background
rope,cut
player,wall,block
counter,counter1,tip,rando
c1,c2
spawn
======
RULES
======
[c1] -> [   ]
[c2] -> [   ]

[rando no block] -> [rando random block]

[rando A|A]  -> [rando random notA|A]
+[rando B|B] -> [rando random notB|B]
+[rando C|C] -> [rando random notC|C]
+[rando D|D] -> [rando random notD|D]
+[rando E|E] -> [rando random notE|E]
+[rando F|F] -> [rando random notF|F]

[rando] -> [   ]

[action player no rope] -> [player rope]
[action player rope] -> [player cut]
down [rope no block|no rope] -> [rope|rope]
down [rope no block|wall] -> [cut|wall]

[> player rope] -> [> player > rope]
[moving rope|rope] -> [moving rope|moving rope]
[> rope block] -> [> rope > block]

[> block |solid] -> cancel
[> player|wall ] -> cancel
[> rope  |wall ] -> [cut|wall]



random [A no c1 no rope|A no c1 no rope] -> [A c1|A c1]
[A c1|A no c1 no rope] -> [A c2|A c2]
[A c2|A no c2] -> [A c2|A c2]
random [B no c1 no rope|B no c1 no rope] -> [B c1|B c1]
[B c1|B no c1 no rope] -> [B c2|B c2]
[B c2|B no c2] -> [B c2|B c2]
random [C no c1 no rope|C no c1 no rope] -> [C c1|C c1]
[C c1|C no c1 no rope] -> [C c2|C c2]
[C c2|C no c2] -> [C c2|C c2]
random [D no c1 no rope|D no c1 no rope] -> [D c1|D c1]
[D c1|D no c1 no rope] -> [D c2|D c2]
[D c2|D no c2] -> [D c2|D c2]
random [E  no c1 no rope|E  no c1 no rope] -> [E  c1|E  c1]
[E  c1|E  no c1 no rope] -> [E  c2|E  c2]
[E  c2|E  no c2] -> [E  c2|E  c2]
random [F  no c1|F  no c1 no rope] -> [F  c1|F  c1]
[F  c1|F  no c1 no rope] -> [F  c2|F  c2]
[F  c2|F  no c2] -> [F  c2|F  c2]

down [rope no block|c2] -> [cut|c2]

[rope|cut] -> [cut| cut]
[cut] -> [   ]

[block c2] -> [   ]

([stationary player] [] -> [player] [])
right [stationary player] [counter|tip] -> [player] [counter c2|tip]

[stationary player] [counter c2] [spawn no rando] -> [player] [counter c2] [spawn rando]
[stationary player] [counter c2] [counter] -> [player] [counter c2] [   ]

[stationary player] [counter1 no c1] -> [player] [counter c1]
right [stationary player] [tip|no counter1 no counter] -> [player] [tip|counter1 c1]
right [stationary player] [counter no c1|no counter1 no counter] -> [player] [counter|counter1 c1]

down [stationary player] [block no rope|no solid] -> [player] [down block|   ]

[spawn rando] [block no rope] -> [spawn rando] [up block]
up [up block|block rope] -> [up block|up block rope]
[up block|player] -> [up block|up player]
[up block rope] -> [up block up rope]
[up rope|rope] -> [up rope|up rope]
[up rope player] -> [up rope up player]

up [up block|wall] -> restart
==============
WINCONDITIONS
==============

=======
LEVELS
=======
...............########...............
...............#......#...............
...............#......#...............
...............#...h..#...............
...............#......#...............
...............#......#...............
...............#......#...............
...............#......#...............
...............#......#...............
...............#......#...............
...............#......#...............
...............#......#...............
...............#??????#...............
...............#??????#...............
...............#??????#...............
...............#??????#...............
...............#******#...............
...............########...............
!....................................!



