title Dead Grass
author ncrecc
homepage www.puzzlescript.net
require_player_movement
noaction

========
OBJECTS
========

PushTerritory
LIGHTGREEN GREEN
11111
01111
11101
11111
10111

PullTerritory
Lightred Red
11111
01111
11101
11111
10111

SweepTerritory
Yellow Lightbrown
11111
01111
11101
11111
10111

DeadGrass
Lightgray Gray
11111
01111
11101
11111
10111

Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

1 = PushTerritory
2 = PullTerritory
3 = SweepTerritory
# = Wall
Q = Player and PushTerritory
A = Player and PullTerritory
Z = Player and SweepTerritory
W = Crate and PushTerritory
S = Crate and PullTerritory
X = Crate and SweepTerritory
E = Crate and Target and PushTerritory
D = Crate and Target and PullTerritory
C = Crate and Target and SweepTerritory
R = Target and PushTerritory
F = Target and PullTerritory
V = Target and SweepTerritory
~ = DeadGrass
! = DeadGrass and Crate
@ = DeadGrass and Player
$ = DeadGrass and Target

Background = PushTerritory or PullTerritory or SweepTerritory or DeadGrass

=======
SOUNDS
=======

sfx1 36772507
sfx2 4170907
sfx3 63575307
endlevel 17009708

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

[> Player PushTerritory|Crate] -> [> Player DeadGrass|> Crate] sfx1
[< Player PullTerritory|Crate] -> [< Player DeadGrass|< Crate] sfx2

(SweepTerritory is more complicated because it shouldn't become deadgrass unless the player actually moves a crate)
[perpendicular Player SweepTerritory|Crate] -> [perpendicular Player action SweepTerritory|perpendicular Crate]

[> Crate|Crate] -> [Crate|Crate]
[> Crate|Wall] -> [Crate|Wall]

[action SweepTerritory|moving Crate] -> [DeadGrass|moving Crate] sfx3

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

message The grass is wilting!

#######
#11r11#
#11w11#
#rw1wr#
#11w11#
#q1r11#
#######

#######1
#1w1w1#1
#1w1w1#1
#qw1w1##
#1w1w1wr
#1w1w1##
#######1

11#####11
###111###
#1111111#
#11r1w11#
#1rr1ww1#
#111q111#
###111###
11#####11

##########
#q1~~11~~#
#~~rw~~11#
#11~~11~~#
#~~11~$w1#
#11$!11~~#
#~~11~~11#
#11~~1r!~#
#~~11~~11#
##########

#######
#2222s#
#a22ss#
#22ss2#
#2ss2f#
#ss2f2#
#######

message Italy!

#######
#11#22#
#1w~f2#
#1w#f2#
#11@22#
##1~2##
#######

message You look familiar...

#######
#3333x#
#z33xx#
#33xx3#
#3xx3v#
#xx3v3#
#######

##q###############
#1111111111111111#
#~~11~~~1~~~1~~11#
#~3~1~333~3~3~3~1#
#~3~3~~11~~~3~3~3#
#~3~3~331~3~3~3~3#
#~~13~~~1~3~3~~13#
#1331133313131331#
#1111111111111111#
#2211221122212211#
#2~~12~212~2~2~22#
#2~21221~222~122~#
#2~2~2~212~2~22~2#
#222~2~2~2~2~1~22#
#1~~~1~1~1~1~111~#
#11111111111111w1#
###############r##
