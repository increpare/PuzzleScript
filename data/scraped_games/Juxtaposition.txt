title Juxtaposition
author Croubble
homepage www.puzzlescript.net
background_color DarkBrown
run_rules_on_level_start
norepeat_action
again_interval 0.04
realtime_interval 0.85

========
OBJECTS
========

Background
White Grey Black
20120
12011
01022
20210
10210

Light
White 
00000
00000
00000
00000
00000

Grey
Grey
00000
00000
00000
00000
00000

Dark
Black
00000
00000
00000
00000
00000

DarkPlayer
Black
.000.
00000
.000.
.0.0.
.0.0.

LightPlayer
White
.000.
00000
.000.
.0.0.
.0.0.

GreyPlayer
Grey #9D9D9D #EEB62F
.000. 
00000
.000.
.0.0.
.0.0.

ActiveDarkPlayer
Black Red
.000.
00100
.000.
.0.0.
.0.0.

ActiveLightPlayer
White Red
.000.
00100
.000.
.0.0.
.0.0.

ActiveGreyPlayer
Grey Red
.000.
00100
.000.
.0.0.
.0.0.

LightGoal
white #FB6900
1...1
.1.1.
..1..
.1.1.
1...1

LightGoal1
white #FB6900
1...1
..1..
.111.
..1..
1...1

DarkGoal
Black #9D9D9D #FB6900
1...1
.1.1.
..1..
.1.1.
1...1

GreyGoal
Grey #FB6900
1...1
.1.1.
..1..
.1.1.
1...1


IsActivePlayer
White
.....
.....
.....
.....
.....

TempBlueMarker
Blue
.....
.....
.....
.....
.....

Crate
#FB6900
00000
0...0
0...0
0...0
00000

CrateTarget
#FB6900
.....
.000.
.0.0.
.000.
.....

Apple
Red
.....
.000.
.000.
.000.
.....

DontActiveDark
Black
.....
.....
.....
.....
.....

DontActiveGrey
Grey
.....
.....
.....
.....
.....

DontActiveLight
White
.....
.....
.....
.....
.....

CrateAnimate
#FB6900
00000
00000
00.00
00000
00000

CrateAnimate1
#FB6900
.0000
0.000
00.00
00000
00000

CrateAnimate2
#FB6900
...00
0..00
00.00
00000
00000

CrateAnimate3
#FB6900
.....
0...0
00.00
00000
00000

CrateAnimate4
#FB6900
.....
0....
00...
00000
00000

CrateAnimate5
#FB6900
.....
0....
00...
000..
0000.

CrateAnimate6
#FB6900
.....
0....
00...
000..
000..

CrateAnimate7
#FB6900
.....
0....
00...
00...
0....

CrateAnimate8
#FB6900
.....
0....
00...
.....
.....

CrateAnimate9
#FB6900
.....
.....
.....
.....
.....

TargetWait
white
.....
.....
.....
.....
.....

PlayerAnim
#FB6900
.....
.....
..0..
..0..
..0..

PlayerAnim1
#FB6900
.....
.....
..0..
.000.
00000

PlayerAnim2
#FB6900
.....
.....
00000
00000
00000

PlayerAnim3
#FB6900
0...0
00.00
00000
00000
00000

PlayerAnim4
#FB6900
00000
00000
00000
00000
00000

PlayerAnim5
#FB6900
00000
00000
00.00
00000
00000

PlayerAnim6
#FB6900
00000
0...0
0...0
0...0
00000

PlayerAnim7
Blue
.....
.....
.....
.....
.....

Extend
white
.....
.....
.....
.....
.....
Extend2
white
.....
.....
.....
.....
.....

Spikes
DarkRed Black
10000
00100
00000
01000
00010

SpreadFireExtend
DarkRed Red 
00001
00011
00001
00011
00001

SpreadFire
DarkRed Red
01011
11111
01101
11111
01110

SpreadFireImminent
RedLightRed
1....
01...
1....
01...
1....
Wait
White
.....
.....
.....
.....
.....

activeTurn
White
.....
.....
.....
.....
.....

=======
LEGEND
=======
Lethal = SpreadFireExtend or SpreadFire or Spikes
CrateAnimations = CrateAnimate or CrateAnimate1 or CrateAnimate2 or CrateAnimate3 or CrateAnimate4 or CrateAnimate5 or CrateAnimate6 or CrateAnimate7 or CrateAnimate8 or CrateAnimate9
PlayerAnimations = PlayerAnim or PlayerAnim1 or PlayerAnim2 or PlayerAnim3 or PlayerAnim4 or PlayerAnim5 or PlayerAnim6 or PlayerAnim7
Tile = Light or Dark or Grey
Target = LightGoal or DarkGoal or GreyGoal or LightGoal1
Player = ActiveLightPlayer or ActiveDarkPlayer or ActiveGreyPlayer
WaitPlayer = LightPlayer or DarkPlayer or GreyPlayer
AllPlayer = Player or WaitPlayer
ADarkPlayer = DarkPlayer or ActiveDarkPlayer
ALightPlayer = LightPlayer or ActiveLightPlayer
AGreyPlayer = GreyPlayer or ActiveGreyPlayer

canPush = AllPlayer or Light or Dark or Grey

w = Light
# = Dark
. = Grey
, = background
l = LightPlayer
m = LightPlayer and Dark
► = LightPlayer and Grey
n = LightGoal and Dark
├ = LightGoal and Grey
d = DarkPlayer

e = DarkPlayer and light
Ä = DarkPlayer and Grey
f = LightGoal
☼ = LightGoal and light

h = GreyPlayer
i = GreyPlayer and Dark
╚ = GreyPlayer and Light
k = LightGoal and Dark
é = LightGoal and Light

§ = Crate and Light
╝ = Crate and Dark 
ë = Crate and Grey 

Ù = CrateTarget and Light
¿ = CrateTarget and Dark 
7 = CrateTarget and Crate and Light 
6 = CrateTarget and Crate and Dark
∟ = CrateTarget and Grey

a = ActiveDarkPlayer and Grey 
♀ = ActiveLightPlayer and Grey
▼ = Grey and Crate and LightGoal
s = SPikes

ƒ = SpreadFire
=======
SOUNDS
=======

(--sfx0 73384100--)
sfx0 88746700
sfx1 89944907
sfx2 7059504
================
COLLISIONLAYERS
================

Background
Extend Extend2
TargetWait
IsActivePlayer
TempBlueMarker
Light,Grey,Dark
LIGHTGOAL, DARKGOAL, GREYGOAL, CRATETARGET, Apple, LightGoal1
DontActiveDark,DontActiveGrey,DontActiveLight
Spikes SpreadFire SpreadFireExtend SpreadFireImminent
LightPlayer,DarkPlayer,GreyPlayer,ActiveDarkPlayer,ActiveLightPlayer,ActiveGreyPlayer,CRATE
CrateAnimations
PlayerAnimations
Wait
activeTurn
======
RULES
======

[ moving Player ] -> [ moving Player activeTurn]

[Action Player | Action Player] -> [Player | Player]
[Player] -> [Player IsActivePlayer]


(--Handle animation--)
[ stationary Player ] [ LightGoal1 no TargetWait] -> [ Player ] [ LightGoal TargetWait ] 
[ stationary Player ] [ LightGoal no TargetWait ] -> [ Player ] [ LightGoal1 TargetWait ] 
[TargetWait] -> []

(--if no player activated, active, in order black, grey, white --)
[IsActivePlayer] [DarkPlayer] -> [IsActivePlayer] [DontActiveDark]
[DarkPlayer] -> [IsActivePlayer ActiveDarkPlayer]
[DontActiveDark] -> [DarkPlayer]

[IsActivePlayer] [GreyPlayer] -> [IsActivePlayer] [DontActiveGrey]
[GreyPlayer] -> [IsActivePlayer ActiveGreyPlayer]
[DontActiveGrey] -> [GreyPlayer]

[IsActivePlayer] [LightPlayer] -> [IsActivePlayer] [DontActiveLight]
[LightPlayer] -> [IsActivePlayer ActiveLightPlayer]
[DontActiveLight] -> [LightPlayer]

[IsActivePlayer] -> []

(--action cycling active players --)

[action ActiveLightPlayer] [DarkPlayer] -> [LightPlayer] [ActiveDarkPlayer]
[action ActiveLightPlayer] [GreyPlayer] -> [LightPlayer] [ActiveGreyPlayer]
[action ActiveDarkPlayer] [GreyPlayer] -> [DarkPlayer] [ActiveGreyPlayer]
[action ActiveDarkPlayer] [LightPlayer] -> [DarkPlayer] [ActiveLightPlayer]
[action ActiveGreyPlayer] [LightPlayer] -> [GreyPlayer] [ActiveLightPlayer]
[action ActiveGreyPlayer] [DarkPlayer] -> [GreyPlayer] [ActiveDarkPlayer]


[> ActiveDarkPlayer | LightPlayer] -> [DarkPlayer | ActiveLightPlayer]
[> ActiveLightPlayer | DarkPlayer] -> [LightPlayer | ActiveDarkPlayer]

[> ADarkPlayer | Dark] -> [> ADarkPlayer | > Dark]
[> ALightPlayer | Light] -> [> ALightPlayer | > Light]
[> AGreyPlayer | Grey] -> [> AGreyPlayer | > Grey]

(--Can't move player onto orange--)
[> Player | Spikes] -> [ | Spikes] sfx2
[> Player | no Light no Grey no Dark] -> [Player | ]

(--Can't push colors on target--)
[> Dark Target] -> [Dark Target]
[> Grey Target] -> [Grey Target]
[> Light Target] -> [Light Target]

(-- Can't push colors on crate target--)
[> Dark CrateTarget] -> [Dark CrateTarget]
[> Grey CrateTarget] -> [Grey CrateTarget]
[> Light CrateTarget] -> [Light CrateTarget]

late [Player Apple] -> [Player]


(-- Apple move --)

[> Light Apple | Dark no Target Apple] -> [Dark Apple | Light Apple]
[> Light Apple | Grey no Target Apple] -> [Grey Apple | Light Apple] 
[> Dark  Apple | Light no Target Apple] -> [Light Apple | Dark Apple]
[> Dark  Apple | Grey no Target Apple] -> [Grey Apple | Dark Apple]
[> Grey  Apple | Light no Target Apple] -> [Light Apple | Grey Apple]
[> Grey  Apple | Dark no Target Apple] -> [Dark Apple| Grey Apple]

[> Light Apple | Dark no Target] -> [Dark | Light Apple]
[> Light Apple | Grey no Target] -> [Grey | Light Apple] 
[> Dark  Apple | Light no Target] -> [Light | Dark Apple]
[> Dark  Apple | Grey no Target] -> [Grey | Dark Apple]
[> Grey  Apple | Light no Target] -> [Light | Grey Apple]
[> Grey  Apple | Dark no Target] -> [Dark | Grey Apple]


(-- Regular move --) 

[> Light no Apple | Dark no Target no Apple] -> [Dark | Light] sfx1
[> Light no Apple | Grey no Target no Apple] -> [Grey | Light] sfx1
[> Dark  no Apple | Light no Target no Apple] -> [Light | Dark] sfx1
[> Dark  no Apple | Grey no Target no Apple] -> [Grey | Dark] sfx1
[> Grey  no Apple | Light no Target no Apple] -> [Light | Grey] sfx1
[> Grey  no Apple | Dark no Target no Apple] -> [Dark | Grey] sfx1
[> Tile | no Tile] -> [Tile | ]

[> ALightPlayer | Light ] -> [ALightPlayer | Light]
[> ADarkPlayer | Dark ] -> [ADarkPlayer | Dark]
[> AGreyPlayer | Grey ] -> [AGreyPlayer | Grey]
[> Player | Crate ] -> [> Player | > Crate ]
[> Crate | no tile] -> [Crate | ]

[CrateAnimate9] -> []
[CrateAnimate8] -> [CrateAnimate9] again
[CrateAnimate7] -> [CrateAnimate8] again
[CrateAnimate6] -> [CrateAnimate7] again
[CrateAnimate5] -> [CrateAnimate6] again
[CrateAnimate4] -> [CrateAnimate5] again
[CrateAnimate3] -> [CrateAnimate4] again
[CrateAnimate2] -> [CrateAnimate3] again
[CrateAnimate1] -> [CrateAnimate2] again
[CrateAnimate] -> [CrateAnimate1] again
late [Crate CrateTarget] -> [CrateAnimate] again sfx0



[PlayerAnim7 Extend2] -> []
[PlayerAnim6 Extend2] -> [PlayerAnim7] again
[PlayerAnim5 Extend2] -> [PlayerAnim6] again
[PlayerAnim4 Player Target Extend2] -> [PlayerAnim5] again
[PlayerAnim3 Extend2] -> [PlayerAnim4] again
[PlayerAnim2 Extend2] -> [PlayerAnim3] again
[PlayerAnim1 Extend2] -> [PlayerAnim2] again
[PlayerAnim Extend2] -> [PlayerAnim1] again


late [Player Target no PlayerAnimations] -> [Player Target PlayerAnim Extend] sfx0 again
[PlayerAnimations Extend] -> [PlayerAnimations Extend2] again
[PlayerAnimations no Extend no Extend2] -> [PlayerAnimations Extend] again

[> Crate | no Crate no CrateTarget ] -> [ | Crate no CrateTarget ] sfx1

(--[CrateAnimate7] -> [CrateAnimate8] again--)
(--[DarkGoal Dark] -> [LightGoal Dark]--)
(--[LightGoal Light] -> [DarkGoal Light]--)


late [SpreadFire no Wait | no SpreadFire no Spikes no SpreadFireExtend Tile] [ActiveTurn] -> [SpreadFire | SpreadFireImminent Tile] [ActiveTurn]
late [SpreadFire] [ActiveTurn] -> [SpreadFireExtend Wait] [ActiveTurn]
late [SpreadFireExtend no Wait| no SpreadFire no Spikes no SpreadFireExtend Tile] [ActiveTurn] -> [SpreadFireExtend | SpreadFire Wait] [ActiveTurn]

late [SpreadFireExtend no Wait] [ActiveTurn] -> [Spikes] [ActiveTurn]
late [Wait] -> []
late [ActiveTurn] -> []

late [Player Lethal] -> [Lethal] sfx2

==============
WINCONDITIONS
==============

All Target on AllPlayer 
no CrateTarget
no CrateAnimations
no Apple
no PlayerAnimations
=======
LEVELS
=======

message 7 - Overgrown Path

#####################wé#
www##############wwww##w
www#######www####www##w#
www#####w#w#w####ww##ww#
ewwww#w#w#wwwww##w##www#
www##w########ww###wwww#
www##wwww######wwwwwwww#

message 6 - Press X to change Characters

w##n##w
w#####w
eww#ww☼
w#####w
w##m##w

message 5 - Broken Bridge

wwwwwwww
wwwwwwww
ww#ww#ww
eww##ww☼
mww##wwn
www##www

message 4 - Leaky Raft

###w#n
###www
#####w
mew##☼
#####w
#####w

message 3 - To win, cover all targets with crates.

wwù###
ww§#m#
ù§e§##
wwwww#
wwwwwù

message 2 - Open River

wwww##¿
ww#wwm#
w###ew#
ww###ww
#ww###w
#╝ww#ww
###wwww


message 1 - Cluttered Passage

wwww
w§##
wm¿w
ewww

message 0 - Home

www##w#ww##ww
e§#www###www¿
www##www#w##w


message 7 - Unsigned Byte Overflow.

,,,,,,,,,,
,ww....##,
,ww....##,
,..,,,,..,
,..,,,,..,
,..,,,,..,
,.ë,├..∟.,
,►ä..├,..,
,,,,,,,,,,

message  6 - Faulty Path

,,,,,,,,,,,
,####...ww,
,####.w.ww,
,####.w.ww,
,☼#m#.wewn,
,##.#.wwww,
,##.#.wwww,
,##...wwww,
,,,,,,,,,,,



message 5 - Page Error

,,,,,,,
,m#ww,,
,#ww#.,
,,we#,,
,,§ùww,
,,wwww,
,,ww,,,
,,,,,,,

message 4 - Leaky Memory

,,,,,,,,,,,
,wwwww,,,,,
,wwwww,,,☼,
,wwwwwwww##
,em.wwwww##
,wwwwwwww##
,wwwww,,,n,
,wwwww,,,,,
,,,,,,,,,,,


message 3 - Execute Violation 

.,..ww,,
∟...www,
,ë,.##ww
∟.,,,##w
.ë#¿╝#├w
►ä├wwwww

message 2 - CPU Meltdown

sssssssss
.ww..ssss
♀..s....├
a..s....├
.##..ssss
sssssssss


message 1 - Combustion

,,,,,,,,,,,
ƒ...ww..##.
ƒ.♀.ww..##├
ƒ.a.##..ww├
ƒ...##..ww.
,,,,,,,,,,,


message 0 - Inferno

,,,,,,,,,,,,,,
,ƒ...........,
,ƒ♀.........├,
,ƒa.........├,
,ƒ...........,
,,,,,,,,,,,,,,


message Victory! You've been the playtest prototype. Thanks for playing! 

