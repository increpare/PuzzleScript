title Clean Up
author Alex Yang
homepage www.puzzlescript.net

color_palette whitingjp
background_color BLACK
text_color LIGHTGRAY
noaction
require_player_movement
key_repeat_interval 0.25

========
OBJECTS
========

Background 
LIGHTGRAY WHITE
00010
01000
00001
00100
00000

Trigger1
LIGHTGRAY WHITE
00010
01000
00001
00100
00000

Wall
BLACK DARKGRAY
00000
01110
00000
11001
00000

PlayerL
DARKBROWN ORANGE BLUE BLACK BROWN
.000.
.110.
22222
.222.
.3.3.

PlayerR
DARKBROWN ORANGE BLUE BLACK BROWN
.000.
.011.
22222
.222.
.3.3.

YellowBlock
YELLOW ORANGE WHITE
02222
10002
10002
10002
11110
BlueBlock
BLUE DARKBLUE LIGHTBLUE
02222
10002
10002
10002
11110
RedBlock
RED DARKRED LIGHTRED
02222
10002
10002
10002
11110
PurpleBlock
PURPLE DARKGRAY BLUE
02222
10002
10002
10002
11110
GreenBlock
GREEN DARKGREEN LIGHTGREEN
02222
10002
10002
10002
11110
OrangeBlock
ORANGE BROWN YELLOW
02222
10002
10002
10002
11110
YellowTarget
YELLOW
.....
.000.
.0.0.
.000.
.....
BlueTarget
BLUE
.....
.000.
.0.0.
.000.
.....
RedTarget
RED
.....
.000.
.0.0.
.000.
.....
PurpleTarget
PURPLE
.....
.000.
.0.0.
.000.
.....
GreenTarget
GREEN
.....
.000.
.0.0.
.000.
.....
OrangeTarget
ORANGE
.....
.000.
.0.0.
.000.
.....

=======
LEGEND
=======

. = Background
# = Wall
* = Trigger1
P = PlayerR

A = RedBlock
B = PurpleBlock
C = BlueBlock
D = GreenBlock
E = YellowBlock
F = OrangeBlock

0 = RedTarget
1 = PurpleTarget
2 = BlueTarget
3 = GreenTarget
4 = YellowTarget
5 = OrangeTarget

Block = A or B or C or D or E or F
Player = PlayerL or PlayerR
Trigger = Trigger1

=======
SOUNDS
=======

sfx0 27819907 (push)
sfx1 21373902 (eat)
(sfx1 74813702 (eat))
(sfx2 43445702 (pull))
sfx2 34274902 (pull)

sfx10 68733508 (level)

================
COLLISIONLAYERS
================

Background
Trigger
0, 1, 2, 3, 4, 5
Player, Wall, A, B, C, D, E, F

======
RULES     
======

[ left Player ] -> [ left PlayerL ]
[ right Player ] -> [ right PlayerR ]

[ Player Trigger1 ] -> [ Player Background ] 

( player can pull blocks out of target, as long as it is unobstructed )
[ < Player | 0 no Block ] -> [ < Player | < A 0 ] sfx2
[ < Player | 1 no Block ] -> [ < Player | < B 1 ] sfx2
[ < Player | 2 no Block ] -> [ < Player | < C 2 ] sfx2
[ < Player | 3 no Block ] -> [ < Player | < D 3 ] sfx2
[ < Player | 4 no Block ] -> [ < Player | < E 4 ] sfx2
[ < Player | 5 no Block ] -> [ < Player | < F 5 ] sfx2

( player can push blocks, even multiple blocks in a row )
[ > Player | Block ] -> [ > Player | > Block ] sfx0
[ > Block | Block ] -> [ > Block | > Block ]

( target eats block )
late [ A 0 ] -> [ 0 ] sfx1
late [ B 1 ] -> [ 1 ] sfx1
late [ C 2 ] -> [ 2 ] sfx1
late [ D 3 ] -> [ 3 ] sfx1
late [ E 4 ] -> [ 4 ] sfx1
late [ F 5 ] -> [ 5 ] sfx1

==============
WINCONDITIONS
==============

No Block

=======     
LEVELS
=======

(
message go forth
message tread carefully
message have courage
message return not along the path, or face infinite wrath
message dance with your body, not with your mind
message the long way, it makes or breaks
message the source it blocks, quietly I mock
)

(
#############
#...........#
#...........#
########....#
##.....##...#
#.p.0.f.#...#
#..###..#...#
#.2###..#...#
#..###..#...#
#.a.5.c.#...#
##.....##...#
########....#
#############
)
(
(basically stolen from heroes of sokobon)
##########
#....#...#
#..c.#2..#
#p.cc....#
#..c.#...#
#....#...#
##########
)

message Level 1 of 8

(push the block to the target)
#########
#.......#
#.p.a.0.#
#.......#
#########

message Level 2 of 8

(practice moving)
########
#####2.#
#..#5..#
#p.##f.#
#.a##.##
#.....0#
########

message Level 3 of 8
(linear path)
#######
#...5.#
###f#.#
#.5...#
#.#f###
#...5p#
###.###
###5###
#######


message Level 4 of 8

(you can pull blocks out of targets)
########
#...c.2#
#5####.#
#.####.#
#f##...#
#.##.#.#
#p#0.a.#
########

(

message Level 5 of 10

(avoid pulling blocks)
#########
#.......#
#.0apc2.#
#########

message Level 6 of 10

(push multiple blocks)
###########
#.........#
#.p.a.a.0.#
#.........#
###########
)

(redundant
(push multiple blocks)
#########
#.0.....#
#2##....#
#.#pcd..#
#..a....#
#..f..3##
#....5###
#....####
#########
)

message Level 5 of 8

(dodging pulls)
########
#0.2.4.#
#p1.3f5#
########

message Level 6 of 8

(back hallway)
#########
#...f.5.#
#...###.#
#.p.###.#
#...###.#
#...a.0.#
#########


(tedious
(swap blocks)
########
#......#
#p0.c..#
#......#
#####.##
#####4##
#####.##
#......#
#.2.a..#
#......#
########
)

(maybe something there, but not interesting enough
########
##....p#
##a###.#
##e###.#
##a###.#
#4eaea.#
##0#####
########
)

(too complicated
###########
#42...#####
##....#####
##.#####.4#
##.#####..#
##.###....#
##3###....#
#.p.....###
#.a.....a0#
###########
)

(no clear lesson
##########
##....4###
##..a.2###
##.###e###
#........#
#.....p.0#
#........#
##########
)

message Level 7 of 8

(learn about blocking targets)
##########
####.....#
####.#.#2#
#0.#.#.#c#
#.pa.5...#
#..#######
##########


(no solution???
(deliberately block the target)
########
######2#
###....#
###.##p#
###.##c#
###...0#
#####..#
#...##.#
#4..e..#
########
)

message Level 8 of 8
(deliberately pull a block and block a target)
#######
##...##
#0ap5.#
#.#.c.#
#...2.#
#######

(too many moves
(must finish in dead end)
#########
#5#..3..#
#0#.df..#
#.#.fd..#
#.#..ca.#
#..p.ac.#
#.....2.#
#########
)
(
(bleh)
#########
#5..#..5#
#...c.p.#
#..###..#
##c#2#c##
#..#.#..#
#..f.f..#
#.......#
#########
)
message All cleaned up!

(unfocused
(deliberately block the target... again?)
##########
########2#
#....p.#c#
#..c.0...#
#.....4#.#
##.##.##.#
##.e.....#
##..######
##########
)

(try out?
#########
#5#..3..#
#0#.fd..#
#.#.df..#
#.#..ca.#
#..p.ac.#
#.....2.#
#########
)