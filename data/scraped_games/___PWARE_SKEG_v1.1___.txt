title ~~ PWARE SKEG v1.1 ~~
author Brian Stottler       â– 
homepage www.puzzlescript.net

text_color red
background_color white

noaction
noundo

========
OBJECTS
========

Background 
Gray

Wall 
White

OneNormal
Red
.....
.....
..0..
.....
.....

TwoNormal
Red DarkRed
.....
.000.
.010.
.000.
.....

ThreeNormal
Red DarkRed
00000
01110
01110
01110
00000

Dead
Red
.0...
...00
.0.0.
0....
..00.

OneKey
Yellow
.....
.....
..0..
.....
.....

TwoKey
Yellow Brown
.....
.000.
.010.
.000.
.....

ThreeKey
Yellow Brown
00000
01110
01110
01110
00000

DeadKey
Yellow
.0...
...00
.0.0.
0....
..00.

OneGate
Gray Black
00000
00000
00100
00000
00000

TwoGate
Gray Black
00000
01110
01010
01110
00000

ThreeGate
Gray Black
11111
10001
10001
10001
11111

Plus
Orange
.....
..0..
.000.
..0..
.....

Minus
Yellow
.....
.....
.000.
.....
.....

Exit
LightGreen
..0..
.0.0.
0...0
.0.0.
..0..

Key
Yellow
.....
000..
0.000
000.0
.....

LockedDoor
DarkGray Brown Black
00000
02220
00200
00200
00000

ForceLeft
Blue
.....
.0...
00000
.0...
.....

ForceRight
Blue
.....
...0.
00000
...0.
.....

ForceUp
Blue
..0..
.000.
..0..
..0..
..0..

ForceDown
Blue
..0..
..0..
..0..
.000.
..0..

=======
LEGEND
=======

. = Background
# = Wall
X = Exit
& = LockedDoor
* = Key

One = OneNormal or OneKey
Two = TwoNormal or TwoKey
Three = ThreeNormal or ThreeKey

NotOne = Two or Three
NotTwo = One or Three
NotThree = One or Two

Player = One or Two or Three

1 = OneNormal
2 = TwoNormal
3 = ThreeNormal

A = OneGate
B = TwoGate
C = ThreeGate

+ = Plus
- = Minus

< = ForceLeft
> = ForceRight
^ = ForceUp
v = ForceDown

=======
SOUNDS
=======

sfx0 41614303 (Death)
sfx1 71003703 (Grow)
sfx2 12761303 (Shrink)
sfx3 43935500 (Key pickup)
sfx4 76144502 (Door unlock)

================
COLLISIONLAYERS
================

Background
ForceLeft, ForceRight, ForceUp, ForceDown
OneGate, TwoGate, ThreeGate, Plus, Minus, Exit, Key
Player, Dead, DeadKey, Wall, LockedDoor

======
RULES     
======     

(Gate size rules)
[> NotOne|OneGate] -> [NotOne|OneGate]
[> NotTwo|TwoGate] -> [NotTwo|TwoGate]
[> NotThree|ThreeGate] -> [NotThree|ThreeGate]

(Grow rules)
late [OneNormal Plus] -> [TwoNormal] sfx1
late [TwoNormal Plus] -> [ThreeNormal] sfx1
late [ThreeNormal Plus] -> [Dead] sfx0
late [OneKey Plus] -> [TwoKey] sfx1
late [TwoKey Plus] -> [ThreeKey] sfx1
late [ThreeKey Plus] -> [DeadKey] sfx0

(Shrink rules)
late [ThreeNormal Minus] -> [TwoNormal] sfx2
late [TwoNormal Minus] -> [OneNormal] sfx2
late [OneNormal Minus] -> [Dead] sfx0
late [ThreeKey Minus] -> [TwoKey] sfx2
late [TwoKey Minus] -> [OneKey] sfx2
late [OneKey Minus] -> [DeadKey] sfx0

(Key pickup rules)

late [OneNormal Key] -> [OneKey] sfx3
late [TwoNormal Key] -> [TwoKey] sfx3
late [ThreeNormal Key] -> [ThreeKey] sfx3

(Unlock rules)
[> OneKey|LockedDoor] -> [> OneNormal|] sfx4
[> TwoKey|LockedDoor] -> [> TwoNormal|] sfx4
[> ThreeKey|LockedDoor] -> [> ThreeNormal|] sfx4

(Force rules)
[RIGHT Player ForceLeft] -> [Player ForceLeft]
[LEFT Player ForceRight] -> [Player ForceRight]
[DOWN Player ForceUp] -> [Player ForceUp]
[UP Player ForceDown] -> [Player ForceDown]
RIGHT [RIGHT Player|ForceLeft] -> [Player|ForceLeft]
LEFT [LEFT Player|ForceRight] -> [Player|ForceRight]
DOWN [DOWN Player|ForceUp] -> [Player|ForceUp]
UP [UP Player|ForceDown] -> [Player|ForceDown]

==============
WINCONDITIONS
==============

Some Player (the player must remain alive)
All Player on Exit

=======     
LEVELS
=======

(Test level
###########
#..A...*++#
#..#....++#
#2.B..>..X#
#..#....--#
#..&..<.--#
###########
)

##############
#1..++-+--..X#
##############

########
#..++..#
#.####.#
#2####X#
#.####.#
#..--..#
########

##########
#.A.#.C..#
#.#.#.#..#
#1B.+.A.X#
#.#.#.#..#
#.C.#.B..#
##########

#########
#C...-#+#
#.#.###.#
#.#2A...#
#.#.#.#.#
#X#.C.#+#
#########

(Trinity)
###########
####.+.&&X#
####B#C####
#-.C...A.+#
#*##.1.#*##
#-.>...<.+#
###########

(Loop)
#########
##-.....#
###.#+#.#
##*.#-#.#
###^###.#
#&&2A...#
#&#^#C#B#
#X#.*.#*#
#########

Message That's it for now. Hopefully I'll keep adding more.