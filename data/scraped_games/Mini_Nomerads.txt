title Mini Nomerads
author Dan Williams
homepage www.activeupgames.com

========
OBJECTS
========

Background
green #11aa11
00000
01000
00000
00010
00000

Target
DarkBlue
.0.0.
0...0
.....
0...0
.0.0.

Wall
BROWN orange darkbrown green
31113
10002
10002
10002
32223

Player
Black red White Blue
.222.
.020.
11111
.333.
.3.3.

ball
lightblue blue darkblue
.011.
01111
11111
11112
.112.

redball
yellow orange
..0..
.000.
00100
.000.
..0..

ballright
transparent

balldown
transparent

ballleft
transparent

ballup
transparent

rublock
white lightgray gray
0....
01...
011..
0111.
12222

rdblock
white lightgray gray
00001
0112.
012..
02...
1....

ldblock
white lightgray gray
00001
.1112
..112
...12
....2

lublock
white lightgray gray
....1
...02
..012
.0112
12222

srblock
pink #ffcccc #ff5599
1....
10...
102..
12...
2....

sdblock
pink #ffcccc #ff5599
11110
.002.
..2..
.....
.....

slblock
pink #ffcccc #ff5599
....0
...12
..102
...02
....2

sublock
pink #ffcccc #ff5599
.....
.....
..1..
.100.
12222

rugen
darkgray gray
.111.
10001
10101
10001
01110

rdgen
darkgray gray
.111.
10001
10101
10001
01110

ldgen
darkgray gray
.111.
10001
10101
10001
01110

lugen
darkgray gray
.111.
10001
10101
10001
01110

srgen
darkgray gray
.111.
10001
10101
10001
01110

sdgen
darkgray gray
.111.
10001
10101
10001
01110

slgen
darkgray gray
.111.
10001
10101
10001
01110

sugen
darkgray gray
.111.
10001
10101
10001
01110

rugenbg
yellow
0....
.....
0.0..
.....
0.0.0

rdgenbg
yellow
0.0.0
.....
0.0..
.....
0....

ldgenbg
yellow
0.0.0
.....
..0.0
.....
....0

lugenbg
yellow
....0
.....
..0.0
.....
0.0.0

srgenbg
yellow
0....
.....
0.0..
.....
0....

sdgenbg
yellow
0.0.0
.....
..0..
.....
.....

slgenbg
yellow
....0
.....
..0.0
.....
....0

sugenbg
yellow
.....
.....
..0..
.....
0.0.0

stopper
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player
O = Target
* = ball
X = redball
@ = redball and Target
1 = rublock
2 = rdblock
3 = ldblock
4 = lublock
5 = srblock
6 = sdblock
7 = slblock
8 = sublock
a = rugen
b = rdgen
c = ldgen
d = lugen
e = srgen
f = sdgen
g = slgen
h = sugen
q = rugenbg
w = rdgenbg
v = ldgenbg
r = lugenbg
t = srgenbg
y = sdgenbg
u = slgenbg
i = sugenbg
block = rublock or rdblock or lublock or ldblock or srblock or sdblock or slblock or sublock
balldir = ballright or balldown or ballleft or ballup
genblock = rugen or rdgen or ldgen or lugen or srgen or sdgen or slgen or sugen
bg = rugenbg or rdgenbg or ldgenbg or lugenbg or srgenbg or sdgenbg or slgenbg or sugenbg

=======
SOUNDS
=======

player MOVE 29986507
block create 8026309
genblock create 8026309
startgame 50407900
restart 90013309
closemessage 35794303

undo 21150709
sfx0 6213103 (redirect)
sfx1 55324302 (split)
sfx2 24543300 (get redball on target)
sfx3 58317102 (destroy block)
sfx4 21946837 (push block)
sfx5 77326104 (ball thud against wall)

================
COLLISIONLAYERS
================

Background
bg
Target, block, genblock
Player, Wall, ball, redball
balldir
stopper

======
RULES
======

[ > Player | block ] -> [ > Player | > block ] sfx4
[ > Player | ball ] -> [ > Player | > ball ] sfx4


([ ball target ] -> [ redball target ])

[ ball | redball ] -> [ redball | redball ] sfx2

[ block wall ] -> [ wall ] sfx3


(ball movement)
[UP    ball]  -> [ball ballup]
[DOWN  ball]  -> [ball balldown]
[LEFT  ball]  -> [ball ballleft]
[RIGHT ball]  -> [ball ballright]

UP  [ ball ballup no stopper | no ball no Wall ] ->     [ |  ballup stopper    ball ] again
DOWN  [ ball balldown no stopper | no ball no Wall ] -> [ |  balldown stopper  ball ] again
LEFT  [ ball ballleft no stopper | no ball no Wall ] -> [ |  ballleft stopper  ball ] again
RIGHT [ ball ballright no stopper| no ball no Wall ] -> [ |  ballright stopper ball ] again

(ball collision)
startloop
vertical [ | ballright slblock | ] -> [ ball balldown | no ball slblock | ball ballup ] sfx1
horizontal [ | balldown sublock | ] -> [ ball ballright | no ball sublock | ball ballleft ] sfx1
vertical [ | ballleft srblock | ] -> [ ball balldown | no ball srblock | ball ballup ] sfx1
horizontal [ | ballup sdblock | ] -> [ ball ballright | no ball sdblock | ball ballleft ] sfx1
endloop

[ ballright lublock ] -> [ ballup lublock ] sfx0
[ ballright ldblock ] -> [ ldblock balldown ] sfx0

[ balldown rublock ] -> [ ballright rublock ] sfx0
[ balldown lublock ] -> [ ballleft lublock ] sfx0

[ ballleft rublock ] -> [ ballup rublock ] sfx0
[ ballleft rdblock ] -> [ balldown rdblock ] sfx0

[ ballup rdblock ] -> [ ballright rdblock ] sfx0
[ ballup ldblock ] -> [ ballleft ldblock ] sfx0





[ > ballleft ] -> [ > ballleft ]
[ > ballright ] -> [ > ballright ]

(block generation)
late up [ player rugen | no block ] -> [ player rugen | rublock ] 
late up [ player rdgen | no block ] -> [ player rdgen | rdblock ] 
late up [ player ldgen | no block ] -> [ player ldgen | ldblock ] 
late up [ player lugen | no block ] -> [ player lugen | lublock ] 
late up [ player srgen | no block ] -> [ player srgen | srblock ] 
late up [ player sdgen | no block ] -> [ player sdgen | sdblock ] 
late up [ player slgen | no block ] -> [ player slgen | slblock ] 
late up [ player sugen | no block ] -> [ player sugen | sublock ] 


[stopper]->[]


==============
WINCONDITIONS
==============

All redball on Target
All target on redball

=======
LEVELS
=======

message Video games.

Message Push the blue ball. Blue balls stick to yellow targets and bounce off of blocks. You can push the blocks and generate them. Push blocks into walls to get rid of them.

Message Level 1 of 9


###########
#.........#
#.p.*..O@.#
#.........#
###########


Message Level 2 of 9

###########
#.........#
#.p.*..3..#
#.........#
#......O..#
#......@..#
###########

message Level 3 of 9

###########
#.........#
#.p.*...3.#
#.........#
#....O.4..#
#....@....#
###########

message Level 4 of 9

###########
#......@..#
#......O..#
#.p.*.....#
#..r......#
#..d......#
###########

message Level 5 of 9

#############
#......O....#
#......@....#
#...........#
#.p.*.......#
#..q.w.v.r..#
#..a.b.c.d..#
#############

message Level 6 of 9

#############
#...@O......#
#....O......#
#...........#
#.p.*...7...#
#...........#
#..q.w.v.r..#
#..a.b.c.d..#
#############

Message Level 7 of 9

###################
#.................#
#........o........#
#.......o@o.......#
#........o........#
#.................#
#.p.*.............#
#.................#
#.q.w.v.r.t.y.u.i.#
#.a.b.c.d.e.f.g.h.#
###################

Message Level 8 of 9

###################
#.................#
#.................#
#........@oo......#
#........oo.......#
#.................#
#.p.*.............#
#.................#
#.q.w.v.r.t.y.u.i.#
#.a.b.c.d.e.f.g.h.#
###################

Message Level 9 of 9

#######################
#.....................#
#.....................#
#.....................#
#.........ooo.........#
#.........o@o.........#
#.........ooo.........#
#.....................#
#...p.*...............#
#.....................#
#...q.w.v.r.t.y.u.i...#
#...a.b.c.d.e.f.g.h...#
#######################

Message You win! There will be a full version of this game coming out eventually. Follow @activeupgames if you want to know when that happens. I love video games!




