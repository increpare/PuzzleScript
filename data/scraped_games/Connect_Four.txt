title Connect Four
author Meow
again_interval 0.1
========
OBJECTS
========
Tile .
YELLOW GRAY
00000
01111
01111
01111
01111
In +
YELLOW GRAY LIGHTGREEN
02222
01111
01111
01111
01111
Out
YELLOW LIGHTGREEN
00000
01111
01111
01111
01111
CursorR
LIGHTRED
.....
.0.0.
....0
.0...
..0.0
CursorB
LIGHTBLUE
.....
.0.0.
....0
.0...
..0.0
Red
RED
.....
.....
..00.
..00.
.....
Blue
BLUE
.....
.....
..00.
..00.
.....
Mark
LIGHTRED LIGHTBLUE
.....
.0101
.1..0
.0..1
.1010
Diag
WHITE
Floor
WHITE
HiddenP
WHITE
.....
.....
.....
.....
.....
Winrar
WHITE
.....
.....
.....
.....
.....
=======
LEGEND
=======
Background=Tile or In or Out
Player=CursorR or CursorB or HiddenP or Winrar
Checker=Red or Blue
@=CursorR and in
-=Out and Floor
_=Tile and Floor
=======
SOUNDS
=======

sfx0 89314907 (place)
sfx1 4755704  (pop)
sfx2 35997900 (win)


================
COLLISIONLAYERS
================
Floor
Diag
Background
Mark
Red Blue
Player
======
RULES
======




(no selecting blank tiles)
[> Player|...|In]->[|...|Player In]
[> Player|...|Out]->[|...|Player Out]
[> Player]->[Player]

(place checkers)
[action CursorR In no checker]->[CursorB Red In] sfx0
[action CursorB In no checker]->[CursorR Blue In] sfx0
(pop checkers)
[action CursorR Out Red]->[CursorB Out] sfx1
[action CursorB Out Blue]->[CursorR Out] sfx1

(gravity)
down [Checker|no checker]->[down Checker|] again

(win detection (orthogonal))
[red|red|red|red]->[mark red|mark red|mark red|mark red]
[blue|blue|blue|blue]->[mark blue|mark blue|mark blue|mark blue]

(win detection (diagonal) (please optimize me))
(pretty please D: )
[red]->[red down Diag]
[Diag Floor]->[Floor]
down [> Diag|]->[|right Diag]
[Diag Floor]->[Floor]
right [> Diag|red]->[down diag|red]
[right Diag]->[]
down [> Diag|]->[|right Diag]
[Diag Floor]->[Floor]
right [> Diag||red]->[down diag||red]
[right Diag]->[]
down [> Diag|]->[|right Diag]
right [Diag|||red]->[down diag|||red]
[right Diag]->[]
right[down Diag|||]->[up Diag|||mark]
up [> Diag|]->[|right Diag]
right[> Diag||]->[up Diag||mark]
up [> Diag|]->[|right Diag]
right[> Diag|]->[up Diag|mark]
up [> Diag|]->[|Mark]

[red]->[red down Diag]
[Diag Floor]->[Floor]
down [> Diag|]->[|left Diag]
[Diag Floor]->[Floor]
left [> Diag|red]->[down diag|red]
[left Diag]->[]
down [> Diag|]->[|left Diag]
[Diag Floor]->[Floor]
left [> Diag||red]->[down diag||red]
[left Diag]->[]
down [> Diag|]->[|left Diag]
left [Diag|||red]->[down diag|||red]
[left Diag]->[]
left[down Diag|||]->[up Diag|||mark]
up [> Diag|]->[|left Diag]
left[> Diag||]->[up Diag||mark]
up [> Diag|]->[|left Diag]
left[> Diag|]->[up Diag|mark]
up [> Diag|]->[|Mark]

[blue]->[blue down Diag]
[Diag Floor]->[Floor]
down [> Diag|]->[|right Diag]
[Diag Floor]->[Floor]
right [> Diag|blue]->[down diag|blue]
[right Diag]->[]
down [> Diag|]->[|right Diag]
[Diag Floor]->[Floor]
right [> Diag||blue]->[down diag||blue]
[right Diag]->[]
down [> Diag|]->[|right Diag]
right [Diag|||blue]->[down diag|||blue]
[right Diag]->[]
right[down Diag|||]->[up Diag|||mark]
up [> Diag|]->[|right Diag]
right[> Diag||]->[up Diag||mark]
up [> Diag|]->[|right Diag]
right[> Diag|]->[up Diag|mark]
up [> Diag|]->[|Mark]

[blue]->[blue down Diag]
[Diag Floor]->[Floor]
down [> Diag|]->[|left Diag]
[Diag Floor]->[Floor]
left [> Diag|blue]->[down diag|blue]
[left Diag]->[]
down [> Diag|]->[|left Diag]
[Diag Floor]->[Floor]
left [> Diag||blue]->[down diag||blue]
[left Diag]->[]
down [> Diag|]->[|left Diag]
left [Diag|||blue]->[down diag|||blue]
[left Diag]->[]
left[down Diag|||]->[up Diag|||mark]
up [> Diag|]->[|left Diag]
left[> Diag||]->[up Diag||mark]
up [> Diag|]->[|left Diag]
left[> Diag|]->[up Diag|mark]
up [> Diag|]->[|Mark]

(checkers can't be moving to win)
[moving checker][mark]->[moving checker][]

(hide cursor/win)
late [Mark][CursorR]->[Mark][HiddenP] sfx2
late [Mark][CursorB]->[Mark][HiddenP] sfx2
[Action HiddenP]->[Winrar]

==============
WINCONDITIONS
==============
some Winrar
(you are the winrar)
=======
LEVELS
=======

@++++++
.......
.......
.......
.......
_______

@++++++
.......
.......
.......
.......
-------