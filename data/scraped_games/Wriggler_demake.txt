title Wriggler demake
author @_ardeej
homepage http://www.clickmazes.com/indexw.htm

require_player_movement
noundo
noaction
key_repeat_interval 0.2
run_rules_on_level_start



========
OBJECTS
========

Floor
#BBC8BB #DDDDDD
00001
00010
00100
01000
10000

Wall
#2F484E #999988
00000
01000
00100
00010
00000



RLinkU
#DD6666
..0..
.....
.....
.....
.....

RLinkD
#DD6666
.....
.....
.....
.....
..0..


RLinkL
#DD6666
.....
.....
0....
.....
.....

RLinkR
#DD6666
.....
.....
....0
.....
.....



BLinkU
#6666DD
..0..
.....
.....
.....
.....

BLinkD
#6666DD
.....
.....
.....
.....
..0..


BLinkL
#6666DD
.....
.....
0....
.....
.....

BLinkR
#6666DD
.....
.....
....0
.....
.....



RA
#DD2222
.....
.000.
.0.0.
.000.
.....

RB
#EEAD00
.....
.000.
.0.0.
.000.
.....

R1
#DD2222
.....
.000.
.0.0.
.000.
.....

R2
#DD2222
.....
.000.
.0.0.
.000.
.....

R3
#DD2222
.....
.000.
.0.0.
.000.
.....

R4
#DD2222
.....
.000.
.0.0.
.000.
.....

R5
#DD2222
.....
.000.
.0.0.
.000.
.....

R6
#DD2222
.....
.000.
.0.0.
.000.
.....

R7
#DD2222
.....
.000.
.0.0.
.000.
.....

R8
#DD2222
.....
.000.
.0.0.
.000.
.....



B0
#2222DD
.....
.000.
.0.0.
.000.
.....

B1
#2222DD
.....
.000.
.0.0.
.000.
.....

B2
#2222DD
.....
.000.
.0.0.
.000.
.....

B3
#2222DD
.....
.000.
.0.0.
.000.
.....

B4
#2222DD
.....
.000.
.0.0.
.000.
.....

B5
#2222DD
.....
.000.
.0.0.
.000.
.....

B6
#2222DD
.....
.000.
.0.0.
.000.
.....

B7
#2222DD
.....
.000.
.0.0.
.000.
.....

B8
#2222DD
.....
.000.
.0.0.
.000.
.....



Rtarget
#C8B0B0 #DD5555 #DDDDDD
00002
00020
00100
02000
20000

Btarget
#BBBBC8 #5555DD #DDDDDD
00002
00020
00100
02000
20000

Mtarget
#CCD0A0 #FFBE55 #DDDDDD
00002
00020
00100
02000
20000



Player
#222222
..0..
.....
0...0
.....
..0..



Temp
#000000
.....
.....
.....
.....
.....



STemp
#000000
.....
.....
.....
.....
.....



=======
LEGEND
=======

Target = Rtarget or Btarget or Mtarget
Background = Floor or Target

R0 = RA or RB

Redball   = RA or R1 or R2 or R3 or R4 or R5 or R6 or R7 or R8
Blueball  = B0 or B1 or B2 or B3 or B4 or B5 or B6 or B7 or B8
Ball      = Redball or RB or Blueball

RLink = RLinkU or RLinkD or RLinkL or RLinkR
BLink = BLinkU or BLinkD or BLinkL or BLinkR
Link = RLink or BLink



, = Player and Floor
. = Floor
# = Wall and Floor

+ = Rtarget
- = Btarget
* = Mtarget

: = Rtarget and Player
; = Btarget and Player

A = RA and Btarget
B = R1 and Btarget
C = R2 and Btarget
D = R3 and Btarget
E = R4 and Btarget
F = R5 and Btarget
G = R6 and Btarget
H = R7 and Btarget
I = R8 and Btarget

J = B0 and Rtarget
K = B1 and Rtarget
L = B2 and Rtarget
M = B3 and Rtarget
N = B4 and Rtarget
O = B5 and Rtarget
P = B6 and Rtarget
Q = B7 and Rtarget
R = B8 and Rtarget

S = RA and Floor
T = R1 and Floor
U = R2 and Floor
W = R3 and Floor
X = R4 and Floor
Y = R5 and Floor
Z = R6 and Floor
Ä = R7 and Floor
Ö = R8 and Floor

0 = B0 and Floor
1 = B1 and Floor
2 = B2 and Floor
3 = B3 and Floor
4 = B4 and Floor
5 = B5 and Floor
6 = B6 and Floor
7 = B7 and Floor
8 = B8 and Floor

9 = RB and Floor
¤ = RB and Mtarget

{ = RB and Rtarget
} = RB and Btarget

[ = RA and Mtarget
] = B0 and Mtarget



=======
SOUNDS
=======

(wriggler moving sound)
SFX0 66585707 


================
COLLISIONLAYERS
================

Background
Ball, Wall
RlinkU, BlinkU
RlinkD, BlinkD
RlinkR, BlinkR, Stemp
RlinkL, BlinkL, Temp
Player



======
RULES
======

(movement)

[ > Player | Wall ] -> [ Player | Wall ]

[ moving Player R0 ] -> [ moving Player moving R0 ]
[ moving Player R8 ] -> [ moving Player moving R8 ]

(these rules are for chopped wrigglers such that, for example, R0 R1 R2 and R4 R5 R6 act separately)

[ R1 no Temp ] -> [ R1 Temp ]
[ R0 ] [ R1 Temp ] [ R2 ] -> [ R0 ] [ R1 no Temp ] [ R2 ]
[ moving Player R1 Temp ] -> [ moving Player moving R1 no Temp ]
[ R2 no Temp ] -> [ R2 Temp ]
[ R1 ] [ R2 Temp ] [ R3 ] -> [ R1 ] [ R2 no Temp ] [ R3 ]
[ moving Player R2 Temp ] -> [ moving Player moving R2 no Temp ]
[ R3 no Temp ] -> [ R3 Temp ]
[ R2 ] [ R3 Temp ] [ R4 ] -> [ R2 ] [ R3 no Temp ] [ R4 ]
[ moving Player R3 Temp ] -> [ moving Player moving R3 no Temp ]
[ R4 no Temp ] -> [ R4 Temp ]
[ R3 ] [ R4 Temp ] [ R5 ] -> [ R3 ] [ R4 no Temp ] [ R5 ]
[ moving Player R4 Temp ] -> [ moving Player moving R4 no Temp ]
[ R5 no Temp ] -> [ R5 Temp ]
[ R4 ] [ R5 Temp ] [ R6 ] -> [ R4 ] [ R5 no Temp ] [ R6 ]
[ moving Player R5 Temp ] -> [ moving Player moving R5 no Temp ]
[ R6 no Temp ] -> [ R6 Temp ]
[ R5 ] [ R6 Temp ] [ R7 ] -> [ R5 ] [ R6 no Temp ] [ R7 ]
[ moving Player R6 Temp ] -> [ moving Player moving R6 no Temp ]
[ R7 no Temp ] -> [ R7 Temp ]
[ R6 ] [ R7 Temp ] [ R8 ] -> [ R6 ] [ R7 no Temp ] [ R8 ]
[ moving Player R7 Temp ] -> [ moving Player moving R7 no Temp ]

[ moving Player B0 ] -> [ moving Player moving B0 ]
[ moving Player B8 ] -> [ moving Player moving B8 ]

[ B1 no Temp ] -> [ B1 Temp ]
[ B0 ] [ B1 Temp ] [ B2 ] -> [ B0 ] [ B1 no Temp ] [ B2 ]
[ moving Player B1 Temp ] -> [ moving Player moving B1 no Temp ]
[ B2 no Temp ] -> [ B2 Temp ]
[ B1 ] [ B2 Temp ] [ B3 ] -> [ B1 ] [ B2 no Temp ] [ B3 ]
[ moving Player B2 Temp ] -> [ moving Player moving B2 no Temp ]
[ B3 no Temp ] -> [ B3 Temp ]
[ B2 ] [ B3 Temp ] [ B4 ] -> [ B2 ] [ B3 no Temp ] [ B4 ]
[ moving Player B3 Temp ] -> [ moving Player moving B3 no Temp ]
[ B4 no Temp ] -> [ B4 Temp ]
[ B3 ] [ B4 Temp ] [ B5 ] -> [ B3 ] [ B4 no Temp ] [ B5 ]
[ moving Player B4 Temp ] -> [ moving Player moving B4 no Temp ]
[ B5 no Temp ] -> [ B5 Temp ]
[ B4 ] [ B5 Temp ] [ B6 ] -> [ B4 ] [ B5 no Temp ] [ B6 ]
[ moving Player B5 Temp ] -> [ moving Player moving B5 no Temp ]
[ B6 no Temp ] -> [ B6 Temp ]
[ B5 ] [ B6 Temp ] [ B7 ] -> [ B5 ] [ B6 no Temp ] [ B7 ]
[ moving Player B6 Temp ] -> [ moving Player moving B6 no Temp ]
[ B7 no Temp ] -> [ B7 Temp ]
[ B6 ] [ B7 Temp ] [ B8 ] -> [ B6 ] [ B7 no Temp ] [ B8 ]
[ moving Player B7 Temp ] -> [ moving Player moving B7 no Temp ]

[ Temp ] -> [ no Temp ]

[ R1 | moving R0 ] -> [ > R1 Temp | moving R0 ]
[ R2 | moving R1 ] -> [ > R2 Temp | moving R1 ]
[ R3 | moving R2 ] -> [ > R3 Temp | moving R2 ]
[ R4 | moving R3 ] -> [ > R4 Temp | moving R3 ]
[ R5 | moving R4 ] -> [ > R5 Temp | moving R4 ]
[ R6 | moving R5 ] -> [ > R6 Temp | moving R5 ]
[ R7 | moving R6 ] -> [ > R7 Temp | moving R6 ]
[ R8 | moving R7 ] -> [ > R8 Temp | moving R7 ]

[ R7 | moving R8 no Temp ] -> [ > R7 | moving R8 no Temp ]
[ R6 | moving R7 no Temp ] -> [ > R6 | moving R7 no Temp ]
[ R5 | moving R6 no Temp ] -> [ > R5 | moving R6 no Temp ]
[ R4 | moving R5 no Temp ] -> [ > R4 | moving R5 no Temp ]
[ R3 | moving R4 no Temp ] -> [ > R3 | moving R4 no Temp ]
[ R2 | moving R3 no Temp ] -> [ > R2 | moving R3 no Temp ]
[ R1 | moving R2 no Temp ] -> [ > R1 | moving R2 no Temp ]
[ R0 | moving R1 no Temp ] -> [ > R0 | moving R1 no Temp ]

[ Temp ] -> [ no Temp ]

[ B1 | moving B0 ] -> [ > B1 Temp | moving B0 ]
[ B2 | moving B1 ] -> [ > B2 Temp | moving B1 ]
[ B3 | moving B2 ] -> [ > B3 Temp | moving B2 ]
[ B4 | moving B3 ] -> [ > B4 Temp | moving B3 ]
[ B5 | moving B4 ] -> [ > B5 Temp | moving B4 ]
[ B6 | moving B5 ] -> [ > B6 Temp | moving B5 ]
[ B7 | moving B6 ] -> [ > B7 Temp | moving B6 ]
[ B8 | moving B7 ] -> [ > B8 Temp | moving B7 ]

[ B7 | moving B8 no Temp ] -> [ > B7 | moving B8 no Temp ]
[ B6 | moving B7 no Temp ] -> [ > B6 | moving B7 no Temp ]
[ B5 | moving B6 no Temp ] -> [ > B5 | moving B6 no Temp ]
[ B4 | moving B5 no Temp ] -> [ > B4 | moving B5 no Temp ]
[ B3 | moving B4 no Temp ] -> [ > B3 | moving B4 no Temp ]
[ B2 | moving B3 no Temp ] -> [ > B2 | moving B3 no Temp ]
[ B1 | moving B2 no Temp ] -> [ > B1 | moving B2 no Temp ]
[ B0 | moving B1 no Temp ] -> [ > B0 | moving B1 no Temp ]

[ Temp ] -> [ no Temp ]



(links)

[ Link ] -> [ no Link ]

(horizontal links)

late right [ R0 | R1 ] -> [ R0 RLinkR | R1 RLinkL ]
late right [ R1 | R2 ] -> [ R1 RLinkR | R2 RLinkL ]
late right [ R2 | R3 ] -> [ R2 RLinkR | R3 RLinkL ]
late right [ R3 | R4 ] -> [ R3 RLinkR | R4 RLinkL ]
late right [ R4 | R5 ] -> [ R4 RLinkR | R5 RLinkL ]
late right [ R5 | R6 ] -> [ R5 RLinkR | R6 RLinkL ]
late right [ R6 | R7 ] -> [ R6 RLinkR | R7 RLinkL ]
late right [ R7 | R8 ] -> [ R7 RLinkR | R8 RLinkL ]

late right [ R1 | R0 ] -> [ R1 RLinkR | R0 RLinkL ]
late right [ R2 | R1 ] -> [ R2 RLinkR | R1 RLinkL ]
late right [ R3 | R2 ] -> [ R3 RLinkR | R2 RLinkL ]
late right [ R4 | R3 ] -> [ R4 RLinkR | R3 RLinkL ]
late right [ R5 | R4 ] -> [ R5 RLinkR | R4 RLinkL ]
late right [ R6 | R5 ] -> [ R6 RLinkR | R5 RLinkL ]
late right [ R7 | R6 ] -> [ R7 RLinkR | R6 RLinkL ]
late right [ R8 | R7 ] -> [ R8 RLinkR | R7 RLinkL ]

late right [ B0 | B1 ] -> [ B0 BLinkR | B1 BLinkL ]
late right [ B1 | B2 ] -> [ B1 BLinkR | B2 BLinkL ]
late right [ B2 | B3 ] -> [ B2 BLinkR | B3 BLinkL ]
late right [ B3 | B4 ] -> [ B3 BLinkR | B4 BLinkL ]
late right [ B4 | B5 ] -> [ B4 BLinkR | B5 BLinkL ]
late right [ B5 | B6 ] -> [ B5 BLinkR | B6 BLinkL ]
late right [ B6 | B7 ] -> [ B6 BLinkR | B7 BLinkL ]
late right [ B7 | B8 ] -> [ B7 BLinkR | B8 BLinkL ]

late right [ B1 | B0 ] -> [ B1 BLinkR | B0 BLinkL ]
late right [ B2 | B1 ] -> [ B2 BLinkR | B1 BLinkL ]
late right [ B3 | B2 ] -> [ B3 BLinkR | B2 BLinkL ]
late right [ B4 | B3 ] -> [ B4 BLinkR | B3 BLinkL ]
late right [ B5 | B4 ] -> [ B5 BLinkR | B4 BLinkL ]
late right [ B6 | B5 ] -> [ B6 BLinkR | B5 BLinkL ]
late right [ B7 | B6 ] -> [ B7 BLinkR | B6 BLinkL ]
late right [ B8 | B7 ] -> [ B8 BLinkR | B7 BLinkL ]

(vertical links)

late down [ R0 | R1 ] -> [ R0 RLinkD | R1 RLinkU ]
late down [ R1 | R2 ] -> [ R1 RLinkD | R2 RLinkU ]
late down [ R2 | R3 ] -> [ R2 RLinkD | R3 RLinkU ]
late down [ R3 | R4 ] -> [ R3 RLinkD | R4 RLinkU ]
late down [ R4 | R5 ] -> [ R4 RLinkD | R5 RLinkU ]
late down [ R5 | R6 ] -> [ R5 RLinkD | R6 RLinkU ]
late down [ R6 | R7 ] -> [ R6 RLinkD | R7 RLinkU ]
late down [ R7 | R8 ] -> [ R7 RLinkD | R8 RLinkU ]

late down [ R1 | R0 ] -> [ R1 RLinkD | R0 RLinkU ]
late down [ R2 | R1 ] -> [ R2 RLinkD | R1 RLinkU ]
late down [ R3 | R2 ] -> [ R3 RLinkD | R2 RLinkU ]
late down [ R4 | R3 ] -> [ R4 RLinkD | R3 RLinkU ]
late down [ R5 | R4 ] -> [ R5 RLinkD | R4 RLinkU ]
late down [ R6 | R5 ] -> [ R6 RLinkD | R5 RLinkU ]
late down [ R7 | R6 ] -> [ R7 RLinkD | R6 RLinkU ]
late down [ R8 | R7 ] -> [ R8 RLinkD | R7 RLinkU ]

late down [ B0 | B1 ] -> [ B0 BLinkD | B1 BLinkU ]
late down [ B1 | B2 ] -> [ B1 BLinkD | B2 BLinkU ]
late down [ B2 | B3 ] -> [ B2 BLinkD | B3 BLinkU ]
late down [ B3 | B4 ] -> [ B3 BLinkD | B4 BLinkU ]
late down [ B4 | B5 ] -> [ B4 BLinkD | B5 BLinkU ]
late down [ B5 | B6 ] -> [ B5 BLinkD | B6 BLinkU ]
late down [ B6 | B7 ] -> [ B6 BLinkD | B7 BLinkU ]
late down [ B7 | B8 ] -> [ B7 BLinkD | B8 BLinkU ]

late down [ B1 | B0 ] -> [ B1 BLinkD | B0 BLinkU ]
late down [ B2 | B1 ] -> [ B2 BLinkD | B1 BLinkU ]
late down [ B3 | B2 ] -> [ B3 BLinkD | B2 BLinkU ]
late down [ B4 | B3 ] -> [ B4 BLinkD | B3 BLinkU ]
late down [ B5 | B4 ] -> [ B5 BLinkD | B4 BLinkU ]
late down [ B6 | B5 ] -> [ B6 BLinkD | B5 BLinkU ]
late down [ B7 | B6 ] -> [ B7 BLinkD | B6 BLinkU ]
late down [ B8 | B7 ] -> [ B8 BLinkD | B7 BLinkU ]



==============
WINCONDITIONS
==============

All Rtarget on Redball
All Btarget on Blueball
All Mtarget on RB



=======
LEVELS
=======

(    WRIGGLER LEGEND

  red target | A B C D E F G H I
 blue target | J K L M N O P Q R
  red floor  | S T U W X Y Z Ä Ö
 blue floor  | 0 1 2 3 4 8 6 7 8
 pink floor  | 9
)

(
 RA pinktarget [
 B0 pinktarget ]
 RB redtarget  {
 RB bluetarget }
)



message This is a demake of Andrea Gilbert's Wriggler puzzles. The originals are linked below.

message Drag the wriggler by its head so it covers the colored tiles.

message 1

#######
#0.#.-#
#1.#.-#
#2.,.-#
#3.#.-#
#4..#-#
#######

message Both ends are draggable.

message 2

##########
#0.###-#.#
#1##.,-#.#
#2.###-..#
#3....-###
#4####-..#
#5..#.-#.#
##########

message Only the ends can be dragged, stepping on other parts has no effect.

message 3

#######
##-#0.#
#.-.1.#
#.-.2.#
#,-#3##
#######

message The wriggler always moves with the cursor if it possibly can.

message 4

#########
##.....##
#--,#.01#
##.....##
#########

message Blue wrigglers go on blue tiles, red wrigglers on red tiles.

message 5

#######
#A.#.J#
#B.#.K#
#C.,.L#
#D.#.M#
#E..#N#
#######

message Sometimes orientation matters: orange head goes on orange tile.

message 6

##########
#9.###+#.#
#T##.,+#.#
#U.###+..#
#W....+###
#X####+..#
#Y..#.*#.#
##########

message You may have trouble letting things go.

message 7

#########
##}B#DE##
#...,...#
##MN#K]##
#########

message 8

########
##KLMNO#
#.J#.#P#
##A#,.Q#
##B..#H#
##CDEFG#
########

message 9

#########
##..#..##
#SBC,JK+#
##..#..##
#########

message 10

######
##,.##
#CNEL#
#BOFK#
#}PG]#
######

message 11

########
###ABJ##
##EDCK.#
#,.NML##
########

message 12

########
###01L##
##UWXM.#
#,.65N##
########

message 13

########
##CNE].#
#,BOFK##
##}PGL.#
########

message 14

#######
#CDEQP#
#B#.#O#
#}.,.N#
#G#.#M#
#HI]KL#
#######

message Thank you for playing!

















