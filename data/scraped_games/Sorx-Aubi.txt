(/.)

title Sorx-Aubi
author Ali Nikkhah
homepage twitter.com/_AliNikk__

run_rules_on_level_start

========
OBJECTS
========

Background
White
00000
00000
00000
00000
00000

Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
LightGrey
00000
00000
00000
00000
00000

NormalPlayer
Gray DarkGray
..0..
.111.
01.10
.111.
..0..

BrokenPlayer
Gray DarkGray
..0..
.111.
01.10
...1.
..0..

SorxCrate
LightRed
00000
0...0
0...0
0...0
00000

AubiCrate
LightBlue
00000
0...0
0...0
0...0
00000

SorxxCrate
Red LightRed
00000
00000
00000
00000
00000

AubiiCrate
Blue LightBlue
.000.
00000
00000
00000
.000.

AubiCorner1
Blue
....0
.....
.....
.....
.....

AubiCorner2
Blue
.....
.....
.....
.....
....0

AubiCorner3
Blue
.....
.....
.....
.....
0....

AubiCorner4
Blue
0....
.....
.....
.....
.....

=======
LEGEND
=======

Crate = SorxCrate or AubiCrate or SorxxCrate or AubiiCrate
Player = NormalPlayer or BrokenPlayer
Obstacle = Player or Crate or Wall
AubiCorner = AubiCorner1 or AubiCorner2 or AubiCorner3 or AubiCorner4

. = Background
# = Wall
P = NormalPlayer
q = BrokenPlayer
r = SorxCrate
b = AubiCrate
x = SorxxCrate
i = AubiiCrate
O = Target


=======
SOUNDS
=======

Crate MOVE 82713707
Player MOVE 8001307

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, SorxxCrate, SorxCrate, AubiiCrate, AubiCrate
AubiCorner1
AubiCorner2
AubiCorner3
AubiCorner4

======
RULES
======

(late DOWN [ GravityPlayerDown | no Obstacle ] -> again
DOWN [ GravityPlayerDown | no Obstacle ] -> [ > GravityPlayerDown | no Obstacle]
DOWN [ < GravityPlayerDown | no NormalPlayer] -> [ GravityPlayerDown | no NormalPlayer])

[ > NormalPlayer ] [ BrokenPlayer ] -> [ > NormalPlayer ] [ < BrokenPlayer ]

startloop

[ > Player | AubiCrate ] -> [ > Player | > AubiCrate ]

[ >  Player | AubiiCrate ] -> [  >  Player | > AubiiCrate  ] 
+ rigid [ moving AubiiCrate | AubiiCrate ] -> [ moving AubiiCrate | moving AubiiCrate ]
+ [ >  Crate | AubiiCrate ] -> [  >  Crate | > AubiiCrate  ] 

[ > Crate | AubiCrate ] -> [  > Crate | > AubiCrate ]

endloop

late [ AubiCrate | SorxxCrate ] -> [ SorxCrate | SorxxCrate] 
late [ SorxCrate | AubiiCrate ] -> [ AubiCrate | AubiiCrate]

[AubiCorner|] -> [|]

late [AubiCorner|] -> [|]

late RIGHT [AubiiCrate | AubiiCrate] -> [AubiiCrate AubiCorner1 AubiCorner2 | AubiiCrate AubiCorner3 AubiCorner4]

late DOWN [AubiiCrate | AubiiCrate] -> [AubiiCrate AubiCorner2 AubiCorner3 | AubiiCrate AubiCorner1 AubiCorner4]

==============
WINCONDITIONS
============== 

All Target on Player

=======
LEVELS
=======

message /.

message The goal of each level is to reach the little black square

message 1. Aubi

#########
#...o...#
#..bbb..#
#.bb.bb.#
#bb...bb#
#..i.i..#
#.i#i#i.#
#..ipi..#
#########

message 2. Frozen

#########
#p..i####
#..ii..##
#..ibii.#
#ii..bii#
#.iibi..#
##..iio##
####i.###
#########

message 3. Sorx

#########
#...o...#
#.......#
#x.....x#
#.b...b.#
#..r.x..#
#...b...#
#..xpr..#
#########

message 4. The Shore

#######
#.....#
#..p..#
#..i..#
#.....#
#r.r.r#
#.r.r.#
#r.r.r#
#.r.r.#
#r.o.r#
#######

message 5. The Switch

#######
#..P.x#
#.i.i.#
#i.i.b#
#.i.x.#
#..i.O#
#######

message 6. Soldiers

#########
#...P...#
#..i.i..#
#...i...#
#.x.x.x.#
#..r.r.r#
#.r.r.r.#
#x.x.xOx#
#########

message 8. Shift

#######
#.pb..#
#r#b..#
#.Or..#
#xrxi.#
#..i..#
#...i.#
#######

message 9. There and Back Again

##########
#........#
#........#
#..xrxx..#
#..x..xp.#
#..biox..#
#..x.ib..#
#..bi.x..#
#..xxrx..#
#........#
#........#
##########

message 10.The Prison

#############
#.....x.x.x.#
#....r.b.r.x#
#.ip..x.x.o.#
#......b.r..#
#.....x.x.x.#
#############

message 11. Upsilon

##########
#........#
#........#
#..ixbi..#
#..b..x..#
#..x.ox..#
#..x..b..#
#..ibxi..#
#........#
#p.......#
##########

message 12. The Quest

###########
##....r####
#...rr..###
#r.rr....##
#xoxr.r...#
####rrx...#
#.........#
#.ii..i.p.#
#.........#
###########

message 13. The Cries

#########
#...#####
#.o.#####
#ix.#####
#..r#####
#.b.x...#
#.o.x.p.#
#x..r..b#
##i######
#...#####
#.p.#####
#...#####
#########

message 14. The Unpredicted Problem

#############
#o....#....p#
#....b#b....#
#...i.#.b...#
#..b.....b..#
#####.X.#####
#....b..b..o#
#...b.#b....#
#.....#.....#
#p....#.....#
#############

message 15. The Failed Sequel

################
#.............p#
#............i.#
#.iiiiiiiiiiii.#
#..i.........i.#
#..i.iii.iii.i.#
#..i.i.bbb.i.i.#
#..i.i.iii.i.i.#
#..i.i.ioi.i.i.#
#..i.i.bbb.i.i.#
#..ibiiiiiiibi.#
#..ibb.bbb.bbi.#
#..iiiii.iiiii.#
#..............#
################

message 16. Dear Fibonacci

###########
#..x#.....#
#...r...i.#
#..i#..rx.#
#b#b#..rx.#
#p#p#..r..#
#######r###
#.........#
#.........#
#.........#
#.........#
#.........#
#....rxxxx# 
#xxxr.ro..#
#.o.r.r...#
#.........#
###########

message Thank you for playing! :)
