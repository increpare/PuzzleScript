title Number Friends -work in progress-
author Vi Hart
homepage vihart.com

background_color brown

text_color black
========
OBJECTS
========

Background 
white LIGHTBLUE DarkGrey
11111
00000
00000
00000
00000 

Line
white LIGHTBLUE LIGHTRED
11121
00020
00020
00020
00020 


Spiral 
white GRAY black lightblue BROWN
11332
40110
10002
41100
40010

Wall
white blue black
11111
12121
11111
12221
11111 


Player 
YELLOW BLACK
..0..
.000.
01010
00000
.1.1.

Nothing
white lightblue red
21112
00000
22222
20002
02220

x
white lightblue darkgray
11111
00000
22022
00200
22022

One
white lightblue DARKGRAY
11211
02200
00200
00200
02220

heavyOne
LIGHTGRAY DARKGRAY gray
11211
02200
00200
00200
02220

Two
white lightblue DARKGRAY
12221
00020
02220
02000
02220

heavyTwo
LIGHTGRAY DARKGRAY gray
12221
00020
02220
02000
02220

Three
white lightblue DARKGRAY
12221
00020
02220
00020
02220

heavyThree
LIGHTGRAY DARKGRAY gray
12221
00020
02220
00020
02220

Four
white lightblue DARKGRAY
12121
02020
02220
00020
00020

heavyFour
LIGHTGRAY DARKGRAY gray
12121
02020
02220
00020
00020

Five
white lightblue DARKGRAY
12221
02000
02220
00020
02220

heavyfive
LIGHTGRAY DARKGRAY gray
12221
02000
02220
00020
02220

six
white lightblue DARKGRAY
12221
02000
02220
02020
02220

seven
white lightblue DARKGRAY
12221
02020
00020
00200
00200

heavyseven
LIGHTGRAY DARKGRAY gray
12221
02020
00020
00200
00200

eight
white LIGHTBLUE DARKGRAY
12221
20002
02220
20002
02220

heavyeight
LIGHTGRAY DARKGRAY gray
12221
20002
02220
20002
02220


ten
white LIGHTBLUE DARKGRAY
21222
20202
20202
20202
20222

n
white

o
black

=======
LEGEND
=======

. = Background
` = Line
$ = Spiral
# = Wall
P = Player
1 = One
2 = Two
3 = Three
4 = Four
5 = Five
6 = six
7 = Seven
8 = eight
t = ten
a = heavyone
b = heavyTwo
c = heavythree
d = heavyfour
e = heavyfive
g = heavyseven
h = heavyeight
moveable = x or one or two or three or four or five or six or seven or eight or ten
number = x or One or Two or Three or Four or five or six or Seven or eight or ten or heavyone or heavyTwo or heavythree or heavyFour or heavyfive or heavyseven or heavyeight

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

n, o 

Background
Player, Wall, Line, Spiral, nothing, number
======
RULES     
======     
 

[nothing] -> [o]
[n] -> [o]
[ ] -> [o]
[number o] -> [number n]

[ >  Player | moveable ] -> [  >  Player | > moveable ] 

late[number o] -> [number n]
late[player n] -> [player o]
late[ten||||||||||n] -> [nothing n||||||||||n]
late[eight||||||||n] -> [nothing n||||||||n]
late[seven|||||||n] -> [nothing n|||||||n]
late[six||||||n] -> [nothing n||||||n]
late[five|||||n] -> [nothing n|||||n]
late[four||||n] -> [nothing n||||n]
late[three|||n] -> [nothing n|||n]
late[two||n] -> [nothing n||n]
late[one|n] -> [nothing n|n]
late[heavyseven||||||||n] -> [nothing n||||||||n]
late[heavyseven|||||||n] -> [nothing n|||||||n]
late[heavyfive||||||n] -> [nothing n||||||n]
late[heavyFour||||n] -> [nothing n||||n]
late[heavythree|||n] -> [nothing n|||n]
late[heavytwo||n] -> [nothing n||n]
late[heavyone|n] -> [nothing n|n]



==============
WINCONDITIONS
==============

No number

=======     
LEVELS
=======



message Hi, ready to make numbers be friends?
$`...........
$`...........
$`...........
$`P.1.1......
$`...........
$`...........
$`...........

message Yay! You made numbers be friends! :D
message For this next one, emember you can press z to undo or r to restart.
$`....1.....
$`..........
$`..........
$`P.1.1.....
$`..........
$`..........
$`.........1

message Now you've got it! Off we go!

$`..........
$`..........
$`..1.......
$`P...1.....
$`..........
$`...1......
$`..........

$`..........
$`..........
$`..........
$`P.2..2....
$`..........
$`..........
$`..........

$`..........
$`..........
$`..........
$`P.22......
$`..........
$`..........
$`..........

$`..........
$`..........
$`..2.......
$`P.22......
$`..........
$`..........
$`..........


$`..........
$`..........
$`.1........
$`P.22......
$`..........
$`..........
$`..........


$`.....2....
$`..2......2
$`.2...2...2
$`P..2......
$`..2.......
$`22.....22.
$`...2.....2

$`..........
$`..........
$`..3.......
$`.3P3......
$`..3.......
$`..........
$`..........

$`2.........
$`..........
$`..........
$`P.........
$`..3.3.....
$`..........
$`..3.......

$`1........1
$`..........
$`.....1....
$`....1.3...
$`.....3P3.1
$`......3...
$`1......1.1


$`1...32....
$`..2...33.2
$`32...2...2
$`P..2...1..
$`..2.1...1.
$`22.....22.
$`3..23.1..2

$`.1.34....34
$`...........
$`...........
$`P.2........
$`...........
$`...........
$`23.4.32..2.

$`......#####
$`......#####
$`.44...#####
$`P...#..444#
$`.44...#####
$`......#####
$`......#####

$`....4######
$`.....#.....
$`....4#.....
$`4.P..#.....
$`.....#.....
$`.....#.4...
$`#..........

$`1.1.1.1.1.1
$`.1.1.1.1.1.
$`1.1.1.1.1.1
$`.1.1p1.1.1.
$`1.1.1.1.1.1
$`.1.1.1.1.1.
$`1.1.1.1.1.1

(
$`2...2.#p..#
$`......#.44#
$`......#.4.#
$`......###..
$`3...#......
$`....#.....#
$`1...#####.#
)

$`..4..4..4..
$`...........
$`.p.........
$`..4..4..4..
$`...........
$`...........
$`..4..4..4..


$`...........
$`p.3.3.3.3.3
$`.##########
$`.1.1.1.1.1.
$`.##########
$`..3.3.3.3.3
$`...........

$`...........
$`...p.t.....
$`5.........5
$`4.........4
$`5.........5
$`7.........7
$`7.........7


$`.....#.....
$`.....#.....
$`.3.....p...
$`.....#...3.
$`.3...#.....
$`.....#.....
$`1...1#1...1


$`....#......
$`.t..#......
$`....#......
$`..p.a......
$`....#......
$`..2.#......
$`7...b......

$`...g.......
$`...g.......
$`...g.......
$`...g..p27..
$`...g.......
$`...g.......
$`...g.......


$`6c.........
$`c5c........
$`.c.........
$`....p2.....
$`.........c.
$`........c5c
$`.........c6


$`###########
$`.....p.....
$`..##.#.##..
$`..4..4..4..
$`..#.#.#.#..
$`...........
$`###########


$`..#...3.#..
$`.#.......#.
$`.#.3..p.3#.
$`.#.##.##.#.
$`.#.3...#.#.
$`.#...#...#.
$`..#.3...#..

$`...........
$`...........
$`..p........
$`..8........
$`.88........
$`.88........
$`888........


$`t.876......
$`...........
$`...........
$`..2.p.2....
$`..........4
$`..........5
$`..........6

(
$`...........
$`...........
$`...........
$`...........
$`...........
$`...........
$`...........


$`#......2..#
$`...........
$`...........
$`3...p7.....
$`..........7
$`...........
$`#.......4.#


$`.##...2c.1.
$`...e4.#..#t
$`..###.#.p#.
$`..#...#.ch.
$`.6#...#.3#.
$`g..#.e5..#.
$`.t.#..##.#.

$`....4..###.
$`.#####.3...
$`..5....###.
$`.#####...2.
$`.6.....####
$`######1####
$`.....tpt...


$`1..2...3...
$`.#######...
$`...........
$`....3.p....
$`...........
$`...........
$`...........
)

message You Win this Work In Progress!
