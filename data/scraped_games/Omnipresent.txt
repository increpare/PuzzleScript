title Omnipresent
author Le Slo
homepage www.puzzlescript.net

background_color #262626

========
OBJECTS
========

Background
#ffffff #ffffff
10001
11111
01110
11111
10001


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
#262626
00000
00000
00000
00000
00000

PlayerPush
#4392f1
.000.
.000.
00000
.000.
.0.0.

PlayerPull
#177e89
.000.
.000.
00000
.000.
.0.0.

CantPull
#dc493a
.000.
.000.
00000
.000.
.0.0.

CantPush
#dc493a
.000.
.000.
00000
.000.
.0.0.

Crate
#dc493a
00000
0...0
0...0
0...0
00000

aux
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = PlayerPush
R = PlayerPull
* = Crate
@ = Crate and Target
O = Target

player = PlayerPush or PlayerPull

=======
SOUNDS
=======

Crate MOVE 95992507
sfx0 67850504
showmessage 73880508
================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate, CantPull, CantPush
aux

======
RULES
======

[CantPull] -> [PlayerPull]
[CantPush] -> [PlayerPush]

[ >  PlayerPush | Crate ] -> [  >  PlayerPush | > Crate  ]
[ <  PlayerPull | Crate ] -> [  <  PlayerPull | < Crate  ]

[ >  PlayerPush | Crate | crate] -> [  PlayerPush aux | Crate | crate ]
[ >  PlayerPush | Crate | wall] -> [  PlayerPush aux | Crate | wall ]

[> playerPush | wall] -> [ CantPush aux | wall]sfx0
[> playerPull | wall] -> [ CantPull aux | wall]sfx0
[> playerPull | crate] -> [ CantPull aux | crate]sfx0

[aux] [> crate] -> [aux] [crate]
[aux] [> PlayerPull] -> [aux] [PlayerPull]sfx0
[aux] [> PlayerPush] -> [aux] [PlayerPush]sfx0

[aux] -> []

[CantPull] -> [CantPull] again
[CantPush] -> [CantPush] again

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

message One pushes, the other one pulls. You are all.

message 1/7

#############
#.....#.....#
#.....#.....#
#.o*p.#.o*r.#
#.....#.....#
#.....#.....#
#############

message 2/7

#############
#.....#.....#
#..o..#..*..#
#.*.*.#.o.o.#
#..op.#..*r.#
#.....#.....#
#############

message 3/7

#############
#.....#.....#
#.....#..*..#
#.***ooo.*..#
#...p.#..*r.#
#.....#.....#
#############

message 4/7

#############
#...........#
#..*o...o*..#
#..r..p..r..#
#..*o...o*..#
#...........#
#############

message 5/7

#############
#...........#
#..*.....*..#
#..pooroop..#
#..*.....*..#
#...........#
#############

message 6/7

##########
#..##....#
#...pr...#
#.o.rp.o.#
#...**...#
#....##..#
##########

message 7/7

###########
#....o....#
#...p.r...#
#.*..@..*.#
#...r.p...#
#....o....#
###########
