title Ice and Tar
author ncrecc
homepage www.puzzlescript.net
(zoomscreen 5x5)
color_palette 14
again_interval 0.05
(verbose_logging)

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111

Deco
LIGHTGREEN GREEN
11101
11111
11111
01111
11111



Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

PlayerUP1
Black Orange White Blue
1..31
21112
20002
.000.
.3...

PlayerUP2
Black Orange White Blue
13..1
21112
20002
.000.
...3.

PlayerDP1
Black Orange White Blue
...3.
.000.
20002
21112
13..1

PlayerDP2
Black Orange White Blue
.3...
.000.
20002
21112
1..31

PlayerLP1
Black Orange White Blue
122..
3100.
.100.
.1003
122..

PlayerLP2
Black Orange White Blue
122..
.1003
.100.
3100.
122..

PlayerRP1
Black Orange White Blue
..221
3001.
.001.
.0013
..221

PlayerRP2
Black Orange White Blue
..221
.0013
.001.
3001.
..221

PlayerUW1
Black Orange White Blue
1..3.
2111.
20002
.0002
.3..1

PlayerUW2
Black Orange White Blue
.3..1
.1112
20002
2000.
1..3.

PlayerDW1
Black Orange White Blue
1..3.
2000.
20002
.1112
.3..1

PlayerDW2
Black Orange White Blue
.3..1
.0002
20002
2111.
1..3.

PlayerLW1
Black Orange White Blue
..221
3100.
.100.
.1003
122..

PlayerLW2
Black Orange White Blue
122..
.1003
.100.
3100.
..221

PlayerRW1
Black Orange White Blue
..221
3001.
.001.
.0013
122..

PlayerRW2
Black Orange White Blue
122..
.0013
.001.
3001.
..221

PlayerUS
Black White Orange
.....
.222.
10001
.000.
.....

PlayerDS
Black White Orange
.....
.000.
10001
.222.
.....

PlayerLS
Black White Orange
..1..
.200.
.200.
.200.
..1..

PlayerRS
Black White Orange
..1..
.002.
.002.
.002.
..1..


Crate
Lightbrown Yellow
00000
0...0
0...0
0...0
00000

Ice
Lightblue

Tar
Darkgray

Iron
Black
.000.
0...0
0...0
0...0
.000.

Balloon
White
.000.
0...0
0...0
0...0
.000.

AgainU
Red
.....
.....
..0..
.....
.....

AgainD
Darkred
.....
.....
..0..
.....
.....

=======
LEGEND
=======

. = Background
# = Wall
> = PlayerRS
v = PlayerDS
< = PlayerLS
^ = PlayerUS
* = Crate
@ = Crate and Target
O = Target
~ = Ice
- = Tar
1 = Crate and Ice
2 = Crate and Tar
3 = Target and Ice
4 = Target and Tar
5 = Crate and Tar and Target
6 = Crate and Ice and Target
7 = PlayerUS and Ice
, = Deco
A = Iron
B = Balloon


PlayerU1 = PlayerUP1 or PlayerUW1
PlayerU2 = PlayerUP2 or PLayerUW2 or PlayerUS
PlayerD1 = PlayerDP1 or PlayerDW1
PlayerD2 = PlayerDP2 or PLayerDW2 or PlayerDS
PlayerL1 = PlayerLP1 or PlayerLW1
PlayerL2 = PlayerLP2 or PLayerLW2 or PlayerLS
PlayerR1 = PlayerRP1 or PlayerRW1
PlayerR2 = PlayerRP2 or PLayerRW2 or PlayerRS

PlayerU = PlayerU1 or PlayerU2
PlayerD = PlayerD1 or PlayerD2
PlayerL = PlayerL1 or PlayerL2
PlayerR = PlayerR1 or PlayerR2

Player = PlayerU or PlayerD or PlayerL or PlayerR

Solid = Crate or Wall or Player or Iron or Balloon
Filler = Crate or Iron or Balloon

Player1 = PlayerU1 or PlayerD1 or PlayerL1 Or PlayerR1
Player2 = PlayerU2 or PlayerD2 or PlayerL2 Or PlayerR2

Blocker = Crate or Wall or Player or Iron

IronBlocker = Player or Wall

=======
SOUNDS
=======

Crate MOVE 36772507
Iron MOVE 39192307
sfx0 58346507
Balloon MOVE 44651907
endlevel 11345500
sfx1 66312107
sfx2 55168304
(84121904)
restart 62937702
undo 76729507

================
COLLISIONLAYERS
================

Background
Deco
Ice, Tar
Target
Player, Wall, Crate, Iron, Balloon
AgainU
AgainD

======
RULES
======

[up PlayerU Tar] -> cancel
[down PlayerD Tar] -> cancel
[right PlayerR Tar] -> cancel
[left PlayerL Tar] -> cancel
[down PlayerU Tar] -> cancel
[up PlayerD Tar] -> cancel
[left PlayerR Tar] -> cancel
[right PlayerL Tar] -> cancel

[left PlayerU Ice] -> cancel
[right PlayerU Ice] -> cancel
[left PlayerD Ice] -> cancel
[right PlayerD Ice] -> cancel
[up PlayerL Ice] -> cancel
[down PlayerL Ice] -> cancel
[up PlayerR Ice] -> cancel
[down PlayerR Ice] -> cancel

[right Player2 Background] -> [right PlayerRW1]
[right Player1 Background] -> [right PlayerRW2]
[left Player2 Background] -> [left PlayerLW1]
[left Player1 Background] -> [left PlayerLW2]
[down Player2 Background] -> [down PlayerDW1]
[down Player1 Background] -> [down PlayerDW2]
[up Player2 Background] -> [up PlayerUW1]
[up Player1 Background] -> [up PlayerUW2]

[> PlayerRW1|Solid] -> [ > PlayerRP1|Solid]
[> PlayerRW2|Solid] -> [ > PlayerRP2|Solid]
[> PlayerLW1|Solid] -> [ > PlayerLP1|Solid]
[> PlayerLW2|Solid] -> [ > PlayerLP2|Solid]
[> PlayerUW1|Solid] -> [ > PlayerUP1|Solid]
[> PlayerUW2|Solid] -> [ > PlayerUP2|Solid]
[> PlayerDW1|Solid] -> [ > PlayerDP1|Solid]
[> PlayerDW2|Solid] -> [ > PlayerDP2|Solid]

[> Player|Filler] -> [> Player|> Filler]

[no Background] -> [Background]

[> Solid] -> [> Solid > Background]
[> Background|] -> [Background|]

[> PlayerRW1 > Background] -> [ > PlayerRP1 Background]
[> PlayerRW2 > Background] -> [ > PlayerRP2 Background]
[> PlayerLW1 > Background] -> [ > PlayerLP1 Background]
[> PlayerLW2 > Background] -> [ > PlayerLP2 Background]
[> PlayerUW1 > Background] -> [ > PlayerUP1 Background]
[> PlayerUW2 > Background] -> [ > PlayerUP2 Background]
[> PlayerDW1 > Background] -> [ > PlayerDP1 Background]
[> PlayerDW2 > Background] -> [ > PlayerDP2 Background]

startloop
[> Solid|stationary Balloon] -> [> Solid|> Balloon]
[> Iron|stationary Filler] -> [> Iron|> Filler]
endloop

[AgainD no Iron] -> []
down [down Iron no AgainD|no IronBlocker] -> [down Iron down AgainD|]
[Iron AgainD|no IronBlocker] -> [down Iron down AgainD|]
down [> Iron AgainD|Iron|no IronBlocker] -> [> Iron > AgainD|> Iron > AgainD|]
down [> Iron AgainD|Filler no Iron|Blocker] -> [Iron|Filler|Blocker]
[stationary AgainD] -> []
down [> AgainD|IronBlocker] -> [|IronBlocker]
[down AgainD] -> again

startloop
[> Solid|stationary Balloon] -> [> Solid|> Balloon]
[> Iron|stationary Filler] -> [> Iron|> Filler]
endloop

[AgainU no Balloon] -> []
up [up Balloon no AgainU|no Blocker] -> [up Balloon up AgainU|]
[Balloon AgainU|no Crate no Blocker] -> [up Balloon up AgainU|]
up [> Balloon AgainU|Balloon|no Blocker] -> [> Balloon > AgainU|> Balloon > AgainU|]
[stationary AgainU] -> []
up [> AgainU|Blocker] -> [|Blocker]
[up AgainU] -> again

[> Player Tar|Wall] -> cancel
[> Player Tar] [> Solid|Wall] -> cancel
[> Player Tar] [> Balloon|Crate] -> cancel
[> Player Tar] [> Crate|Iron] -> cancel
[> Player Tar] [> Balloon|Iron] -> cancel
[> Player Tar] [> Crate|Crate] -> cancel

[> Player no Ice no Tar] -> [> Player] sfx0
[> Player Ice] -> [> Player Ice] sfx1
[> Player Tar] -> [> Player Tar] sfx2

==============
WINCONDITIONS
==============

All Target on Filler
No AgainU
No AgainD
some target
some player

=======
LEVELS
=======

#########
#.~~~~~.#
#.~~1~~o#
#o~~1~~.#
#>~~~~~.#
#########

######-
#vo..##
#-2-2-#
##..o.#
-######

#######
#o.~.o#
#.~1~.#
#~1~1~#
#.~1~<#
#o.~.o#
#######


########
##.--.##
#.o-2o.#
#-2-#---
#--#-2--
#.o2-o.#
##^--.##
########

########
##.--.##
#.o-2o.#
#-2-~--#
#--~-2-#
#.o2-o.#
##^--.##
########

########
##.--.##
#.o-2o.#
#-2~~--#
#--~~2-#
#.o2-o.#
##^--.##
########

v###-~~-~.
.~~~~~~-~.
.#-~31~~#.
.#~####~#.
.#~~13~-#.
.~-~~~~~~.
.~-~~-###.

#,,#..
,~3,.<
,~~#,#
,6~~~,
,~~1~,
,~~#,#
#,,,..

#######.
#..#..#.
>a*...#.
#.....#.
#,,,####
#...2oo#
#...####
#####...

####..#
#,.,o.#
#,.,o.#
##,#b##
#,.,b.#
#,.,..<
#######

########-####
.,.....**...,
##a#####-##,#
~#*#........#
~#b#.b##.b..#
##o#.#...#.##
..3..#.a##.#~
.#o#.#.a.#.#~
.#a#.b...b.#~
.#*#.......#~
.#b#########~
..^#~~~~~~~~~

(
####
.,..
##a.
~#*.
~#b.
##o.
..3.
.#o#
.#a#
.#*#
.#b#
..^#
)

#####4#
#,.,#4#
#,a,#4#
#,.,#-#
#,.,*-#
#,b#,,#
#,...^#
####,,#
