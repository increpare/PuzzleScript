title Repeater
author Nils Jung

run_rules_on_level_start
norepeat_action

(verbose_logging)

========
OBJECTS
========

Background .
black

GoodBlock +
#060 #090
00000
01110
01110
01110
00000

BadBlock -
#600 #900 
00000
01110
01110
01110
00000

NeutralBlock ~
#03C #05E
00000
01110
01110
01110
00000

Wall #
#000 #111
00000
01110
01110
01110
00000

Cursor
yellow
00.00
0...0
.....
0...0
00.00

PathU
yellow
..0..
..0..
..0..
.....
.....

PathD
yellow
.....
.....
..0..
..0..
..0..

PathL
yellow
.....
.....
000..
.....
.....

PathR
yellow
.....
.....
..000
.....
.....

PathUAlt
transparent

PathDAlt
transparent

PathLAlt
transparent

PathRAlt
transparent

CursorActive
orange
00.00
0...0
.....
0...0
00.00

Number0 0
white
.0000
.0..0
.0..0
.0..0
.0000

Number1 1
white
....0
...00
....0
....0
....0

Number2 2
white
.0000
....0
.0000
.0...
.0000

Number3 3
white
.0000
....0
.0000
....0
.0000

Number4 4
white
.0...
.0.0.
.0000
...0.
...0.

Number5 5
white
.0000
.0...
.0000
....0
.0000

Number6 6
white
.0000
.0...
.0000
.0..0
.0000

Number7 7
white
.0000
....0
....0
....0
....0

Number8 8
white
.0000
.0..0
.0000
.0..0
.0000

Number9 9
white
.0000
.0..0
.0000
....0
.0000

init
transparent

flip
transparent

flipAlt
transparent

tail
transparent

tailAlt
transparent

head
transparent

once
transparent

nonce
transparent

ReduceNumber
transparent

ReduceNumber2
transparent

ReduceNumber3
transparent

LastNumber
transparent

=======
LEGEND
=======

Player = Cursor or CursorActive
Block = GoodBlock or BadBlock or NeutralBlock
Path = PathU or PathD or PathL or PathR or PathUAlt or PathDAlt or PathLAlt or PathRAlt
Number = Number0 or Number1 or Number2 or Number3 or Number4 or Number5 or Number6 or Number7 or Number8 or Number9
Reduce = ReduceNumber or ReduceNumber2 or ReduceNumber3

=======
SOUNDS
=======

sfx0 76709907 (Cursor Action)
sfx1 7040905 (CursorActive Action)
endlevel 34064103

================
COLLISIONLAYERS
================

Background
Block, Wall
Cursor, CursorActive
Number
PathU
PathD
PathL
PathR
PathUAlt
PathDAlt
PathLAlt
PathRAlt
init
flip
flipAlt
tail, tailAlt
head
once, nonce
ReduceNumber
ReduceNumber2
ReduceNumber3
LastNumber

======
RULES
======

[no init no Cursor] -> [Cursor]
RIGHT [no init Cursor | ... | no init Cursor] -> [Cursor | ... | ]
DOWN [no init Cursor | ... | no init Cursor] -> [Cursor | ... | ]
[no init Cursor] -> [Cursor nonce]
[no init Number] -> [Number LastNumber]
RIGHT [no init LastNumber | ... | no init LastNumber] -> [ | ... | LastNumber]

[no init] -> [init]

[ACTION Cursor no Block] -> CANCEL
[ACTION Cursor] [LastNumber] -> [CursorActive flip tail] [LastNumber ReduceNumber ReduceNumber2 ReduceNumber3] sfx0

[> CursorActive | no Block] -> CANCEL
[> CursorActive | Path] -> CANCEL

UP [> CursorActive | ] [LastNumber] -> [PathU | CursorActive PathD flip] [LastNumber ReduceNumber]
DOWN [> CursorActive | ] [LastNumber] -> [PathD | CursorActive PathU flip] [LastNumber ReduceNumber]
LEFT [> CursorActive | ] [LastNumber] -> [PathL | CursorActive PathR flip] [LastNumber ReduceNumber]
RIGHT [> CursorActive | ] [LastNumber] -> [PathR | CursorActive PathL flip] [LastNumber ReduceNumber]

startloop
[Reduce Number9] -> [Number8]
[Reduce Number8] -> [Number7]
[Reduce Number7] -> [Number6]
[Reduce Number6] -> [Number5]
[Reduce Number5] -> [Number4]
[Reduce Number4] -> [Number3]
[Reduce Number3] -> [Number2]
[Reduce Number2] -> [Number1]
[Reduce Number1] -> [Number0]
RIGHT [Number | Reduce Number0] -> [Reduce Number | Number9]
RIGHT [no Number | Number0 | Number] -> [ | | Number]
RIGHT [no Number | Reduce Number0] -> CANCEL
endloop

[ACTION CursorActive] -> [Cursor head] sfx1

startloop
[nonce] -> [once]
[flip GoodBlock] -> [BadBlock]
[flip BadBlock] -> [GoodBlock]
[flipAlt GoodBlock] -> [BadBlock]
[flipAlt BadBlock] -> [GoodBlock]
[flip NeutralBlock] -> [NeutralBlock]
[flipAlt NeutralBlock] -> [NeutralBlock]
[head Wall] [tail] -> [head Wall] []
[head Wall] [tailAlt] -> [head Wall] []
UP [once] [tail PathU | PathD] [head | ] -> [nonce] [ | tail] [PathUAlt | head PathDAlt flipAlt]
DOWN [once] [tail PathD | PathU] [head | ] -> [nonce] [ | tail] [PathDAlt | head PathUAlt flipAlt]
LEFT [once] [tail PathL | PathR] [head | ] -> [nonce] [ | tail] [PathLAlt | head PathRAlt flipAlt]
RIGHT [once] [tail PathR | PathL] [head | ] -> [nonce] [ | tail] [PathRAlt | head PathLAlt flipAlt]
UP [once] [tailAlt PathUAlt | PathDAlt] [head | ] -> [nonce] [ | tailAlt] [PathU | head PathD flip]
DOWN [once] [tailAlt PathDAlt | PathUAlt] [head | ] -> [nonce] [ | tailAlt] [PathD | head PathU flip]
LEFT [once] [tailAlt PathLAlt | PathRAlt] [head | ] -> [nonce] [ | tailAlt] [PathL | head PathR flip]
RIGHT [once] [tailAlt PathRAlt | PathLAlt] [head | ] -> [nonce] [ | tailAlt] [PathR | head PathL flip]
[tail no PathU no PathD no PathL no PathR Path] -> [tailAlt Path]
[tailAlt no PathUAlt no PathDAlt no PathLAlt no PathRAlt Path] -> [tail Path]
endloop

[head] [Path] -> [head] []
[head] -> []

==============
WINCONDITIONS
==============

no BadBlock

=======
LEVELS
=======

message Level 1 of 7
#########
#+-+++-+#
#-+---+-#
#+-+++-+#
#+-+++-+#
#+-+++-+#
#-+---+-#
#+-+++-+#
#########
.......16

message Level 2 of 7
........#########
......###++++~~~#
....###++++--~~~#
#####++++--+-~~~#
#~~~+++--+--+~~~#
#~~~+--+--+++~~~#
#~~~-+--++++#####
#~~~--++++###....
#~~~++++###......
#########......12

message Level 3 of 7
##########################
#~~~++++++++++++++++++~~~#
#~~~++++++++++++++++++~~~#
#~~~+-++-++-++-++-++-+~~~#
#~~~++++++++++++++++++~~~#
#~~~++++++++++++++++++~~~#
##########################
........................15

message Level 4 of 7
###############
#++++++++~~~++#
#++------~~~++#
#+#++++++~~~++#
#++++++++~~~++#
#+#++++++~~~++#
#++------~~~++#
#++++++++~~~++#
###############
.............12

message Level 5 of 7
...........###########
........####++++++~~~#
.....####++++++---~~~#
######++++++---+--~~~#
#~~~+++++---+---++~~~#
#~~~++---+---+++++~~~#
#~~~--+---++++++######
#~~~---++++++####.....
#~~~++++++####........
###########.........11

message Level 6 of 7
#############
#~~~~~~~~~~~#
#~~~~~~~~~~~#
#~~+-+++-+~~#
#~~-+-+-+-~~#
#~~+-+++-+~~#
#~~+++++++~~#
#~~+-+++-+~~#
#~~-+-+-+-~~#
#~~+-+++-+~~#
#~~~~~~~~~~~#
#~~~~~~~~~~~#
#############
...........14

message Level 7 of 7
...........##########
.........###++++~~~~#
.......###++++++~~~~#
.....###++++++-+~~~~#
######++++++-++-~~~~#
#~~~~+++++-++--+~~~~#
#~~~~+++-++--+++~~~~#
#~~~~+-++--+++++~~~~#
#~~~~++--++++++######
#~~~~--++++++###.....
#~~~~++++++###.......
#~~~~++++###.........
##########.........10

message Congratulations!