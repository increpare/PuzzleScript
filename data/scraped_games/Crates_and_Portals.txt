title Crates and Portals
author Mrquaotta
homepage www.puzzlescript.net

========
OBJECTS
========

Background
DarkGray Gray
00000
01110
01110
01110
00000

Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
#262626
00000
00000
00000
00000
00000

FloorA
#262626 Blue
11111
00000
00000
00000
00000

FloorB
#262626 Orange
11111
00000
00000
00000
00000

FloorBT
#262626 Orange
11111
00000
00000
00000
00000

Break
#262626 Orange
.1.1.
0.0.0
.0.0.
0.0.0
.0.0.

BreakT
#262626 Orange
.1.1.
0.0.0
.0.0.
0.0.0
.0.0.

PlayerR
DarkGreen White Orange Black
..2..
.1333
.221.
.000.
.0.0.

PlayerL
DarkGreen White Orange Black
..2..
3331.
.122.
.000.
.0.0.

PlayerR2
DarkGreen White Orange Black
..2..
.1333
.221.
.000.
.0.0.

PlayerL2
DarkGreen White Orange Black
..2..
3331.
.122.
.000.
.0.0.

PlayerDeadR
DarkGreen White Orange Black
.....
.....
.1100
2130.
33320


PlayerDeadL
DarkGreen White Orange Black
.....
.....
0011.
.0312
02333

PlayerClimb
DarkGreen White Orange Black
2.2.2
21112
.1133
.0003
.0.03

PlayerBack
DarkGreen White Orange Black
..2..
.111.
21133
20003
.0.03

PortalA
DarkBlue Blue
.000.
01110
01110
01110
.000.

PortalB
DarkBlue Orange
.000.
01110
01110
01110
.000.

PortalAT
DarkBlue Blue
.000.
01110
01110
01111
.000.

PortalBT 
DarkBlue Orange
.000.
01110
01110
01110
.000.

DroidR
LightBlue Black
.000.
00000
00011
0...0
0...0

DroidL
LightBlue Black
.000.
00000
11000
0...0
0...0

DroidRT
LightBlue Black
.000.
00000
00011
0...0
0...0

DroidLT
LightBlue Black
.000.
00000
11000
0...0
0...0

LaserR
Red
.....
.....
00000
.....
.....

LaserL
Red
.....
.....
00000
.....
.....

CrateM
Brown Orange Yellow
22222
01112
01112
01112
00002

CrateW
DarkBrown Brown LightBrown
00000
0.1.0
01110
0.1.0
00000

CrateWT
DarkBrown Brown
00000
0.1.0
01110
0.1.0
00000

Ladder
#4d4d4d
0...0
00000
0...0
00000
0...0

ExitOpen
Black
.000.
00000
00000
00000
00000

ExitLocked
Black #66b3ff
.101.
11111
01010
11111
01010

TempR
Transparent

TempL
Transparent

GateCheck
Transparent

=======
LEGEND
=======

. = Background
# = Wall
_ = FloorA
% = FloorB
- = Break
P = PlayerR
L = PlayerL
: = DroidR
" = DroidL
@ = CrateM
* = CrateW
` = ExitOpen
$ = ExitLocked and GateCheck
& = Ladder
/ = PortalA
' = PortalB
O = Target


Player = PlayerR or PlayerL or PlayerDeadR or PlayerDeadL or PlayerClimb or PlayerBack
PlayerRL = PlayerR or PlayerL
PClimbBack = PlayerClimb or PlayerBack
PlayerDead = PlayerDeadR or PlayerDeadL
Crate = CrateM or CrateW
Portal = PortalA or PortalB
Droid = DroidR or DroidL
Stand = Crate or FloorA or FloorB or Break or FloorBT or BreakT or Droid
Object = Portal or Wall or Stand
Detect = Object or CrateWT or DroidRT or DroidLT or Player
Moveable = Crate or Portal or Droid
CrushA = Crate or Droid or Player
Floor = FloorA or FloorB or Break or Wall
CrushB = CrateM or Droid
Laser = LaserR or LaserL
Warp = Crate or Droid
DroidLaserL = DroidL or LaserL
DroidLaserR = DroidR or LaserR

=======
SOUNDS
=======

Player Move 36772507
Moveable Move 36772507
sfx0 71146307
sfx1 34308502
sfx2 61954103
sfx3 20557704
sfx4 83540908

================
COLLISIONLAYERS
================
 
Background
Target, Ladder, ExitOpen, ExitLocked
Detect, PlayerL2, PlayerR2, TempR, TempL, PortalAT, PortalBT, Laser, GateCheck

======
RULES
======

Late [ PlayerClimb no Ladder ] -> [ PlayerBack no Ladder ]
Late [ PlayerBack Ladder ] -> [ PlayerClimb Ladder ]
Down [ Down PlayerRL | Ladder ] -> [ PlayerBack | Ladder ]
[ Down PlayerRL | Floor ] -> [ PlayerRL | Floor ]
[ Vertical PlayerRL Ladder ] -> [ PlayerClimb Ladder ]
[ Right PlayerClimb ] -> [ PlayerR ]
[ Left PlayerClimb ] -> [ PlayerL ]
[ Right PlayerBack ] -> [ PlayerR ]
[ Left PlayerBack ] -> [ PlayerL ]
[ Right PlayerL ] -> [ PlayerR ]
[ Left PlayerR ] -> [ PlayerL ]
[ > PlayerDead ] -> [ PlayerDead ]
Horizontal [ Portal | > Player | Warp | Portal ] -> [ Portal | Warp | Player | Portal ] sfx2
Up [ > Player | Moveable ] -> [ Player | Moveable ]
[ > Player | Moveable ] -> [ > Player | > Moveable ]


[ CrateWT ] -> [ ] sfx1
[ DroidLT ] -> [ ] sfx1
[ DroidRT ] -> [ ] sfx1


Down [ Player | no FloorBT ] [ FloorBT ] -> [ Player | no FloorBT ] [ Break ]
Down [ Player Ladder | FloorB ] -> [ Player Ladder | FloorBT ]
Down [ Player | no BreakT ] [ BreakT ] -> [ Player | no BreakT ] [ ]
Down [ Player Ladder | Break ] -> [ Player Ladder | BreakT ]
Down [ Player | no FloorBT ] [ FloorBT ] -> [ Player | no FloorBT ] [ Break ]
Down [ Player | FloorB ] -> [ Player | FloorBT ]
Down [ Player | no BreakT ] [ BreakT ] -> [ Player | no BreakT ] [ ]
Down [ Player | Break ] -> [ Player | BreakT ]


[ Moving Player ] -> [ Moving Player ] again 
Down [ Stationary Moveable ] -> [ > Moveable ] again
Down [ Stationary PlayerDead ] -> [ > PlayerDead ] again
Down [ Stationary Player no Ladder | no Ladder ] -> [ > Player no Ladder | no Ladder ] again
Down [ CrushA | Portal ] -> [ CrushA | ] sfx1
Down [ CrushB | CrateW ] -> [ CrushB | ] sfx1
[ Up Player no Ladder ] -> [ Player ]


Right [ > Player | Warp | PortalA ] [ PortalB | Object ] -> [ TempR | Warp | PortalA ] [ PortalB | Object ]
Right [ > Player | Warp | PortalB ] [ PortalA | Object ] -> [ TempR | Warp | PortalB ] [ PortalA | Object ]
Right [ > Player | Portal | Warp ] [ Object | Portal ] -> [ TempR | Portal | Warp ] [ Object | Portal ]
Right [ > Player | Floor ] -> [ TempR | Floor ]
Up [ TempR | no Object ] ->  [ | v PlayerR2 ]
Left [ > Player | Warp | PortalA ] [ PortalB | Object ] -> [ TempL | Warp | PortalA ] [ PortalB | Object ]
Left [ > Player | Warp | PortalB ] [ PortalA | Object ] -> [ TempL | Warp | PortalB ] [ PortalA | Object ]
Left [ > Player | Portal | Warp ] [ Object | Portal ] -> [ TempL | Portal | Warp ] [ Object | Portal ]
Left [ > Player | Floor ] -> [ TempL | Floor ]
Up [ TempL | no Object ] ->  [ | ^ PlayerL2 ]


[ > Player | Warp | PortalA ] [ PortalB | ] -> [ | > Player | PortalAT ] [ PortalB | Warp ] sfx2
[ > Player | Warp | PortalB ] [ PortalA | ] -> [ | > Player | PortalBT ] [ PortalA | Warp ] sfx2
[ > Player | PortalA | Warp ] [ | PortalB ] -> [ | > Player | PortalAT ] [ Warp | PortalB ] sfx2
[ > Player | PortalB | Warp ] [ | PortalA ] -> [ | > Player | PortalBT ] [ Warp | PortalA ] sfx2
[ > Player | PortalAT ] -> [ Player | PortalAT ]
[ > Player | PortalBT ] -> [ Player | PortalBT ]
[ PortalAT ] -> [ PortalA ]
[ PortalBT ] -> [ PortalB ]


Right [ > Player | Object | Object ] -> [ TempR | Object | Object ]
Up [ TempR | no Object ] ->  [ | v PlayerR ]
[ TempR ] -> [ PlayerR ]
Left [ > Player | Object | Object ] -> [ TempL | Object | Object ]
Up [ TempL | no Object ] ->  [ | ^ PlayerL ]
[ TempL ] -> [ PlayerL ]


[ Action Player ] [ PortalA ] [ PortalB ] -> [ Player ] [ ] [ ] sfx0
Right [ Action PlayerR | Object ] -> [ PlayerR | Object ]
Right [ Action PlayerR | ] [ PortalA ] -> [ PlayerR | PortalB ] [ PortalA ] sfx0
Right [ Action PlayerR | ] -> [ PlayerR | PortalA ] sfx0
Left [ Action PlayerL | Object ] -> [ PlayerL | Object ]
Left [ Action PlayerL | ] [ PortalA ] -> [ PlayerL | PortalB ] [ PortalA ] sfx0
Left [ Action PlayerL | ] -> [ PlayerL | PortalA ] sfx0


[ Laser ] -> [ ]
Late Right [ DroidR | no Detect ] -> [ DroidR | LaserR ]
Late Left [ DroidL | no Detect ] -> [ DroidL | LaserL ]
Late Right [ LaserR | no Detect ] -> [ LaserR | LaserR ]
Late Left [ LaserL | no Detect ] -> [ LaserL | LaserL ]
Late Right [ DroidLaserR | PortalA ] [ PortalB | no Detect ] -> [ DroidLaserR | PortalA ] [ PortalB | LaserR ]
Late Right [ DroidLaserR | PortalB ] [ PortalA | no Detect ] -> [ DroidLaserR | PortalB ] [ PortalA | LaserR ]
Late Left [ DroidLaserL | PortalA ] [ PortalB | no Detect ] -> [ DroidLaserL | PortalA ] [ PortalB | LaserL ]
Late Left [ DroidLaserL | PortalB ] [ PortalA | no Detect ] -> [ DroidLaserL | PortalB ] [ PortalA | LaserL ]
Late Right [ LaserR | no Detect ] -> [ LaserR | LaserR ]
Late Left [ LaserL | no Detect ] -> [ LaserL | LaserL ]
[ > Moveable | Laser ] -> [ | Moveable ]

Late Right [ CrateW | DroidLaserL ] -> [ CrateWT | DroidLaserL ]
Late Left [ CrateW | DroidLaserR ] -> [ CrateWT | DroidLaserR ]
Late Right [ PlayerR | DroidLaserL ] -> [ PlayerDeadR | DroidLaserL ] sfx3
Late Right [ PlayerL | DroidLaserL ] -> [ PlayerDeadL | DroidLaserL ] sfx3
Late Left [ PlayerR | DroidLaserR ] -> [ PlayerDeadR | DroidLaserR ] sfx3
Late Left [ PlayerL | DroidLaserR ] -> [ PlayerDeadL | DroidLaserR ] sfx3
Late Right [ PClimbBack | DroidLaserL ] -> [ PlayerDeadR | DroidLaserL ] sfx3
Late Left [ PClimbBack | DroidLaserR ] -> [ PlayerDeadL | DroidLaserR ] sfx3
Late Right [ DroidL | DroidLaserL ] -> [ DroidLT | DroidLaserL ]
Late Left [ DroidR | DroidLaserR ] -> [ DroidRT | DroidLaserR ]

Late Right [ PortalA | DroidLaserL ] [ PlayerR | PortalB ] -> [ PortalA | DroidLaserL ] [ PlayerDeadR | PortalB ] sfx3
Late Right [ PortalA | DroidLaserL ] [ PlayerL | PortalB ] -> [ PortalA | DroidLaserL ] [ PlayerDeadL | PortalB ] sfx3
Late Right [ PortalB | DroidLaserL ] [ PlayerR | PortalA ] -> [ PortalB | DroidLaserL ] [ PlayerDeadR | PortalA ] sfx3
Late Right [ PortalB | DroidLaserL ] [ PlayerL | PortalA ] -> [ PortalB | DroidLaserL ] [ PlayerDeadL | PortalA ] sfx3
Late Right [ PortalA | DroidLaserL ] [ PClimbBack | PortalB ] -> [ PortalA | DroidLaserL ] [ PlayerDeadR | PortalB ] sfx3
Late Right [ PortalB | DroidLaserL ] [ PClimbBack | PortalA ] -> [ PortalB | DroidLaserL ] [ PlayerDeadR | PortalA ] sfx3

Late Left [ PortalA | DroidLaserR ] [ PlayerL | PortalB ] -> [ PortalA | DroidLaserR ] [ PlayerDeadL | PortalB ] sfx3
Late Left [ PortalA | DroidLaserR ] [ PlayerR | PortalB ] -> [ PortalA | DroidLaserR ] [ PlayerDeadR | PortalB ] sfx3
Late Left [ PortalB | DroidLaserR ] [ PlayerL | PortalA ] -> [ PortalB | DroidLaserR ] [ PlayerDeadL | PortalA ] sfx3
Late Left [ PortalB | DroidLaserR ] [ PlayerR | PortalA ] -> [ PortalB | DroidLaserR ] [ PlayerDeadR | PortalA ] sfx3
Late Left [ PortalA | DroidLaserR ] [ PClimbBack | PortalB ] -> [ PortalA | DroidLaserR ] [ PlayerDeadL | PortalB ] sfx3
Late Left [ PortalB | DroidLaserR ] [ PClimbBack | PortalA ] -> [ PortalB | DroidLaserR ] [ PlayerDeadL | PortalA ] sfx3

Late Right [ PortalA | DroidLaserL ] [ CrateW | PortalB ] -> [ PortalA | DroidLaserL ] [ CrateWT | PortalB ]
Late Right [ PortalB | DroidLaserL ] [ CrateW | PortalA ] -> [ PortalB | DroidLaserL ] [ CrateWT | PortalA ] 
Late Left [ PortalA | DroidLaserR ] [ CrateW | PortalB ] -> [ PortalA | DroidLaserR ] [ CrateWT | PortalB ]
Late Left [ PortalB | DroidLaserR ] [ CrateW | PortalA ] -> [ PortalB | DroidLaserR ] [ CrateWT | PortalA ]

Late Right [ PortalA | DroidLaserL ] [ DroidL | PortalB ] -> [ PortalA | DroidLaserL ] [ DroidLT | PortalB ]
Late Right [ PortalB | DroidLaserL ] [ DroidL | PortalA ] -> [ PortalB | DroidLaserL ] [ DroidLT | PortalA ]
Late Left [ PortalA | DroidLaserR ] [ DroidR | PortalB ] -> [ PortalA | DroidLaserR ] [ DroidRT | PortalB ]
Late Left [ PortalB | DroidLaserR ] [ DroidR | PortalA ] -> [ PortalB | DroidLaserR ] [ DroidRT | PortalA ]


[ no GateCheck ExitLocked ] -> [ GateCheck ExitLocked ]
[ Crate Target ] [ [ ExitLocked ] -> [ Crate Target ] [ ExitOpen ] 
[ no Crate Target ] [ ExitOpen ] -> [ no Crate Target ] [ ExitLocked ]
[ GateCheck ExitOpen ] -> [ ExitOpen ] sfx4


[ PlayerR2 ] -> [ PlayerR ]
[ PlayerL2 ] -> [ PlayerL ]


Down [ Player ExitOpen | Stand ] -> Win

==============
WINCONDITIONS 
==============

=======
LEVELS
=======

Message Room 1
###########
#.........#
#.........#
#.........#
#.........#
#........`#
#........_#
#@.p.....##
#________##

Message Room 2
###########
#.........#
#.........#
#.........#
#.........#
#......`.l#
#....._..@#
#...@.#___#
#_____#####

Message Room 3
##########
#........#
#........#
#........#
#........#
#`.......#
#_...__&.#
#..p.@.&.#
#________#

Message Room 4
###########
#.........#
#....`....#
#....@....#
#.&___....#
#.&.......#
#.&.......#
#.&......@#
#.&....___#
#...p..####
#______####

Message Room 5
##########
#........#
#........#
#........#
#.._.....#
#.&......#
#.&......#
#.&..`...#
#__@..l__#
###____###

Message Room 6
###########
#.........#
#.*...l..@#
#.________#
#.........#
#........`#
#.......__#
#.__....###
#_##____###

Message Room 7
#########
#.......#
#.......#
#`..p...#
#.*.@.@.#
#._____.#
#.#####.#
#_#####_#

Message Room 8
#########
#.......#
#.......#
#....__&#
#......&#
#.....l&#
#...@.__#
#`**_*###
#.__#_###
#_#######

Message Room 9
#########
#.......#
#.......#
#..p....#
#._@.._.#
#&#___#.#
#&......#
#&.....`#
#&......#
#&.@..._#
#**_***##

Message Room 10
#########
#.....###
#....@###
#__&__###
#..&....#
#..&....#
#.p&....#
#***....#
#___...*#
#`.#__*_#
#.....*.#
#_____*_#

Message Room 11
#########
#.......#
#.......#
#.......#
#.......#
#...l...#
#...@...#
#...__..#
#$o.....#
#_______#

Message Room 12
###########
#.........#
#..@...l..#
#.._____..#
#.........#
#.........#
#..___....#
#*_###....#
#*...#....#
#*..$#...o#
#____#____#

Message Room 13
##########
#........#
#........#
#........#
#........#
#........#
#o..@.@..#
#.p.@$@o.#
#______**#

Message Room 14
###########
#.........#
#.........#
#...._@...#
#....#__&.#
#.......&.#
#$......&.#
#__.....&.#
##o._*.l&.#
##_*#*****#

Message Room 15
##########
#........#
#........#
#._$_....#
#&#_#....#
#&.......#
#&.......#
#&.......#
#_p..o...#
##_.**@..#
###__*___#

Message Room 16
###########
#.........#
#.........#
#p.......@#
#%%%%%%%%%#
#.........#
#....`....#
#.........#
#_________#

Message Room 17
###########
#@........#
#%%%&-----#
#$..&.....#
#..._@...l#
#---#%%%%%#
#.........#
#........o#
#_________#

Message Room 18
########
#......#
#......#
#...%&.#
#....&.#
#..$.&.#
#.&-.&.#
#.&..&@#
#.&.l&@#
#o..*__#
#*%.-###
#_#_o###

Message Room 19
###########
#.........#
#.........#
#..@%%%%..#
#.&-#..`..#
#.&.......#
#.&...%%%%#
#.&.......#
#...**....#
#.p@*%...*#
#%_%%#%%%%#

Message Room 20
############
#..........#
#.*........#
#.*p.@$.@..#
#-___%%___%#
#...#..#...#
#...#oo#...#
#...#**#...#
#....__....#
#.o......o.#
#*________*#

Message Room 21
###########
#.........#
#.........#
#...@*l...#
#...___...#
#.........#
#.........#
#.........#
#:...`..."#
#_________#

Message Room 22
###########
#.........#
#.........#
#......--.#
#........&#
#........&#
#........&#
#.......o-#
#:.$.@..l.#
#_________#

Message Room 23
############
#..........#
#..........#
#...&_.....#
#...&..`...#
#...&..l...#
#.&__.%%...#
#.&........#
#.&.@......#
#.&._....."#
#___#______#

Message Room 24
############
#..........#
#..........#
#&:o..$.o"&#
#&%-.&_.-%&#
#&...&....&#
#&...&....&#
#&_..&..._&#
#&#p.**..#&#
###______###

Message Room 25
############
#..........#
#..........#
#.........@#
#..-%--____#
#.&....#...#
#.&....*..`#
#.&...._..*#
#.&p..:#..*#
#____*_#___#

Message Room 26
############
#..........#
#.*..o.....#
#&----.....#
#&.........#
#&".....--&#
#&_$....o.&#
#&......--&#
#___&.....&#
#...&...".&#
#.p.&...@.&#
#______*___#

Message Room 27
###########
#.........#
#.........#
#.........#
#&%%%%%%..#
#&........#
#&........#
#&........#
#&..$...lo#
#:%%_****_#
#_###____##

Message Room 28
############
#..........#
#..........#
#....&.....#
#.@p...@...#
#.___%%_...#
#..........#
#........."#
#........._#
#..........#
#...:.....`#
#__________#

Message Room 29
############
#..........#
#..:$.@o...#
#&---.__&..#
#&.....#&..#
#&.....#__&#
#&........&#
#&......."&#
#&%%%_&._-&#
#....#&.#p&#
#....#&***&#
#____#___%_#

Message Room 30
###########
#.........#
#..p.....@#
#..--&...@#
#$...&.___#
#....&..."#
#---.&____#
#:...&...o#
#____&..__#
###..&._###
###____####


