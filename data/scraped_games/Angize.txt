(/.)

title Angize
author Ali Nikkhah
homepage twitter.com/_AliNikk__

run_rules_on_level_start

again_interval 0.0001

========
OBJECTS
========

Background
#e0e0e0 LightBlue
11111
00001
00001
00001
00001

Wall
#d0d0d0 #0099FF
10000
10000
10000
10000
11111

NormalBaby
#fda white
.000.
.000.
00000
.111.
.0.0.

ParalyzedBaby
#909090 white
.000.
.000.
00000
.111.
.0.0.

NormalEye1
White Black Blue
.000.
00022
00021
00022
.000.

NormalEye2
White Black Blue
.000.
00000
00000
02220
.212.

NormalEye3
White Black Blue
.000.
22000
12000
22000
.000.

NormalEye4
White Black Blue
.212.
02220
00000
00000
.000.

RedEye1
White Black DarkRed
.000.
00022
00021
00022
.000.

RedEye2
White Black DarkRed
.000.
00000
00000
02220
.212.

RedEye3
White Black DarkRed
.000.
22000
12000
22000
.000.

RedEye4
White Black DarkRed
.212.
02220
00000
00000
.000.

RayH
Red
.....
.....
00000
.....
.....

RayV
Red
..0..
..0..
..0..
..0..
..0..

=======
LEGEND
=======

Player = NormalBaby
Baby = NormalBaby or ParalyzedBaby
NormalEye = NormalEye1 or NormalEye2 or NormalEye3 or NormalEye4
RedEye = RedEye1 or RedEye2 or RedEye3 or RedEye4
Eye1 = NormalEye1 or RedEye1
Eye2 = NormalEye2 or RedEye2
Eye3 = NormalEye3 or RedEye3
Eye4 = NormalEye4 or RedEye4
Eye = NormalEye or RedEye
Obstacle = Baby or Wall or Eye
MovingObstacle = Baby or Eye
Ray = RayH  or RayV
MovableObstacle = ParalyzedBaby

. = Background
# = Wall
+ = NormalBaby
> = NormalEye1
v = NormalEye2
< = NormalEye3
^ = NormalEye4
1 = RedEye1
2 = RedEye2
3 = RedEye3
4 = RedEye4
- = ParalyzedBaby

=======
SOUNDS
=======


sfx0 41755904

================
COLLISIONLAYERS
================

Background
Player, Wall, NormalEye1, NormalEye2, NormalEye3, NormalEye4, ParalyzedBaby, RedEye1, RedEye2, RedEye3, RedEye4
RayH
RayV

======
RULES
======
[ NormalBaby ] -> [ ParalyzedBaby ]

(remove all shots at the start of the turn)
[Ray] -> [] again

(main game logic here)

RIGHT [  Eye1 | no Obstacle ] -> [ Eye1 | RIGHT RayH ] again
RIGHT [ RIGHT RayH | no Obstacle ] -> [ RIGHT RayH | RIGHT RayH ] again
RIGHT [ Eye1 | ParalyzedBaby ] -> [ Eye1 | NormalBaby ] again

DOWN [  Eye2 | no Obstacle ] -> [ Eye2 | DOWN RayV ] again
DOWN [ DOWN RayV | no Obstacle ] -> [ DOWN RayV | DOWN RayV ] again
DOWN [ Eye2 | ParalyzedBaby ] -> [ Eye2 | NormalBaby ] again

LEFT [  Eye3 | no Obstacle ] -> [ Eye3 | LEFT RayH ] again
LEFT [ LEFT RayH | no Obstacle ] -> [ LEFT RayH | LEFT RayH ] again
LEFT [ Eye3 | ParalyzedBaby ] -> [ Eye3 | NormalBaby ] again

UP [  Eye4 | no Obstacle ] -> [ Eye4 | UP RayV ] again
UP [ UP RayV | no Obstacle ] -> [ UP RayV | UP RayV] again
UP [ Eye4 | ParalyzedBaby ] -> [ Eye4 | NormalBaby ] again

[ > Ray | ParalyzedBaby ] -> [ > Ray | NormalBaby ]

[moving Ray] -> [stationary Ray]

(Babies will die if they go into a red eye)
LEFT [ > NormalBaby | RedEye1 | stationary Obstacle] -> [| RedEye1 | stationary Obstacle] sfx0
UP [ > NormalBaby | RedEye2 | stationary Obstacle] -> [| RedEye2 | stationary Obstacle] sfx0
RIGHT [ > NormalBaby | RedEye3 | stationary Obstacle] -> [| RedEye3 | stationary Obstacle] sfx0
DOWN [ > NormalBaby | RedEye4 | stationary Obstacle] -> [| RedEye4 | stationary Obstacle] sfx0

(Eyes can be moved)
[ > MovingObstacle | MovingObstacle ] -> [ > MovingObstacle | > MovingObstacle ]
[ > MovableObstacle | stationary Obstacle ] -> [ stationary MovableObstacle | stationary Obstacle ]

(RedEye kills babies)
RIGHT [ > RedEye1 | Baby | stationary Obstacle ] -> [ > RedEye1 || Obstacle] sfx0
DOWN [ > RedEye2 | Baby | stationary Obstacle ] -> [ > RedEye2 || Obstacle] sfx0
LEFT [ > RedEye3 | Baby | stationary Obstacle ] -> [ > RedEye3 || Obstacle] sfx0
UP [ > RedEye4 | Baby | stationary Obstacle ] -> [ > RedEye4 || Obstacle] sfx0

[ action Player ] -> win

==============
WINCONDITIONS
============== 

no ParalyzedBaby

=======
LEVELS
=======

message /.

message Babys are paralyzed when they are unmotivated.

message They get motivated when an eye is looking at them.

message Leave no baby unmotivated.

message You can press X to skip a level!

##########
#>.......#
#>.......#
#>.#v----#
#>......+#
#>......+#
##########

(*o)
#######
#.....#
#.....#
#..-v+<
>.+-..#
#^....#
#######

(**o)
########
>+....+<
>+v--v+<
>+....+<
>+..#.+<
########

(**o)
#######
#..--.#
#.--..#
#>>>#.#
#...+.<
#..+..<
#####^#

(***o)
#########
#>...+.-#
#>....-.#
#>.#>...#
#>..+.-.#
#>...+..#
#########

########
#>...+.#
#>....-#
#>.#>..#
#>..+.-#
#>.+.-.#
########

(#######
#>....#
#>.+--#
#>.#>.#
#>.+--#
#>....#
#######)

#######
#>.+--#
#>.#>.#
#>.+--#
#>....#
#>....#
#######

######
#>+-##
#>+.##
#>.>.#
#>+.##
#>+-##
######

(**o)
#####.##
#>.#.>.#
#>+.-.-#
#>.+.-.#
#>.....#
########

(***o)
##########
##vvvvvvv#
#>.......#
#>.+.#...#
#>.......#
#>.#.-.###
#>.......#
#>.+.#.>.#
#>.......#
##########

(***o)
###########
#...v.v...#
#....-....#
#>...v...<#
#..->+<-..#
#>...^...<#
#....-....#
#...^.^...#
###########

#####################
>...................<
>...................<
>...................<
>...................<
#####.###############
#.........###########
#>+--.--+<###########
#>+-.^.-+<###########
#>+-...-+<###########
#####################

(***o)
#############
#...v.v.v...#
#...........#
#.....+.....#
#>.........<#
#.....^.....#
#>.+.<->.+.<#
#.....v.....#
#>.........<#
#.....+.....#
#...........#
#...^.^.^...#
#############


(
###############
###vv#v#v#vv###
###..........##
#>.+.......+.<#
#>..#v#v#v#..<#
##..>.....<..##
#>..#.-.-.#..<#
##..>.....<..##
#>..#.-.-.#..<#
##..>.....<..##
#>..#^#^#^#..<#
#>.+.......+.<#
###.........###
###^^#^#^#^^###
###############)

message Thanks for playing!

((*o)
########
#......#
1.+---.#
#......#
########

###vvv#
#.....#
#..---#
#...1+#
#..---#
#######)