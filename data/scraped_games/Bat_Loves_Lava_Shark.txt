title Bat Loves Lava Shark
author Tobin Mollett
homepage www.puzzlescript.net

realtime_interval 0.08

========
OBJECTS
========

background
black
00000
00000
00000
00000
00000

air
purple darkgreen 
00010
11111
01000
11111
00010

Wall
Grey darkgrey
00010
00100
11000
00101
00010

airblock
blue darkblue
00010
00100
11000
00101
00010

lavablock
blue darkred
00010
00100
11000
00101
00010

balloon
blue red darkred
.000.
01120
02110
01110
.000.

Bat1
black red white
.....
00.00
.101.
.2.2.
.....

Bat2
black red white
.....
.0.0.
01010
02.20
.....

Shark1
blue white black
.....
.....
000..
01121
1111.

Shark2
blue white
.0...
.00..
00000
00000
00001

Shark3
blue
.....
0....
00..0
.0000
0...0

Monkey1
brown lightbrown
1.0..
.000.
..0.1
.0.0.
.0.0.

Monkey2
brown lightbrown
..0.1
.000.
1.0..
.0.0.
.0.0.

MonkeyChar
black
0.0..
.000.
..0.0
.0.0.
.0.0.

Lava
darkred red
00000
10000
00000
00010
00000

temp
white
.....
.....
.....
.....
.....


=======
LEGEND
=======

. = air
, = Lava
b = bat1 and air
} = shark1 and lava
- = shark2 and lava
3 = shark3 and lava
m = monkey1 and air
# = wall
o = airblock and air
0 = lavablock and lava
x = balloon and air

Bat = Bat1 or Bat2
Shark = Shark1 or Shark2 or Shark3
Monkey = monkey1 or monkey2 or monkeychar
block = airblock or lavablock

Movable = Monkey or block or balloon
Permanent = wall

Player = Bat or Shark

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Lava, air
Bat, Shark, block, monkey, wall, balloon
temp

======
RULES
======

[ > bat | lava ] -> [ bat | lava ]
[ > shark | no lava ] -> [ shark | ]

[ > shark1 | monkey ] -> [ > shark1 | ]

down [ stationary movable ] -> [ > movable ]

[ > Bat | movable ] -> [ > bat | > movable ]
[ > Shark | movable | no movable ] -> [ > shark | > movable | ]
[ > Shark | movable | movable | no movable ] -> [ > shark | > movable | > movable | ]
[ > shark | movable | permanent ] -> [ shark | movable | permanent ]
[ > shark | movable | movable | permanent ] -> [ shark | movable | movable | permanent ]
[ > shark | movable | movable | movable ] -> [ shark | movable | movable | movable ]

[ Moving shark | Stationary shark ] -> [ shark | shark ]

(late left [ shark1 | no shark2 ] -> [ shark1 | shark2 ]
late left [ shark2 | no shark3 ] -> [ shark2 | shark3 ]
late left [ shark3 | shark ] -> [ shark3 | ])

late [ monkey lava ] -> [ monkeychar lava ]
late [ block lava ] -> [ lavablock lava ]

late up [ balloon lava | ] -> [ lava | lava ]
late horizontal [ lava | no lava no permanent ] -> [ lava | lava ] again
late down [ lava | no lava no permanent ] -> [ lava | lava ] again

late [ bat ] -> [ bat temp ]
late [ bat1 temp ] -> [ bat2 ]
late [ bat2 temp ] -> [ bat1 ]

late [ monkey ] -> [ monkey temp ]
late [ monkey1 temp ] -> [ monkey2 ]
late [ monkey2 temp ] -> [ monkey1 ]

late [temp] -> []

==============
WINCONDITIONS
==============

no monkey

=======
LEVELS
=======

message Bat loves Lava Shark.

message Lava Shark loves monkeys. He loves to eat them.

message Monkeys love life, and they hate being burned alive and then eaten by a shark.

message Can love survive?

##############
........#.....
......#...#...
..b..######...
......###.....
.......##...m.
........#..##.
,,,,,,,,#,,,,,
,3-},,###,,,,,
,,,,,,,#,,,##,
,,,,,,,0,,,,#,
##############

############
.......#....
.......#.m..
..b....#.#..
.......o....
.......#....
.......#.###
,,,,,,,,0#,#
,,3-},,,0###
,,,,,,,,0,##
,,,##,,,0###
,,,,#,,,0,##
############

##############
..............
..............
..b...........
......x.......
......#....m..
...........#..
..............
....#.........
,,,,,,,,,#...#
,3-},,,,,#...#
,,,,,,,,,#...#
##############