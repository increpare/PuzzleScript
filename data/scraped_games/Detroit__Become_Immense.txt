title Detroit: Become Immense
author Guilherme S. Tows
homepage zarat.us
run_rules_on_level_start

========
OBJECTS
========

Background 
BLACK

Wall #
BLUE

GrowR R
Green
..0..
...0.
00000
...0.
..0..

GrowL L
Green
..0..
.0...
00000
.0...
..0..

GrowU U
Green
..0..
.000.
0.0.0
..0..
..0..

GrowD D
Green
..0..
..0..
0.0.0
.000.
..0..

Player
Black
.....
.0.0.
.....
.000.
.....

Body
White
.000.
00000
00000
00000
.000.

BodyD
White
.....
.....
.....
.....
00000

BodyU
White
00000
.....
.....
.....
.....

BodyL
White
0....
0....
0....
0....
0....

BodyR
White
....0
....0
....0
....0
....0

WinL
Black

WinR
Black

WinU
Black

WinD
Black

LoseL
Black

LoseR
Black

LoseU
Black

LoseD
Black

WinStar
Black

=======
LEGEND
=======

. = Background
@ = Player and Body
WinX = WinR or WinL or WinD or WinU
Lose = LoseR or LoseL or LoseD or LoseU
Grow = GrowR or GrowL or GrowD or GrowU
BodyX = BodyD or BodyU or BodyL or BodyR

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
WinU, LoseU
WinD, LoseD
WinR, LoseR
WinL, LoseL
Grow, WinStar
Body
BodyD
BodyU
BodyL
BodyR
Player
Wall

======
RULES     
======     

[ BodyX ] -> [ ]
[ WinX ] -> [ ]
[ Lose ] -> [ ]
[ > Player ] [ stationary Body ] -> [ > Player ] [ > Body ]
[ > Body | Wall ] -> cancel
[ > Player | ] -> [ | Player ]
[ > Body | no Body ] -> [ | Body ]

right [ Body GrowR | no Wall no Body ] -> [ Body | Body ]
left [ Body GrowL | no Wall no Body ] -> [ Body | Body ]
down [ Body GrowD | no Wall no Body ] -> [ Body | Body ]
up [ Body GrowU | no Wall no Body ] -> [ Body | Body ]

down [ Body no BodyD | no bodyU Body ] -> [ Body BodyD | BodyU Body ]
right [ Body no BodyR | no bodyL Body ] -> [ Body BodyR | BodyL Body ]

right [ Body | no Body no LoseR ] -> [ Body | LoseR ]
left  [ Body | no Body no LoseL ] -> [ Body | LoseL ]
down  [ Body | no Body no LoseD ] -> [ Body | LoseD ]
up    [ Body | no Body no LoseU ] -> [ Body | LoseU ]

[ LoseL Wall ] -> [ WinL Wall ]
[ LoseR Wall ] -> [ WinR Wall ]
[ LoseD Wall ] -> [ WinD Wall ]
[ LoseU Wall ] -> [ WinU Wall ]

[ WinX ] [ Grow ] -> [  ] [ Grow ]

[ WinL ] [ WinR ] [ WinD ] [ WinU ] ->   win
==============
WINCONDITIONS
==============


=======     
LEVELS
=======

message 1.Get all arrows so that you 2.Become unable to move

############
#.L.#..#...#
#..#...#.R.#
#.......#..#
#..........#
#....@.....#
#..........#
#.#........#
#..#.D..#.L#
#U.#.......#
############

