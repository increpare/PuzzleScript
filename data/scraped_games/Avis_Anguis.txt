title Avis Anguis
author Terzalo [puzzles by CHz]
homepage terzalo.com

run_rules_on_level_start
again_interval 0.1
key_repeat_interval 100
verbose_logging 

========
OBJECTS
========

BackgroundSky
#42b9ff

BackgroundSoil
#705730

RedHead
#d61212 #ffe033
00000
00000
00100
00110
00000

RedTail1
#d61212

RedTail2
#eb4c4c

RedOverN1
#d61212
.000.
.....
.....
.....
.....

RedOverS1
#d61212
.....
.....
.....
.....
.000.

RedOverW1
#d61212
.....
0....
0....
0....
.....

RedOverE1
#d61212
.....
....0
....0
....0
.....

RedOverN2
#eb4c4c
.000.
.....
.....
.....
.....

RedOverS2
#eb4c4c
.....
.....
.....
.....
.000.

RedOverW2
#eb4c4c
.....
0....
0....
0....
.....

RedOverE2
#eb4c4c
.....
....0
....0
....0
.....

GreenHead
#33830b #ffe033
00000
00000
00100
00110
00000

GreenTail1
#33830b

GreenTail2
#48a619

GreenOverN1
#33830b
.000.
.....
.....
.....
.....

GreenOverS1
#33830b
.....
.....
.....
.....
.000.

GreenOverW1
#33830b
.....
0....
0....
0....
.....

GreenOverE1
#33830b
.....
....0
....0
....0
.....

GreenOverN2
#48a619
.000.
.....
.....
.....
.....

GreenOverS2
#48a619
.....
.....
.....
.....
.000.

GreenOverW2
#48a619
.....
0....
0....
0....
.....

GreenOverE2
#48a619
.....
....0
....0
....0
.....

BlueHead
#302cf7 #ffe033
00000
00000
00100
00110
00000

BlueTail1
#302cf7

BlueTail2
#3868f2

BlueOverN1
#302cf7
.000.
.....
.....
.....
.....

BlueOverS1
#302cf7
.....
.....
.....
.....
.000.

BlueOverW1
#302cf7
.....
0....
0....
0....
.....

BlueOverE1
#302cf7
.....
....0
....0
....0
.....

BlueOverN2
#3868f2
.000.
.....
.....
.....
.....

BlueOverS2
#3868f2
.....
.....
.....
.....
.000.

BlueOverW2
#3868f2
.....
0....
0....
0....
.....

BlueOverE2
#3868f2
.....
....0
....0
....0
.....

Eyes
white
.....
.0.0.
.....
.....
.....

Box1Front
#ffea77 #8d834d
00000
01110
01.10
01110
00000

Box1Back
#8d834d
.....
.000.
.0.0.
.000.
.....

Box1BackH
#8d834d
.....
0...0
.....
0...0
.....

Box1BackV
#8d834d
.0.0.
.....
.....
.....
.0.0.

Box2Front
#c9f3ff #38869c
00000
01110
01.10
01110
00000

Box2Back
#38869c
.....
.000.
.0.0.
.000.
.....

Box2BackH
#38869c
.....
0...0
.....
0...0
.....

Box2BackV
#38869c
.0.0.
.....
.....
.....
.0.0.

Box3Front
#d3d4ff #686b96
00000
01110
01.10
01110
00000

Box3Back
#686b96
.....
.000.
.0.0.
.000.
.....

Box3BackH
#686b96
.....
0...0
.....
0...0
.....

Box3BackV
#686b96
.0.0.
.....
.....
.....
.0.0.

Soil
#99753d

Grass
#99753d #abff34
11111
01101
00000
00000
00000

LightWater
#cdfeff #3572b0
00000
11111
11111
11111
11111

DarkWater
#cdfeff #19446f
00000
11111
11111
11111
11111

Spikes
#ededed #706e6d
.0.0.
01110
.101.
01110
.0.0.

Banana
#abff34 #fcfa2b
....0
11111
..111
111.1
..11.

Strawberry
#abff34 #fc2b2b
.00..
0011.
01111
.1111
..11.

PortalClosed
#ededed
.....
..0..
.000.
..0..
.....

Portal1
#ffebfe #ff8043 #ffcdb6 
.111.
12021
10001
12021
.111.

Portal2
#ffebfe #ff8043 #ffcdb6 
.111.
12021
10001
12021
.111.

Exit
white #fc2b2b #ff9d34 #fcfa2b #91dc00 #0066ff #ff43f4
.611.
60002
50002
50003
.443.

ExitClosed
white #fc2b2b #91dc00 #0066ff #ff43f4
.....
..1..
.402.
..3..
.....

TeleportOK
white
0.0.0
.....
0...0
.....
0.0.0

TeleportBlocked
white #b60000
1.0.1
.1.1.
0.1.0
.1.1.
1.0.1

UndoHint1
white black darkgray
.1111
.1000
.1022
.1000
.1002

UndoHint2
white black darkgray
1111.
0001.
2201.
2001.
0001.

UndoHint3
white black darkgray
.1022
.1000
.1222
.1111
.....

UndoHint4
white black darkgray
2201.
0001.
2221.
1111.
.....

Last
transparent
(Purple
.....
.....
..0..
.....
.....)

FruitConsumed
transparent

CanMove
transparent

Player
transparent

SnakeCounter
transparent

00
transparent
(white)

01
transparent
(lightgray)

02
transparent
(gray)

03
transparent
(darkgray)

04
transparent
(black)

05
transparent

North
white
..0..
.000.
0.0.0
..0..
..0..

South
white
..0..
..0..
0.0.0
.000.
..0..

West
white
..0..
.0...
00000
.0...
..0..

East
white
..0..
...0.
00000
...0.
..0..

Active
transparent
(yellow
0...0
.....
.....
.....
0...0)

Temp
transparent

Temp2
transparent

Temp3
transparent

BoxBlocker
transparent

CornerNW
#42b9ff
0....
.....
.....
.....
.....

CornerNE
#42b9ff
....0
.....
.....
.....
.....

CornerSW
#42b9ff
.....
.....
.....
.....
0....

CornerSE
#42b9ff
.....
.....
.....
.....
....0

CornerSoilNW
#705730
0....
.....
.....
.....
.....

CornerSoilNE
#705730
....0
.....
.....
.....
.....

CornerSoilSW
#705730
.....
.....
.....
.....
0....

CornerSoilSE
#705730
.....
.....
.....
.....
....0


=======
LEGEND
=======

. = BackgroundSky
, = BackgroundSoil

1 = RedHead
2 = GreenHead
3 = BlueHead

↑ = North
↓ = South
← = West
→ = East

● = Box1Front
○ = Box1Back
■ = Box2Front
□ = Box2Back
▪ = Box3Front
▫ = Box3Back

# = Soil
* = Spikes
~ = LightWater

p = Portal1
E = ExitClosed

b = Banana
w = Strawberry

Background = BackgroundSky or BackgroundSoil

Water = LightWater or DarkWater

RedTail = RedTail1 or RedTail2
RedSnake = RedHead or RedTail

GreenTail = GreenTail1 or GreenTail2
GreenSnake = GreenHead or GreenTail

BlueTail = BlueTail1 or BlueTail2
BlueSnake = BlueHead or BlueTail

Tail = RedTail or GreenTail or BlueTail
Head = RedHead or GreenHead or BlueHead
Snake = RedSnake or GreenSnake or BlueSnake

Box1 = Box1Front or Box1Back
Box2 = Box2Front or Box2Back
Box3 = Box3Front or Box3Back
Box = Box1 or Box2 or Box3
BoxFront = Box1Front or Box2Front or Box3Front
BoxBack = Box1Back or Box2Back or Box3Back

Pushable = Snake or Box1Front or Box2Front or Box3Front

PortalOpen = Portal1 or Portal2

Ground = Soil or Grass
Hazard = Water or Spikes
Fruit = Banana or Strawberry
Solid = RedSnake or GreenSnake or BlueSnake or Box1Front or Box2Front or Box3Front or Ground or Fruit
Number = 00 or 01 or 02 or 03 or 04 or 05
Direction = North or South or West or East

TeleportHint = TeleportOK or TeleportBlocked

CornerN = CornerNE or CornerNW
CornerS = CornerSE or CornerSW
CornerW = CornerNW or CornerSW
CornerE = CornerNE or CornerSE

Corner = CornerNE or CornerNW or CornerSE or CornerSW
CornerSoil = CornerSoilNE or CornerSoilNW or CornerSoilSE or CornerSoilSW

RedOverlap = RedOverS1 or RedOverN1 or RedOverW1 or RedOverE1 or RedOverS2 or RedOverN2 or RedOverW2 or RedOverE2
GreenOverlap = GreenOverS1 or GreenOverN1 or GreenOverW1 or GreenOverE1 or GreenOverS2 or GreenOverN2 or GreenOverW2 or GreenOverE2
BlueOverlap = BlueOverS1 or BlueOverN1 or BlueOverW1 or BlueOverE1 or BlueOverS2 or BlueOverN2 or BlueOverW2 or BlueOverE2
Overlap = RedOverlap or GreenOverlap or BlueOverlap

BoxBackHV = Box1BackH or Box1BackV or Box2BackH or Box2BackV or Box3BackH or Box3BackV

=======
SOUNDS
=======

sfx0 53380107
sfx1 98382700
sfx2 20652903
sfx3 71426104
undo 91478104

================
COLLISIONLAYERS
================

Background
Direction
Box1Back
Box2Back
Box3Back
Box1BackH
Box1BackV
Box2BackH
Box2BackV
Box3BackH
Box3BackV
Exit, ExitClosed
PortalClosed, PortalOpen
Solid, BoxBlocker
Overlap
CornerSoilNE
CornerSoilNW
CornerSoilSE
CornerSoilSW
CornerNE
CornerNW
CornerSE
CornerSW
Eyes
Water, Spikes
UndoHint1, UndoHint2, UndoHint3, UndoHint4
TeleportHint

Player
Number
Last
Active
SnakeCounter
FruitConsumed
CanMove
Temp
Temp2
Temp3

======
RULES
======

(======Level Start======)

(create necessary objects)

random [ ] -> [ Temp ]
[ Player ][ Temp ] -> [ Player ][ ]
[ Soil ][ Temp ] -> [ Soil Player SnakeCounter ][ ]

(turn second Portal1 into Portal2)

random [ Portal1 ] -> [ Portal1 Temp ]
random [ Portal1 no Temp ] -> [ Portal2 ]
[ Portal1 no Temp ] -> [ ]
[ Portal1 Temp ] -> [ Portal1 ]

(plant grass for fancy look)

down [ no Ground | Soil ] -> [  | Grass ]

(make wter dark under ground tiles)

up [ LightWater | Ground ] -> [ DarkWater | Ground ]
up [ LightWater | BackgroundSoil ] -> [ DarkWater | BackgroundSoil ]

(cut corners from ground)

left [ Ground no CornerSW no CornerSoilSW | no Ground ] -> [ Ground CornerSW | ]
right [ Ground no CornerSE no CornerSoilSE | no Ground ] -> [ Ground CornerSE | ]

down [ Ground CornerS | Ground ] -> [ Ground | Ground ]
down [ Ground CornerS | Water ] -> [ Ground | Water ]

down [ Ground CornerSW | BackgroundSoil ] -> [ Ground CornerSoilSW | BackgroundSoil ]
down [ Ground CornerSE | BackgroundSoil ] -> [ Ground CornerSoilSE | BackgroundSoil ]
left [ Ground CornerSW | BackgroundSoil ] -> [ Ground CornerSoilSW | BackgroundSoil ]
right [ Ground CornerSE | BackgroundSoil ] -> [ Ground CornerSoilSE | BackgroundSoil ]

(fill holes in background boxes)

[ Box1Back | no Box1 | ... | Box1 ] -> [ Box1Back | Box1Back | ... | Box1 ]
[ Box2Back | no Box2 | ... | Box2 ] -> [ Box2Back | Box2Back | ... | Box2 ]
[ Box3Back | no Box3 | ... | Box3 ] -> [ Box3Back | Box3Back | ... | Box3 ]

(set snake counter -- late because it uses Active)

late [ SnakeCounter no Number ][RedHead][GreenHead][BlueHead] -> [ SnakeCounter 03 ][RedHead][GreenHead][BlueHead]
late [ SnakeCounter no Number ][Active Head][no Active Head] -> [ SnakeCounter 02 ][Active Head][no Active Head]
late [ SnakeCounter no Number ] -> [ SnakeCounter 01 ]

(create snakes)

up [ RedHead | no Snake South ] -> [ RedHead | RedTail2 Last South ]
down [ RedHead | no Snake North ] -> [ RedHead | RedTail2 Last North ]
left [ RedHead | no Snake East ] -> [ RedHead | RedTail2 Last East ]
right [ RedHead | no Snake West ] -> [ RedHead | RedTail2 Last West ]

up [ RedTail1 Last | no Snake South ] -> [ RedTail1 | RedTail2 Last South ]
+ down [ RedTail1 Last | no Snake North ] -> [ RedTail1 | RedTail2 Last North ]
+ left [ RedTail1 Last | no Snake East ] -> [ RedTail1 | RedTail2 Last East ]
+ right [ RedTail1 Last | no Snake West ] -> [ RedTail1 | RedTail2 Last West ]

+ up [ RedTail2 Last | no Snake South ] -> [ RedTail2 | RedTail1 Last South ]
+ down [ RedTail2 Last | no Snake North ] -> [ RedTail2 | RedTail1 Last North ]
+ left [ RedTail2 Last | no Snake East ] -> [ RedTail2 | RedTail1 Last East ]
+ right [ RedTail2 Last | no Snake West ] -> [ RedTail2 | RedTail1 Last West ]

[ RedHead ] -> [ RedHead Last ]
[ RedHead Last ][ RedTail Last ] -> [ RedHead ][ RedTail Last ]

up [ GreenHead | no Snake South ] -> [ GreenHead | GreenTail2 Last South ]
down [ GreenHead | no Snake North ] -> [ GreenHead | GreenTail2 Last North ]
left [ GreenHead | no Snake East ] -> [ GreenHead | GreenTail2 Last East ]
right [ GreenHead | no Snake West ] -> [ GreenHead | GreenTail2 Last West ]

up [ GreenTail1 Last | no Snake South ] -> [ GreenTail1 | GreenTail2 Last South ]
+ down [ GreenTail1 Last | no Snake North ] -> [ GreenTail1 | GreenTail2 Last North ]
+ left [ GreenTail1 Last | no Snake East ] -> [ GreenTail1 | GreenTail2 Last East ]
+ right [ GreenTail1 Last | no Snake West ] -> [ GreenTail1 | GreenTail2 Last West ]

+ up [ GreenTail2 Last | no Snake South ] -> [ GreenTail2 | GreenTail1 Last South ]
+ down [ GreenTail2 Last | no Snake North ] -> [ GreenTail2 | GreenTail1 Last North ]
+ left [ GreenTail2 Last | no Snake East ] -> [ GreenTail2 | GreenTail1 Last East ]
+ right [ GreenTail2 Last | no Snake West ] -> [ GreenTail2 | GreenTail1 Last West ]

[ GreenHead ] -> [ GreenHead Last ]
[ GreenHead Last ][ GreenTail Last ] -> [ GreenHead ][ GreenTail Last ]

up [ BlueHead | no Snake South ] -> [ BlueHead | BlueTail2 Last South ]
down [ BlueHead | no Snake North ] -> [ BlueHead | BlueTail2 Last North ]
left [ BlueHead | no Snake East ] -> [ BlueHead | BlueTail2 Last East ]
right [ BlueHead | no Snake West ] -> [ BlueHead | BlueTail2 Last West ]

up [ BlueTail1 Last | no Snake South ] -> [ BlueTail1 | BlueTail2 Last South ]
+ down [ BlueTail1 Last | no Snake North ] -> [ BlueTail1 | BlueTail2 Last North ]
+ left [ BlueTail1 Last | no Snake East ] -> [ BlueTail1 | BlueTail2 Last East ]
+ right [ BlueTail1 Last | no Snake West ] -> [ BlueTail1 | BlueTail2 Last West ]

+ up [ BlueTail2 Last | no Snake South ] -> [ BlueTail2 | BlueTail1 Last South ]
+ down [ BlueTail2 Last | no Snake North ] -> [ BlueTail2 | BlueTail1 Last North ]
+ left [ BlueTail2 Last | no Snake East ] -> [ BlueTail2 | BlueTail1 Last East ]
+ right [ BlueTail2 Last | no Snake West ] -> [ BlueTail2 | BlueTail1 Last West ]

[ BlueHead ] -> [ BlueHead Last ]
[ BlueHead Last ][ BlueTail Last ] -> [ BlueHead ][ BlueTail Last ]

(======Pre-Movement======)

[ BackGroundSoil no Snake Corner ] -> [ BackgroundSoil ]

[ BoxBackHV ] -> [  ]

(if there's an undo hint, then a snake is dead -- skip all moves until player presses undo)

[ UndoHint1 ] -> cancel

(remove teleport hints)

[ moving Player ][ TeleportHint ] -> [ moving Player ][ ]

(switch active snake)

[action Player][Active RedHead][GreenHead] -> [Player][RedHead][Active GreenHead]
[action Player][Active RedHead][BlueHead] -> [Player][RedHead][Active BlueHead]
[action Player][Active GreenHead][BlueHead] -> [Player][GreenHead][Active BlueHead]
[action Player][Active GreenHead][RedHead] -> [Player][GreenHead][Active RedHead]
[action Player][Active BlueHead][RedHead] -> [Player][BlueHead][Active RedHead]
[action Player][Active BlueHead][GreenHead] -> [Player][BlueHead][Active GreenHead]

(check if there's an active snake head)

random [ ] -> [ Temp ]
[ Active Head ][ Temp ] -> [ Active Head ][ ]
[ Head ][ Temp ] -> [ Active Head ][ ]

(make whole snakes Active)

[ Active RedHead ][ no Active RedSnake ] -> [ Active RedHead ][ Active RedSnake ]
[ Active RedHead ][ Active no RedSnake ] -> [ Active RedHead ][ no RedSnake ]

[ Active GreenHead ][ no Active GreenSnake ] -> [ Active GreenHead ][ Active GreenSnake ]
[ Active GreenHead ][ Active no GreenSnake ] -> [ Active GreenHead ][ no GreenSnake ]

[ Active BlueHead ][ no Active BlueSnake ] -> [ Active BlueHead ][ Active BlueSnake ]
[ Active BlueHead ][ Active no BlueSnake ] -> [ Active BlueHead ][ no BlueSnake ]

(======Movement======)

(apply gravity)

startloop
rigid down [ stationary RedSnake | no Ground ] -> [ down RedSnake |  ]
+ rigid [ down RedSnake | stationary RedSnake ] -> [ down RedSnake | down RedSnake ]

rigid down [ stationary GreenSnake | no Ground ] -> [ down GreenSnake |  ]
+ rigid [ down GreenSnake | stationary GreenSnake ] -> [ down GreenSnake | down GreenSnake ]

rigid down [ stationary BlueSnake | no Ground ] -> [ down BlueSnake |  ]
+ rigid [ down BlueSnake | stationary BlueSnake ] -> [ down BlueSnake | down BlueSnake ]

rigid down [ stationary Box1 | no Ground ] -> [ down Box1 |  ]
+ rigid [ down Box1 | stationary Box1 ] -> [ down Box1 | down Box1 ]

rigid down [ stationary Box2 | no Ground ] -> [ down Box2 |  ]
+ rigid [ down Box2 | stationary Box2 ] -> [ down Box2 | down Box2 ]

rigid down [ stationary Box3 | no Ground ] -> [ down Box3 |  ]
+ rigid [ down Box3 | stationary Box3 ] -> [ down Box3 | down Box3 ]
endloop

(check if player can move -- no snakes and boxes are falling)

[ Player no CanMove ] -> [ Player CanMove ]

[ CanMove ][ moving Pushable ] -> [  ][ moving Pushable ]

(using again prevents player from making undoable moves while snakes are falling)

[ Player ] -> again

(if not falling, apply movement to active Head)

[ CanMove ][ moving Player ][ stationary Head Active ] -> [ CanMove ][ moving Player ][ moving Head Active ] sfx0

(late-check if head of active snake moved at all)

[ CanMove ][ Active > Head ] -> [ CanMove ][ Active > Head Temp3 ]
late [ Active Head Temp3 ] -> cancel
late [ Temp3 ] -> [ ]

(don't let snakes move into themselves)

[ CanMove ][ > Head Active | Snake Active ] -> cancel

(consume fruit)

[ CanMove ][ > Head Active | Fruit ][ Player ] -> [ CanMove ][ > Head Active |  ][ Player FruitConsumed ] sfx1

(move the whole snake after the head)

[ CanMove ][ up Head Active ] -> [ CanMove ][ up Head Active North ]
[ CanMove ][ down Head Active ] -> [ CanMove ][ down Head Active South ]
[ CanMove ][ left Head Active ] -> [ CanMove ][ left Head Active West ]
[ CanMove ][ right Head Active ] -> [ CanMove ][ right Head Active East ]

[ CanMove ][ > Head Active ][ stationary Tail Active North ] -> [ CanMove ][ > Head Active ][ up Tail Active North ]
[ CanMove ][ > Head Active ][ stationary Tail Active South ] -> [ CanMove ][ > Head Active ][ down Tail Active South ]
[ CanMove ][ > Head Active ][ stationary Tail Active West ] -> [ CanMove ][ > Head Active ][ left Tail Active West ]
[ CanMove ][ > Head Active ][ stationary Tail Active East ] -> [ CanMove ][ > Head Active ][ right Tail Active East ]

(let snakes push each other and boxes)

startloop
rigid [ CanMove ][ moving RedSnake | stationary RedSnake ] -> [ CanMove ][ moving RedSnake | moving RedSnake ]
+ [ CanMove ][ >  Pushable no RedSnake | stationary RedSnake ] -> [ CanMove ][  >  Pushable no RedSnake | > RedSnake  ]

 rigid [ CanMove ][ moving GreenSnake | stationary GreenSnake ] -> [ CanMove ][ moving GreenSnake | moving GreenSnake ]
+ [ CanMove ][ >  Pushable no GreenSnake | stationary GreenSnake ] -> [ CanMove ][  >  Pushable no GreenSnake | > GreenSnake  ]

rigid [ CanMove ][ moving BlueSnake | stationary BlueSnake ] -> [ CanMove ][ moving BlueSnake | moving BlueSnake ]
+ [ CanMove ][ >  Pushable no BlueSnake | stationary BlueSnake ] -> [ CanMove ][  >  Pushable no BlueSnake | > BlueSnake  ]

rigid [ CanMove ][ moving Box1 | stationary Box1 ] -> [ CanMove ][ moving Box1 | moving Box1 ]
+ [ CanMove ][ >  Pushable no Box1Front | stationary Box1Front ] -> [ CanMove ][  >  Pushable no Box1Front | > Box1Front  ]

rigid [ CanMove ][ moving Box2 | stationary Box2 ] -> [ CanMove ][ moving Box2 | moving Box2 ]
+ [ CanMove ][ >  Pushable no Box2Front | stationary Box2Front ] -> [ CanMove ][  >  Pushable no Box2Front | > Box2Front  ]

rigid [ CanMove ][ moving Box3 | stationary Box3 ] -> [ CanMove ][ moving Box3 | moving Box3 ]
+ [ CanMove ][ >  Pushable no Box3Front | stationary Box3Front ] -> [ CanMove ][  >  Pushable no Box3Front | > Box3Front  ]
endloop

(don't let snakes commit suicide and kill each other)

[ CanMove ][ > Snake | Hazard ] -> cancel

(don't let boxes fall through spikes)

[ > Pushable no Snake | Spikes ] -> [ > Pushable | Spikes BoxBlocker ]
late [ BoxBlocker ] -> [ ]

(stop boxes and snakes from moving into active snake)
(this is necessary to prevent snakes from pushing objects that will clash into them from behind -- deadlock)

[ CanMove ][ > Pushable no RedSnake | RedSnake Active no Last ] -> cancel
[ CanMove ][ > Pushable no GreenSnake | GreenSnake Active no Last ] -> cancel
[ CanMove ][ > Pushable no BlueSnake | BlueSnake Active no Last ] -> cancel

[ CanMove ][ > Pushable no RedSnake | perpendicular RedSnake Active Last ] -> cancel
[ CanMove ][ > Pushable no GreenSnake | perpendicular GreenSnake Active Last ] -> cancel
[ CanMove ][ > Pushable no BlueSnake | perpendicular BlueSnake Active Last ] -> cancel

(move snake attributes)

[ > Snake stationary Active ] -> [ > Snake > Active ]
[ > Snake stationary Last ] -> [ > Snake > Last ]

(don't move Last mark if fruit was consumed)

[ FruitConsumed ][ > Snake Active > Last ] -> [ FruitConsumed ][ > Snake Active stationary Last ]

[ up Head Active stationary Last ] -> [ up Head Active stationary Last North ]
[ down Head Active stationary Last ] -> [ down Head Active stationary Last South ]
[ left Head Active stationary Last ] -> [ left Head Active stationary Last West ]
[ right Head Active stationary Last ] -> [ right Head Active stationary Last East ]

[ > Snake stationary Active ] -> [ > Snake > Active ]

(only move directions if move wasn't made by player)

[ CanMove ][ > Snake stationary Direction no Active ] -> [ CanMove ][ > Snake > Direction ]
[ stationary Player ][ > Snake stationary Direction ] -> [ stationary Player ][ > Snake > Direction ]

(move corners)

[ Snake > Direction stationary Corner ] -> [ Snake > Direction > Corner ]
[ Snake > Direction stationary CornerSoil ] -> [ Snake > Direction > CornerSoil ]

(remove direction from last part if snake isn't growing)

[ CanMove ][ > Snake Active > Last Direction ] -> [ CanMove ][ > Snake Active > Last ]

(open portals)

random [ > RedSnake PortalClosed ][ no Pushable PortalClosed ] -> [ > RedSnake PortalClosed Temp ][ PortalClosed Temp2 ]
[ > RedSnake | PortalClosed Temp ][ PortalClosed Temp2 ] -> [ > RedSnake | PortalClosed ][ PortalClosed ]
[ > RedSnake | PortalClosed Temp2 ][ PortalClosed Temp ] -> [ > RedSnake | PortalClosed ][ PortalClosed ]

[ PortalClosed Temp ][ PortalClosed Temp2 ] -> [ Portal1 ][ Portal2 ]

random [ > GreenSnake PortalClosed ][ no Pushable PortalClosed ] -> [ > GreenSnake PortalClosed Temp ][ PortalClosed Temp2 ]
[ > GreenSnake | PortalClosed Temp ][ PortalClosed Temp2 ] -> [ > GreenSnake | PortalClosed ][ PortalClosed ]
[ > GreenSnake | PortalClosed Temp2 ][ PortalClosed Temp ] -> [ > GreenSnake | PortalClosed ][ PortalClosed ]

[ PortalClosed Temp ][ PortalClosed Temp2 ] -> [ Portal1 ][ Portal2 ]

random [ > BlueSnake PortalClosed ][ no Pushable PortalClosed ] -> [ > BlueSnake PortalClosed Temp ][ PortalClosed Temp2 ]
[ > BlueSnake | PortalClosed Temp ][ PortalClosed Temp2 ] -> [ > BlueSnake | PortalClosed ][ PortalClosed ]
[ > BlueSnake | PortalClosed Temp2 ][ PortalClosed Temp ] -> [ > BlueSnake | PortalClosed ][ PortalClosed ]

[ PortalClosed Temp ][ PortalClosed Temp2 ] -> [ Portal1 ][ Portal2 ]

random [ > Box1Front PortalClosed ][ no Pushable PortalClosed ] -> [ > Box1Front PortalClosed Temp ][ PortalClosed Temp2 ]
[ > Box1Front | PortalClosed Temp ][ PortalClosed Temp2 ] -> [ > Box1Front | PortalClosed ][ PortalClosed ]
[ > Box1Front | PortalClosed Temp2 ][ PortalClosed Temp ] -> [ > Box1Front | PortalClosed ][ PortalClosed ]

[ PortalClosed Temp ][ PortalClosed Temp2 ] -> [ Portal1 ][ Portal2 ]

random [ > Box2Front PortalClosed ][ no Pushable PortalClosed ] -> [ > Box2Front PortalClosed Temp ][ PortalClosed Temp2 ]
[ > Box2Front | PortalClosed Temp ][ PortalClosed Temp2 ] -> [ > Box2Front | PortalClosed ][ PortalClosed ]
[ > Box2Front | PortalClosed Temp2 ][ PortalClosed Temp ] -> [ > Box2Front | PortalClosed ][ PortalClosed ]

[ PortalClosed Temp ][ PortalClosed Temp2 ] -> [ Portal1 ][ Portal2 ]

random [ > Box3Front PortalClosed ][ no Pushable PortalClosed ] -> [ > Box3Front PortalClosed Temp ][ PortalClosed Temp2 ]
[ > Box3Front | PortalClosed Temp ][ PortalClosed Temp2 ] -> [ > Box3Front | PortalClosed ][ PortalClosed ]
[ > Box3Front | PortalClosed Temp2 ][ PortalClosed Temp ] -> [ > Box3Front | PortalClosed ][ PortalClosed ]

[ PortalClosed Temp ][ PortalClosed Temp2 ] -> [ Portal1 ][ Portal2 ]

(======Post-Movement======)

(reset temporary objects)

late [ CanMove ] -> [ ]

late [ FruitConsumed ] -> [ ]

(create new snake part)

late [ Active RedSnake ][ Last no Snake | RedHead ] -> [ Active RedSnake ][ Last RedTail2 Active | RedHead ]
late [ Active RedSnake ][ Last no Snake | RedTail1 ] -> [ Active RedSnake ][ Last RedTail2 Active | RedTail1 ]
late [ Active RedSnake ][ Last no Snake | RedTail2 ] -> [ Active RedSnake ][ Last RedTail1 Active | RedTail2 ]

late [ Active GreenSnake ][ Last no Snake | GreenHead ] -> [ Active GreenSnake ][ Last GreenTail2 Active | GreenHead ]
late [ Active GreenSnake ][ Last no Snake | GreenTail1 ] -> [ Active GreenSnake ][ Last GreenTail2 Active | GreenTail1 ]
late [ Active GreenSnake ][ Last no Snake | GreenTail2 ] -> [ Active GreenSnake ][ Last GreenTail1 Active | GreenTail2 ]

late [ Active BlueSnake ][ Last no Snake | BlueHead ] -> [ Active BlueSnake ][ Last BlueTail2 Active | BlueHead ]
late [ Active BlueSnake ][ Last no Snake | BlueTail1 ] -> [ Active BlueSnake ][ Last BlueTail2 Active | BlueTail1 ]
late [ Active BlueSnake ][ Last no Snake | BlueTail2 ] -> [ Active BlueSnake ][ Last BlueTail1 Active | BlueTail2 ]

(teleport snakes)

late [ Pushable Portal2 ][ no Pushable Portal1 ] -> [ Pushable Portal1 ][ Portal2 ]

startloop
late [ RedSnake no Temp ][ RedSnake Portal1 no Temp3 ][ Portal2 ] -> [ RedSnake ][ RedSnake Portal1 Temp Temp3 ][ Portal2 Temp2 Temp3 ]
late [ GreenSnake no Temp ][ GreenSnake Portal1 no Temp3 ][ Portal2 ] -> [ GreenSnake ][ GreenSnake Portal1 Temp Temp3 ][ Portal2 Temp2 Temp3 ]
late [ BlueSnake no Temp ][ BlueSnake Portal1 no Temp3 ][ Portal2 ] -> [ BlueSnake ][ BlueSnake Portal1 Temp Temp3 ][ Portal2 Temp2 Temp3 ]

late up [ Temp Temp3 North | Snake no Temp ][ Temp2 Temp3 | no Temp2 ] -> [ Temp North | Snake Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late down [ Temp Temp3 South | Snake no Temp ][ Temp2 Temp3 | no Temp2 ] -> [ Temp South | Snake Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late left [ Temp Temp3 West | Snake no Temp ][ Temp2 Temp3 | no Temp2 ] -> [ Temp West | Snake Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late right [ Temp Temp3 East | Snake no Temp ][ Temp2 Temp3 | no Temp2 ] -> [ Temp East | Snake Temp Temp3 ][ Temp2 | Temp2 Temp3 ]

late down [ Temp Temp3 | Snake North no Temp ][ Temp2 Temp3 | no Temp2 ] -> [ Temp | Snake North Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late up [ Temp Temp3 | Snake South no Temp ][ Temp2 Temp3 | no Temp2 ] -> [ Temp| Snake South Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late right [ Temp Temp3 | Snake West no Temp ][ Temp2 Temp3 | no Temp2 ] -> [ Temp | Snake West Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late left [ Temp Temp3 | Snake East no Temp ][ Temp2 Temp3 | no Temp2 ] -> [ Temp | Snake East Temp Temp3 ][ Temp2 | Temp2 Temp3 ] 

late [ Temp3 ] -> [ ]
endloop

late [ Temp2 no Solid no Hazard ][ Player ] -> [ Temp2 TeleportOK ][ Player CanMove ]
late [ Temp2 no TeleportOK ][ CanMove ] -> [ Temp2 TeleportBlocked ][ ]
late [ Temp2 no TeleportOK ] -> [ Temp2 TeleportBlocked ]

late [ CanMove ][ TeleportHint ] -> [ CanMove ][ ]

startloop
late [ CanMove ][ Temp2 no RedSnake ][ RedSnake Portal1 no Temp3 ][ Portal2 ] -> [ CanMove ][ Temp2 ][ RedSnake Portal1 Temp3 ][ RedSnake Portal2 Temp3 ]
late [ CanMove ][ Temp2 no GreenSnake ][ GreenSnake Portal1 no Temp3 ][ Portal2 ] -> [ CanMove ][ Temp2 ][ GreenSnake Portal1 Temp3 ][ GreenSnake Portal2 Temp3 ]
late [ CanMove ][ Temp2 no BlueSnake ][ BlueSnake Portal1 no Temp3 ][ Portal2 ] -> [ CanMove ][ Temp2 ][ BlueSnake Portal1 Temp3 ][ BlueSnake Portal2 Temp3 ]

late [ CanMove ][ Temp Temp3 Last ][ Temp2 Temp3 ] -> [ CanMove ][ Temp Temp3 ][ Temp2 Temp3 Last ]

late up [ CanMove ][ Temp Temp3 North | Snake Temp ][ Temp2 Temp3 | Temp2 ] -> [ CanMove ][ Temp | Temp Temp3 ][ Temp2 North | Snake Temp2 Temp3 ]
+ late down [ CanMove ][ Temp Temp3 South | Snake Temp ][ Temp2 Temp3 | Temp2 ] -> [ CanMove ][ Temp | Temp Temp3 ][ Temp2 South | Snake Temp2 Temp3 ]
+ late left [ CanMove ][ Temp Temp3 West | Snake Temp ][ Temp2 Temp3 | Temp2 ] -> [ CanMove ][ Temp | Temp Temp3 ][ Temp2 West | Snake Temp2 Temp3 ]
+ late right [ CanMove ][ Temp Temp3 East | Snake Temp ][ Temp2 Temp3 | Temp2 ] -> [ CanMove ][ Temp | Temp Temp3 ][ Temp2 East | Snake Temp2 Temp3 ]

late down [ CanMove ][ Temp Temp3 | Snake North Temp ][ Temp2 Temp3 | Temp2 ] -> [ CanMove ][ Temp | Temp Temp3 ][ Temp2 | Snake North Temp2 Temp3 ]
+ late up [ CanMove ][ Temp Temp3 | Snake South Temp ][ Temp2 Temp3 | Temp2 ] -> [ CanMove ][ Temp | Temp Temp3 ][ Temp2 | Snake South Temp2 Temp3 ]
+ late right [ CanMove ][ Temp Temp3 | Snake West Temp ][ Temp2 Temp3 | Temp2 ] -> [ CanMove ][ Temp | Temp Temp3 ][ Temp2 | Snake West Temp2 Temp3 ]
+ late left [ CanMove ][ Temp Temp3 | Snake East Temp ][ Temp2 Temp3 | Temp2 ] -> [ CanMove ][ Temp | Temp Temp3 ][ Temp2 | Snake East Temp2 Temp3 ]

late [ CanMove ][ Temp Temp3 Last ][ Temp2 Temp3 ] -> [ CanMove ][ Temp Temp3 ][ Temp2 Temp3 Last ]

late [ CanMove ][ Temp3 ] -> [ CanMove ][ ]
endloop

late [ CanMove ][ Temp Active ][ Temp2 no Active ] -> [ CanMove ][ Temp ][ Temp2 Active ]

late [ CanMove ][ Temp Snake ] -> [ CanMove ][ ]
late [ CanMove ][ Temp Direction ] -> [ CanMove ][ ]

late [ CanMove ] -> [ ]
late [ Temp ] -> [ ]
late [ Temp2 ] -> [ ]

(teleport boxes)

late down [ Box1Front Portal1 | Box1 no Direction ] -> [ Box1Front Portal1 | Box1 North ]
+ late up [ Box1Front Portal1 | Box1 no Direction ] -> [ Box1Front Portal1 | Box1 South ]
+ late right [ Box1Front Portal1 | Box1 no Direction ] -> [ Box1Front Portal1 | Box1 West ]
+ late left [ Box1Front Portal1 | Box1 no Direction ] -> [ Box1Front Portal1 | Box1 East ]

late down [ Box1Front Portal1 ][ Box1 Direction | Box1 no Direction no Portal1 ] -> [ Box1Front Portal1 ][ Box1 Direction | Box1 North ]
+ late up [ Box1Front Portal1 ][ Box1 Direction | Box1 no Direction no Portal1 ] -> [ Box1Front Portal1 ][ Box1 Direction | Box1 South ]
+ late right [ Box1Front Portal1 ][ Box1 Direction | Box1 no Direction no Portal1 ] -> [ Box1Front Portal1 ][ Box1 Direction | Box1 West ]
+ late left [ Box1Front Portal1 ][ Box1 Direction | Box1 no Direction no Portal1 ] -> [ Box1Front Portal1 ][ Box1 Direction | Box1 East ]


late down [ Box2Front Portal1 | Box2 no Direction ] -> [ Box2Front Portal1 | Box2 North ]
+ late up [ Box2Front Portal1 | Box2 no Direction ] -> [ Box2Front Portal1 | Box2 South ]
+ late right [ Box2Front Portal1 | Box2 no Direction ] -> [ Box2Front Portal1 | Box2 West ]
+ late left [ Box2Front Portal1 | Box2 no Direction ] -> [ Box2Front Portal1 | Box2 East ]

late down [ Box2Front Portal1 ][ Box2 Direction | Box2 no Direction no Portal1 ] -> [ Box2Front Portal1 ][ Box2 Direction | Box2 North ]
+ late up [ Box2Front Portal1 ][ Box2 Direction | Box2 no Direction no Portal1 ] -> [ Box2Front Portal1 ][ Box2 Direction | Box2 South ]
+ late right [ Box2Front Portal1 ][ Box2 Direction | Box2 no Direction no Portal1 ] -> [ Box2Front Portal1 ][ Box2 Direction | Box2 West ]
+ late left [ Box2Front Portal1 ][ Box2 Direction | Box2 no Direction no Portal1 ] -> [ Box2Front Portal1 ][ Box2 Direction | Box2 East ]


late down [ Box3Front Portal1 | Box3 no Direction ] -> [ Box3Front Portal1 | Box3 North ]
+ late up [ Box3Front Portal1 | Box3 no Direction ] -> [ Box3Front Portal1 | Box3 South ]
+ late right [ Box3Front Portal1 | Box3 no Direction ] -> [ Box3Front Portal1 | Box3 West ]
+ late left [ Box3Front Portal1 | Box3 no Direction ] -> [ Box3Front Portal1 | Box3 East ]

late down [ Box3Front Portal1 ][ Box3 Direction | Box3 no Direction no Portal1 ] -> [ Box3Front Portal1 ][ Box3 Direction | Box3 North ]
+ late up [ Box3Front Portal1 ][ Box3 Direction | Box3 no Direction no Portal1 ] -> [ Box3Front Portal1 ][ Box3 Direction | Box3 South ]
+ late right [ Box3Front Portal1 ][ Box3 Direction | Box3 no Direction no Portal1 ] -> [ Box3Front Portal1 ][ Box3 Direction | Box3 West ]
+ late left [ Box3Front Portal1 ][ Box3 Direction | Box3 no Direction no Portal1 ] -> [ Box3Front Portal1 ][ Box3 Direction | Box3 East ]

random late [ BoxFront Portal1 ][ Portal2 ] -> [ BoxFront Portal1 Temp ][ Portal2 Temp2 TeleportOK ]

startloop
random late [ Box1 no Temp ][ Box1Front Portal1 ][ Portal2 ] -> [ Box1 ][ Box1Front Portal1 Temp3 ][ Portal2 Temp3 ]
random late [ Box2 no Temp ][ Box2Front Portal1 ][ Portal2 ] -> [ Box2 ][ Box2Front Portal1 Temp3 ][ Portal2 Temp3 ]
random late [ Box3 no Temp ][ Box3Front Portal1 ][ Portal2 ] -> [ Box3 ][ Box3Front Portal1 Temp3 ][ Portal2 Temp3 ]

late down [ Box Temp Temp3 | BoxFront North ][ Temp2 Temp3 |  ]  -> [ Box Temp | BoxFront North Temp Temp3 ][ Temp2 | Temp2 Temp3 TeleportOK ]
+ late up [ Box Temp Temp3 | BoxFront South ][ Temp2 Temp3 |  ] -> [ Box Temp | BoxFront South Temp Temp3 ][ Temp2 | Temp2 Temp3 TeleportOK ]
+ late right [ Box Temp Temp3 | BoxFront West ][ Temp2 Temp3 |  ] -> [ Box Temp | BoxFront West Temp Temp3 ][ Temp2 | Temp2 Temp3 TeleportOK ]
+ late left [ Box Temp Temp3 | BoxFront East ][ Temp2 Temp3 |  ] -> [ Box Temp | BoxFront East Temp Temp3 ][ Temp2 | Temp2 Temp3 TeleportOK ]

+ late down [ Box Temp Temp3 | BoxBack North ][ Temp2 Temp3 |  ]  -> [ Box Temp | BoxBack North Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late up [ Box Temp Temp3 | BoxBack South ][ Temp2 Temp3 |  ] -> [ Box Temp | BoxBack South Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late right [ Box Temp Temp3 | BoxBack West ][ Temp2 Temp3 |  ] -> [ Box Temp | BoxBack West Temp Temp3 ][ Temp2 | Temp2 Temp3 ]
+ late left [ Box Temp Temp3 | BoxBack East ][ Temp2 Temp3 |  ] -> [ Box Temp | BoxBack East Temp Temp3 ][ Temp2 | Temp2 Temp3 ]

late [ Temp Temp3 Direction ][ Temp2 Temp3 ] -> [ Temp ][ Temp2 ]
endloop

late random [ Player ] -> [ Player CanMove ]
late [ Temp2 TeleportOK Solid ] -> [ Temp2 TeleportBlocked Solid ]
late [ Temp2 TeleportOK Hazard ] -> [ Temp2 TeleportBlocked Hazard ]
late random [ Temp2 TeleportBlocked ][ CanMove ] -> [ Temp2 TeleportBlocked ][ ]

late [ CanMove ][ Portal1 Box1Front ][ Temp2 TeleportOK ] -> [ CanMove ][ Portal1 Box1Front ][ Box1Front ]
late [ CanMove ][ Portal1 Box1Front ][ Temp2 no TeleportOK ] -> [ CanMove ][ Portal1 Box1Front ][ Box1Back ]

late [ CanMove ][ Portal1 Box2Front ][ Temp2 TeleportOK ] -> [ CanMove ][ Portal1 Box2Front ][ Box2Front ]
late [ CanMove ][ Portal1 Box2Front ][ Temp2 no TeleportOK ] -> [ CanMove ][ Portal1 Box2Front ][ Box2Back ]

late [ CanMove ][ Portal1 Box3Front ][ Temp2 TeleportOK ] -> [ CanMove ][ Portal1 Box3Front ][ Box3Front ]
late [ CanMove ][ Portal1 Box3Front ][ Temp2 no TeleportOK ] -> [ CanMove ][ Portal1 Box3Front ][ Box3Back ]

late [ CanMove ][ Temp Direction ] -> [ CanMove ][ Temp ]
late [ CanMove ][ Portal1 Box1Front ][ Temp no Portal1 Box1 ] -> [ CanMove ][ Portal1 Box1Front ][ ]
late [ CanMove ][ Portal1 Box2Front ][ Temp no Portal1 Box2 ] -> [ CanMove ][ Portal1 Box2Front ][ ]
late [ CanMove ][ Portal1 Box3Front ][ Temp no Portal1 Box3 ] -> [ CanMove ][ Portal1 Box3Front ][ ]
late [ CanMove ][ Portal1 BoxFront ] -> [ CanMove ][ Portal1 ]

late [ CanMove ] -> [ ]
late [ Temp ] -> [ ]
late [ Temp2 ] -> [ ]

late [ Pushable PortalOpen ][ Portal1 ][ Portal2 ] -> [ Pushable PortalClosed ][ PortalClosed ][ PortalClosed ]

(snakes die when touching water and spikes)

late [ RedSnake Hazard ][ RedSnake Last ] -> [ RedSnake Hazard ][ RedSnake ]
late [ RedSnake Hazard ][ RedSnake Number ] -> [ RedSnake Hazard ][ RedSnake ]
late [ RedSnake Hazard ][ RedSnake no Hazard ] -> [ RedSnake Hazard ][ ]

late [ GreenSnake Hazard ][ GreenSnake Last ] -> [ GreenSnake Hazard ][ GreenSnake ]
late [ GreenSnake Hazard ][ GreenSnake Number ] -> [ GreenSnake Hazard ][ GreenSnake ]
late [ GreenSnake Hazard ][ GreenSnake no Hazard ] -> [ GreenSnake Hazard ][ ]

late [ BlueSnake Hazard ][ BlueSnake Last ] -> [ BlueSnake Hazard ][ BlueSnake ]
late [ BlueSnake Hazard ][ BlueSnake Number ] -> [ BlueSnake Hazard ][ BlueSnake ]
late [ BlueSnake Hazard ][ BlueSnake no Hazard ] -> [ BlueSnake Hazard ][ ]

(draw undo hint when snake dies)

late random up [ Snake Hazard | ] -> [ Snake Hazard | UndoHint1 ]
late random right [ UndoHint1 | ] -> [ UndoHint1 | UndoHint2 ]
late random down [ UndoHint1 | ] -> [ UndoHint1 | UndoHint3 ]
late random right [ UndoHint3 | ] -> [ UndoHint3 | UndoHint4 ]

late [ Snake Hazard ] -> [ Hazard ] sfx3

(boxes vanish when touching water)

late [ Box1 Water ][ Box1 no Water ] -> [ Box1 Water ][ ]
late [ Box2 Water ][ Box2 no Water ] -> [ Box2 Water ][ ]
late [ Box3 Water ][ Box3 no Water ] -> [ Box3 Water ][ ]

late [ Box Water ] -> [ Water ]

(check if there are fruits left)

late [ ExitClosed ] -> [ Exit ]
late [ Fruit ][ Exit ] -> [ Fruit ][ ExitClosed ]

(remove snakes that reached portal and count down)

late [ RedHead Exit ][ RedSnake Last ] -> [ RedHead Exit ][ RedSnake ]
late [ RedHead Exit ][ RedSnake Number ] -> [ RedHead Exit ][ RedSnake ]
late [ RedHead Exit ][ RedTail ] -> [ RedHead Exit ][ ]

late [ GreenHead Exit ][ GreenSnake Last ] -> [ GreenHead Exit ][ GreenSnake ]
late [ GreenHead Exit ][ GreenSnake Number ] -> [ GreenHead Exit ][ GreenSnake ]
late [ GreenHead Exit ][ GreenTail ] -> [ GreenHead Exit ][ ]

late [ BlueHead Exit ][ BlueSnake Last ] -> [ BlueHead Exit ][ BlueSnake ]
late [ BlueHead Exit ][ BlueSnake Number ] -> [ BlueHead Exit ][ BlueSnake ]
late [ BlueHead Exit ][ BlueTail ] -> [ BlueHead Exit ][ ]

late [ Head Exit ][ SnakeCounter 01 ] -> [ Head Exit ][ SnakeCounter 00 ]
late [ Head Exit ][ SnakeCounter 02 ] -> [ Head Exit ][ SnakeCounter 01 ]
late [ Head Exit ][ SnakeCounter 03 ] -> [ Head Exit ][ SnakeCounter 02 ]

late [ Head Exit ] -> [ Exit ] sfx2

(remove snakeless directions and last marks)

late [ Direction no Snake ] -> [ ]
late [ Last no Snake ] -> [ ]

(add eyes to active snake)

late [ Eyes ] -> []
late [ Active Head ] -> [ Active Head Eyes ]

(add round corners to snakes)

late down [ Tail North | no North ] -> [ Tail North CornerSW CornerSE | ]
late up [ Tail South | no South ] -> [ Tail South CornerNW CornerNE | ]
late right [ Tail West | no West ] -> [ Tail West CornerSE CornerNE | ]
late left [ Tail East | no East ] -> [ Tail East CornerSW CornerNW | ]

late up [ Tail North | Tail CornerS ] -> [ Tail North | Tail ]
late down [ Tail South | Tail CornerN ] -> [ Tail South | Tail ]
late left [ Tail West | Tail CornerE ] -> [ Tail West | Tail ]
late right [ Tail East | Tail CornerW ] -> [ Tail East | Tail ]

late [ Corner no Snake no Ground ] -> [ ]
late [ CornerSoil no Snake no Ground ] -> [ ]

late [ Snake CornerNW BackgroundSoil ] -> [ Snake CornerSoilNW BackgroundSoil ]
late [ Snake CornerNE BackgroundSoil ] -> [ Snake CornerSoilNE BackgroundSoil ]
late [ Snake CornerSW BackgroundSoil ] -> [ Snake CornerSoilSW BackgroundSoil ]
late [ Snake CornerSE BackgroundSoil ] -> [ Snake CornerSoilSE BackgroundSoil ]

late [ Snake CornerSoilNW no BackgroundSoil ] -> [ Snake CornerNW ]
late [ Snake CornerSoilNE no BackgroundSoil ] -> [ Snake CornerNE ]
late [ Snake CornerSoilSW no BackgroundSoil ] -> [ Snake CornerSW ]
late [ Snake CornerSoilSE no BackgroundSoil ] -> [ Snake CornerSE ]

(place overlaps on snakes)

late [ Overlap ] -> [ ]

late [ RedTail1 North ] -> [ RedTail1 North RedOverN2 ]
late [ RedTail1 South ] -> [ RedTail1 South RedOverS2 ]
late [ RedTail1 West ] -> [ RedTail1 West RedOverW2 ]
late [ RedTail1 East ] -> [ RedTail1 East RedOverE2 ]

late [ RedTail2 North ] -> [ RedTail2 North RedOverN1 ]
late [ RedTail2 South ] -> [ RedTail2 South RedOverS1 ]
late [ RedTail2 West ] -> [ RedTail2 West RedOverW1 ]
late [ RedTail2 East ] -> [ RedTail2 East RedOverE1 ]

late [ GreenTail1 North ] -> [ GreenTail1 North GreenOverN2 ]
late [ GreenTail1 South ] -> [ GreenTail1 South GreenOverS2 ]
late [ GreenTail1 West ] -> [ GreenTail1 West GreenOverW2 ]
late [ GreenTail1 East ] -> [ GreenTail1 East GreenOverE2 ]

late [ GreenTail2 North ] -> [ GreenTail2 North GreenOverN1 ]
late [ GreenTail2 South ] -> [ GreenTail2 South GreenOverS1 ]
late [ GreenTail2 West ] -> [ GreenTail2 West GreenOverW1 ]
late [ GreenTail2 East ] -> [ GreenTail2 East GreenOverE1 ]

late [ BlueTail1 North ] -> [ BlueTail1 North BlueOverN2 ]
late [ BlueTail1 South ] -> [ BlueTail1 South BlueOverS2 ]
late [ BlueTail1 West ] -> [ BlueTail1 West BlueOverW2 ]
late [ BlueTail1 East ] -> [ BlueTail1 East BlueOverE2 ]

late [ BlueTail2 North ] -> [ BlueTail2 North BlueOverN1 ]
late [ BlueTail2 South ] -> [ BlueTail2 South BlueOverS1 ]
late [ BlueTail2 West ] -> [ BlueTail2 West BlueOverW1 ]
late [ BlueTail2 East ] -> [ BlueTail2 East BlueOverE1 ]

(cut corners from background soil)

late left [ BackgroundSoil | no BackgroundSoil no Ground ] -> [ BackgroundSoil CornerSW CornerNW | ]
late right [ BackgroundSoil | no BackgroundSoil no Ground ] -> [ BackgroundSoil CornerSE CornerNE | ]

late down [ BackgroundSoil CornerS | Ground ] -> [ BackgroundSoil | Ground ]
late down [ BackgroundSoil CornerS | BackgroundSoil ] -> [ BackgroundSoil | BackgroundSoil ]
late down [ BackgroundSoil CornerS | Water ] -> [ BackgroundSoil | Water ]

late up [ BackgroundSoil CornerN | Ground ] -> [ BackgroundSoil | Ground ]
late up [ BackgroundSoil CornerN | BackgroundSoil ] -> [ BackgroundSoil | BackgroundSoil ]

late [ Snake BackgroundSoil CornerNW no CornerSoilNW ] -> [ Snake BackgroundSoil ]
late [ Snake BackgroundSoil CornerNE no CornerSoilNE ] -> [ Snake BackgroundSoil ]
late [ Snake BackgroundSoil CornerSW no CornerSoilSW ] -> [ Snake BackgroundSoil ]
late [ Snake BackgroundSoil CornerSE no CornerSoilSE ] -> [ Snake BackgroundSoil ]

late [ BoxFront Corner ] -> [ BoxFront ]

(add connections to background boxes)

late horizontal [ Box1Back no Box1BackH | Box1 ] -> [ Box1Back Box1BackH | Box1 ]
late horizontal [ Box1Back no Box1BackH | Box1Back ] -> [ Box1Back Box1BackH | Box1Back ]
late vertical [ Box1Back no Box1BackV | Box1 ] -> [ Box1Back Box1BackV | Box1 ]
late vertical [ Box1Back no Box1BackV | Box1Back ] -> [ Box1Back Box1BackV | Box1Back ]

late horizontal [ Box2Back no Box2BackH | Box2 ] -> [ Box2Back Box2BackH | Box2 ]
late horizontal [ Box2Back no Box2BackH | Box2Back ] -> [ Box2Back Box2BackH | Box2Back ]
late vertical [ Box2Back no Box2BackV | Box2 ] -> [ Box2Back Box2BackV | Box2 ]
late vertical [ Box2Back no Box2BackV | Box2Back ] -> [ Box2Back Box2BackV | Box2Back ]

late horizontal [ Box3Back no Box3BackH | Box3 ] -> [ Box3Back Box3BackH | Box3 ]
late horizontal [ Box3Back no Box3BackH | Box3Back ] -> [ Box3Back Box3BackH | Box3Back ]
late vertical [ Box3Back no Box3BackV | Box3 ] -> [ Box3Back Box3BackV | Box3 ]
late vertical [ Box3Back no Box3BackV | Box3Back ] -> [ Box3Back Box3BackV | Box3Back ]

==============
WINCONDITIONS
==============

Any SnakeCounter on 00

=======
LEVELS
=======

..w..
.....
.....
.....
.3...
.2←..
.→→1.
ee#ee
~~#~~

.......
→→1.2←e
###.###
.......
.w...w.
###.###
#######

..............
..............
..............
..→→→2...#....
..■■■■■..#....
..●●■●●..#....
..●.□1●..#3←##
..●.■↑●..#####
..●.→↑●..,,,,,
.#●●○●●#.,,,,e
####.#########
####~#########