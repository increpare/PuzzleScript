title Stealth Agent Gemini
author Harry Lewis
homepage www.puzzlescript.net
norepeat_action
text_color #00FF00
noundo
run_rules_on_level_start
========
OBJECTS
========
(b and W refer to the color of the player or enemy)
BackgroundW
BLACK 

BackgroundB
White

Border
Black

WallB (green)
#339933

WallW (blue)
#333399

ExitW
Black #333333 #666666 #999999 White
01210
12321
23432
12321
01210

ExitB
White LightGrey Grey Darkgrey Black
01210
12321
23432
12321
01210


PlayerB
White Black #00FF00
.121.
.212.
11111
11111
.1.1.

PlayerBDead
White Black #00FF00
.1.1.
11111
11111
.212.
.121.

PlayerW
White #0000FF
.010.
.101.
00000
00000
.0.0.

PlayerWDead
White #0000FF
.0.0.
00000
00000
.101.
.010.
(Dummys for tutorial levels)
SuperDummyB
White Black #00FF00
.121.
.212.
11111
11111
.1.1.
DummyW
White #0000FF
.010.
.101.
00000
00000
.0.0.
SuperDummyW
White #0000FF
.010.
.101.
00000
00000
.0.0.

CrateW
#CC00CC #FF00FF 
11111
10001
10001
10001
11111

CrateB
#FF9900 #B26B00
11111
10001
10001
10001
11111

WEnemy
White #FF0000 lightGREY grey
.333.
02120
01210
02220
.0.0.

WEnemyH
White #FF0000
.000.
00100
01010
00000
.0.0.

WEnemyS
#FF0000 white lightgrey
.000.
00100
01010
00000
.0.0.

BEnemy
Black #FF0000 #424242 #2D2D2D #1C1C1C
.333.
02120
01210
02220
.0.0.

BEnemyH
Black #FF0000
.000.
00100
01010
00000
.0.0.

BEnemyS
#FF0000 black
.000.
00100
01010
00000
.0.0.


TempA
WHITE #FF0000
.....
.....
..0..
.....
.....

TempB
WHITE #FF0000
.....
.....
..0..
.....
.....

TempC
BLACK #FF0000
.....
.....
..0..
.....
.....

TempD
BLACK #FF0000
.....
.....
..0..
.....
.....

=======
LEGENDx
=======

o = border
. = BackgroundW
# = WallW
W = PlayerW
B = PlayerB
D = SuperDummyB
M = DummyW
3 = SuperDummyW
* = CrateW
X = BEnemy
Y = WEnemy
E = ExitW

Dummy = DummyW
SuperDummy = SuperDummyW or SuperdummyB
Player = PlayerW or PlayerB or Dummy or SuperDummy
Wall = WallW or WallB
Background = BackgroundW or BackgroundB
Crate = CrateW or CrateB
Enemy = WEnemy or BEnemy or WEnemyS or BEnemyS or WEnemyH or BEnemyH
Block = Wall or Crate or Enemy
Corpse = PlayerBDead or PlayerWDead
TempW = TempA or TempB
TempL = TempC or TempD
Temp = TempW or TempL
Exit = ExitB or ExitW


=======
SOUNDS
=======

sfx1 75888107
sfx2 35291907
sfx3 34580302
sfx4 93472705
EndLevel 6411900
================
COLLISIONLAYERS
================

Background
Border
Exit
Player, Wall, Crate, Enemy, Corpse, SuperDummy
TempA
TempB
TempC
TempD

======
RULES     
======     
[ > Player] -> [> Player] sfx1
[ > Player | Crate ] -> [ > Player | > Crate ] 
[ > Crate | Crate ] -> [ > Crate | > Crate ] 

[ > Player | Enemy ] -> [ > Player | > Enemy ]
[ Action PlayerW]  -> [ PlayerB ] sfx4    
[ Action PlayerB]  -> [ PlayerW ] sfx4
[ Action SuperDummyB] -> win
[ Action SuperDummyW] -> win

late [PlayerB] [WallW] -> [PlayerB] [WallB]
late [PlayerW] [WallB] -> [PlayerW] [WallW]
late [PlayerB] [BackgroundW] -> [PlayerB] [BackgroundB]
late [PlayerW] [BackgroundB] -> [PlayerW] [BackgroundW]
late [PlayerB] [CrateW] -> [PlayerB] [CrateB]
late [PlayerW] [CrateB] -> [PlayerW] [CrateW]
late [PlayerB] [ExitW] -> [PlayerB] [ExitB]
late [PlayerW] [ExitB] -> [PlayerW] [ExitW]
late [WEnemy BackgroundB] -> [WEnemyH BackgroundB]
late [WEnemyH BackgroundW] -> [WEnemy BackgroundW]
late [BEnemy BackgroundW] -> [BEnemyH BackgroundW]
late [BEnemyH BackgroundB] -> [BEnemy BackgroundB]


late [WEnemy | No Block] -> [WEnemy | TempA]
late [WEnemy | TempA | No Block] -> [WEnemy | TempA | TempB ]
late [WEnemy| ... | TempA | TempB | No Block] -> [WEnemy|...| TempA | TempB | TempA]
late [WEnemy| ... | TempB | TempA | No Block] -> [WEnemy|...| TempB | TempA | TempB]
late [WEnemy| ... | TempA | TempB | No Block] -> [WEnemy|...| TempA | TempB | TempA]
late [WEnemy| ... | TempB | TempA | No Block] -> [WEnemy|...| TempB | TempA | TempB]
late [WEnemy| ... | TempA | TempB | No Block] -> [WEnemy|...| TempA | TempB | TempA]
late [WEnemy| ... | TempB | TempA | No Block] -> [WEnemy|...| TempB | TempA | TempB]

late [BEnemy | No Block] -> [BEnemy | TempC]
late [BEnemy | TempC | No Block] -> [BEnemy | TempC | TempD ]
late [BEnemy| ... | TempC | TempD | No Block] -> [BEnemy|...| TempC | TempD | TempC]
late [BEnemy| ... | TempD | TempC | No Block] -> [BEnemy|...| TempD | TempC | TempD]
late [BEnemy| ... | TempC | TempD | No Block] -> [BEnemy|...| TempC | TempD | TempC]
late [BEnemy| ... | TempD | TempC | No Block] -> [BEnemy|...| TempD | TempC | TempD]
late [BEnemy| ... | TempC | TempD | No Block] -> [BEnemy|...| TempC | TempD | TempC]
late [BEnemy| ... | TempD | TempC | No Block] -> [BEnemy|...| TempD | TempC | TempD]

late [WEnemy| TempW PlayerW] -> [WEnemyS| PlayerW]
late [BEnemy| TempL PlayerB] -> [BEnemyS| PlayerB]

late [WEnemy| No Block | TempW PlayerW] -> [WEnemyS| | PlayerW]
late [BEnemy| No Block | TempL PlayerB] -> [BEnemyS| | PlayerB]
late [WEnemy| TempW DummyW] -> [WEnemyS| PlayerWDead] sfx3
late [WEnemy|No Block| ... | TempW PlayerW] -> [WEnemyS|| ... | PlayerW]
late [BEnemy|No Block| ... | TempL PlayerB] -> [BEnemyS|| ... | PlayerB]
late [WEnemy|...|No Block | TempW PlayerW] -> [WEnemyS|...|| PlayerW]
late [BEnemy|...|No Block | TempL PlayerB] -> [BEnemyS|...|| PlayerB]
late [WEnemy| ... | TempL DummyW] -> [WEnemyS| ... | PlayerWDead] sfx3
[Temp] -> []
late [WEnemyS][PlayerW] -> [WEnemyS][PlayerWDead] sfx3
late [BEnemyS][PlayerB] -> [BEnemyS][PlayerBDead] sfx3

==============
WINCONDITIONS
==============
All Player on Exit
No Corpse
=======     
LEVELS
=======
Message You are an agent for a top-secret organisation.

Message It's so secret it doesn't even have a name.

Message You work in the darkness, silently taking out the organisation's enemies.

Message They call you Stealth Agent Gemini.

Message Here you are in action.

#####
#...#
#.D.#
#...#
#####

Message You're hiding in the dark with infra-red goggles.

Message With the goggles you can see stuff most people can't in the dark.

Message Here's what you see with the goggles.

#####
#...#
#.3.#
#...#
#####

Message Cool, eh?

Message Get to the exit to finish a level.

#####oo
#...###
#.M..E#
#...###
#####oo

#######
#M..#E#
###.#.#
#...#.#
#.###.#
#.....#
#######

Message Nice one.

Message There are enemy agents hiding in the dark.

Message They also have infra-red goggles. Don't let them see you.

Message If you are seen, press R to restart the level.

ooo###ooo
####Y####
#M.....E#
###.#.###
oo#...#oo
o##.#.##o
o#Y...Y#o
o#######o

Message Good Job.

Message You can switch the room's lights on and off by pressing X or space.

Message Enemies with infra-red goggles are blinded by the lights.

ooo###ooo
####Y####
#W.....E#
###.#.###
oo#.#.#oo
o##.#.##o
o#Y...Y#o
o#######o

Message There are other enemy agents dressed in black.

Message These guys can see you when the lights are on, but not when they're off.

o###o###o
##X###X##
#B..#..E#
###.#.###
oo#.#.#oo
o##.#.##o
o#Y...Y#o
o#######o

Message If you can see an enemy's shape, they can see you.

Message Otherwise you are hidden.

o###ooo###o
##Y#####Y##
#W..##...E#
###.##.####
o#X.##.X#oo
o##.##.##oo
o#Y..#.#ooo
o###...#ooo
ooo##XX#ooo
oooo####ooo

Message You can push blocks by walking into them.

Message Blocks will obstruct the vision of enemies.

oooooooooo###o
ooooooooo##E##
##########..Y#
#B*..........#
###X#Y#X#Y#X##
oo###########o

Message Multiple blocks in a row can all be pushed.

ooo###oooo
ooo#X#oooo
oo##.#oooo
###..#oooo
#...*#####
#B..*..EX#
#...*#####
###..#oooo
oo##.#oooo
ooo#Y#oooo
ooo###oooo

Message MANY MANY BLOCKS.

oooooooo###oooooooooooooooooooooooo
###o###o#X#oooooooooooooooooooooooo
#X###X###.#########################
#W**.***.****.*****.****.***.**.*.#
###Y###Y#.#########################
oo###o###E#oooooooooooooooooooooooo
oooooooo###oooooooooooooooooooooooo

Message You can also push enemy agents.

Message Don't worry, they won't catch you if you push them.

ooo#####oo
####.X.###
#B...*.#E#
####.Y...#
ooo##.####
oooo###ooo

Message Enemies will obstruct other enemy's vision like the blocks.

ooo####oo
ooo#..###
o###.Y#Y#
##......#
#WX....E#
###Y#Y###
oo#####oo



Message That's all you need to know for now.

Message Remember

Message Different Color Enemy = Safe

Message Same Color = Unsafe

Message Press R to restart if you die.

Message Have fun!

ooooo###ooo
######Y####
#W..X....E#
##.......##
o#*#YX.#.Y#
##.X.#...##
#X.......#o
#X.....*.#o
#X..###YY#o
o####oo###o

oo#########
oo#X...E.Y#
oo##..#####
ooo##.#oooo
oooo#.#oooo
ooo##.#####
oo##W.X...#
oo#..X....#
o##..#Y##.#
o#.X####..#
##*.....*.#
#.Y.*.#####
###...#oooo
oo###X#oooo
oooo###oooo
Message More to Come!