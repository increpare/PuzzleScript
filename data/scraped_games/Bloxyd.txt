title Bloxyd
again_interval 0.3
(youtube 43UUex_YWxs)
========
OBJECTS
========
Pushing
red
.0.0.
0...0
.....
0...0
.0.0.

UL
#506060 #00F0FF #a0dddd #405252 #304040
30200
43230
11230
43330
44443

UR
#506060 #00F0FF #a0dddd #405252 #304040
30200
43230
43111
43330
44443

DR
#506060 #00F0FF #a0dddd #405252 #304040
30000
43330
43111
43230
44243

DL
#506060 #00F0FF #a0dddd #405252 #304040
30000
43330
11230
43230
44243

LRU
#506060 #00F0FF #a0dddd #405252 #304040
30200
43230
11111
43330
44443

LRD
#506060 #00F0FF #a0dddd #405252 #304040
30000
43330
11111
43230
44243

UDL
#506060 #00F0FF #a0dddd #405252 #304040
30200
43230
11230
43230
44243

UDR
#506060 #00F0FF #a0dddd #405252 #304040
30200
43230
43111
43230
44243

UD
#506060 #00F0FF #a0dddd #405252 #304040
30200
43230
43230
43230
44243

LR
#506060 #00F0FF #a0dddd #405252 #304040
30000
43330
11111
43330
44443

U
#506060 #00F0FF #a0dddd #405252 #304040
30200
43230
43130
43330
44443

D
#506060 #00F0FF #a0dddd #405252 #304040
30000
43330
43130
43230
44243

L
#506060 #00F0FF #a0dddd #405252 #304040
30000
43330
11230
43330
44443

R
#506060 #00F0FF #a0dddd #405252 #304040
30000
43330
43211
43330
44443

X
#506060 #00F0FF #a0dddd #405252 #304040
30200
43230
11111
43230
44243

Player
#1a1a1a gray black #2a2a2a

.220.
22010
02002
30222
.302.

Background
#afafaf #808080 gray
21111
02221
02221
02221
00002

Wall
#202020 #4a4a4a #606060 #3a3a3a
13331
01112
01112
03332
03332

Bomb
#1a1a1a #606060 black #2a2a2a white
..44.
..00.
.2010
.3200
..32.

Explosion1
black
.....
.....
.....
.....
.....

Explosion2
orange yellow
0.0.0
.010.
01010
.010.
0.0.0

Exit
blue #01317f #63a3ff #ccccff
20003
12030
11300
10120
01112

Inv
#006600
00.0.
.....
.....
.....
.0.00

Crack
#afafaf #808080 gray black
31111
03233
02321
02321
33032

Pit
black

Dynamite
#F06060 yellow #ae2343
.11..
..02.
..02.
..02.
..22.

wood
#7f4822 #5f3812 #af4822 #ff7872 #402020
02223
10.02
1...2
40.02
44110

graybomb
#6a6a6a #a0a0a0 #303030 #2a2a2a white
..44.
..00.
.2010
.3200
..32.

key
white
..0..
.0.0.
..0..
..0..
..00.

metal
#415174 #82b0d4 white #314162 #606060 black #2a2a2a
01122
35541
36551
36651
33330

door
#303030 #8b8bab #b0b0b0 #4b4b5b #202020
11111
11411
11411
33033
33333

=======
LEGEND
=======
. = Background
$ = exit
@ = inv
# = Wall
& = wood
* = wood and bomb
% = wood and dynamite
c = crack
z = UR
7 = DL
J = UL
F = DR
T = LRD
w = LRU
k = UDR
3 = UDL
| = UD
- = LR
P = Player
b = bomb
\ = key
I = dynamite
o = dynamite and crack
' = graybomb
; = graybomb and crack
6 = pit
m = metal
y = door

Solid = Wall
connector = ul or ur or dr or dl or lru or lrd or udl or udr or u or l or d or r or ud or lr or x
UU = LRU or UDR or UDL or UL or UR or UD or U or x
DD = UDR or UDL or LRD or DL or DR or UD or D or x
LL = LRU or UDL or LRD or UL or DL or LR or L or x
RR = LRU or UDR or LRD or UR or DR or LR or R or x
item = bomb or dynamite or key
explodable = bomb or dynamite or graybomb or metal
item2 = item
breakable = connector or player or wood
=======
SOUNDS
=======
SFX1 65276106
SFX2 82624702
SFX3 5643508
SFX4 22079708
(SFX5 21487508)
SFX5 19328304
SFX6 90411302
SFX7 66650507
SFX8 61607902
SFX9 18837707
================
COLLISIONLAYERS
================
Background
crack, pit
item, graybomb
Player, Wall, connector, exit, inv, wood, door, metal
explosion1, explosion2, pushing
======
RULES
======
[ > Player | Connector | Connector ] -> [ Player | > Connector pushing | Connector ]
[ > Connector | Connector ] -> [ > Connector | > Connector ]
[ > Connector | wood ] -> [ > connector | > wood ] again sfx7
[ > Connector | no Connector ] -> [ > Connector | pushing ]
late [pushing no connector][pushing connector]->[pushing connector][] sfx1
late [ pushing | connector ]->[ pushing | pushing connector ]
late up [ pushing UU | no DD ] -> [ UU | ]
late down [ pushing DD | no UU ] -> [ DD | ]
late left [ pushing LL | no RR ] -> [ LL | ]
late right [ pushing RR | no LL ] -> [ RR | ]
late [ no pushing connector | pushing connector ] -> [ connector | connector ]
late [ pushing connector ] -> [ bomb pushing connector ]

late [ explosion2 | explodable ] -> [ explosion2 | explosion1 ] sfx2 again
late [ explosion2 breakable ] -> [ explosion2 ]
late [ explosion2 crack ] -> [ explosion2 pit ]
late [ explosion2 ] -> [] again
late [ explosion1 ] -> [ explosion2 ] again
late [ player | bomb pushing connector ] -> [ player | explosion1 connector ] again

late [ player pit ] -> [ pit ] sfx6

[ wood pit ] -> [] sfx8
[ > Player | wood ] -> [ Player | > wood ] again sfx7
[ explosion2 | wood ] -> [ explosion2 | > wood ]

(use of inventory)
([ pushing bomb ] -> [ explosion1 ] sfx2)
[ pushing dynamite ] -> [ explosion1 ] sfx2
left [ action player no item no graybomb] [ inv item | no item ] -> [ pushing item player ] [ inv | ]

(inventory)
late left [ Player item no pushing ] [ wall | inv no item ] -> [ Player ] [ wall | inv item ] sfx9
late left [ Player item no pushing ] [ inv item2 | inv no item ] -> [ Player ] [ inv item2 | inv item ] sfx9
late [ key | door ] -> [ | ] sfx5
late [ pushing no dynamite ] -> []

[ > Player | exit ] -> win sfx3

==============
WINCONDITIONS
==============

=======
LEVELS
=======
#########
#.......#
#.......#
#p.....$#
#.......#
#.......#
#########
#@@@@@@@#
#########

##########
#......m.#
#......m.#
#......m.#
#.p.i..m$#
#......m.#
#......m.#
#......m.#
##########
#@@@@@@@@#
##########

##########
#........#
#..666666#
#..6'''m$#
#i.6'6666#
#........#
#.......p#
##########
#@@@@@@@@#
##########

#########
#i..y..i#
#.p.#####
#...#####
#\..m.m$#
#########
#@@@@@@@#
#########

############
#....6\66$6#
#.b..6m66y6#
#....6'66y6#
#.p....'.m6#
#....6'6666#
#.i..6m6...#
#....6\6...#
############
#@@@@@@@@@@#
############

##########
#...&.6..#
#...&.6..#
#p..&.6.$#
#...&.6..#
#...&.6..#
#...&.6..#
##########
#@@@@@@@@#
##########

###########
#....66..$#
#..6.66666#
#..6.&.666#
#..6.6.666#
#p.6.6.&.6#
#..6.&.6.6#
#..6666666#
###########
#@@@@@@@@@#
###########

###########
#....&6...#
#....&6...#
#....&6.&&#
#....&6.%$#
#p...&6.&&#
#....%6...#
#....&6...#
###########
#@@@@@@@@@#
###########

###########
#.........#
#666.p.6..#
#666...6..#
#6%.6.66%6#
#6&66.6$%6#
#6.66.6666#
#6....&.66#
#6.&&.6.66#
#666666666#
###########
#@@@@@@@@@#
###########

#############
#$###########
#mmmm'mc#.\.#
#....##.y...#
#mmmm'mc#..p#
#....##.#.b.#
#.......#bi.#
#############
##@@@@@@@@@##
#############

#########
#..###..#
#p.###..#
#..lr-.$#
#..###..#
#..###..#
#########
#@@@@@@@#
#########

#################
#####&i&#.$6....#
######.##..6....#
#...##r7#..6....#
#.p.wlrt-6&;;''m#
#...#|###66666#m#
#...--lrw....##m#
#...#####....mmm#
#################
####@@@@@@@@@####
#################

###########
##y.mpm.&##
##&.&.*.*##
#.c.cic.c.#
#6m#m#m#m6#
#..6......#
#..6.lutr-.
#..6#...-.#
#$.6..i\..#
###########
#@@@@@@@@@#
###########

##########
#.....p.##
#...x...##
#..rfj..##
#.lzxxx'm$
#..7ux..##
#...d...##
#.......##
##########

################
################
#..###6#..c..c.#
#..;##by...ccci#
#.dm;6*#..c....#
#pul-&666666666#
#.km;6*........#
#..;##b........#
#..###6#;c&.ccm#
##u##..#;##&##;#
#.l-k.#$m..m;;;#
#\d#############
#..#############
####@@@@@@@@@@@#
################

###########
#.p.......#
##..###..##
#.f7.#.fl.#
#.zj.#.z7.#
#.rl.#.rj.#
#....#....#
###.###\###
###y#######
##.......##
##.77j-f.##
##.rz'-j.##
##.f-'zl.##
##...'...##
#####m#####
#####$#####
##@@@@@@@##
###########

###############
#p.*6..6..6&*.#
#%..6..6$.##*.#
#666*..#####*.#
#.....#..c**m.#
#.....#...###.#
#....tfz..****#
#....j7w..#####
#.....#...#####
###############
#@@@@@@@@@@@@@#
###############

(###############
#.............#
#....-k3|z....#
#....z-wj|p...#
#....7f.-j....#
#....ftt-|....#
#....3kw7|....#
#.............#
#.............#
###############
###@@@@@@@@@###
###############)

###############
######...######
######...######
##.....p.....##
##;';;3zw;;;;##
##;';;jft;;;;##
##;'';kx7;;;;##
##;;';;;;;;;;##
##;;''''''';;##
##;;;;;;;;';;##
##mmmmmmmmmmm##
##&&&&&&&&&&&##
##66666666666##
##.....$.....##
###############
#@@@@@@@@@@@@@#
###############

#################
#.$.............#
#6&6666666666666#
#i.6666666666666#
##.6666666666666#
#...............#
#r-ll--r-l-lllrl#
#r--rrllr-r-rr-l#
#.........&.....#
#.......p.&..c&c#
#############mmm#
####@@@@@@@@@####
#################

#############
#...........#
#.....f-.p..#
#.....|.....#
#....7z3....#
#####flj#####
#....k-w....#
#...........#
#.....$.....#
#...........#
#############
###@@@@@@@###
#############

#############
#$.y#...##\##
#;;y#.#b#.m.#
#;;.6%#.#.m.#
#rt\uy.';'cc#
#.7djp..#.m.#
#..z....#.m.#
#.......##\##
#############
#@@@@@@@@@@@#
#############

#ddd##############
6||u.'m.b#.......#
6d|u...6666.&7zt.#
6udu..66666...3r.#
6udd..6&mm666..u##
6||d.66bp.b&66.cm#
#uuu66m.bb.m66#m\#
#.##66m.b.bm66##b#
#\##66&b..i666;;##
####i666mm&6'''''#
###byyy66666666;'#
###.######6;;;;;'#
#.....##b\m.lr...#
##m#m#############
##m#;#############
##m.m$#@@@@@@@@@@#
##################

####################
####.......p.......#
####.....-|7.......#
####.....f.w.......#
####....t|w|w..;;;.#
#$yymmmmj\|\f##;.;##
####....t7z|z..rul.#
####.....|.t....t..#
####.....-j|.......#
####...............#
####################
##@@@@@@@@@@@@@@@@##
####################

(####################
#.p..#.....#....####
#...f-7...ft7'''####
##.#|\|#m#kx3##m####
#...z-j...zwj.6'####
#....#.....#..6'####
#............6&m.m$#
#....#.....#..6.#;##
#...f-l...df7.6.#;##
####z-7#y#|zj####;##
#.i.r-j...z-l'''m;##
#....#.....#....####
####################
###@@@@@@@@@@@######
####################)

####################
#....#;;...#....####
#.p.-z-;..k7j'''####
##.#|\f#m#zxw##m####
#...7|j...3ft.6'####
#....#.....#..6'####
#............6&m.m$#
#....#.....#..6.#;##
#...-rz...-7d.6.#;##
####--l#y#fzj####;##
#.i.fj7...|lz'''m;##
#....#.....#....####
####################
###@@@@@@@@@@@######
####################