title Memory path
author Le Slo
homepage www.puzzlescript.net

background_color #4d4d00
text_color #dbdb00
noaction



========
OBJECTS
========

background
#292900
00000
00000
00000
00000
00000

wall
#4d4d00
00000
00000
00000
00000
00000


crateWall
#4d4d00 #292900
01101
11011
10110
01101
11011

target
#2E6A8D
.....
.000.
.0.0.
.000.
.....

crate
#949400
00000
0...0
0...0
0...0
00000

crateup
#dbdb00
00000
0.0.0
0...0
0...0
00000

crateright
#dbdb00
00000
0...0
0..00
0...0
00000

cratedown
#dbdb00
00000
0...0
0...0
0.0.0
00000

crateleft
#dbdb00
00000
0...0
00..0
0...0
00000

crateupch
#949400
00000
0.0.0
0...0
0...0
00000

craterightch
#949400
00000
0...0
0..00
0...0
00000

cratedownch
#949400
00000
0...0
0...0
0.0.0
00000

crateleftch
#949400
00000
0...0
00..0
0...0
00000

player
#949400 yellow
.000.
00100
01110
00100
.000.


charged
transparent

pushed
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player

* = Crate
@ = Crate and Target
O = Target
d = crateWall
l = crateleft
r = crateright
u = crateup
j = cratedown

x = target and player


CratesNoMove = crate or CrateUpCh or CrateLeftCh or CrateDownCh or CrateRightCh
cratesMove = CrateUp or CrateLeft or CrateDown or CrateRight
crates = CratesNoMove or cratesMove
collision = crates or player or wall
collisionL = crates or player or wall 
=======
SOUNDS
=======

sfx0 91405500

showmessage 92307308

startlevel 20530308

player cantmove 57023704

================
COLLISIONLAYERS
================

Background
pushed
charged
Target
crateWall
Player, Wall, Crates, collisionL

======
RULES
======

[> Player | crates] -> [ > player| crates > pushed]
startLoop
[> pushed | crates] -> [ > pushed| crates > pushed]

endLoop


[CrateUpCh] -> [CrateUp charged]
[CrateLeftCh] -> [CrateLeft charged]
[CrateDownCh] -> [CrateDown charged]
[CrateRightCh] -> [CrateRight charged]

up [CrateUp no charged no pushed| no collision] -> [ > CrateUpCh | no collision]
left [CrateLeft no charged no pushed| no collision] -> [ > CrateLeftCh | no collision]
down [CrateDown no charged no pushed| no collision] -> [ > CrateDownCh | no collision]
right [CrateRight no charged no pushed| no collision] -> [ > CrateRightCh | no collision]

up [CrateUp no charged no pushed| collision] -> [ Crate | collision ]
left [CrateLeft no charged no pushed| collision] -> [ Crate | collision ]
down [CrateDown no charged no pushed| collision] -> [ Crate | collision ]
right [CrateRight no charged no pushed| collision] -> [ Crate | collision ]

[ > player | crateWall] -> [ player | crateWall]

[> Player | no crates no wall] -> [ | player]

down [ >  Player | Crates | ] -> [  >  Player | > cratedownCh | ] sfx0
up [ >  Player | Crates | ] -> [  >  Player | > crateupCh | ] sfx0
left [ >  Player | Crates | ] -> [  >  Player | > crateleftCh | ] sfx0
right [ >  Player | Crates | ] -> [  >  Player | > craterightCh | ] sfx0

startLoop

[ > crates | CrateUpCh	] -> [ > crates | > CrateUpCh	]
[ > crates | CrateLeftCh	] -> [ > crates | > CrateLeftCh	]
[ > crates | CrateDownCh	] -> [ > crates | > CrateDownCh	]
[ > crates | CrateRightCh	] -> [ > crates | > CrateRightCh]


[ > crates | CrateUp	] -> [ > crates | > CrateUpCh	]
[ > crates | CrateLeft	] -> [ > crates | > CrateLeftCh	]
[ > crates | CrateDown	] -> [ > crates | > CrateDownCh	]
[ > crates | CrateRight	] -> [ > crates | > CrateRightCh]
[ > crates | Crate] -> [ > crates | > Crate]




endLoop

[charged] -> []
[pushed] -> []


==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

(debug

#########
#.......#
#p*lruj.#
#.......#
#p*llll.#
#..o....#
#p*rrrr.#
#.......#
#p*uuuu.#
#.......#
#p*jjjj.#
#.......#
#p*ru...#
#.......#
#########)

message >... .... ....

########
#......#
#...p..#
#..*#..#
#..o#..#
#......#
#......#
########

message >>.. .... ....

########
#......#
#.op.*.#
#......#
########

message >>>. .... ....

#######
#.....#
#.*...#
#...*.#
#.p.oo#
#....##
#######

message >>>> .... ....

##########
##########
#####...##
#####.j.##
#####...##
##..#.o.##
##.op.l.##
##..#...##
##########


message >>>> >... ....

########
#....lo#
#..p.lo#
#....lo#
########

message >>>> >>.. ....

#######
#.....#
#.**p.#
#.....#
#.oo#.#
#.....#
#######

message >>>> >>>. ....

########
#......#
#....lo#
#..p.lo#
#....lo#
#......#
########

message >>>> >>>> ....

#########
#.......#
#.......#
#.*.po..#
#.o..*..#
#.......#
#########

message >>>> >>>> >...

##########
#..##..###
#........#
#.#......#
#.*.p.####
#..o..####
#..#..####
#.###.####
##########

message >>>> >>>> >>..

########
#......#
#.p*...#
#.**...#
#...oo.#
#...o..#
#......#
########

message >>>> >>>> >>>.

##########
##########
#####...##
#####.j.##
#####...##
##....o.##
##po..l.##
##......##
##########

message >>>> >>>> >>>>

#########
#.......#
#.*.....#
#...p...#
#.....*.#
#.......#
##.######
#o.######
##.######
#########



