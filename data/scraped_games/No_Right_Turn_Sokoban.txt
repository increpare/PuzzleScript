title No Right Turn Sokoban
author Kalev Tait
homepage www.puzzlescript.net
noaction

========
OBJECTS
========

Background
GREEN

Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
DARKBROWN

PlayerU
White
..0..
.000.
0000.
.000.
.....

PlayerL
White
.....
.000.
0000.
.000.
..0..

PlayerD
White
.....
.000.
.0000
.000.
..0..

PlayerR
White
..0..
.000.
.0000
.000.
.....

Crate
Orange
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

. = Background
# = Wall
P = PlayerU
* = Crate
@ = Crate and Target
O = Target

Player = PlayerU or PlayerL or PlayerD or PlayerR


=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

(Rotate player depending on direction, or cancel move)

[ Left PlayerU ] -> [ Left PlayerL ]
[ Down PlayerU ] -> cancel
[ Right PlayerU ] -> cancel

[ Down PlayerL ] -> [ Down PlayerD ]
[ Right PlayerL ] -> cancel
[ Up PlayerL ] -> cancel

[ Right PlayerD ] -> [ Right PlayerR ]
[ Up PlayerD ] -> cancel
[ Left PlayerD ] -> cancel

[ Up PlayerR ] -> [ Up PlayerU ]
[ Left PlayerR ] -> cancel
[ Down PlayerR ] -> cancel

(Move crate)

[ >  Player | Crate ] -> [  >  Player | > Crate  ]

(Csncel any move into a wall, by player or crate)

[ > Player | Wall ] -> cancel
[ > Crate | Wall ] -> cancel


==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

Message First a quick reminder of the rules of 'no right turn'...

#######
#.....#
#.#p#.#
#..o..#
###.#.#
#..*..#
#.#.#.#
#.....#
#.#.#.#
#.....#
#######

Message Now, a proper 'no right turn' maze.

###################
#.................#
#.#####.#.###.#.#.#
#.................#
#.#.#.#.###.#.#.###
#.......#.....#...#
#.#.#.###.#.###.#.#
#...#.......#.....#
#.###.###.###.#.#.#
#.....#.#.*.o.#.#.#
#.#.#.###p#.#.#.#.#
#.............#...#
###.###.#.#.#.#.###
#.........#.#.....#
#.#.#.#.#.#.#.#.#.#
#...#.........#...#
###################

Message But what does 'no right turn' mean for a more open sokoban level?

#########
#.......#
#.o.#...#
#...#.*.#
#...#...#
##.###.##
#...#...#
#.*.#...#
#...#.o.#
#.p.....#
#########

Message Okay, lets try this all in a puzzle

###########
#....######
#..@.....##
##...#.*.##
##...#...##
###.####.##
#....#....#
#.......o.#
#.p..#....#
##...###..#
###########
