title Hamiltwo
author Zithral
homepage www.puzzlescript.net

background_color BLACK
text_color WHITE
noaction
key_repeat_interval 0.5
run_rules_on_level_Start


========
OBJECTS
========

Background 
BLACK

Wall
GRAY
00000
00000
00000
00000
00000

Zero
WHITE
.....
.000.
.0.0.
.000.
.....

One
WHITE
.....
.00..
..0..
.000.
.....

Two
WHITE
.....
.00..
..0..
..00.
.....

Three
WHITE
.....
.000.
..00.
.000.
.....

Four
WHITE
.....
.0.0.
.000.
...0.
.....

Five
WHITE
.....
..00.
..0..
.00..
.....

Six
WHITE
.....
.0...
.000.
.000.
.....

Seven
WHITE
.....
.000.
...0.
...0.
.....

Eight
WHITE
.....
..00.
.000.
.000.
.....

Nine
WHITE
.....
.000.
.000.
...0.
.....

LivePlayer
BLUE
00000
0...0	
0...0	
0...0	
00000

DeadPlayer
RED
00000
0...0	
0...0	
0...0	
00000

Sub
TRANSPARENT


=======
LEGEND
=======

0 = Background and Wall
1 = Background and One
2 = Background and Two
3 = Background and Three
4 = Background and Four
5 = Background and Five
6 = Background and Six
7 = Background and Seven
8 = Background and Eight
9 = Background and Nine
+ = Background
P = Background and LivePlayer

Q = Background and LivePlayer and Two

Player = LivePlayer or DeadPlayer

Digit = Zero or One or Two or Three or Four or Five or Six or Seven or Eight or Nine


=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background

Player, Wall
Digit
Sub


======
RULES     
======

[ > DeadPlayer ] -> [ DeadPlayer ]

[ > LivePlayer Digit | no Wall ] -> [ > LivePlayer Digit Sub | no Wall ]
[ > LivePlayer | Digit no Wall ] -> [ > LivePlayer | Digit Sub no Wall ]

[ Nine Sub ] -> [ Eight ]
[ Eight Sub ] -> [ Seven ]
[ Seven Sub ] -> [ Six ]
[ Six Sub ] -> [ Five ]
[ Five Sub ] -> [ Four ]
[ Four Sub ] -> [ Three ]
[ Three Sub ] -> [ Two ]
[ Two Sub ] -> [ One ]
[ One Sub ] -> [ Zero ]

[ Zero Sub ] [ LivePlayer ] -> [ Zero ] [ DeadPlayer ]


==============
WINCONDITIONS
==============

no Nine
no Eight
no Seven
no Six
no Five
no Four
no Three
no Two
no One


=======     
LEVELS
=======

0000000
0p++++0
0+224+0
0+456+0
0+688+0
0+++++0
0000000

0000000
0++2+20
0+0+0+0
02+Q+20
0+0+0+0
02+2+20
0000000



