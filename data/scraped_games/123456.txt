title 123456
author chaotic_iak
homepage chaosatthesky.wordpress.com

(123456: 12345's sequel)

run_rules_on_level_start
key_repeat_interval 0.15

========
OBJECTS
========
(wall and stuff)
background
white

wall
#111

l0
#555
.00..
0..0.
0..0.
0..0.
.00..

l1
#555
..0..
.00..
..0..
..0..
.000.

l2
#555
000..
...0.
.00..
0....
0000.

l3
#555
000..
...0.
.00..
...0.
000..

l4
#555
..0..
.00..
0.0..
0000.
..0..

l5
#555
0000.
0....
000..
...0.
000..

l6
#555
.00..
0....
000..
0..0.
.00..

l7
#555
0000.
...0.
..0..
.0...
.0...

l8
#555
.00..
0..0.
.00..
0..0.
.00..

l9
#555
.00..
0..0.
.000.
...0.
.00..

(objects)

d1
#600 #c66
00000
00000
00100
00000
00000

d2
#660 #cc6
00000
01000
00000
00010
00000

d3
#060 #6c6
00000
01000
00100
00010
00000

d4
#066 #6cc
00000
01010
00000
01010
00000

d5
#006 #66c
00000
01010
00100
01010
00000

d6
#606 #c6c
00000
01010
01010
01010
00000

eraser
#396 #6c9
.000.
00100
01010
00100
.000.

plus
#963 #c96
.000.
00100
01110
00100
.000.

minus
#369 #69c
.000.
00000
01110
00000
.000.

invert
#693 #9c6
.000.
00100
00100
00100
.000.

poison
#936 #c69
.000.
01100
01010
01100
.000.

corpse
black #936
00000
01100
01010
01100
00000

neutral
#666 #999
.000.
00000
00100
00000
.000.

(ground)

target
#ccc white
00000
01110
01010
01110
00000

g1
#f99 #a66
00000
00000
00100
00000
00000

g2
#ff9 #aa6
00000
01000
00000
00010
00000

g3
#9f9 #6a6
00000
01000
00100
00010
00000

g4
#9ff #6aa
00000
01010
00000
01010
00000

g5
#99f #66a
00000
01010
00100
01010
00000

g6
#f9f #a6a
00000
01010
01010
01010
00000

(marker)

skiplevel
black

exhaust
black

=======
LEGEND
=======
. = background
# = wall
0 = wall and l0
1 = wall and l1
2 = wall and l2
3 = wall and l3
4 = wall and l4
5 = wall and l5
6 = wall and l6
7 = wall and l7
8 = wall and l8
9 = wall and l9
q = d1
w = d2
e = d3
r = d4
t = d5
y = d6
/ = eraser
+ = plus
- = minus
' = invert
; = poison
" = neutral
z = target
a = g1
s = g2
d = g3
f = g4
g = g5
h = g6

dig = d1 or d2 or d3 or d4 or d5 or d6
special = eraser or minus or plus or invert or poison or neutral
object = dig or special or wall or corpse
marker = skiplevel or exhaust

gate = g1 or g2 or g3 or g4 or g5 or g6
ground = target or gate

level = l0 or l1 or l2 or l3 or l4 or l5 or l6 or l7 or l8 or l9

player = dig or special

=======
SOUNDS
=======
endlevel 82179903
sfx0 88198506 (merge)
sfx1 15824902 (death)
sfx10 58297103 (skip confirm)

================
COLLISIONLAYERS
================
marker
background
ground
object
level

======
RULES
======
right [action player][level|level] -> [action player][level|action level]
[action player] -> [player]
right [level|level no skiplevel] -> [level skiplevel|level]
right [level skiplevel|action level skiplevel] -> win
right [level skiplevel|action level] -> [level skiplevel|level skiplevel] sfx10
right [orthogonal player][level skiplevel|level] -> [orthogonal player][level|level skiplevel]

[corpse] -> restart

startloop

[> player|wall] -> [player|wall]

[> d1|stationary d1] -> [exhaust|d2] sfx0
[> d1|stationary d2] -> [d1|d2]
[> d1|stationary d3] -> [d1|d3]
[> d1|stationary d4] -> [d1|d4]
[> d1|stationary d5] -> [d1|d5]
[> d1|stationary d6] -> [d1|d6]

[> d2|stationary d1] -> [d2|d1]
[> d2|stationary d2] -> [exhaust|d3] sfx0
[> d2|stationary d3] -> [d2|d3]
[> d2|stationary d4] -> [d2|d4]
[> d2|stationary d5] -> [d2|d5]
[> d2|stationary d6] -> [d2|d6]

[> d3|stationary d1] -> [d3|d1]
[> d3|stationary d2] -> [d3|d2]
[> d3|stationary d3] -> [exhaust|d4] sfx0
[> d3|stationary d4] -> [d3|d4]
[> d3|stationary d5] -> [d3|d5]
[> d3|stationary d6] -> [d3|d6]

[> d4|stationary d1] -> [d4|d1]
[> d4|stationary d2] -> [d4|d2]
[> d4|stationary d3] -> [d4|d3]
[> d4|stationary d4] -> [exhaust|d5] sfx0
[> d4|stationary d5] -> [d4|d5]
[> d4|stationary d6] -> [d4|d6]

[> d5|stationary d1] -> [d5|d1]
[> d5|stationary d2] -> [d5|d2]
[> d5|stationary d3] -> [d5|d3]
[> d5|stationary d4] -> [d5|d4]
[> d5|stationary d5] -> [exhaust|d6] sfx0
[> d5|stationary d6] -> [d5|d6]

[> d6|stationary d1] -> [d6|d1]
[> d6|stationary d2] -> [d6|d2]
[> d6|stationary d3] -> [d6|d3]
[> d6|stationary d4] -> [d6|d4]
[> d6|stationary d5] -> [d6|d5]
[> d6|stationary d6] -> [exhaust|] sfx0

[> dig|stationary eraser] -> [|] sfx0
[> eraser|stationary dig] -> [|] sfx0

[> d1|stationary minus] -> [|] sfx0
[> d2|stationary minus] -> [|d1] sfx0
[> d3|stationary minus] -> [|d2] sfx0
[> d4|stationary minus] -> [|d3] sfx0
[> d5|stationary minus] -> [|d4] sfx0
[> d6|stationary minus] -> [|d5] sfx0
[> minus|stationary d1] -> [|] sfx0
[> minus|stationary d2] -> [|d1] sfx0
[> minus|stationary d3] -> [|d2] sfx0
[> minus|stationary d4] -> [|d3] sfx0
[> minus|stationary d5] -> [|d4] sfx0
[> minus|stationary d6] -> [|d5] sfx0

[> d1|stationary plus] -> [|d2] sfx0
[> d2|stationary plus] -> [|d3] sfx0
[> d3|stationary plus] -> [|d4] sfx0
[> d4|stationary plus] -> [|d5] sfx0
[> d5|stationary plus] -> [|d6] sfx0
[> d6|stationary plus] -> [|] sfx0
[> plus|stationary d1] -> [|d2] sfx0
[> plus|stationary d2] -> [|d3] sfx0
[> plus|stationary d3] -> [|d4] sfx0
[> plus|stationary d4] -> [|d5] sfx0
[> plus|stationary d5] -> [|d6] sfx0
[> plus|stationary d6] -> [|] sfx0

[> d1|stationary invert] -> [|d6] sfx0
[> d2|stationary invert] -> [|d5] sfx0
[> d3|stationary invert] -> [|d4] sfx0
[> d4|stationary invert] -> [|d3] sfx0
[> d5|stationary invert] -> [|d2] sfx0
[> d6|stationary invert] -> [|d1] sfx0
[> invert|stationary d1] -> [|d6] sfx0
[> invert|stationary d2] -> [|d5] sfx0
[> invert|stationary d3] -> [|d4] sfx0
[> invert|stationary d4] -> [|d3] sfx0
[> invert|stationary d5] -> [|d2] sfx0
[> invert|stationary d6] -> [|d1] sfx0

[> dig|stationary poison] -> [|corpse] sfx1
[> poison|stationary dig] -> [|corpse] sfx1

[> special|stationary special] -> [special|special]

[> d1|g2 no object no exhaust] -> [d1|g2]
[> d1|g3 no object no exhaust] -> [d1|g3]
[> d1|g4 no object no exhaust] -> [d1|g4]
[> d1|g5 no object no exhaust] -> [d1|g5]
[> d1|g6 no object no exhaust] -> [d1|g6]
[> d2|g1 no object no exhaust] -> [d2|g1]
[> d2|g3 no object no exhaust] -> [d2|g3]
[> d2|g4 no object no exhaust] -> [d2|g4]
[> d2|g5 no object no exhaust] -> [d2|g5]
[> d2|g6 no object no exhaust] -> [d2|g6]
[> d3|g1 no object no exhaust] -> [d3|g1]
[> d3|g2 no object no exhaust] -> [d3|g2]
[> d3|g4 no object no exhaust] -> [d3|g4]
[> d3|g5 no object no exhaust] -> [d3|g5]
[> d3|g6 no object no exhaust] -> [d3|g6]
[> d4|g1 no object no exhaust] -> [d4|g1]
[> d4|g2 no object no exhaust] -> [d4|g2]
[> d4|g3 no object no exhaust] -> [d4|g3]
[> d4|g5 no object no exhaust] -> [d4|g5]
[> d4|g6 no object no exhaust] -> [d4|g6]
[> d5|g1 no object no exhaust] -> [d5|g1]
[> d5|g2 no object no exhaust] -> [d5|g2]
[> d5|g3 no object no exhaust] -> [d5|g3]
[> d5|g4 no object no exhaust] -> [d5|g4]
[> d5|g6 no object no exhaust] -> [d5|g6]
[> d6|g1 no object no exhaust] -> [d6|g1]
[> d6|g2 no object no exhaust] -> [d6|g2]
[> d6|g3 no object no exhaust] -> [d6|g3]
[> d6|g4 no object no exhaust] -> [d6|g4]
[> d6|g5 no object no exhaust] -> [d6|g5]
[> special|gate no object no exhaust] -> [special|gate]

endloop

[exhaust] -> []

==============
WINCONDITIONS
==============
all dig on target
all target on dig
no corpse

=======
LEVELS
=======
message Have you played the original 12345?

#########
#.......#
#.qwert.#
#...z...#
#.#zzz#.#
#..#z#..#
#...#...#
#.......#
01#######

message You haven't? Go play it; google "12345 chaotic_iak".

#########
#q.q..###
#####.###
#w.w...##
######.##
#e.e....#
#######.#
#zzz....#
02#######

message Yes, same-numbered blocks merge and increase in value. Doesn't last long though...

#########
#r.r..###
#####.###
#t.t...##
######.##
#y.y....#
#######.#
#zz.....#
03#######

message All the blocks must go to the targets! The thing I haven't told you is that all targets must be filled too. Z to undo and R to restart, remember!

#########
#r.r..###
#####.###
#t.t...##
######.##
#y.y....#
#######.#
#zzz....#
04#######

message So what happens if there are no targets?

#########
#..q....#
#.....t.#
#w......#
#...y...#
#......e#
#.r.....#
#....q..#
05#######

message You think well. That will prove useful for the rest of the game.

#########
#.......#
#.t...t.#
####z...#
#..r.r..#
#...z####
#.t...t.#
#.......#
06#######

message At this moment, clear levels (no targets) are easy compared to target levels. Try to figure out why.

#########
#.qeeeee#
#qqe.e.e#
#.qe...e#
#.q.....#
#qqqw..w#
#..w.w.w#
#..w..ww#
07#######

message Yes, literally any reasonable move wins, except for certain cases where you manage to trap yourself.

#########
#.......#
#.......#
#.qwe...#
#.ert.t.#
#.rqe...#
#.......#
#.......#
08#######

message But target levels get boring too...do they?

#########
#.#.#qq.#
#q#...###
#q.zz...#
##.zzz.##
#...zz.q#
###...#q#
#.qq#.#.#
09#######

message Well, if you find a level boring, press X twice before moving to skip it. Or perhaps if you've solved it before.

#########
#q.e#r.w#
#.zq#.z.#
#w.r#qqe#
#########
#qwq#q.w#
#.z.#qz.#
#r.e#e.r#
10#######

message Oh ho, what is this?

#########
#.......#
#.q.z.q.#
#...#...#
##f###s##
#...#...#
#.w...e.#
#.......#
11#######

message A block can only pass through a "gate" of the same number.

#########
#q.#...e#
#..#d#..#
#.#.r.#f#
#.s...s.#
#a#.z.#.#
#..#g#..#
#q...f.w#
12#######

message If you toyed enough with the previous one, you will find that the gates aren't that strict...

#########
#w#.....#
#a...q..#
#.#######
#...z...#
#######.#
#..q...a#
#.....#e#
13#######

message You can let trailing blocks of whatever numbers pass a gate!

#########
#w#e#r#t#
#a#a#a#a#
#.......#
##.#q#.##
#.......#
#s#d#f#g#
#r#r#t#q#
14#######

message Feels like smuggling illegal goods.

#########
#..#y#..#
#...g...#
#.q.w.q.#
#..e.e..#
#.w.e.w.#
#..e.e..#
#.q.w.q.#
15#######

message Thus let's make a redo of Level 11.

#########
#.......#
#.w.z.q.#
#...#...#
##f###s##
#...#...#
#.q...e.#
#.......#
16#######

message If you know what to expect, this isn't that hard...

#########
#.......#
#.q.q.q.#
#...#####
#.w.asdz#
#...#####
#.q.q.q.#
#.......#
17#######

message But this one might be pretty hard.

#########
#...#...#
#.q.d.q.#
#...#z..#
##a###f##
#...#...#
#.e.s.w.#
#...#...#
18#######

message Which naturally becomes even harder.

#########
#qd.s.fe#
#a#f#f#s#
#.fzs.s.#
#a#d#d#s#
#.d.fza.#
#a#a#a#f#
#ef.a.dq#
19#######

message With a little help...

#########
#.....g.#
#....#q##
#..q.####
#.......#
####.t..#
##z#....#
#zg.....#
20#######

message Long time friend, the eraser! Or the zero block. Just see how it looks like.

#########
#.......#
#.#qqq#.#
#.q../q.#
#.q.w.q.#
#.qw..q.#
#.#qqq#.#
#.......#
21#######

message No, erasers can't get through gates.

#########
#...#...#
#.q.#./.#
#...#...#
#q.qs...#
#...#...#
#.q.#...#
#...#...#
22#######

message And yes, erasers can be smuggled too.

#########
#.#...#.#
#.#.q.#.#
#yd...a/#
#.#.z.#.#
#ef...sq#
#.#.w.#.#
#.#...#.#
23#######

message Erasers don't annihilate each other. Neither do they need to be used.

#########
#.......#
#..e/e..#
#.r./.r.#
#.e.#.e.#
#.r.e.r.#
#..ere..#
#.......#
24#######

message Remember one-way streets in level 20?

#########
#e#...sq#
#a...#.##
#.#rge..#
#..#/#..#
#..e#w#.#
##.#...d#
#rf...#q#
25#######

message Plus and minus, also old friends!

#########
#..-.#.-#
#.t.h...#
#+.#.+..#
#########
#..-.#.-#
#...a.w.#
#+.#.+..#
26#######

message Which means you got to learn when to raise and when to lay low.

#########
#+#qrq#-#
#a#wew#d#
#..ere..#
#.#wew#.#
#.#qrq#.#
#.#####.#
#.......#
27#######

message Life is harsh.

#########
#...#...#
#.+.#.t.#
#...#e.e#
#...f...#
#q.q#...#
#.w.#.e.#
#...#...#
28#######