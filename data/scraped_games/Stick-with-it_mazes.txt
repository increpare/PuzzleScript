title Stick-with-it mazes
author Andrea Gilbert 
homepage http://www.clickmazes.com

========
OBJECTS
========

Background
black

pA
red
..0..
.000.
00.00
.000.
..0..

pB
blue
..0..
.000.
00.00
.000.
..0..

pC
yellow
..0..
.000.
00.00
.000.
..0..

pD
green
..0..
.000.
00.00
.000.
..0..

pWild
red blue yellow green 
..0..
.100.
11.33
.223.
..2..

goal
white
..0..
.0.0.
0.0.0
.0.0.
..0..

vA
red
..0..
..0..
..0..
..0..
..0..

hA
red
.....
.....
00000
.....
.....

vB
blue
..0..
..0..
..0..
..0..
..0..

hB
blue
.....
.....
00000
.....
.....

vC
yellow
..0..
..0..
..0..
..0..
..0..

hC
yellow
.....
.....
00000
.....
.....

vD
green
..0..
..0..
..0..
..0..
..0..

hD
green
.....
.....
00000
.....
.....

vXe
darkgrey
.000.
.000.
.000.
.000.
.000.

hXe
darkgrey
.....
00000
00000
00000
.....

xX 
darkgrey
.....
.000.
.000.
.000.
.....

nogo
transparent
void
transparent
sA
transparent
sB
transparent
sC
transparent
sD
transparent

bar
transparent
.....
.....
..0..
.....
.....

over
transparent
flag
transparent
twin
transparent

=======
LEGEND
=======

. = void
a = hA
1 = vA
b = hB
2 = vB
c = hC
3 = vC
d = hD
4 = vD
- = hXe
! = vXe
+ = xX
o = bar
x = nogo

g = goal
p = pA and sA 
wall = vXe or hXe or xX or nogo
stepA = vA or hA 
stepB = vB or hB
stepC = vC or hC
stepD = vD or hD
stepX = stepA or stepB or stepC or stepD or bar
notA = stepB or stepC or stepD or wall or void
notB = stepA or stepC or stepD or wall or void
notC = stepA or stepB or stepD or wall or void
notD = stepA or stepB or stepC or wall or void
Grid = void or stepX or wall 
Player = pA or pB or pC or pD or pWild
sX = sA or sB or sC or sD
Tmp = flag or over or twin

=======
SOUNDS
=======

================
COLLISIONLAYERS
================      

Background
Grid
sA, sB, sC, sD
Player
Goal 
Tmp

======
RULES
======

(remove temporary flags)
[ tmp ] -> []

(cancel illegal steps)
[ > Player | wall ] -> cancel
[ > pA sA | notA ] -> cancel
[ > pB sB | notB ] -> cancel
[ > pC sC | notC ] -> cancel
[ > pD sD | notD ] -> cancel

(player is wild)
[ > pWild | stepA | ] -> [ | stepA over | pA sA ] 
[ > pWild | stepB | ] -> [ | stepB over | pB sB ] 
[ > pWild | stepC | ] -> [ | stepC over | pC sC ] 
[ > pWild | stepD | ] -> [ | stepD over | pD sD ] 
[ > pWild | bar | ] -> [ | bar over | pWild ] 

(player is sticky)
[ > pA sA | stepA | ] -> [ | stepA over | pA sA ]
[ > pB sB | stepB | ] -> [ | stepB over | pB sB ]
[ > pC sC | stepC | ] -> [ | stepC over | pC sC ]
[ > pD sD | stepD | ] -> [ | stepD over | pD sD ]
[ > Player sX | bar | ] -> [ | bar over | player sX ]

(mark a twin cell)
late [ Player | bar | ] -> [ Player | bar | twin ]

(review/reset the stick-with-it marker)
late [ pA sA | stepA no over ] -> [ pA sA flag | stepA ]
late [ pB sB | stepB no over ] -> [ pB sB flag | stepB ]
late [ pC sC | stepC no over ] -> [ pC sC flag | stepC ]
late [ pD sD | stepD no over ] -> [ pD sD flag | stepD ]
(also check the twin cell)
late [ pA sA ] [ twin | stepA ] -> [ pA sA flag ] [ twin | stepA ]
late [ pB sB ] [ twin | stepB ] -> [ pB sB flag ] [ twin | stepB ]
late [ pC sC ] [ twin | stepC ] -> [ pC sC flag ] [ twin | stepC ]
late [ pD sD ] [ twin | stepD ] -> [ pD sD flag ] [ twin | stepD ]

(if no flag set - go wild)
late [ Player sX no flag ] -> [ pWild no sX ]

late [ Player Goal ] -> [ Player ] 

==============
WINCONDITIONS
==============

No Goal

=======
LEVELS
=======

Message To the white diamond 

Message Keep stepping over the same colour if you can

Message Maze 55A (2016) [1/5]

...............
..+-+-+-+-+-+..
..!.4.4.3.1.!..
..+d+c+c+a+b+..
..!.2.2.2.3.!..
xx+b+c+c+d+b+xx
xp1.1.4.4.3.3gx
xx+d+a+b+c+b+xx
..!.3.3.2.1.!..
..+b+a+c+a+a+..
..!.2.4.4.4.!..
..+-+-+-+-+-+..
...............

Message Maze 55B (2021) [2/5]

...............
..+-+-+-+-+-+..
..!.3.o.4.o.!..
xx+o+d+a+c+a+..
xp1.4.1.2.2.!..
xx+a+c+d+c+o+..
..!.4.2.2.1.!..
..+o+b+a+d+c+xx
..!.3.3.1.2.3gx
..+a+b+b+d+o+xx
..!.o.4.o.2.!..
..+-+-+-+-+-+..
...............

(Message Maze 55C 
...............
..+-+-+-+-+-+..
..!.3.1.4.1.!..
..+c+b+d+c+a+..
..!.3.3.2.2.!..
xx+b+b+b+c+d+xx
xp1.1.1.3.1.1gx
xx+b+d+d+d+d+xx
..!.4.2.2.3.!..
..+b+c+d+c+c+..
..!.3.3.4.1.!..
..+-+-+-+-+-+..
...............)

Message Maze 66A (2021) [3/5]

.................
..+-+-+-+-+-+-+..
..!.2.4.2.4.4.!..
xx+b+d+b+a+c+c+..
xp1.1.1.1.4.3.!..
xx+c+c+d+b+d+b+..
..!.1.3.3.1.1.!..
..+c+a+d+b+c+b+..
..!.4.1.2.4.2.!..
..+c+d+d+d+b+c+xx
..!.1.2.2.2.1.1gx
..+a+d+c+a+d+c+xx
..!.3.3.1.1.3.!..
..+-+-+-+-+-+-+..
.................

Message Maze 66B (2021) [4/5] 

.................
xx+-+-+-+-+-+-+..
xp1.o.4.1.o.3.!..
xx+c+a+o+d+d+o+..
..!.3.1.3.3.4.!..
..+o+d+a+d+b+c+..
..!.2.2.1.3.o.!..
..+a+d+c+b+d+a+..
..!.o.2.1.1.2.!..
..+d+d+c+b+d+o+..
..!.1.1.3.1.1.!..
..+o+d+a+o+d+b+xx
..!.2.o.2.4.o.1gx
..+-+-+-+-+-+-+xx
.................

Message Maze 66C (2021) [5/5] 

.................
..+-+-+-+-+-+-+..
..!.o.3.3.o.3.!..
..+d+b+o+a+b+o+..
..!.1.1.4.4.4.!..
..+o+b+a+a+c+d+xx
..!.1.3.2.2.o.3gx
xx+d+c+d+c+b+d+xx
xp1.o.4.1.4.1.!..
xx+c+a+a+d+b+o+..
..!.4.4.3.2.4.!..
..+o+a+d+o+c+c+..
..!.3.o.2.1.o.!..
..+-+-+-+-+-+-+..
.................

Message Thank you for playing :)