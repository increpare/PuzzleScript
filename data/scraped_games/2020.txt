title 2020
author bregehr
homepage www.puzzlescript.net
run_rules_on_level_start

========
OBJECTS
========

Background 
#c9c9c9  

Wall 
#3b3b3b

wall1
#575757
00000
.....
.....
.....
.....

wall2
#666666
0....
0....
0....
0....
0....

wall3
#262626
.....
.....
.....
.....
00000

wall4
#303030
....0
....0
....0
....0
....0

Player 
#2471d6 #1a5bb0
0101.
01010
00000
00000
.0.0.

playerleft
#2471d6 #1a5bb0 #c9c9c9
21010
01010
00000
00000
20202


win
transparent

two
#029c5b
0000.
....0
.000.
0....
00000

zero
#029c42
.000.
0...0
0...0
0...0
.000.

=======
LEGEND
=======

. = Background
# = Wall
2 = two
0 = zero
p = player
pushable = two or zero
walldeco = wall1 or wall2 or wall3 or wall4
noPlayerLeft = wall or pushable

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Player, Wall, pushable
playerleft
wall3
wall4
wall1
wall2
win

======
RULES     
======   



(movement and checking)
[> player | pushable] -> [> player | > pushable] 

[> player | wall] -> cancel
[> pushable | Wall] -> cancel
[> pushable | pushable] -> cancel


(player turning sprite)
[left Player no playerleft] -> [left player left playerleft]
[right player playerleft] -> [right player]
[ playerleft > Player | ] -> [ > player | playerleft]






(winning)
late right [two | zero | two | zero] -> [two win| zero win| two win| zero win]
late down [two | zero | two | zero] -> [two win| zero win| two win| zero win]

(walls)
up [wall| no wall] -> [wall wall1 | ]
left [wall| no wall] -> [wall wall2 | ]
down [wall| no wall] -> [wall wall3 | ]
right [wall| no wall] -> [wall wall4 | ]

==============
WINCONDITIONS
==============

all pushable on win

=======     
LEVELS
=======

message Level 1/5
########
#...####
#.p.####
#...####
#.2....#
#..020.#
#......#
########

message Level 2/5
#########
#.......#
#.p2200.#
#.......#
#########

message Level 3/5
########
#p...###
#.#0.###
#.#..###
#.#.####
#.202..#
#.#.#..#
#......#
#####..#
########

message Level 4/5
#######
#2#...#
#0#22.#
#2#..p#
#0#00.#
###...#
#######

message Level 5/5
#######
#p....#
#..2..#
#.2#2.#
#..#..#
#.0#0.#
#..0..#
#.....#
#######

message Thanks for playing and have a Happy New Year!



