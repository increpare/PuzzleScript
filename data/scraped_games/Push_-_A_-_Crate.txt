title Push - A - Crate
author hodori01
homepage www.puzzlescript.net

background_color orange
text_color white

========
OBJECTS
========

Background 
GREEN  Lightgreen DarkGreen
12010
00101
11001
10120
10010

Target 
DarkBlue  LightBlue Black White
.1.1.
13031
.020.
13031
.1.1.

Wall 
BROWN Grey
01110
10001
01110
10001
01110

FakeCrate
Orange DarkBrown
11011
10001
00.00
10001
11011

ControlPlayer 
Blue  Red 
.000.
.101.
00000
.000.
.0.0.

NCrate 
Orange DarkBrown
11111
10001
10.01
10001
11111

MixCrate
Orange DarkBrown
11111
10101
11.11
10101
11111

Dummy
Blue  Red
.000.
.101.
00000
.000.
.0.0.

JCrate
Orange DarkBrown
11111
10001
10001
10001
11111

LightCrate
Orange 
00000
0...0
0...0
0...0
00000

MatchCrate
Orange DarkBrown
10101
00000
10.01
00000
10101

RMatchCrate
Red DarkBrown
10101
00000
10.01
00000
10101

BMatchCrate
Blue DarkBrown
10101
00000
10.01
00000
10101

YMatchCrate
Yellow DarkBrown
10101
00000
10.01
00000
10101

PMatchCrate
Purple DarkBrown
10101
00000
10.01
00000
10101

GMatchCrate
Green DarkBrown
10101
00000
10.01
00000
10101

CratePiece1
Orange
.....
.....
..0..
.....
.....

CratePiece2
Orange
.....
.0...
.....
...0.
.....

CratePiece3
Orange
.....
.0...
..0..
...0.
.....

CratePiece4
Orange
.....
.0.0.
.....
.0.0.
.....

CratePiece5
Orange
.....
.0.0.
..0..
.0.0.
.....

Monster
Red Black White
1...1
11011
.202.
.000.
..0..

abyss
Black  White
10000
00001
01000
00000
00100

=======
LEGEND
=======

. = Background
# = Wall
P = ControlPlayer
* = NCrate
O = Target
x = FakeCrate
! = Target and ControlPlayer
D = Dummy
M = MixCrate
J = JCrate
L = LightCrate
? = MatchCrate
R = RMatchCrate
B = BMatchCrate
: = PMatchCrate
Y = YMatchCrate
G = GMatchCrate
1 = Cratepiece1
2 = Cratepiece2
3 = Cratepiece3
4 = Cratepiece4
5 = Cratepiece5
K = Monster
, = abyss

Player = ControlPlayer or Dummy
Crate = NCrate or MixCrate or JCrate or LightCrate or MatchCrate or RMatchCrate or BMatchCrate OR YMatchCrate or PMatchCrate or GMatchCrate
Cratepiece = Cratepiece1 or Cratepiece2 or Cratepiece3 or Cratepiece4 or Cratepiece5

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Target, abyss
Player, Wall, Crate, FakeCrate, Cratepiece, Monster

======
RULES     
======     

[ >  ControlPlayer | ... | NCrate ] -> [  NCrate | ... | ControlPlayer  ]
[ > ControlPlayer | Mixcrate | Mixcrate] -> [ | Controlplayer | MixCrate ]
[ > ControlPlayer | MixCrate ] -> [ > ControlPlayer | > Mixcrate ]
[ > ControlPlayer | JCrate ] -> [ > ControlPlayer | > JCrate ]
[ > ControlPlayer | LightCrate ] -> [ > ControlPlayer | > LightCrate ]
[ > ControlPlayer | MatchCrate ] -> [ > ControlPlayer | > MatchCrate ]
[ > ControlPlayer | RMatchCrate ] -> [ > ControlPlayer | > RMatchCrate ]
[ > ControlPlayer | BMatchCrate ] -> [ > ControlPlayer | > BMatchCrate ]
[ > ControlPlayer | YMatchCrate ] -> [ > ControlPlayer | > YMatchCrate ]
[ > ControlPlayer | PMatchCrate ] -> [ > ControlPlayer | > PMatchCrate ]
[ > ControlPlayer | GMatchCrate ] -> [ > ControlPlayer | > GMatchCrate ]
[ > ControlPlayer | Cratepiece ] -> [ > ControlPlayer | > Cratepiece ]
[ > Crate | LightCrate ] -> [ > Crate | > LightCrate ]
[ > Crate | Cratepiece ] -> [ > Crate | > Cratepiece ]
[ > Cratepiece | Cratepiece ] -> [ > Cratepiece | > Cratepiece ]
late [ MatchCrate | MatchCrate | MatchCrate] -> [ | | ] 
late [ RMatchCrate | RMatchCrate | RMatchCrate] -> [ | | ] 
late [ BMatchCrate | BMatchCrate | BMatchCrate] -> [ | | ] 
late [ PMatchCrate | PMatchCrate | PMatchCrate] -> [ | | ] 
late [ YMatchCrate | YMatchCrate | YMatchCrate] -> [ | | ] 
late [ GMatchCrate | GMatchCrate | GMatchCrate] -> [ | | ] 
[ > LightCrate | Wall ] -> [ | Wall ]
[ > Cratepiece1 | Cratepiece1] -> [ | Cratepiece2 ]
[ > Cratepiece2 | Cratepiece2] -> [ | Cratepiece3 ]
[ > Cratepiece3 | Cratepiece3] -> [ | Cratepiece4 ]
[ > Cratepiece4 | Cratepiece4] -> [ | Cratepiece5 ]
[ > Cratepiece5 | Cratepiece5] -> [ | JCrate ]
[ Monster | ... | ControlPlayer ] -> [ > Monster | ... | ControlPlayer ]
late [ Monster | ControlPlayer ] -> restart
late [ Controlplayer abyss ] -> restart

==============
WINCONDITIONS
==============

All Crate on Target 
All Target on Crate

=======     
LEVELS
=======

message Push - A - Crate.

#########
#.......#
#.......#
#.P.J.O.#
#.......#
#.......#
#########

message Warp - A - Crate.

#########
#.......#
#.......#
#.P.*.O.#
#.......#
#.......#
#########

message Fake - A - Crate.
#########
#x#x#x#x#
#########
#x#x#x#*#
#########
#x#x#x#x#
#########
#x#x#x#!#
#########

message Mix - a - Crate.

##########
#P.M.M..O#
##########

message Smash - A - Crate.

###
#O#
#L#
#L#
#L#
#L#
#.#
#J#
#P#
###

message Match - A - Crate.

##########
#P.......#
#...?....#
#....?...#
#....?...#
##########


message Everything moves together. ( and by now, I'm bored of " - A - Crate").

#########
#.o.*.*.#
#.......#
#o..p..*#
#.......#
#.*.o.o.#
#########
#.......#
#.D.D.D.#
#.......#
#########

message Get rid of all crates.

##############################
#..#........................P#
#..#............##############
#..L.........................#
#..L.........................#
#..L.........................#
#..L...?.....................#
#..L........?..?.............#
#..L.........?......?....?...#
#..L.................?.......#
#..L.............?...........#
#......................?.....#
##############################

message Haul the crates!

#################################
#.......LLL.........L...........#
#...P..LLLLLL.LLLL.LL....LLLLL..#
#............L..........L.......#
#################################

message *sob* *sob* My crates are in pieces... *sob*

#################
#...............#
#P.1.1.2.3.4.5.O#
#...............#
#################

message Mix & Match... classic! 

#####################
#.........M........O#
#........M.M........#
#.......M...M.......#
#......M..M..M......#
#P...?....?....?....#
#####################

message Candy Crush

#####################
#..................P#
#...:.:..Y.Y....B...#
#Y...:..Y..BG.......#
#.Y.R..R..R..G.B.?..#
#Y...?....G....?....#
#####################

message Trick the monsters!

###################,,,,,,,,,,,,,,
#........K........#,,,,,,,,,,,,,,
#.....K.....K.....#,,,,,,,,,,,,,,
#K.......P.......K#,,,,,,,,,,,,,,
#....K...1...K....#,,,,,,,,,,,,,,
#.................#,,,,,,,,,,,,,,
#........1........#,,,,,,,,,###,,
#.................#,,,,,,,,,#.#,,
#........2........#,,,,,,,,,#O#,,
#.................#,,,,,,,,,#.#,,
#........3........#,,,,,,,,,#.#,,
#........4........#,,,,,,,,,#.##,
#........5........###########..##
#...............................#
#.................###########...#
###################,,,,,,,,,#####

message Don't fall in!

.,...,...,...,.
P..,...,...,..o
,,,,,,,,,,,,..J
,,,,,,,,,,,,,..
