title Dark Maze 3
author Adam Gates
homepage www.puzzlescript.net
noaction
run_rules_on_level_start

========
OBJECTS
========

Background
DARKGRAY

Wall #
White

FixedWall *
White

Door
BROWN WHITE YELLOW
10001
00000
00020
00000
00000

Hall
RED
.....
.....
.....
.....
.....

New .
Red

Dark
BLACK

GazeH
RED

GazeV
RED

Player @
BLUE
.....
.000.
.000.
.000.
.....

Exit
YELLOW
.....
.000.
.000.
.000.
.....

================
LEGEND
================

Opening = Door or Hall or Hall or Hall

================
SOUNDS
================

================
COLLISIONLAYERS
================

Background
Exit, Opening
Player, Wall, FixedWall, New
Dark, GazeH, GazeV

================
RULES
================

random [ New ] -> [ Player ]
random [ New ] -> [ New Exit ]

startloop
random [ New | Wall | No New ] -> [ | random Opening | ]
random [ New | Wall | No New ] -> [ | random Opening | ] ( workaround for bug )
endloop

late [ ] -> [ Dark ]
late HORIZONTAL [ | Player | ] -> [ GazeH | Player | GazeH ]
late HORIZONTAL [ GazeH No Wall No Door | Dark ] -> [ GazeH | GazeH ]
late VERTICAL   [ | GazeH | ] -> [ No Dark | | No Dark ]
late VERTICAL   [ | Player | ] -> [ GazeV | Player | GazeV ]
late VERTICAL   [ GazeV No Wall No Door | Dark ] -> [ GazeV | GazeV ]
late HORIZONTAL [ | GazeV | ] -> [ No Dark | | No Dark ]

================
WINCONDITIONS
================

All Exit on Player

=======     
LEVELS
=======

*****************
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*###############*
*.#.#.#.#.#.#.#.*
*****************
