title Stackoban
author Toomber
homepage www.puzzlescript.net

run_rules_on_level_start
again_interval 0.1
realtime_interval 0.15



(
========
COLOURS
========

Blue #050533 #0D698B #0b5a78 #0f7196 #0c5875 #2c757d
Orange/Red #E34234
White #F2F1E8
Light Blue #0D698B
Dark Purple #050533
Grey #454057 #4f4a61 #4b455c

)

========
OBJECTS
========

Background1
#454057 #4f4a61 #4b455c
10220
02212
21001
00212
21122

BackgroundBlack
black

Wall
#0D698B  #0f7196 #0b5a78 #0c5875
10220
03312
21001
00313
21122


Player
Black Orange #b0271a #7d2118
.000.
.111.
22222
.333.
.3.3.


1Target
DarkBlue orange
.....
.000.
.010.
.000.
.....

2Target
DarkBlue orange
.....
00000
01010
00000
.....

3Target
DarkBlue orange
00000
01010
00000
.010.
.000.

4Target
DarkBlue orange
00000
01010
00000
01010
00000

1Crate
#E34234 Yellow
00000
00000
00.00
00000
00000

2Crate
#E34234 Yellow
00000
00000
0.0.0
00000
00000

3Crate
#E34234 Yellow
00000
0.0.0
00000
00.00
00000

4Crate
#E34234 Yellow
00000
0.0.0
00000
0.0.0
00000

FallingCrate1
#E34234 Yellow
.....
.000.
.010.
.000.
.....

FallingCrate2
#E34234 Yellow (#0f7196)
.....
.....
..0..
.....
.....

CrateColouringOn
Yellow
.....
.000.
.000.
.000.
.....

CrateColouringOff
#cee4eb
.....
.000.
.000.
.000.
.....


Hole
#050533

ToppleUp 
transparent

ToppleDown
TRANSPARENT

ToppleRight
TRANSPARENT

ToppleLeft
TRANSPARENT

WallCoversDone
transparent

WallCoverUp
#2c757d
00000
.....
.....
.....
.....

WallCoverDown
#2c757d
.....
.....
.....
.....
00000

WallCoverRight
#2c757d
....0
....0
....0
....0
....0

WallCoverLeft
#2c757d
0....
0....
0....
0....
0....

WallCoverUpRightCorner
#2c757d
....0
.....
.....
.....
.....

WallCoverDownRightCorner
#2c757d
.....
.....
.....
.....
....0

WallCoverUpLeftCorner
#2c757d
0....
.....
.....
.....
.....

WallCoverDownLeftCorner
#2c757d
.....
.....
.....
.....
0....

UpBugFront
darkbrown #2c757d darkgreen red
.0.0.
0...0
.313.
.111.
11211

DownBugFront
darkbrown #2c757d darkgreen red
11211
.111.
.313.
0...0
.0.0.

RightBugFront
darkbrown #2c757d darkgreen red
1..0.
113.0
211..
113.0
1..0.

LeftBugFront
darkbrown #2c757d darkgreen red
.0..1
0.311
..112
0.311
.0..1

UpBugBack
darkbrown #2c757d darkgreen
02120
01210
.020.
.....
.....

DownBugBack
darkbrown #2c757d darkgreen
.....
.....
.020.
01210
02120

RightBugBack
darkbrown #2c757d darkgreen
...00
..012
..221
..012
...00

LeftBugBack
darkbrown #2c757d darkgreen
00...
210..
122..
210..
00...

UpBugSpawner
transparent

CinematicEdge
black

BugsPushingTrue
transparent

BugsHatchingTrue
transparent

BugSFX1
transparent

BugSFX2
transparent

BugSFX3
transparent

BugSFXTrigger
transparent


=======
LEGEND
=======

Background = Background1 or BackgroundBlack
. = Background1
, = BackgroundBlack
# = Wall
P = Player
h = hole

1 = 1Crate and CrateColouringOff
2 = 2Crate and CrateColouringOff
3 = 3Crate and CrateColouringOff
4 = 4Crate and CrateColouringOff

a = 1Target
b = 2Target
c = 3Target
d = 4Target

r = ToppleUp

6 = 1Crate and 1Target

Crate = 1Crate or 2Crate or 3Crate or 4Crate
MultipleCrates = 2Crate or 3Crate or 4Crate
FallingCrate = FallingCrate1 or FallingCrate2
Target = 1Target or 2Target or 3Target or 4Target
Collider = Crate or Wall or Player
ToppleStatus = ToppleUp or ToppleDown or ToppleRight or ToppleLeft
Graphics = CrateColouringOn or CrateColouringOff
WallCover = WallCoverUp or WallCoverDown or WallCoverRight or WallCoverLeft or WallCoverUpRightCorner or WallCoverDownRightCorner or WallCoverUpLeftCorner or WallCoverDownLeftCorner


(buuuugs)
BugFront = UpBugFront or DownBugFront or RightBugFront or LeftBugFront 
u=UpBugFront
BugBack = UpBugBack or DownBugBack or RightBugBack or LeftBugBack
g = UpBugBack
z = CinematicEdge
y=UpBugSpawner
BugSFX = BugSFX1 or BugSFX2 or BugSFX3
w = BugSFXTrigger
x = BugsPushingTrue
j = BugsHatchingTrue
k = BugSFXTrigger and Hole


=======
SOUNDS
=======

SFX0 36772507
SFX1 22145300 (51423107)

Crate MOVE 36772507
1Crate Create 36396507
2Crate Create 24878507
3Crate Create 24878507
4Crate Create 24878507

FallingCrate1 create 45203300
BugSFX1 create 67058302
BugSFX2 create 56496507
BugSFX3 create 26743705

================
COLLISIONLAYERS
================

Background
ToppleStatus,
Target, Hole,
Graphics,
Player, Wall, Crate, FallingCrate
WallCoversDone,
WallCoverUp,
WallCoverDown,
WallCoverRight,
WallCoverLeft,   
WallCoverUpRightCorner,   
WallCoverDownRightCorner,   
WallCoverUpLeftCorner,   
WallCoverDownLeftCorner,
BugFront, 
BugBack,
CinematicEdge, UpBugSpawner, BugSFX, BugSFXTrigger, BugsPushingTrue, BugsHatchingTrue

======
RULES
======

(clear status objects)
[ moving Player ] [ ToppleStatus ] -> [ moving Player ] []
([ ScreenEdgePit ToppleStatus ] -> [ )

(stop walking into pits)
[ > Player | Hole ] -> [ Player | Hole ] 



(topple 2crates)
UP [ 2Crate ToppleUp | no Collider] -> [ 1Crate | 1Crate ] 
DOWN [ 2Crate ToppleDown | no Collider] -> [ 1Crate | 1Crate ]
RIGHT [ 2Crate ToppleRight | no Collider] -> [ 1Crate | 1Crate ]
LEFT [ 2Crate ToppleLeft | no Collider] -> [ 1Crate | 1Crate ]

(topple 3crates)
UP [ 3Crate ToppleUp | no Collider] -> [ 1Crate | 2Crate ToppleUp ] 
DOWN [ 3Crate ToppleDown | no Collider] -> [ 1Crate | 2Crate ToppleDown] 
RIGHT [ 3Crate ToppleRight | no Collider] -> [ 1Crate | 2Crate ToppleRight] 
LEFT [ 3Crate ToppleLeft | no Collider] -> [ 1Crate | 2Crate ToppleLeft] 

(topple 4crates)
UP [ 4Crate ToppleUp | no Collider] -> [ 1Crate | 3Crate ToppleUp] 
DOWN [ 4Crate ToppleDown | no Collider] -> [ 1Crate | 3Crate ToppleDown] 
RIGHT [ 4Crate ToppleRight | no Collider] -> [ 1Crate | 3Crate ToppleRight] 
LEFT [ 4Crate ToppleLeft | no Collider] -> [ 1Crate | 3Crate ToppleLeft]

(stop toppling if there's something in the way and play a sound)
UP [ Crate ToppleUp | Collider] -> [ Crate | Collider] SFX1
DOWN [ Crate ToppleDown | Collider] -> [ Crate | Collider] SFX1
RIGHT [ Crate ToppleRight | Collider] -> [ Crate | Collider] SFX1
LEFT [ Crate ToppleLeft | Collider] -> [ Crate | Collider] SFX1


(make player push crates with a space in front and start them toppling)
UP [ > Player | MultipleCrates | no Collider] -> [  >  Player | > MultipleCrates | ToppleUp ]
DOWN [ >  Player | MultipleCrates | no Collider] -> [  >  Player | > MultipleCrates | ToppleDown ]
RIGHT [ >  Player | MultipleCrates | no Collider] -> [  >  Player | > MultipleCrates | ToppleRight ]
LEFT [ >  Player | MultipleCrates | no Collider] -> [  >  Player | > MultipleCrates | ToppleLeft ]

(make player push all other types of crates)
[ >  Player | stationary Crate ] -> [  >  Player | > Crate  ]

(combine stacks of crates)
[ > 1Crate | 1Crate] -> [ | 2Crate] 
[ > 1Crate | 2Crate] -> [ | 3Crate]
[ > 2Crate | 1Crate] -> [ | 3Crate]
[ > 1Crate | 1Crate] -> [ | 3Crate]
[ > 1Crate | 3Crate] -> [ | 4Crate]
[ > 3Crate | 1Crate] -> [ | 4Crate]
[ > 2Crate | 2Crate] -> [ | 4Crate]

(make crates fall in holes + sometimes make bug sounds)
[FallingCrate2 Hole BugSFXTrigger] -> [Hole random BugSFX]
[ FallingCrate2 Hole ] -> [  Hole ]
[ FallingCrate1 Hole ] -> [  FallingCrate2 Hole ] again
[ Crate Hole no ToppleStatus] -> [  FallingCrate1 Hole ] again

(--------------------graphical effects---------------------)


late [ Graphics ] -> []
late [ 1Crate no 1Target ] -> [ 1Crate CrateColouringOff ]
late [ 2Crate no 2Target ] -> [ 2Crate CrateColouringOff ]
late [ 3Crate no 3Target ] -> [ 3Crate CrateColouringOff ]
late [ 4Crate no 4Target ] -> [ 4Crate CrateColouringOff ]
late [ 1Crate 1Target ] -> [ 1Crate 1Target CrateColouringOn ]
late [ 2Crate 2Target ] -> [ 2Crate 2Target CrateColouringOn ]
late [ 3Crate 3Target ] -> [ 3Crate 3Target CrateColouringOn ]
late [ 4Crate 4Target ] -> [ 4Crate 4Target CrateColouringOn ]

(create side borders)
late up [ Wall no WallCoversDone | no Wall ] -> [ Wall WallCoverUp | ]
late down [ Wall no WallCoversDone | no Wall ] -> [ Wall WallCoverDown | ]
late right [ Wall no WallCoversDone | no Wall ] -> [ Wall WallCoverRight | ]
late left [ Wall no WallCoversDone | no Wall ] -> [ Wall WallCoverLeft | ]


(create corners)
late down [ Wall no WallCoversDone | ] -> [ Wall WallCoverDownRightCorner WallCoverDownLeftCorner | ] 
late up [ Wall no WallCoversDone | ] -> [ Wall WallCoverUpRightCorner WallCoverUpLeftCorner | ]

(remove extra corners)
late up [ Wall WallCoverUpRightCorner no WallCoversDone | no WallCoverRight ] -> [ Wall | ] 
late right [ Wall WallCoverUpRightCorner no WallCoversDone | no WallCoverUp ] -> [ Wall | ] 
late down [ Wall WallCoverDownRightCorner no WallCoversDone | no WallCoverRight ] -> [ Wall | ] 
late right [ Wall WallCoverDownRightCorner no WallCoversDone | no WallCoverDown ] -> [ Wall | ] 
late up [ Wall WallCoverUpLeftCorner no WallCoversDone | no WallCoverLeft ] -> [ Wall | ]
late left [ Wall WallCoverUpLeftCorner no WallCoversDone | no WallCoverUp ] -> [ Wall | ] 
late down [ Wall WallCoverDownLeftCorner no WallCoversDone | no WallCoverLeft ] -> [ Wall | ] 
late left [ Wall WallCoverDownLeftCorner no WallCoversDone | no WallCoverDown ] -> [ Wall | ] 

(finish covering walls)
late [ Wall no WallCoversDone ] -> [ Wall WallCoversDone] 

(-----------------------------------------)

(--------- bugs (for end) ---------) 

[BugsHatchingTrue][1crate][2Crate][BugFront CinematicEdge] -> [BugsHatchingTrue][random BugFront][1crate random BugFront][CinematicEdge]
[BugsHatchingTrue][3crate][1Crate][BugFront CinematicEdge] -> [BugsHatchingTrue][2Crate random BugFront][random BugFront][CinematicEdge]
[BugsHatchingTrue][4crate][1Crate][BugFront CinematicEdge] -> [BugsHatchingTrue][3Crate random BugFront][random BugFront][CinematicEdge]
[BugsHatchingTrue][1crate|1crate][BugFront CinematicEdge] -> [BugsHatchingTrue][random BugFront|random BugFront][CinematicEdge]
[BugsHatchingTrue][1crate][BugFront CinematicEdge] -> [BugsHatchingTrue][random BugFront][CinematicEdge]

[UpBugFront] -> [up UpBugFront]
[DownBugFront] -> [down DownBugFront]
[RightBugFront] -> [right RightBugFront]
[LeftBugFront] -> [left LeftBugFront]
[BugsPushingTrue][> BugFront | Crate] -> [BugsPushingTrue][> BugFront | > Crate]
[Crate CinematicEdge] -> [CinematicEdge] SFX0
[ BugFront BugSFXTrigger] -> [BugFront random BugSFX]
[BugFront CinematicEdge][UpBugSpawner] -> [CinematicEdge][UpBugFront]
[BugFront CinematicEdge] -> [CinematicEdge]

([BugsHatchingTrue][BugFront | BugFront | BugFront |no BugFront] -> [BugsHatchingTrue][random BugFront | random BugFront | | random BugFront])
[BugsHatchingTrue][> BugFront | BugFront] -> [BugsHatchingTrue][random BugFront | random BugFront]

late [BugBack] -> [ ] 
late up [ DownBugFront | ] -> [ DownBugFront | DownBugBack] 
late down [ UpBugFront | ] -> [ UpBugFront | UpBugBack]
late right [ LeftBugFront | ] -> [ LeftBugFront | LeftBugBack]
late left [ RightBugFront | ] -> [ RightBugFront | RightBugBack]

(-----------------------------------------)

(continue until stack fully toppled)
late [ Player ] -> [ Player ] again


==============
WINCONDITIONS
==============

All 1Target on 1Crate 
All 2Target on 2Crate 
All 3Target on 3Crate 
All 4Target on 4Crate 
All Crate on Target
No FallingCrate1
No FallingCrate2
No ToppleStatus
No BugFront


=======
LEVELS
=======

message Hey Engineer! 
message Somehow all our fuel cells have been unseated. Can you put them back in place?
message You should be able to stack up to 4 of them. Don't worry about being gentle, they're built to withstand occasional falls.

#####
#a.a#
#.2##
#..##
#...#
#.p.#
##### (final)

########
#...a..#
#p.11..#
#.a#####
######## (final)

#######
##..a##
#.2...#
#.b.1.#
###p###
####### (final)

#######
#..#..#
#..1.p#
#a.#a.#
##.1..#
##..###
####### (final)

########
##.p...#
##1111.#
#aaaa###
######## (final)

########
###..a.#
#...#..#
#a2.1a.#
###.#.##
###.p.##
######## (final)

#######
#..####
#.2####
#aa..##
#p.#.##
#.a1..#
###...#
####### (final)

#########
#hhh##..#
#hhh##..#
######c.#
######..#
#.3.....#
#...p...#
#########(final)

#########
#...#####
#p.1....#
###1.c..#
##.1.####
##...####
######### (final)

#########
##..#..##
#..1#1..#
#p..d...#
#..1#1..#
##..#..##
######### (final)

###########
##..##...##
##.2...2.##
##..##.####
##p.aab####
########### (final)

message You might want to watch out, there are a few more holes down here than there were on my last visit. Must be ... err ... erosion?

#######
##k...#
#...h.#
#.hb..#
#..#.p#
##...##
#..2.h#
#k....#
####### (final)

##########
#hhhhhhhh#
#hhh...hh#
#kh..2bhh#
#hh.b..hh#
#hh.2.phh#
#hh..h.kh#
#hhhhhhhh#
########## (final)

#########
#....h..#
#..1.h..#
#p...ha.#
#..1.h..#
#....h..#
######### (final)

#############
##hhhhhhhh.##
#hh.....hk..#
#h...4..hhh.#
#hap....hhha#
#h...1..hhh.#
#hk.....hh..#
##hhhhhhhh.##
############# (final)

message Looks like some of these rooms have TOO MUCH fuel. (who'd have thought that was possible?). 
message This stuff expires quickly so dump any extra you find. Anywhere will do, so long as it's not cluttering up the floor.  

##########
#........#
#.1.p..a.#
#...#....#
#.1.#hhhh#
#...#kh###
########## (final)
 
#########
#....a..#
#.hhh#..#
#.hhh#1.#
#.hhh#..#
#..4.#..#
#......p#
######### (final)
 
#########
#.......#
#.#ha4..#
#..hhh..#
#.ahhh4.#
#.4hhha.#
#..hpa..#
#.......#
######### (final)

########
##ah####
#hhha.##
#.3h..##
#.3....#
#...p..#
######## (final)

#########
#...a..##
#..h#ka.#
#..hhh4.#
#.1hhh4.#
#..hh#4.#
#..h.p..#
##...#..#
######### (final)

#########
#..h....#
#.4haaa.#
#.1haaa.#
#.1hbaa.#
#..hhhhk#
#.p.324.#
#.......#
######### (one this type)

########
##hh.a##
###.#hh#
#..hh.##
#aaaa..#
##.334.#
##..p..#
######## (final)

#########
#####..p#
###b11d.#
#..1111.#
#..1111##
###h11c##
####..###
####..###
#########(one this type if possible)

########
#.hhh..#
#hhhhh.#
#hhhkhh#
#.hhhkh#
#hhhhhh#
#.4444.#
#......#
#.p....#
##.##### (end level)

message Hey Engineer, good news! I think I've figured out what's been mucking around with our fuel AND our floor! 
message We've got a slight fuel-rod bug infestation. They're fascinating creatures really. They reproduce by hijacking the latent energy in fuel rods. But don't worry, they can only lay and hatch their eggs in complete darkness.
message Say, where did you end up putting those extra fuel cells we had lying around?
message ...
message Oh no, what have you done?

zzzzzzz
,,,,,,x
,,,,,,,
,w,,1,,
,,1,,,,
,www,1,
,,,,w,,
,yyuyy,
zzzzzzz

zzzzzzzzzzzz
,,,,,,,,,,,x
,,,,,,,,,,,,
,,,,,,,1,,,,
,,,,1,,,,,,,
,,1,,,,,11,,
,,w,,1,u,,,,
,,,,u,,g,,,,
,,y,gy,,uy,,
zzzzzzzzzzzz

zzzzzzzzzz
,,,,,,,,,x
,,,w,,w,,,
,,,1111,,,
,,,w,,w,,,
,,,,,w,,,,
,,,uyyu,,,
zzzzzzzzzz

zzzzzzz
,,,,,,x
,,,w,,,
,,,w,,,
,w,,,,,
,,,,,,,
,,,1,,,
,1,,,1,
,,,u,,,
,u,g,u,
zzzzzzz

zzzzzzzzz
z,,,,j,,z
z,,,,,,,z
z,,,w,1,z
z,w1,,,,z
z,w,,,,,z
z,w,1,w,z
z,u,,,,,z
zzzzzzzzz

zzzzzzzzzzzzz
z,,,,,,,,,,jz
z,1,1w,,1,,,z
zw,,1,1,,,w,z
z,,1w111,1,,z
zw,,1223311,z
z,,123432,1,z
zw1,134341,,z
z,w113231,1,z
z,w,11111w,,z
z,,,1,1,,1,,z
z,1,,,w1,w,,z
z,,,,,u,1,,,z
zzzzzzzzzzzzz

message THE END. Thanks for playing!


(rejected ideas/concepts)

(

#######
#h..b##
#..h..#
#....h#
#h#p..#
#...#.#
#.2.h.#
#.....#
#######

##########
######..##
######..##
#.a.....##
#.hhhh.###
#.hhhh.###
#.4......#
#..###.p.#
##########

######
#h...#
##.h.#
#.b..#
#.#.p#
#...##
#.2.h#
#....#
######

#######
##..h##
#h.c..#
#.....#
#1.h.##
#a#h.h#
#..h..#
#....h#
#..3.##
##....#
##p.h.#
#######

############
###h##..h###
#......c.h##
#..###h..###
##.#h#.hh###
##.#..hh.#h#
##.##h.hh###
##.h##...hh#
#......3.#.#
#...h#.p..h#
#..###....##
############

#########
#.......#
#.......#
#.......#
#.......#
#.......#
#.......#
#..p....#
#########

########
#..hh..#
#..hh..#
#...bh.#
#hh....#
#hh.hh.#
#...hh.#
#.2....#
#.p.h..#
########


##########
#.c.##.a.#
#...##...#
#...##...# 
#.2.pa.3.#
#...##...#
##########

#######
##b11d#
..1111#
..1111#
.#h11c#
.##..##
p....##

#######
#######
##...##
##...##
##.#1##
##..1.#
##1#1.#
#.1..##
#.1#.##
##.p.##
#######
#######

########
#.hh.a##
#h#.#hh#
#..hh.##
#.....##
#.333..#
#..p..##
#..a..##
########

########
#ahh.a##
#a#a#hh#
#..hh.##
#.....##
#.333..#
#.....##
#..p..##
########


hhhhhhh......
hhhhhh.......
hhhhhh.......
hhhhh....b...
hhhhh........
hhhh........h
hhh........hh
hh.......hhhh
hh.p...hhhhhh
h.....hhhhhhh
....hhhhhhhhh

hhhhhhhhhhh
hhhhhhhhhhh
hhhhhh..bhh
hhah.h.hhhh
hhhhah.hhhh
hhhhhh.hhhh
...........
..4..4..4..
.....p.....

........
........
..hhhh..
.2habh3.
..hchh..
..hhh...
...4....
.....p..

##########
##..######
##p.1..###
#..#.#...#
#.1...1..#
####b#####
####a#####
##########


#############
#...........#
#..p4aaaa...#
#...........#
#############

(ONE WAY SYSTEM)		

#######
#p#...#
#.a1..#
####.##
####..#
#######
)
