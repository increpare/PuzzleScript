title Summer
run_rules_on_level_start
background_color #14D0D0
again_interval 0.06
text_color #FFFF50

========
OBJECTS
========

Background 1
darkgrey

unlit
#70F044

lit 
#FBFB68


summer
#f41010 #600404 #FFB008
.000.
00000
01010
00000
.000.

spring ?
#109010 #041904
.000.
00000
01010
00000
.000.



lightu 
#FFC008
.....
..0..
.0.0.
.....
.....



lightr 
#FFC008
.....
..0..
...0.
..0..
.....

lightl 
#FFC008
.....
..0..
.0...
..0..
.....

lightd 
#FFC008
.....
.....
.0.0.
..0..
.....


u1
#FF5800
.....
..0..
.0.0.
.....
.....

r1
#FF5800
.....
..0..
...0.
..0..
.....

l1
#FF5800
.....
..0..
.0...
..0..
.....

d1
#FF5800
.....
.....
.0.0.
..0..
.....

u2
#FF7800
.....
..0..
.0.0.
.....
.....

r2
#FF7800
.....
..0..
...0.
..0..
.....

l2
#FF7800
.....
..0..
.0...
..0..
.....

d2
#FF7800
.....
.....
.0.0.
..0..
.....

u3
#FF9800
.....
..0..
.0.0.
.....
.....

r3
#FF9800
.....
..0..
...0.
..0..
.....

l3
#FF9800
.....
..0..
.0...
..0..
.....

d3
#FF9800
.....
.....
.0.0.
..0..
.....

u4
#FFA800
.....
..0..
.0.0.
.....
.....

r4
#FFA800
.....
..0..
...0.
..0..
.....

l4
#FFA800
.....
..0..
.0...
..0..
.....

d4
#FFA800
.....
.....
.0.0.
..0..
.....

x0
transparent

x1
transparent

x2
transparent

x3
transparent

x4
transparent

x5
transparent

plusone
transparent

ux
transparent

dx
transparent

lx
transparent

rx
transparent


block w
#14D0D0



glass q
brown lightbrown white
.000.
02110
02120
01120
.000.

water
#1040E0

ver
#1040E0 #A07004
01110
01110
01110
01110
01110

hor
#1040E0 #A07004
00000
11111
11111
11111
00000

bloomed [
#209A00
.000.
00000
00000
00000
.000.

flower >
#209A00 #20BA00
.....
..0..
.010.
..0..
.....

door e
#80F4F4 #14D0D0
00011
00001
00000
00001
00011



bad .
#985000 white #ff5064
..2..
.000.
.000.
.000.
..1..




lose
#985000 white #ff5064 black
..2..
.303.
.000.
.000.
..1..

nemir z
#985000 #14D0D0
0....
00...
0.0..
0..0.
00000

semir x
#985000 #14D0D0
00000
0..0.
0.0..
00...
0....

nwmir c
#985000 #14D0D0
....0
...00
..0.0
.0..0
00000
swmir v
#985000 #14D0D0
00000
.0..0
..0.0
...00
....0

diag b
#985000 #14D0D0
00000
00..0
0.0.0
0..00
00000

diago n
#985000 #14D0D0
00000
0..00
0.0.0
00..0
00000


nodoor
transparent

wins
transparent

start
transparent

vert
transparent

new
transparent

more
transparent
=======
LEGEND
=======

mirror = swmir or semir or nwmir or nemir
floor = lit or unlit
diags = diag or diago
light = lightu or lightd or lightr or lightl 



flow = flower or bloomed
cre =  block or mirror or diags or door
crae = block or glass or bad or flow
crate =  block or glass or mirror or bad or diags or summer or flow
push =  mirror or diags
t = bad and lit
fir = u1 or d1 or l1 or r1
sec = u2 or d2 or l2 or r2
thi = u3 or d3 or l3 or r3
fou = u4 or d4 or l4 or r4
xs = x0 or x1 or x2 or x3 or x4 or x5

2 = summer and start

k= water 

player = summer

=======
SOUNDS
=======

SFX1 71317504
SFX2 54283103
SFX3 32265107
SFX4 3967707
SFX5 25216505
SFX6 37188907

================
COLLISIONLAYERS
================

Background

floor
vert
water, hor, ver
lightu
lightr
lightd
lightl

more
nodoor, wins, lose
bad
flow
start, new

player, glass, block, door, mirror, diags, spring
plusone
x0
x1
x2
x3
x4
x5
u1, u2, u3, u4
d1, d2, d3, d4
l1, l2, l3, l4
r1, r2, r3, r4
ux
lx
rx
dx




======
RULES
======


[spring new] -> [spring]

up [spring no new|no block] -> [|spring new] again
right [spring no new|no block] -> [|spring new] again
[spring no new] -> win

[xs] -> []
[fir] -> []
[sec] -> []
[thi] -> []
[fou] -> []


[summer][no floor no door no nodoor no block] -> [summer][unlit]


[new] -> []
[light]->[]






[> summer| water] -> cancel
[> summer| flow] -> cancel
[> summer| push| no crae no water] -> [> summer|> push|] SFX6
startloop
right [> nemir| swmir] -> [|stationary diag]
up [> nemir| swmir] -> [|stationary diag]
down [> swmir| nemir] -> [|stationary diag]
left [> swmir| nemir] -> [|stationary diag]
left [> nwmir| semir] -> [|stationary diago]
up [> nwmir| semir] -> [|stationary diago]
down [> semir| nwmir] -> [|stationary diago]
right [> semir| nwmir] -> [|stationary diago]
[> push|push| no crae no water] -> [> push|> push|]
endloop
[> push| stationary push] -> cancel
[> summer| stationary push] -> cancel

late [summer] -> [summer lightu lightr lightd lightl x0]



startloop 

late [light more] -> [light]

late up [lightu xs no flow  no more|no lightu no cre no door ] -> [lightu xs| lightu more xs plusone]
late down [lightd xs no flow  no more|no lightd no cre no door  ] -> [lightd xs| lightd more xs plusone]
late left [lightl xs no flow  no more|no lightl no cre no door  ] -> [lightl xs| lightl more xs plusone]
late right [lightr xs no flow  no more|no lightr no cre no door  ] -> [lightr xs| lightr more xs plusone]


late up[lightu no more no flow xs|semir no lightr] -> [lightu xs|lightr more semir xs plusone]
late up[lightu no more no flow xs|swmir no lightl] -> [lightu xs|lightl more swmir xs plusone]
late down[lightd no more no flow xs|nemir no lightr] -> [lightd xs|lightr more nemir xs plusone]
late down[lightd no more no flow xs|nwmir no lightl] -> [lightd xs|lightl more nwmir xs plusone]
late left[lightl no more no flow xs|semir no lightd] -> [lightl xs|lightd more semir xs plusone]
late left[lightl no more no flow xs|nemir no lightu] -> [lightl xs|lightu more nemir xs plusone]
late right[lightr no more no flow xs|swmir no lightd] -> [lightr xs|lightd more swmir xs plusone]
late right[lightr no more no flow xs|nwmir no lightu] -> [lightr xs|lightu more nwmir xs plusone]

late up [lightu no crae mirror|light flow no new] -> [lightu mirror|bloomed new] 
late down [lightd no crae mirror|light flow no new] -> [lightd mirror|bloomed new]  
late left [lightl no crae mirror|light flow no new] -> [lightl mirror|bloomed new]  
late right [lightr no crae mirror|light flow no new] -> [lightr mirror|bloomed new] 
late up [lightu no crae mirror|flow no new] -> [lightu mirror|bloomed new]  
late down [lightd no crae mirror|flow no new] -> [lightd mirror|bloomed new] 
late left [lightl no crae mirror|flow no new] -> [lightl mirror| bloomed new] 
late right [lightr no crae mirror|flow no new] -> [lightr mirror|bloomed new] 
late up [lightu no crae mirror|light flow no new] -> [lightu summer|bloomed new] 
late down [lightd no crae summer|light flow no new] -> [lightd  summer|bloomed new]  
late left [lightl no crae summer|light flow no new] -> [lightl  summer|bloomed new]  
late right [lightr no crae summer|light flow no new] -> [lightr  summer|bloomed new] 
late up [lightu no crae summer|flow no new] -> [lightu summer|bloomed new]  
late down [lightd no crae summer|flow no new] -> [lightd summer|bloomed new]  
late left [lightl no crae summer|flow no new] -> [lightl summer| bloomed new] 
late right [lightr no crae summer|flow no new] -> [lightr summer|bloomed new]

late up [lightu no crate water|light flow no new floor] -> [ver|lit] SFX1 again
late down [lightd no crate water|light flow no new floor] -> [ver|lit] SFX1 again
late left [lightl no crate water|light flow no new floor] -> [hor|lit] SFX1 again
late right [lightr no crate water|light flow no new floor] -> [hor|lit] SFX1 again
late up [lightu no crate water|flow no new floor] -> [ver|lit] SFX1 again
late down [lightd no crate water|flow no new floor] -> [ver|lit] SFX1 again
late left [lightl no crate water|flow no new floor] -> [hor|lit] SFX1 again
late right [lightr no crate water|flow no new floor] -> [hor|lit] SFX1 again

late up [lightu no crate no water no hor no ver|light flow no new floor ux] -> [bloomed new ux|lit] again SFX3
late down [lightd no crate no water  no hor no ver|light flow no new floor dx] -> [bloomed new dx|lit] again SFX3
late left [lightl no crate no water  no hor no ver|light flow no new floor lx] -> [bloomed new lx|lit] again SFX3
late right [lightr no crate no water  no hor no ver|light flow no new floor rx] -> [bloomed new rx|lit] again SFX3
late up [lightu no crate no water no hor no ver|flow no new floor ux] -> [bloomed  ux new|lit] again SFX3
late down [lightd no crate no water  no hor no ver|flow no new floor dx] -> [bloomed dx new|lit] again SFX3
late left [lightl no crate no water  no hor no ver|flow no new floor lx] -> [bloomed lx new|lit] again SFX3
late right [lightr no crate no water  no hor no ver|flow no new floor rx] -> [bloomed rx new|lit] again SFX3

late up [lightu no crate no water no hor no ver|light flow no new floor no ux] -> [lightu |light flow floor ux] again
late down [lightd no crate no water  no hor no ver|light flow no new floor no dx] -> [lightd |light flow floor dx] again
late left [lightl no crate no water  no hor no ver|light flow no new floor no lx] -> [lightl |light flow floor lx] again
late right [lightr no crate no water  no hor no ver|light flow no new floor no rx] -> [lightr |light flow floor rx] again
late up [lightu no crate no water no hor no ver|flow no new floor no ux] -> [lightu |flow floor ux] again
late down [lightd no crate no water  no hor no ver|flow no new floor no dx] -> [lightd |flow floor dx] again
late left [lightl no crate no water  no hor no ver|flow no new floor no lx] -> [lightl |flow floor lx] again
late right [lightr no crate no water  no hor no ver|flow no new floor no rx] -> [lightr |flow floor rx] again

late [x0 plusone] -> [x1]
late [x1 plusone] -> [x2]
late [x2 plusone] -> [x3]
late [x3 plusone] -> [x4]
late [x4 plusone] -> [x5]


endloop

late [summer unlit] -> [summer lit]
late [light unlit] -> [light lit]
late [summer|flower] -> [summer| bloomed]
late [light flow] -> [bloomed]


late [lightu x4] -> [lightu u4 x4]
late [lightd x4] -> [lightd d4 x4]
late [lightl x4] -> [lightl l4 x4]
late [lightr x4] -> [lightr r4 x4]
late [lightu x3] -> [lightu u3 x3]
late [lightd x3] -> [lightd d3 x3]
late [lightl x3] -> [lightl l3 x3]
late [lightr x3] -> [lightr r3 x3]
late [lightu x2] -> [lightu u2 x2]
late [lightd x2] -> [lightd d2 x2]
late [lightl x2] -> [lightl l2 x2]
late [lightr x2] -> [lightr r2 x2]

late [lightu x1] -> [lightu u1 x1]
late [lightd x1] -> [lightd d1 x1]
late [lightl x1] -> [lightl l1 x1]
late [lightr x1] -> [lightr r1 x1]





late [summer][door] -> [summer][nodoor new lit]

late [nodoor new lit][unlit no water] -> [door][unlit no water]
late [lose]->restart
late [light bad] -> [light lose] SFX5
late [nodoor new] -> [nodoor] SFX2
late [player nodoor] -> [wins player]


late [start][summer wins floor] -> win


==============
WINCONDITIONS
==============

=======
LEVELS
=======



message Ughh. Not again.

w1111111e
w111w1w1w
w1111111w
w1w111w1w
w1111111w
w1w1w111w
21111111w

message Millenium in and millenium out,

message It's always the same.

message I'm SO BOOOOOORED.

message [TREES GROW AND MOVE TOWARDS SUNLIGHT]

w>>>>w
w1111w
w1111w
w1111w
21111e
w1111w
w1111w
w1111w
w>>>>w


message And all these stupid rabbits do is sleep.

message Whatever.

message I guess they're more trouble when they're awake anyway.

message [DON'T WAKE BUNNIES UP BY SHINING LIGHT IN THEIR EYES]

21111111w
w1111111w
w1w1t1w1w
w11www11w
w1111111e

message UGHHH.

message Why do rabbits always have to find the least convenient places to rest?


w1111111w
w1w111>1w
21w1t111e
w1w111>1w
w1111111w

message Who keeps replacing my healthy grown trees with these totally WIMPY plants, anyways?

wwwwwww
2111www
w1111ww
w1111>w
ww11>1w
www>11e

message Obviously someone with bad taste.

message The bigger trees clearly look way better.

message [WHEN TREES FALL IN THE WATER, THEY CAN BE USED AS BRIDGES]

wkkk1>w
211k11e
w11k11w
w11k11w
wkkkkkw
w11k11w
w1>k11w
w11k>1w
w11kkkw



w11111111
w1111kkkw
wkk1kk11w
w1k?k111w
w1kkk111w
w11>1111w
w1111111w
21111111w

message WHO THE HELL?

message Get BACK here!

w1111111e
w1111kkkw
wkk1kk11w
w1k1k111w
w1kkk111w
w11>1111w
w1111111w
21111111w

message She looks so similar to me.

message It was almost like looking in a mirror or something.

message Although much less blinding, of course!

message [MIRRORS REFLECT LIGHT]

wx1111e
w11v1ww
w1w1w1w
wwx1v1w
2c1111w

message I'm going to go find her to tell her to stop getting rid of my grown trees.

message What the HECK is her problem!?

w1111111w
21111111w
w111c1c1w
w1111111w
w1v1t111w
w1111111e
w1111111w

message I bet she's also the one who keeps getting rid of all the fruit I make each year.

message She probably doesn't even have any idea how ANNOYING she is!

message [SUMMER CAN JUMP OVER GAPS OF WATER WHEN BRIDGES DON'T CONNECT]

w1kkk1k1>e
w1k>kk>k1w
w1kk1k>kkw
wk1k>kk>kw
wk1kk1k1kw
w1k>k1k1kw
21kkk1kkkw



message Maybe she's also the one who makes it so cold whenever I start to come around.

message I'm going to go find her and get her to stop!

w1k>>>>e
w1k>>>>w
w1k>>>>w
w1k>>>>w
w1kkkkkw
2111111w




message GAHH!

message Why is she rushing away so fast?

message Where could she even be going!?


wx111>e
w1x11>w
w11x1>w
w111x>w
21111ww

message She must be intentionally trying to AGGREVATE me.

message And doesn't want me to catch her.


w11>111w
w1>111ke
w>111k1w
w111k11w
w11k111w
w1k111>w
2k111>1w
w111>11w


message Well that just makes me want to get to her EVEN MORE!

message YOU CAN RUN, BUT YOU CAN'T HIDE!


wwwwww1>w
wwwwww11e
www1111cw
w11x1v11w
w11c1z11w
w11111www
2xcwwwwww
w11wwwwww



message These set ups seem to be getting more and more complicated.


w1111111e
w11>1>11w
w11111>1w
w111t111w
w11111>1w
w1111111w
21111111w





message She must be putting these traps in my way to stop me from getting to her.


wwtwtwtwtwtww
w11111111111w
w11111111111w
w1111v111111w
2z1x1c1c1111e
w11111111111w







message Whatever.

message She's an idiot if she ACTUALLY BELIEVES she can stop me.

211111111111e
ww1111v1111ww
[w11v1w1z11w1
[wx111w111vw1
1111w111w11k1
kkkkkkkkkkkk1
[1w1111111w11
w11111111111w


message If I catch her, maybe I can get her to stop resetting everything I do.


ww111wwww
211111www
w>1>1>1ww
ww111111w
www>1>1>e
wwww111ww
wwwwwwwww



message Then everything will finally stay the way it SHOULD be.

w11111w
w11111w
w1xxv1w
21ztv1e
w1zcc1w
w11111w
w11111w



message So then summer will last ETERNALLY!

message You can't run forever!

www11111www
www11111www
www11111www
w1111w1111t
w1xv1w1111t
w1zc1w1111w
21111w1111e
wwwwwwttwww


message END OF PART TWO
