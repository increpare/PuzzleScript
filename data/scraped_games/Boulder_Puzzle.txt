Title Boulder Puzzle
Author Connorses/Loneship Games
background_color darkblue
again_interval .1
noaction

run_rules_on_level_start

color_palette 6
========
OBJECTS
========
bg1 .
black
bg2 ,
darkbrown black
10101
01110
01111
11110
10001

player @
orange red yellow
.0.0.
.202.
.111.
.0001
.0.0.

dead_player
red darkred
.1.1.
.111.
.000.
.1110
.1.1.

dirt
lightbrown brown darkbrown
22222
12121
11111
21212
11111
wall
grey darkred
01110
00000
11011
11011
00000
(darkgreen darkbrown
01110
10101
00000
11011
01110)
rock
grey lightgrey darkgrey
.011.
00011
21100
22021
.220.
diamond
darkblue lightblue purple white
..1..
.013.
22111
.220.
..0..

teleport +
blue white lightblue
20002
0.1.0
01.10
0.1.0
20002

tpg (teleport ghost)
white
.0.0.
.000.
.000.
.0000
.000.

tpg2
lightblue
.0.0.
.000.
.000.
.0000
.000.

dtp
blue
.0.0.
.000.
.000.
.000.
.000.
dtp2
lightblue
.0.0.
.000.
.000.
.000.
.000.

goal
orange yellow
00000
01110
01100
01100
01110
open Ω
orange black darkgrey
00000
01110
01110
01110
02220

key k
yellow darkblue
11.11
00..1
0.000
00.00
11.11

chest c
orange darkblue yellow blue lightblue
.000.
03340
11211
03330
00000
(markers for boulder physics:)
sL (solid to the left)
transparent
.....
.....
.0...
.....
.....
sR (right)
transparent
.....
.....
...0.
.....
.....
sB (below)
transparent

bdemon d
blue lightblue white blue
.1.1.
.101.
.212.
.000.
00000

rdemon r
red white blue
.1.1.
.101.
.202.
.000.
00000

rdg
pink white
.1.1.
.101.
.101.
.000.
00000

alerted
transparent

arriving
transparent

lava ≈
orange red
11111
10010
01101
10010
10101

burn_graphic
yellow red
..0..
.00..
.010.
01110
.000.

burn_graphic2
yellow red
.....
..0..
..00.
.010.
..0..
=======
LEGEND
=======
background = bg1 or bg2

# = wall
o = rock
* = diamond and bg1
% = dirt and bg2
g = goal

demon = rdemon or bdemon
falling = rock or diamond or chest or key
solid = dirt or wall or player or goal or falling or teleport or demon
pushable = rock or key or chest
required = diamond or chest
crushable = player or demon

flammable = player or key or demon

graphic = burn_graphic or burn_graphic2
=======
SOUNDS
=======
startgame 66267908
(cool menu bloop:75730108)
(shpoo: 98937108)
(14031908)
endlevel 58911108(67416308)
startlevel 47908
dirt destroy 98564107(22608904)

sfx0 88715100 (pickup)
sfx1 82490501(91094303 diamond falling)
sfx2 71424702(85658702) (rock falling)
sfx3 38957708(teleport)
sfx4 76746507(36756507)
sfx5 25753509(22047109) (unlock chest)

sfx6 37906502(death)
sfx7 67857102(also death)

(58633108)(13120109)()(45419902)
tpg create 11155709

rdg create 33561903(24744508)
rdg create 35701702

dtp create 38957708
sfx8 46628104 (bump)

lava create 24744508
================
COLLISIONLAYERS
================
background
open, dead_player
lava
solid
sL
sR
sB
tpg tpg2
dtp dtp2
alerted
arriving
rdg
graphic
======
RULES
======
[burn_graphic2] -> [  ]
up [burn_graphic|no burn_graphic2] -> [  |burn_graphic2] again

(check win condition for opening the door)
[goal] -> [open]
[open] [required] -> [goal] [required]

(clear temporary objects)
[sL] -> [ ]
[sR] -> [ ]
[sB] -> [ ]
[tpg2]->[ ]
[dtp2]->[ ]
[rdg] ->[ ]

[tpg] -> [tpg2] again (teleport animation)

[moving player] [alerted] -> [moving player] [ ]

[> player|dirt] -> [ |player] again
[> player|diamond] -> [player| ] sfx0 again
[> player] -> [> player] again

down[> player|lava] -> cancel

(teleporter physics)
[> player|teleport|...|no solid] -> [  |teleport > tpg|...|  ] sfx3

[> tpg|solid] -> [tpg|> tpg solid]

[> tpg|no solid] -> [tpg|tpg player] again

(sokoban push)
horizontal [> player|pushable] -> [> player|> pushable] sfx4 again
horizontal [> player|key|chest] -> [  |player|diamond] sfx5
[> player| wall] -> [> player| wall] sfx8

(open chest w/key)
[> key|chest]->[  |diamond] sfx5
down [key|chest] -> [  |diamond] sfx5

(gravity)
down [falling|no solid] -> [down falling| ] again

down [falling|down falling] -> [down falling|down falling] again

[> diamond|no solid|solid] -> [> diamond|no solid|solid] sfx1
[> rock|no solid|solid] -> [> rock|no solid|solid] sfx2

(cascading physics)
down [solid|no solid] -> [solid|sB]

right [stationary falling|solid] -> [falling sR|solid]
left  [stationary falling|solid] -> [falling sL|solid]
right [stationary falling|sB] -> [falling sR|sB]
left  [stationary falling|sB] -> [falling sL|sB]

down [falling|falling sR no sL] -> [left falling|falling sr] again
down [falling|falling sL no sR] -> [right falling|falling sr] again
down [falling|stationary falling no sR no sL] -> [right falling|falling] again

down [falling|moving falling] -> [stationary falling|moving falling]
(killing player and monsters)
down [falling|no solid|crushable] -> [down falling|no solid| ] sfx6 sfx7

(red demon charge)
[rdemon|...|player] -> [> rdemon|...|player]
[> rdemon|no solid] -> [rdg|> rdemon] again

(blue demon teleport)
[dtp|...|player]->[> dtp|...|player]
[> dtp|solid] -> [dtp|> dtp solid]
[> dtp|no solid] -> [dtp|arriving bdemon] again
[bdemon no arriving] [arriving bdemon] -> [ ] [arriving bdemon] again
[dtp] [arriving] -> [dtp2] [  ]
[dtp2] [dtp] -> [dtp2] [dtp2]
[dtp] -> [ ]

(liquid flow)
horizontal [lava|flammable] -> [lava|burn_graphic] again
+ down [lava|flammable] -> [lava|burn_graphic]
+ horizontal [lava|no solid no lava] -> [lava|lava]
+ down [lava|no solid no lava] -> [lava|lava] again
+ [> flammable|lava] -> [burn_graphic|lava] again

(late rules)
late [solid lava] -> [solid]
late [bdemon no alerted|solid|...|player] -> [alerted bdemon dtp|solid|...|player] again
late [demon|player] -> [demon|dead_player] again
==============
WINCONDITIONS
==============
no diamond
some open on player
=======
LEVELS
=======

.#####...
.#...#...
.#.@.#...
.#...#...
.#.*.####
.#%%%%..#
.###*%og#
.#%#%%o.#
.########


#########
#*.*.o.o#
#o....g.#
#*.o..%.#
#%.%o%%%#
#%%%o%%@#
#*%%*%%%#
#%%.o.%%#
#%o%%%%*#
#########

#############
#%%%%oo%%%%%#
#..#%oo%%%%%#
#@#*%%%%%%%%#
#%%##*#%%%%%#
#%%%#*#%%%oo#
#%%%#%#%%%*o#
#%%%%%%%%%%##
#%%%%%%%%%%g#
#############

#########
.##o%g#..
.#%o%%#..
.#%o%o#..
.##*oo#..
..#%#*#..
..#%o%#..
..#o%o#..
..#%%%#..
..#%@%#..
..#%%%#..
#########

##########
###k#..%.#
#%%%%%%%%#
#%...%%%%#
#%...%%.%#
#%.c.%%@%#
#%######%#
#%...%%%%#
#%.g.%%%%#
##########

##########
###%%%%*%#
###%%%**%#
##%%%%k*%#
#%%%%%##o#
#......%o#
#c..@..%o#
#%%%#..%%#
#%g%#..%%#
##########

###########
##ooooooo##
#%#ooooo#%#
#%o#ooo#%%#
#o%%#*#%%%#
#%g%%%%%@%#
#.........#
#.........#
#%%%%.%%%%#
#%%%%%%%%%#
###########

##########
#..#kk#..#
#..%kk%..#
#..%kk%..#
#...kk...#
#...%#...#
#........#
#o@....#.#
#%#...o#%#
#%#cccc#%#
#%#cccc#g#
##########

#############
#..#%go%%#..#
#..+##*##+..#
####%#%%%#..#
#oo#%%#%%...#
####%%%%%...#
#++##########
#..#....%+..#
#..+%+.%%#.@#
#############

#############
#...+%*%o%+.#
#.@.#%%%%%#.#
#...#.....#.#
#####.......#
#...#%%%o%#.#
#.g.#%o%o%#.#
#...#%%%%%#.#
#...#.%..%+.#
#############

#############
#...+%o%*%+.#
#.@.#%%%%%#.#
#...#.....#.#
#####.......#
#...#%%%%%#.#
#.g.##o#o##.#
#...#%%%%%#.#
#...#.%..%+.#
#############

##############
#...%%%%%*%%%#
#...%*%o%*%%%#
#...%%%%%%%%%#
##%##.....*%%#
#%%%#.....%o%#
#%%o#..r..*o%#
#o%o#.....%%%#
#o%%#.....o%%#
##%#######%%%#
#.....##%%%%%#
#...@.##%%%g%#
#.....##%%%%%#
##############
(
#############
#%%%%%%%%%%%#
#%%%%%%%%%%%#
#%%%%%%%%%%%#
#@%%%%%%%%%%#
#%%%%...%%%%#
#%%%..r..%%%#
#%%%.....%o%#
####.....%%%#
#..#.....%%%#
#.g###.##+%%#
#..#.....%%%#
####%%%%%####
#############
)

#############
#%%%%%%%%%%%#
#%%%%%%%%%%%#
#%%%%%%%%%%%#
#%%%%...%%%%#
#%%%..r..%%%#
#%%%.....%%%#
####.....%@%#
#..#.o*..%%%#
#g.#oo*.o+%%#
#..#%%%%%%%%#
####%%%%%%%%#
#############

###############
#.............#
#.............#
#....o...o....#
#@..##...##...#
#####r...r#...#
#%..##...##...#
###%#r...r+...#
#%%%#.%%**#%%%#
#%%%#%*%%%#%%%#
##%%%%%%%%#...#
#%%##%%%%%+.g.#
###############

#############
#%%#%%%%%#%%#
#%%#%ooo%#%%#
#..#%%%%%#%%#
#..+%%..%#%%#
#@.#%%..%#%%#
####%%%%%####
#..#%%%%%#..#
#..#%%*%%+..#
#g.#######..#
#############

##########
#...%%%%%#
#...*%%k@#
#...%%.%%#
#....oc%%#
#g.r######
##########

###########
#%%%%.%%%%#
#%%%%*%%%%#
#%%%%%%%%%#
#%%*%d%*%%#
#%%#%%%#%%#
#%%%%%%%%%#
#%%%***%%%#
#%%%%%%%%%#
#%g%%%%%@%#
#%%%%%%%%%#
###########

###########
#g#%*%#%%%#
#%%%*%#%d%#
###%%%#%%%#
#%%%%%#%%%#
#%*@%%#%%%#
###########

#%%%%d%%%%#
#%%%%%%%%%#
#%%%%.%%%%#
#%%%%%%%%%#
#%%%%.%%%%#
###########
#%%%%%%%%%#
#%@%%%%*%g#
#####%%%%%#
#%*%%%%*%%#
#%%%%%%%%%#
###########

(#####..#####
#...####...#
#.g......@.#
#%*%%%%%%%%#
#oo%%*%**%%#
#oo.%o%oo%%#
#*%.o%%o%o%#
#*..%..*%*%#
#%.....%.*.#
#....o...%.#
#....%.....#
#≈≈≈≈≈≈≈≈≈≈#
#≈≈≈≈≈≈≈≈≈≈#
############)

...........#
......%%%o%#
≈≈≈≈*%%%%%%#
≈≈≈%*%%%%%%#
≈≈%%%%o%%%%#
≈≈%%%%%%%%%#
≈%%%%%%%%%%#
≈≈*........#
≈≈####%%%%%#
≈%#%%#%%..%#
≈≈#%%#%%@g%#
############

..##########
..#........#
..#..g...@.#
..#%*%%*%%%#
..#%%o%%%o%#
###%%%%%o%%#
#≈#%%%%%%%%#
#≈≈≈≈≈≈≈%%%#
###≈≈*≈≈%%%#
###%%%%%%%%#
###%%%%%%%%#
############

############
#%%%%%%%%%%#
#%%%%o%%%%%#
###*o%%%%%*≈
#*###%*%%%%#
#%%%%%%%%%%#
#...%%%%%%%#
#r###o%%%o*#
###%%%oo.%%#
#.%%o%oo%..#
#%%%%.%%%%%#
#%%%%......#
#.....#%%%%#
#.g.@.#%%%%#
############

(###########
#%%%%%%%%%#
#%@%%%%%k%#
#%%%%%%%o%#
#%%%%%%%%%#
#%%.....%%#
#%#..%d.#%#
##%.....%##
#%%.....%%#
#%%%.%.%%%#
#%%%.#c#%%#
###########)

(##############
#%%%%%%%%%%%%#
#%%%%%%%%%%%%#
#%%%%%%%%%%%%#
#%%%%%%%g%%%%#
#%%.....%%%%%#
#%%.***.%%%%%#
#%%%*d*%%.%%%#
#%%.***.%%oo%#
#%%%###%%%oo%#
#%%%%%%%%%%%%#
#%@%%%%%%%%%%#
#%%%%%%%%%%%%#
##############)