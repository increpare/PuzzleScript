title Repel
author chaotic_iak
homepage chaosatthesky.wordpress.com

(Changelog
26-Jul-2014: Version 1.00, with 7 levels
26-Jul-2014: Version 1.01, implemented skip level
13-Jun-2015: Version 1.02, no automatic music and more readable messages
)

run_rules_on_level_start
norepeat_action
key_repeat_interval 0.15
again_interval 0.125

========
OBJECTS
========
background
#fff #eee
11111
10001
10001
10001
11111

bgu
#fff
..0..
.....
.....
.....
.....

bgd
#fff
.....
.....
.....
.....
..0..

bgl
#fff
.....
.....
0....
.....
.....

bgr
#fff
.....
.....
....0
.....
.....

wall
#222

player
#c00
.....
.000.
.000.
.000.
.....

stopper
#aaa
..0..
.000.
00000
.000.
..0..

exit
#c00
00000
0...0
0...0
0...0
00000

hint
#ccf
.....
.000.
.0.0.
.000.
.....

shard
#393 #4a4
.....
.01..
.001.
.000.
.....

noshard
#555 #666
.....
.01..
.001.
.000.
.....

emerald
#3c3 #4d4
.....
.011.
00011
.000.
..0..

shardblank
#000
.....
.00..
.000.
.000.
.....

emeraldblank
#000
.....
.000.
00000
.000.
..0..

mu
#33f #99f
.111.
.000.
.000.
.000.
.....

md
#33f #99f
.....
.000.
.000.
.000.
.111.

ml
#33f #99f
.....
1000.
1000.
1000.
.....

mr
#33f #99f
.....
.0001
.0001
.0001
.....

mh
#33f #99f
.....
10001
10001
10001
.....

mv
#33f #99f
.111.
.000.
.000.
.000.
.111.

bu
#333 #999
.111.
.000.
.000.
.000.
.....

bd
#333 #999
.....
.000.
.000.
.000.
.111.

bl
#333 #999
.....
1000.
1000.
1000.
.....

br
#333 #999
.....
.0001
.0001
.0001
.....

bh
#333 #999
.....
10001
10001
10001
.....

bv
#333 #999
.111.
.000.
.000.
.000.
.111.

d0
#ccc
..00.
.0..0
.0..0
.0..0
..00.

d1
#ccc
...0.
..00.
...0.
...0.
..000

d2
#ccc
.000.
....0
..00.
.0...
.0000

d3
#ccc
.000.
....0
..00.
....0
.000.

d4
#ccc
...0.
..00.
.0.0.
.0000
...0.

d5
#ccc
.0000
.0...
.000.
....0
.000.

d6
#ccc
..00.
.0...
.000.
.0..0
..00.

d7
#ccc
.0000
....0
...0.
..0..
..0..

d8
#ccc
..00.
.0..0
..00.
.0..0
..00.

d9
#ccc
..00.
.0..0
..000
....0
..00.

movingu
black

movingd
black

movingl
black

movingr
black

playertarget
black

removehints
black

skiplevel
black

hintcounter
black

startlevel
black

processing
black

processing2
black

pointer
black

justmoved
black

=======
LEGEND
=======
mov = mu or md or ml or mr or mh or mv
blo = bu or bd or bl or br or bh or bv
repu = mu or mv or bu or bv
repd = md or mv or bd or bv
repl = ml or mh or bl or bh
repr = mr or mh or br or bh
collectibles = shard or noshard or emerald
blank = shardblank or emeraldblank
object = mov or blo or player or wall or stopper or collectibles
repel = mov or blo
marker = startlevel or processing or processing2 or playertarget or pointer
movingmarker = movingu or movingd or movingl or movingr
digit = d0 or d1 or d2 or d3 or d4 or d5 or d6 or d7 or d8 or d9

. = background
# = wall
p = player and removehints and startlevel
o = exit
i = hint
` = shard
l = noshard
~ = emerald
w = mu
s = md
a = ml
d = mr
q = mv
e = mh
t = bu
g = bd
f = bl
h = br
r = bv
y = bh
0 = d0 and wall
1 = d1 and wall
2 = d2 and wall
3 = d3 and wall
4 = d4 and wall
5 = d5 and wall
6 = d6 and wall
7 = d7 and wall
8 = d8 and wall
9 = d9 and wall

=======
SOUNDS
=======
player cantmove 99427504
mov move 69999307
blo move 69999307
noshard destroy 22945100
shard destroy 5840900
emerald destroy 3463700
stopper create 22397106
stopper destroy 93130906
endlevel 57747503
sfx0 81274706
sfx1 7263306

================
COLLISIONLAYERS
================
startlevel, pointer, processing2
playertarget, removehints, skiplevel, hintcounter, processing
justmoved
movingu
movingd
movingl
movingr
background
bgu
bgd
bgl
bgr
exit, hint
stopper
wall, player, mov, blo
collectibles
digit, blank

======
RULES
======
(level start)
right [no wall|no wall] -> [bgr|bgl]
down [no wall|no wall] -> [bgd|bgu]
right [startlevel][digit|digit||] -> [startlevel][digit|digit||pointer]
right [startlevel][shard no processing][pointer|] -> [startlevel][shard processing][shardblank|pointer]
random right [shardblank|pointer|] -> [shardblank||pointer]
right [startlevel][emerald no processing][pointer|] -> [startlevel][emerald processing][emeraldblank|pointer]
right [startlevel][digit|digit] -> [startlevel][digit|digit hintcounter]

(mechanics)
[action player no stopper][stopper] -> cancel
right [action player no removehints no skiplevel][d0|d1] -> cancel
[action player stopper skiplevel] -> win
[action player stopper removehints][hint] -> [action player stopper removehints][]
[action player stopper removehints] -> [action player stopper skiplevel]
[action player stopper] -> [player]
[action player] -> [player stopper]
right [orthogonal player stopper][d0|d1] -> [orthogonal player][d0|d1]
[> player removehints] -> [> player]
[> player skiplevel] -> [> player]
[> player|] -> [> player|playertarget]
[> player|mov] -> [> player|> mov] again

right [|no playertarget repr|no playertarget repl|] -> [|movingl repr|movingr repl|] again
down  [|no playertarget repd|no playertarget repu|] -> [|movingu repd|movingd repu|] again
down  [repd|no playertarget repu|] -> [repd|movingd repu|] again
[movingl movingr] -> []
[movingu movingd] -> []
[movingu repel] -> [up repel]
[movingd repel] -> [down repel]
[movingl repel] -> [left repel]
[movingr repel] -> [right repel]
[> repel justmoved|no repel|< repel] -> [repel justmoved|no repel|< repel]
[> repel||< repel] -> [repel||repel]
[> repel no playertarget|stationary object] -> [repel|object]
[> repel|< repel] -> [repel|repel]
[> repel|perpendicular repel] -> [repel|perpendicular repel]
[> repel|perpendicular player] -> [repel|perpendicular player]
[> repel|playertarget] -> [repel|playertarget]

startloop
random [> repel no justmoved no processing|] -> [> repel processing|processing2]
[> repel justmoved|processing2] -> [repel justmoved|processing2]
[> repel no processing|processing2] -> [> repel|processing processing2]
[> repel|processing processing2] -> [repel|processing processing2]
[processing processing2] -> []
[processing2] -> []
endloop

startloop
random [> repel no processing|] -> [> repel processing|processing2]
[> repel no processing|processing2] -> [> repel|processing processing2]
[> repel|processing processing2] -> [repel|processing processing2]
[processing processing2] -> []
[processing2] -> []
endloop

[orthogonal object][justmoved] -> [orthogonal object][]
[orthogonal repel] -> [orthogonal repel orthogonal justmoved]

late right [player shard][no shardblank|shardblank] -> [player][|shard]
late right [player emerald][no emeraldblank|emeraldblank] -> [player][|emerald]
late [player collectibles] -> [player]

late [shard no wall][player exit] -> sfx1
late [player hint] -> sfx0

late right [player hint][d0|d1 hintcounter] -> message X TWICE BEFORE MOVING TO SKIP LEVEL
late right [player hint][d0|d1|hintcounter] -> message PRIMARY GOAL: GO TO RED SQUARE; SECONDARY GOAL: COLLECT DIAMONDS
late right [player hint][d0|d1||hintcounter] -> message ERROR: "RULES.TXT" DOES NOT EXIST

late right [player hint][d0|d2 hintcounter] -> message GREEN SHARDS REQUIRED, GRAY SHARDS OPTIONAL
late right [player hint][d0|d2|hintcounter] -> message PRESS X FOR PUT/TAKE STOPPER; YOU ONLY HAVE ONE

late right [player hint][d0|d5 hintcounter] -> message YOU DON'T NEED YOUR STOPPER

late right [player hint][hintcounter|] -> [player][|hintcounter]
late right [player hint] -> [player]
late [marker] -> []

==============
WINCONDITIONS
==============
all player on exit
no shardblank

=======
LEVELS
=======
###########
#..d......#
#..h..s.a##
#i#~###t..#
#.##.g....#
#i#o.t....#
#ip#.h....#
#######...#
01#########

############
#`..#.....##
#hfl...#.t~#
###.i.s...##
#o#`hf~r.###
#p.i.l#..###
#######t####
02##########

############
#.....###~##
#..#.t`..f.#
#...#......#
##w.#~g.w..#
#..s.#.....#
#s..#`##...#
#.#..d.fg..#
#to#.###...#
#p..d.f#...#
############
03##########

###############
#...l.p#...#`##
#.s#g.t.....f.#
#..ot#.#.d.#..#
######....#...#
##.......q.ghf#
##............#
#~tt##..f..#..#
##..##.#~#....#
###############
04#############

####################
##~ig..g#`#g...#~###
##..r....f`r.#..h.##
###hfa...##t#...f.##
##..t....#.h.ad...##
#...#.....#..#.....#
#.#....g.........#.#
#..####.####.####..#
#h.e.y.y.y.y.y.ep.f#
##.####.####.####.##
##................##
######....w...######
#########..##..o####
05##################

###################
#.g...#......#...##
#.t...d......#..to#
#.p#hfl.flshf##..##
#.g###.f.......f.##
#.t####~#.t.....###
###################
06#################

###################
#.....#......#~####
#.a~..#....a..h..##
###.###.###.....go#
#..f.#....#.....f.#
#....g........f..##
#.pd.t..lhf###.####
#....f..gq..#....##
#...##..t...#....##
#########..####..##
07#################
