realtime_interval 0.25
throttle_movement 0.5
title Minecraft
author Mojang
homepage www.puzzlescript.net
background_color #593D29
text_color gray
zoomscreen 17x17
noundo
youtube Gpd85y_iTxY
require_player_movement

========
OBJECTS
========

Background 
#69BA30 #5DB22A #6EC33B
01001
02010
10102
02010
10100

Sand
#EEE59D #F3E9A2 #E4DB97 #E4DB97
10130
02010
30101
01021
20010  

WoodPlanks 
#68512F #B4905A #BC9660 #AA8A54
32112
21221
00000
11123
23212

Stone
#747474 #6D6D6D #838383 #7A7A7A
01220
20333
00131
02201
03332

Player 
#261B0B #171354 #BC8567 white
00000
02220
22222
31213
22222

Skeleton
(shoots arrows)
#C9C9C9 #878787 #0A0A0A
11111
10001
00000
22022
00000

Target
(Looks out for the player and fires arrows)
black
.....
.....
.....
.....
.....

LeftArrow
(Arrow going Left)
#896430 #C0C0C0 #E2E2E2 #9E9E9E
.....
.2.11
2100.
.3.11
.....

RightArrow
(Arrow going Right)
#896430 #C0C0C0 #E2E2E2 #9E9E9E
.....
11.2.
.0012
11.3.
.....

DownArrow
(Arrow going Down)
#896430 #C0C0C0 #E2E2E2 #9E9E9E
.1.1.
.101.
..0..
.213.
..2..

UpArrow
(Arrow going Up)
#896430 #C0C0C0 #E2E2E2 #9E9E9E
..2..
.213.
..0..
.101.
.1.1.

Zombie
(follows players)
#2A5B11 #0A0A0A #73D63E
00000
02220
22222
11211
22222

Spider
(goes randomly until they encounter a player)
#D50E0E #0C0907 #51473E #554B41 #4B423A #433B33
23454
03420
20405
32354
31315

Creeper
(Blows up near players)
#0A0A0A #2BD823 #0CB700 #87E580
13112
00100
21031
10002
30101

Enderman
(kills on contact if player goes into its range of sight)
#F7DDFD #E580FC black #161616
32223
23232
22322
01310
23332

EnderParticle
(Effect for when Endermen teleport)
#E580FC
....0
0....
..00.
.0.0.
..0..

Blank
(Blank object for particles)
Black
.....
.....
.....
.....
.....

=======
LEGEND
=======

. = Background
- = Sand
, = Stone
# = WoodPlanks
P = Player
A = leftarrow
S = Skeleton
Z = Zombie
Enemy = Spider or Zombie or Skeleton
Seeker = Spider or Zombie or Creeper
R = Spider
E = Enderman
C = Creeper
Object = WoodPlanks or Player or Skeleton or Zombie or Spider or Enderman or Creeper
Arrow = LeftArrow or RightArrow or DownArrow or UpArrow
EnderParticles = Enderparticle or Blank or Blank or Blank 


=======
SOUNDS
=======

SFX0 93448108
Player Move 96363907


(Slime? 65441108)

SFX2 84781301

SFX1 11071108

Arrow create 38215704

(75296308)

(Spider 14988902)

================
COLLISIONLAYERS
================

Background
Sand, Stone
Target
Player, WoodPlanks, Spider, Skeleton, Zombie, Enderman, Enemy, Creeper
LeftArrow, RightArrow, UpArrow, DownArrow
EnderParticle, Blank

======
RULES     
======     

[ Seeker |         stationary Player ] -> [ > Seeker |         Player ]
[ Seeker ||        stationary Player ] -> [ > Seeker ||        Player ]
[ Seeker |||       stationary Player ] -> [ > Seeker |||       Player ]
[ Seeker ||||      stationary Player ] -> [ > Seeker ||||      Player ]
[ Seeker |||||     stationary Player ] -> [ > Seeker |||||     Player ]
[ Seeker ||||||    stationary Player ] -> [ > Seeker ||||||    Player ]
[ Seeker |||||||   stationary Player ] -> [ > Seeker |||||||   Player ]
[ Seeker ||||||||  stationary Player ] -> [ > Seeker ||||||||  Player ]
[ Seeker ||||||||| stationary Player ] -> [ > Seeker ||||||||| Player ]

[ Player | Enemy ] -> [ No Player | Enemy] SFX2
[ > Arrow | Player ] -> [ Arrow | No Player ]

random [stationary Player] [ Spider ] -> [stationary Player] [ > Spider ]


[ LeftArrow ] -> [ left LeftArrow ]
[ RightArrow ] -> [ Right RightArrow ]
[ DownArrow ] -> [ Down DownArrow ]
[ UpArrow ] -> [ Up UpArrow ]


[ > Arrow | Object ] -> [ No Arrow | Object ]
[ Player | No Object ] -> [ Player | Target ]
[ Player |...| Target | No Object ] -> [ Player |...| Target | Target]
(late [ Target ] [ Arrow ] -> [ No Target ] [ Arrow ])




right [ Player |...| Target | Skeleton ]->[ Player |...| LeftArrow  | Skeleton ]
left  [ Player |...| Target | Skeleton ]->[ Player |...| RightArrow | Skeleton ]
up    [ Player |...| Target | Skeleton ]->[ Player |...| DownArrow  | Skeleton ]
down  [ Player |...| Target | Skeleton ]->[ Player |...| UpArrow    | Skeleton ]


late [ Player |...| Enderman ] -> [ Enderman |...| EnderParticle ] SFX0
[ Enderman | no Object ] -> [ Enderman | random EnderParticles ]

[ Creeper || Player ] -> [ No Creeper || No Player ] SFX1
[ Creeper | Player ] -> [ No Creeper | No Player ] SFX1

late [ Enderparticles ] -> [ Enderparticles ]

==============
WINCONDITIONS
==============

=======     
LEVELS
=======

.................................
.............................e...
..z..............................
.............s...................
.................................
.................................
.....................r...........
.................................
.................................
.................................
.................................
.................................
-................................
-................................
-................................
-................................
-................................
-................................
--...............................
--...............................
--...............................
--...............................
--..p.................#######....
---...................#.....#....
----....---...........#.....#....
------------..........#.....#....
-------------.........###.###....
--------------...................
----------------...-----......c..
----------------------------.....
---------------------------------


