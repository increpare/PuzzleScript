title IDIBAW
author TheGreatEscaper
run_rules_on_level_start
========
OBJECTS
========

Background
WHITE

Temp
White

tempv
White

tempv2
purple
.....
..0..
..0..
..0..
.....

temph
White

temph2
purple
.....
.....
.000.
.....
.....

Exit
Black LIGHTBLUE
.111.
.111.
11111
.111.
11111

Wall
DarkGray

Hole
Black

Fear
Darkgray
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0


Player
Black DARKGRAY Grey Lightgray
.111.
.101.
11111
.333.
22222

memory
pink purple green yellow red orange blue
04153
62341
45602
31256
56034




Love
Pink
.000.
00000
00000
00000
.000.

Despair
Purple
.000.
00000
00000
00000
.000.

Dead
black grey
.000.
01010
00100
01010
.000.

drop
white
.....
.....
.....
.....
.....



=======
LEGEND
=======

. = Background
O = Hole
P = Player
X = Exit
# = Wall
L = love
F = fear
Q = Hole and Fear
D = Despair
U = love and Hole


M = memory

tempd = tempv or temph
moveable = love or despair
obstacle = Wall or exit or despair or dead or memory
=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background
hole
temp
tempv
temph
drop
tempv2
temph2
Exit
Player, Wall, Love, dead, despair memory
Fear

======
RULES
======



(player can't move into stuff)
[> player|hole] -> cancel
[> player|fear] -> cancel
[> player|dead] -> cancel
[> player|memory] -> cancel

(blocks can't move into stuff)
[> moveable| wall] -> cancel
[> moveable| exit] -> cancel

(player can push blocks)
[> player| moveable] -> [> player | > moveable]


(how love works, idk really)
startloop
+ rigid [ moving love| love ] -> [ moving love | moving love ]
endloop

(how despair works)
[temph] -> [no temph]
[tempv] -> [no tempv]
startloop
late [despair] -> [despair temph tempv]
late horizontal [temph|no temph no moveable no obstacle no love] -> [temph| temph]
late vertical [tempv|no tempv no moveable no obstacle no love] -> [tempv|tempv]
endloop
late horizontal [temph|love] -> [temph| no love dead]
late vertical [tempv|love] -> [tempv| no love dead]
(clear thingos)
late [temph] -> [no temph]
late [tempv] -> [no tempv]

(how love drops, idk really)
startloop
late [love hole] -> [love hole drop]
endloop
startloop
late [love no drop|love drop] -> [love no drop| love no drop]
endloop
late [drop] -> [no moveable no drop no hole]


(visual cue)
[temph2] -> [no temph2]
[tempv2] -> [no tempv2]
startloop
late [despair] -> [despair temph2 tempv2]
late horizontal [temph2|no temph2 no moveable no obstacle no love] -> [temph2| temph2]
late vertical [tempv2|no tempv2 no moveable no obstacle no love] -> [tempv2|tempv2]
endloop

(not multiban, also things can't push love)
[ > Moveable no Player | Obstacle] -> cancel
[ > Moveable no player no love| love] -> cancel





(skip level)
[ action player no temp ] -> [temp player]
[ action player temp ] -> [ player exit no temp]
late [temp no player] -> [no temp]
==============
WINCONDITIONS
==============

All Player on Exit

=======
LEVELS
=======

OOOOOOOOOOO
OOOO...OOOO
OO.......OO
O...OO...OO
OXOOOOO...O
OOOOOOO...O
OPOOOOO...O
O...OO...OO
OO.......OO
OOOO...OOOO
OOOOOOOOOOO

message IDIBAW

message a game about stuff

message and some other things

message LOVE I

######
X..OO#
###O.#
###O.#
#.L..#
#..L.#
#P...#
######

##########
X.OOOO.L.P
#.OOO....#
#OOOO.L..#
#OO.L....#
#OO......#
##########


(#########
#.......P
#.L.L...#
#.....###
#.L.L.###
#.....###
####OO###
####OOO.#
####OOO.X
#########)

(##########
#OO.....O#
#OOO.L.L.P
#.OOO.L..#
#.LOOO.L.#
#..LOOO..#
#O...OOO.#
#OO...OOO#
#OOO...OO#
##X#######)

#########
#.......P
#..L.L..#
#.L.L.L.#
#..L.L..#
#.......#
####OO###
####OO###
#.OOOO###
X.OOOO###
#########

#########
#.......P
#L.L.L..#
#.L.L.L.#
#.......#
####OOO##
####OOO##
#.OOOOO##
#.OOOOO##
X.OOOOO##
#########

(#P#########
#.......###
#L.L.L.L###
#.L.L.L..##
#.....#..##
####OO#.###
####OO#.###
#.OOOO..###
X.OOOO#####
###########)


(##########
#........P
#.######.#
#..#####.#
#...####.#
#.L.L###.#
#L.L..##.#
#.L.L..#.#
##.OO....#
###OO####L
#OOOOOO..L
L.OOOO..LL
L..OO...LL
LL.....LLL
LLLLXLLLLL
LLLLLLLLLL)

OOOOOOOOOOO
OOOO...OOOO
OO.......OO
OX..OO...OO
OLOOOOO...O
OOOOOOO...O
OPOOOOO...O
O...OO...OO
OO.......OO
OOOO...OOOO
OOOOOOOOOOO

message FEAR I

(###########
###O..L####
X..O.F.L.P#
###O.L.####
###########)

##############
#####........#
#####.L.L.L..P
#####..L.L.L.#
#.....L.L.L..#
#............#
#OQ###########
#OO###########
#OOOO........#
#OOOO........X
##############



OOOOOOOOOOO
OOOO...OOOO
OO.......OO
OFX.OO...OO
OLOOOOO...O
OOOOOOO...O
OPOOOOO...O
O...OO...OO
OO.......OO
OOOO...OOOO
OOOOOOOOOOO



message LOVE II

#########
###...###
###.L.###
X.L..L..P
###.L..##
####..###
#########

OOOOOOOOOOO
OOOO...OOOO
OOX......OO
OFL.OO...OO
OLOOOOO...O
OOOOOOO...O
OPOOOOO...O
O...OO...OO
OO.......OO
OOOO...OOOO
OOOOOOOOOOO

message DESPAIR I

#############
####......###
####......###
####.LLL..###
####..LL..###
####....D.###
###........P#
X.OO.########
#############

#P#############X#
#..###...#####OO#
#..###.#.#####OO#
#......O......OO#
##..LL.O.......##
##.L...O.......##
##.L.LLO.......##
##.....O.......##
###########D#####
#################

message MEMORY VERSIONS (might belong to level set of remixed levels)

(#P############X#
M.MMMMMMMMMMMOOM
#..###...####OO#
#..###.#.####OO#
#......O.....OO#
##..LL.O......##
##.L...O......##
##.L.LLO......##
##.....O......##
###########D####
################)

#P#############MMM
#..###...####OOOOM
#..###.#.####OOOOX
#......O.....OOOOM
##..LL.O......#MMM
##.L...O......#MMM
##.L.LLO....MM#MMM
##..MM.O....MM#MMM
####MM#####D###MMM
###############MMM

MPMMMMMMMMMMMMMXM
M..MMMMMMMMMMMOOM
M.DMMMMMMMMMMMOOM
#..###...#####OO#
#..###.#.#####OO#
#......O......OO#
##..LL.O.......##
##.L...O.......##
##.L.LLO.......##
##.....O.......##
##########MLM####
###########M#####


OOOOOOOOOOO
OOOO...OOOO
OOD......OO
OFLXOO...OO
OLOOOOO...O
OOOOOOO...O
OPOOOOO...O
O...OO...OO
OO.......OO
OOOO...OOOO
OOOOOOOOOOO