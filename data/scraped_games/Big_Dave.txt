title Big Dave
author HugoBDesigner
homepage hugobdesigner.blogspot.com
run_rules_on_level_start
noaction

========
OBJECTS
========

Void
Transparent

Background
Transparent

BackTexture
DarkBlue

Wall
#606060 #404040
00111
11001
01110
11000
00101

PlayerT
#804000 #FFC080 #004080
.000.
11111
00100
12121
11111

PlayerL
#FFC080 #408000 #800000
.0...
.0.12
00112
00112
001.2

PlayerC
#800000 #408000 #C0FF80
00100
01210
02220
01210
01110

PlayerR
#FFC080 #800000 #408000
...0.
12.0.
12200
12200
1.200

PlayerB
#004080 #804000
00000
00.00
00.00
00.00
11.11


Crate
DarkBrown Orange
00000
01110
01110
01110
00000

Target
Yellow Pink Blue
0...0
..1..
.121.
..1..
0...0

CrateTarget
Orange DarkBrown Blue
01110
12221
12221
12221
01110

CantMoveUp
Transparent

CantMoveDown
Transparent

CantMoveLeft
Transparent

CantMoveRight
Transparent

Marker1
Transparent

Marker2
Transparent

=======
LEGEND
=======

Player = PlayerT OR PlayerL OR PlayerC OR PlayerR OR PlayerB
CantMove = CantMoveUp OR CantMoveDown OR CantMoveLeft OR CantMoveRight
Collidable = Wall
Pushable = Crate OR CrateTarget
Marker = Marker1 OR Marker2


x = Background AND Void
. = Background
# = Wall
P = PlayerC
* = Crate
@ = Crate AND Target
O = Target

=======
SOUNDS
=======
EndLevel 81640500
SFX0 51970907 (Regular walk)
SFX1 15917907 (Push walk)
SFX2 25755107 (Multiple push walk)

================
COLLISIONLAYERS
================

Background
BackTexture, Void
Target
PlayerT, PlayerL, PlayerC, PlayerR, PlayerB, Wall, Crate, CrateTarget
CantMoveUp
CantMoveDown
CantMoveLeft
CantMoveRight
Marker1
Marker2

======
RULES
======
(Reset some stuff)
[CantMove] -> []
[Marker] -> []
[CrateTarget] -> [Crate Target]
[No Void No BackTexture] -> [BackTexture]

(Build the player)
UP[PlayerC | NO PlayerT] -> [PlayerC | PlayerT]
LEFT[PlayerC | NO PlayerL] -> [PlayerC | PlayerL]
DOWN[PlayerC | NO PlayerB] -> [PlayerC | PlayerB]
RIGHT[PlayerC | NO PlayerR] -> [PlayerC | PlayerR]


(Predetermines if objects can be pushed in any given direction)
UP[Pushable | Collidable] -> [Pushable CantMoveUp | Collidable]
DOWN[Pushable | Collidable] -> [Pushable CantMoveDown | Collidable]
LEFT[Pushable | Collidable] -> [Pushable CantMoveLeft | Collidable]
RIGHT[Pushable | Collidable] -> [Pushable CantMoveRight | Collidable]
startloop
	UP[Pushable | CantMoveUp] -> [Pushable CantMoveUp | CantMoveUp]
	DOWN[Pushable | CantMoveDown] -> [Pushable CantMoveDown | CantMoveDown]
	LEFT[Pushable | CantMoveLeft] -> [Pushable CantMoveLeft | CantMoveLeft]
	RIGHT[Pushable | CantMoveRight] -> [Pushable CantMoveRight | CantMoveRight]
endloop

UP[> Player | CantMoveUp] -> [Player CantMoveUp | CantMoveUp]
DOWN[> Player | CantMoveDown] -> [Player CantMoveDown | CantMoveDown]
LEFT[> Player | CantMoveLeft] -> [Player CantMoveLeft | CantMoveLeft]
RIGHT[> Player | CantMoveRight] -> [Player CantMoveRight | CantMoveRight]

[MOVING Player | STATIONARY Player] -> [MOVING Player | MOVING Player]
[> Player | Collidable] -> [Player CantMoveUp | Collidable]
[Player CantMove | Player] -> [Player CantMove | Player CantMoveUp]
[MOVING Player CantMove] -> cancel

(Handle Movement)
[> Player | Pushable] -> [> Player | > Pushable]
[> Pushable | Pushable] -> [> Pushable | > Pushable]

(Sound effects)
[> Pushable | > Pushable] -> [> Pushable Marker2 | > Pushable Marker2]
[> Player | Pushable] -> [> Player Marker1 | Pushable]
[> Player | Pushable Marker2] -> [> Player Marker2 | Pushable Marker2]
[Player Marker1 | Player] -> [Player Marker1 | Player Marker1]
[Player Marker2 | Player] -> [Player Marker2 | Player Marker2]

[> Player No Marker] -> [> Player]SFX0 (Regular walk)
[> Player Marker1 No Marker2] -> [> Player Marker1]SFX1 (Push walk)
[> Player Marker1 Marker2] -> [> Player Marker1 Marker2]SFX2 (Multiple push walk)

(Endgame)
LATE[Crate Target] -> [CrateTarget]


==============
WINCONDITIONS
==============

ALL Target ON Crate

=======
LEVELS
=======

message 'Tis but a proof o' concept

xxx#######xx
####.....###
#..........#
#.....**...#
#..p..**...#
#..........#
##........##
x#######.##x
xxxxxx#...#x
xxxxxx#...#x
xxxxxx#...#x
xxxxxx#.o.#x
xxxxxx#####x
