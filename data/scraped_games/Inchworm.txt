title Inchworm
author Tobin Mollett
homepage www.puzzlescript.net

again_interval 0.1
run_rules_on_level_start

========
OBJECTS
========

Background
orange
00000
00000
00000
00000
00000

Day
lightblue
00000
00000
00000
00000
00000

Night
black
00000
00000
00000
00000
00000

Sun_Home
black
.....
.....
.....
.....
.....

Moon_Home
black
.....
.....
.....
.....
.....

Sun
yellow
.000.
00000
00000
00000
.000.

Moon
white
..00.
...00
0..00
00000
.000.

Player
pink
..0..
.000.
.000.
00.00
00.00

Worm_Left
pink
.....
.....
...00
00000
000..

Worm_Right
pink
.....
.....
00...
00000
..000

Exit
darkblue blue
.....
1...1
01.10
00100
.100.

Earth
brown
00000
00000
00000
00000
00000

Wall
purple darkblue
00100
11011
00100
00100
11011

Rock
grey
.000.
00000
00000
00000
.000.

Seed
lightbrown brown
000..
0100.
00100
.0010
..000

Seedling
lightgreen
.....
.....
.....
.0.0.
..0..

Plant_Leaf
lightgreen green
.0000
0000.
0000.
.111.
..1..

Plant_Stalk
green
..0..
..000
..00.
000..
.00..

Spore
red
.....
.....
.....
..0..
.000.

Mushroom_Cap
red white grey lightgrey
.000.
00010
01000
0.2.0
..3..

Mushroom_Stalk
lightgrey
..0..
..0..
.000.
.000.
00000

mark
black
.....
.....
.....
.....
.....


=======
LEGEND
=======

. = Day
S = Sun and Day
# = Wall
% = Earth
P = Player and Day
t = Seedling and Day
m = Spore and Day
o = Rock and day
* = seed and day
x = Exit and day

Weight = Player or rock or seed
Obstacle = Wall or Mushroom_Cap or Mushroom_Stalk or Plant_Leaf or Plant_Stalk or Weight or Earth

=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background
Day, Night
Sun, Moon
Seedling, Spore, Exit
Player, Worm_Left, Worm_Right, Wall, Mushroom_Cap, Mushroom_Stalk, Plant_Leaf, Plant_Stalk, Rock, seed, Earth
mark, Sun_Home, Moon_home

======
RULES
======

( Player can't go up )

vertical [ > Player ] -> cancel

( Player Animation )

[ Worm_Left mark | Worm_Right ] -> [ Player | ]
[ Worm_Left | Worm_Right mark ] -> [ | Player ]

right [ > Player | no obstacle ] -> [ Worm_Left | Worm_Right mark ] again
left [ > Player | no Obstacle ] -> [ Worm_Right | Worm_Left mark ] again
right [ > Player | weight | no obstacle ] -> [ Worm_Left | Worm_Right mark | weight ] again
left [ > Player | weight | no obstacle ] -> [ Worm_Right | Worm_Left mark | weight ] again

( Gravity )

down [ stationary weight ] -> [ > Weight ] again

( Day to Night )

down [ Action Player ] [ Sun ] -> [ Player ] [ Sun Moon_home ] 
down [ Action Player ] [ Moon ] -> [ Player ] [ Moon Sun_Home ] 

down [ Sun ] [ Moon_Home ] -> [ > Sun ] [ Moon_Home ] again
down [ Moon ] [ Sun_Home ] -> [ > Moon ] [ Sun_Home ] again

up [ Sun no Sun_Home ] [ Sun_Home ] -> [ > Sun ] [ Sun_Home ] again
up [ Moon no Moon_Home ] [ Moon_Home ] -> [ > Moon ] [ Moon_Home ] again

( Seedlings and Mushrooms grow and shrink )
late up [ no Mushroom_Stalk | Night Mushroom_Cap | Weight | ] -> [ | Night Mushroom_Stalk | Mushroom_Cap | Weight ]
late up [ no Mushroom_Stalk | Night Mushroom_Cap | ] -> [ | Night Mushroom_Stalk | Mushroom_Cap ]
late up [ Spore Night Weight | ] -> [ Mushroom_Cap Night | Weight ] again
late [ Spore Night ] -> [ Mushroom_Cap Night ] again

late up [ no Plant_Stalk | Day Plant_Leaf | Weight | ] -> [ | Day Plant_Stalk | Plant_Leaf | Weight ]
late up [ no Plant_Stalk | Day Plant_Leaf | ] -> [ | Day Plant_Stalk | Plant_Leaf ]
late up [ Seedling Day Weight | ] -> [ Plant_Leaf Day | Weight ] again
late [ Seedling Day ] -> [ Plant_Leaf Day ] again

late down [ Plant_Leaf Night | no Plant_Stalk ] -> [ Seedling Night | ]
late down [ Plant_Leaf Night | Plant_Stalk ] -> [ Night | Plant_Leaf ]

late down [ Mushroom_Cap Day | no Mushroom_Stalk ] -> [ Spore Day | ]
late down [ Mushroom_Cap Day | Mushroom_Stalk ] -> [ Day | Mushroom_Cap ]

( seeds turn into seedlings)

late down [ Seed | Earth ] -> [ Seedling | Earth ]

( Sun and Moon switch places out of sight )

late [ Sun Wall ] [ Day ] -> [ Sun Wall ] [ Night ]
late [ Moon Wall ] [ Night ] -> [ Moon Wall ] [ Day ]

late [ Sun Wall ] -> [ Sun Wall mark ]
late [ Moon Wall ] -> [ Moon Wall mark ]
late [ Sun mark ] -> [ Moon ]
late [ Moon mark ] -> [ Sun ]

==============
WINCONDITIONS
==============
 all exit on Player

=======
LEVELS
=======

message Move left and right with the arrow keys. Press X or Space to wait.

...........
...........
...*...s...
..##.......
.t.p......x
%%%##....##
####.m...##
########%##