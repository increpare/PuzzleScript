title Rush Hour
author Hannes Petri

========
OBJECTS
========

Background
DARKBLUE

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black
..0..
..0..
00.00
..0..
..0..

CarLeft
PINK PURPLE
.1111
10000
10000
10000
.1111

CarRight
PINK PURPLE
1111.
00001
00001
00001
1111.

CarTop
PINK PURPLE
.111.
10001
10001
10001
10001

CarBottom
PINK PURPLE
10001
10001
10001
10001
.111.

CarMiddleV
PINK PURPLE
10001
10001
10001
10001
10001

CarMiddleH
PINK PURPLE
11111
00000
00000
00000
11111

Target
RED YELLOW BLUE
.000.
01110
01210
01110
.000.

Movement
TRANSPARENT

=======
LEGEND
=======

. = Background
# = Wall
+ = Player
O = Target

n = CarTop
u = CarBottom
| = CarMiddleV
[ = CarLeft
] = CarRight
- = CarMiddleH

CarV = CarTop OR CarBottom OR CarMiddleV
CarH = CarLeft OR CarRight OR CarMiddleH
Car = CarV OR CarH
Obstacle = Car OR Wall
CarEnd = CarTop OR CarBottom OR CarLeft OR CarRight

=======
SOUNDS
=======
================
COLLISIONLAYERS
================

Background
Target
Car
Movement
Player, Wall

======
RULES
======

( Add movement tag to the targeted end piece. )
DOWN [ ACTION Player CarBottom | NO Obstacle ] -> [ DOWN Player CarBottom Movement |]
UP [ ACTION Player CarTop | NO Obstacle ] -> [ UP Player CarTop Movement |]
LEFT [ ACTION Player CarLeft | NO Obstacle ] -> [ LEFT Player CarLeft Movement |]
RIGHT [ ACTION Player CarRight | NO Obstacle ] -> [ RIGHT Player CarRight Movement |]

( Cause piece with movement tag to actually move. )
[ MOVING Player Car Movement ] -> [ MOVING Player MOVING Car ]

( Move the rest of the car along with the end piece. Make sure adjacent end pieces don't stick to each other, unless they belong to the same car (i.e. two-length cars))

VERTICAL [ MOVING CarV Player | CarV ] -> [ MOVING CarV Player | MOVING CarV ]
VERTICAL [ MOVING CarV | CarV NO CarEnd ] -> [ MOVING CarV | MOVING CarV ]
VERTICAL [ MOVING CarV NO CarEnd | CarV ] -> [ MOVING CarV | MOVING CarV ]

HORIZONTAL [ MOVING CarH Player | CarH ] -> [ MOVING CarH Player | Moving CarH ]
HORIZONTAL [ MOVING CarH | CarH NO CarEnd ] -> [ MOVING CarH | MOVING CarH ]
HORIZONTAL [ MOVING CarH NO CarEnd | CarH ] -> [ MOVING CarH | MOVING CarH ]


==============
WINCONDITIONS
==============

SOME Car ON Target

======= 
LEVELS
=======




########...
#[]n..n#...
#n.u[]|#...
#u[]n.u...O
#[].|..#...
#...u[]#...
#[]...+#...
########...
