title dungeons
author -2016-
flickscreen 13x7

========
OBJECTS
========

Background
#BDBDBD #D8D8D8 #F2F2F2
22221
21110
21110
21110
10000

Background2
#ACA063 #C7B972 #DACB7C
22221
21110
21110
21110
10000

Decoground
#BDBDBD #D8D8D8 #F2F2F2 #70A175
22231
31113
23310
21130
10030

Decoground2
#ACA063 #C7B972 #DACB7C #9F9459
22121
21010
21003
21010
10000

Decoground2b
#ACA063 #C7B972 #DACB7C #9F9459
22121
21010
10003
21110
10000

WallBg
#000000
00000
00000
00000
00000
00000

FakeWall
#000000 #1C1C1C #424242
22221
01100
20010
21010
10000

DecogroundR
#BDBDBD #D8D8D8 #F2F2F2 #B74227
22231
31113
23310
21130
10030

Target1
Black #AB7126
.....
.111.
.111.
.111.
.....

Target2
#265C87
.....
.000.
.000.
.000.
.....

Target3
#6E2F41 Black
.....
.000.
.000.
.000.
.....

TargetR
#8E2E16
.....
.000.
.000.
.000.
.....

Wall
#000000 #1C1C1C #424242
22221
21110
21110
21110
10000

Wall2
#4C4937 #66614B #7F7A60
22221
21110
21110
21110
10000

FakeWall2
#4C4937 #66614B #7F7A60
22221
01100
20010
21010
10000

Player
Black #3BB253 White #49D565
.111.
12321
10301
13331
.111.

Crate1
#CA852C #FFAA3A #E89B35
11112
12220
12.20
12220
20000

Crate2
#3E97E0 #5FB8FF #3683C1
11110
10002
10.02
10002
02222

IceCrate
#8F4CE6 #7D44C8 #6231A1
00001
01112
01112
01112
12222

CrateG 
#664635 #50372A #7A543F
22220
21.01
2.0.1
20.21
01111

CrateR
#C6411F #F44C23 #E0451E
11112
12220
12.20
12220
20000

Water
#2955CE #3267F8 #4873E9
01101
20110
20102
01010
11001


HoleFilled
#6B5549 #5C4C43 #7E6457 #2955CE #3267F8
22220
21301
24041
20421
01111

CrateH
#B34B68 #8C3D54 #D55378
.222.
20001
2...1
20001
.111.

CrateV
#B34B68 #8C3D54 #D55378
.222.
20.01
20.01
20.01
.111.

TeleC
#3FE1A6 #39C592 #32AB7F
.000.
01.12
0...2
01.12
.222.

DarkPlayer
Black #C65151 White #AE4949
.111.
12321
10301
13331
.111.

Switch
#9AEC77 #BCFF9F #73B059 Black
11110
10002
10302
10002
02222

Switch2
#9AEC77 #BCFF9F #73B059 Black
11110
10002
10002
10032
02222

Switch3
#9AEC77 #BCFF9F #73B059 Black
11110
13002
10002
10002
02222

Starget
Black #557A45
.....
.111.
.101.
.111.
.....

CrateDuo
#E5E043 #FFF94B #D7D23F Black
11110
13332
13.32
13332
02222




CrateOne
#E5E043 #FFF94B #D7D23F Black
11110
10302
13.32
10302
02222



CrateLock
#E5E043 #FFF94B #D7D23F Black
11110
10002
10.02
10002
02222



TargetLock
#B0AC35
.....
.000.
.000.
.000.
.....

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate1
@ = Crate1 and Target1
O = Target1
e = Crate2
d = Crate2 and Target2
f = Target2
i = IceCrate
x = Water
G = CrateG
y = HoleFilled
+ = CrateH
- = CrateV
l = Target3
, = CrateH and Target3
; = CrateV and Target3
t = TeleC
/ = Crate1 and Target2
\ = Crate2 and Target1
° = WallBg
! = Decoground
ß = DarkPlayer
1 = Switch
2 = Switch2
3 = Switch3
s = Starget
~ = DecogroundR
' = FakeWall
r = CrateR
€ = targetR
4 = CrateR and targetR
9 = CrateDuo
8 = CrateOne
7 = CrateLock
5 = TargetLock
6 = TargetLock and CrateDuo
k = TargetLock and CrateOne
a = Background2
b = Decoground2
c = Decoground2b
h = Wall2
j = FakeWall2 and Background2
m = Crate1 and Background2
n = Crate2 and Background2
0 = CrateR and Background2
q = CrateDuo and Background2
[ = CrateOne and Background2
u = CrateH and Background2
] = CrateV and Background2 
w =IceCrate and Background2
z = TeleC and Background2
{ = Player and Background2
} = DarkPlayer and Background2
´ = CrateG and Background2
" = Switch and Background2
§ = Switch2 and Background2
$ = Switch3 and Background2
obstacle = Player or Wall or Wall2 or FakeWall or FakeWall2 or Crate1 or Crate2 or IceCrate or Water or CrateG or CrateH or CrateV or TeleC or WallBg or DarkPlayer or Switch or Switch2 or Switch3 or CrateR or CrateDuo or CrateOne or CrateLock
SOUNDS
=======

Crate1 MOVE 36772507
Crate2 MOVE 36772507
CrateH MOVE 36772507
CrateV MOVE 36772507
CrateG MOVE 36772507
CrateR MOVE 36772507
IceCrate MOVE 36772507
Switch MOVE 36772507
Switch2 MOVE 36772507
Switch3 MOVE 36772507
endlevel 83744503
startgame 92244503

================
COLLISIONLAYERS
================

Background, 
Target1,Target2,HoleFilled,Target3, TargetR, Decoground, Background2, Starget, TargetLock, DecoGroundR, Background2, Decoground2, Decoground2b
Player, Wall, Wall2, FakeWall, FakeWall2 Crate1,Crate2,IceCrate,Water,CrateG,CrateH,CrateV,TeleC,WallBg,DarkPlayer,Switch,Switch2,Switch3,CrateR,CrateDuo,CrateOne,CrateLock

======
RULES
======

[ >  Player | Crate1 ] -> [  >  Player | > Crate1  ]
[ >  Player | Crate2 ] -> [  >  Player | > Crate2  ]
[ >  Player | IceCrate ] -> [  >  Player | > iceCrate  ]
[ >  Player | CrateG ] -> [  >  Player | > CrateG  ]
[ <  Player | CrateR ] -> [  <  Player | < CrateR  ]

late[ IceCrate | IceCrate ] -> [  |  ]
[ > CrateG | Water ] -> [ | HoleFilled ]

Horizontal [ > Player | CrateH ] -> [ > Player | > CrateH ]
Vertical [ > Player | CrateV ] -> [ > Player | > CrateV ]

[> Player | TeleC | no obstacle ] -> [  | TeleC | Player]

[> Player | IceCrate | WallBg ] -> [ | Player | WallBg]
[> DarkPlayer | WallBg ] -> [ | WallBg]
[> Player | Crate1 | WallBg ] -> [ | Player | WallBg]
[> Player | Crate2 | WallBg ] -> [ | Player | WallBg]

[> Player ] [DarkPlayer] -> [ > Player ] [> Darkplayer ]

[ >  Player | Switch | no obstacle ] -> [  | Player | Switch2  ]
[ >  Player | Switch2 | no obstacle ] -> [  | Player | Switch3  ]
[ >  Player | Switch3 | no obstacle ] -> [  | Player | Switch  ]

[ > Player | CrateOne | no obstacle ] -> [ | Player | CrateLock ]
[ > Player | CrateDuo | no obstacle ] -> [ |Player | CrateOne ]
[ > Player | FakeWall ] -> [ > Player | ]
[ > Player | FakeWall2 ] -> [ > Player | ]
==============
WINCONDITIONS
==============

All Target1 on Crate1
All Target2 on Crate2
All TargetR on CrateR
No IceCrate
All CrateH on Target3
All CrateV on Target3
All STarget on Switch
No Switch2
No Switch3
All TargetLock on CrateLock

=======
LEVELS
=======
message 1/2 - BLACK & WHITE DUNGEON

#''#######################
#€...#~.##.!##..!.#i...i!#
#.!.8...85..###/..\~..1#.#
#.p.8...8..r.t...........#
'...8.#!8..###.\../..!g#~#
'~..5#r###i*o#~#..!!.!.#s#
#####''r#..###.##xxxxxg#'#
#####''#°i####.#°xxxxx'''#
#l!.5..gi..~'+-+i!!lxx####
#...9..!.####..~l.!..,!ss#
##~k6..#'#..tl,i...i.-...#
'€...\#~.6..##ss!.123123.#
'€..!/#..!####ss...~....~#
'''################t######
##°#####''##'''####!######
##i!...,~.##'',.~xx.xx...#
#d..;.#..!xx##.g.xygxx.!.#
#~d#.###.##.~.;..xy.xy...#
#..#..#.;.@.##...xx.xx~gg#
#.!'.+.~...i'l#!.xi!ixxyx#
########'i''##############

message 2/2 - PHARAO DUNGEON                  

°°°°hhhhh°°°°hhhhhhhhhhhhhhhhhhhhhhhhhh°°°°°°°hhhhhh
°°hhbaaaahh°whxcaaaaaaaaahhaaaaaaaaaaah°°°w°°°haaaah
°hfcam{naaohbjxmaaaaaaaaa##aaaaaaaaaaajacaaabajaaaah
°hoaancmacfhwhxaaaaaaaaaazjaaaaaaaaaaah°°°w°°°haaaah
°°hhaaaaahh°°hxabaaaaaaaa##aaaaaaaaaaajabaaacajmaaah
°°°°h#b#h°°°@jy´aaaaaaaaahhaaaaahqaaaah°°°a°°°haaaah
°°°°°#j#°°°hwjhhh#aaa#hhhhhhhhhahchhhhh°@wawd°haaaah
hhhhh#c#hhhhjjhhh#aaa#hhhhhiac€ahahhhhhhj#j#jhhaaaah
haaaaaaaaaaajhaaaaaaaaaahhhaiadahaacaahhaaaa[aaaaaah
haaaaaaaaaaa##aaaaaaaaaahlulha0bhaua;l##aaaa[aaaaaah
hbaaaaaaaaaaaaaaaaaaaaaaahh]hhhhha;a,azaqaaa6a5aaaah
ha0aaaaaaaaa##aaaaaaaaaaahhaaaachaaaab##aaaa[aaaaaah
h0aa0caaaaaahhaaaaaaaaaaahhba´´ahc´´a0jhaaaa[5aaaaah
hhh0hhhhhhhhhhhhh#aaa#hhhhhhh#xxyxx#hhjhh#w#hhhhhhhh
hhh€hhhhhhhhjjhhh#aaa#hhhhhhh#xxxhx#hhjhh#j#hhhhhhhh
haaabaxxx€€€jjaaaaaaazaabhhaaaaaaaaaa€jhaaaaaaaaaaah
hcaaa´xxaba´##aaaaaaahaaa##´aaaaaaaaaahhaaaaaaaaaaah
haaa´xxxaaaaczaaaaaaaahcaaabaaaaaaaaaahhaaaaaaaaaaah
haa´axxcaaaa##aaaaaaaahaa##´aaaaaaaaaahhaaaaaaaaa,ah
habaxxxa´abahhaaaaaaaaahhhhaahaaaaaaaahhaaaaaaaaa,ah
hh#j#hh#j#hhhhhhhhaaahjjjjhhhlhhhhhhhhhhhhjhhhhhhjhh
°°#a#°°#a#°°°hhhhhaaahhhhjjjhjhhhhjjjhh°°°b°°°hhhjh°
hhj/h°°hch°°°haaaaaaaaaaahhacaxxxxyhabh°°°a°°hhabahh
haba\caazaacajaaaaass"§$a##aa;x.##yaa´##hhahhha/.\ah
haaah°°hah°°°haaaaabasasaaaaa,x.}#x´´ajabaacajc.x.ah
hxach°°hahhhhhaaaaass$§"a##aa]xg.'xbaa##hhhhhha\./ah
hhhhh°°hcabaajaaaaaaaaaaahhabaxxyxxacah°°°°°°hhaachh
°°°°°°°hhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhh°°°°°°°hhhhh°

message END

