title Omniban
author Patrickgh3
homepage www.puzzlescript.net
noaction
background_color black
text_color #b5f09e

========
OBJECTS
========

Background
(#406e30 #4a7d37)
#2c591d #336621
00000
00000
00010
00100
00000

Target
#0a191f
.....
.000.
.0.0.
.000.
.....

Wall
(#7bbf60 #7bbf60)
#4c8237 #5a8f45
10011
1...0
0...0
0...1
11001
(
10011
10000
00000
00001
11001
)

Human
#bf8aaf
.....
..0..
00000
..0..
.0.0.

Crate
#d48d4e
.000.
0...0
0...0
0...0
.000.

Hole
#0a191f #112930 #173642
.222.
11111
00000
00000
.000.
(
.000.
0...0
0...0
0...0
.000.
)

Omni1
#51f0e5
.....
.....
..0..
.....
.....

Omni2
#f0e851
.....
.....
..0..
.....
.....

Omni3
#f75454
.....
.....
..0..
.....
.....

Omni4
#6ad145
.....
.....
..0..
.....
.....

Player
Red
.....
.....
.....
.....
.....

=======
LEGEND
=======

. = Background
# = Wall
H = Human
* = Crate
@ = Crate and Target
O = Target
i = Hole
p = Omni1
q = Wall and Omni1
u = Crate and Omni1
r = Omni2
s = Wall and Omni2
x = Omni3
y = Omni3 and Wall
z = Omni1 and Target
t = Wall and Player
w = Player
a = Wall and Hole
b = Hole and Player
c = Hole and Omni2
d = Human and Target
e = Omni4
f = Omni4 and Wall
g = Wall and Target
j = Human and Omni1

Floors = Target or Hole
Objs = Crate or Wall or Human

=======
SOUNDS
=======

Omni1 move 2065107
Omni2 move 21787707
Omni3 move 77011707
Omni4 move 21712907

sfx0 45987107
sfx1 5580307
sfx2 26411107
sfx3 49435107
sfx4 19502507
sfx10 24925702

================
COLLISIONLAYERS
================

Background
Target, Hole
Human, Wall, Crate
Omni1, Omni2, Omni3, Omni4
Player

======
RULES
======

( Move omniscient beings using the arrow keys )
( I'm not sure the best bay to do different players. I just
put a dummy Player in the corner of each level. )
[ > Player ] [ Omni1 ] -> [ > Player ] [ > Omni1 ]
[ > Player ] [ Omni2 ] -> [ > Player ] [ > Omni2 ]
[ > Player ] [ Omni3 ] -> [ > Player ] [ > Omni3 ]
[ > Player ] [ Omni4 ] -> [ > Player ] [ > Omni4 ]

( Omniscient beings move other objects )
[ > Omni3 Crate ] -> [ > Omni3 > Crate ]

[ > Omni1 Crate ] -> [ > Omni1 > Crate ]
[ > Omni1 Target ] -> [ > Omni1 > Target ]
[ > Omni1 Human ] -> [ > Omni1 > Human ]

[ > Omni2 Crate ] -> [ > Omni2 > Crate ]
[ > Omni2 Target ] -> [ > Omni2 > Target ]
[ > Omni2 Human ] -> [ > Omni2 > Human ]
[ > Omni2 Hole ] -> [ > Omni2 > Hole ]

[ > Omni4 Crate ] -> [ > Omni4 > Crate ]
[ > Omni4 Target ] -> [ > Omni4 > Target ]
[ > Omni4 Human ] -> [ > Omni4 > Human ]
[ > Omni4 Hole ] -> [ > Omni4 > Hole ]
[ > Omni4 Wall ] -> [ > Omni4 > Wall ]

( Human pushes crates )
[ > Human | Crate ] -> [ > Human | > Crate ]

( Crates fill holes )
late [ Crate Hole ] -> [ ] sfx10

( sound effects when moving objects )
[ > Crate | no Objs ] -> [ > Crate | no Objs] sfx0
[ > Human | no Objs ] -> [ > Human | no Objs] sfx1
[ > Wall | no Objs ] -> [ > Wall | no Objs] sfx2
[ > Target | no Floors ] -> [ > Target | ] sfx3
[ > Hole | no Floors ] -> [ > Hole | ] sfx4


==============
WINCONDITIONS
==============

All Target on Crate     

=======     
LEVELS
=======

( move blocks )

###########
###########
##.......##
##.......##
##...#...##
##.*.#.o.##
##...#...##
###########
y#########t

###########
##......###
##......###
##.*...o###
##......###
##.*...o###
##......###
##......###
y#########t

...........
...........
...........
...*...o...
...........
...*...o...
...........
...........
x.........w

( move blocks and goals )

###########
######...##
######.o.##
######...##
###########
##...######
##.*.######
##...######
q#########t

###########
##...#...##
##.o.#.o.##
##...#...##
###########
###########
###*###*###
###########
q#########t

######.....
#...##.....
#.o.##..o..
#...##.....
######.....
######.....
##*###..*..
######.....
q#####....w

( holes )


###########
###########
##iiii..*##
##i..i...##
##io.i...##
##i..i...##
##iiii..*##
###########
q#########t

###########
###########
##.......##
##.....ii##
##..u..io##
##.....ii##
##.......##
###########
##########t

.....*.....
.i.........
i.iii.i.iii
.....i.i...
.*.i...i.o.
....iii..i.
iii....ii.i
...iiii....
p.........w

.....i...##
.....i...##
.....i.o.##
.....i...##
.z.*.i.*.##
.....i...##
.....i...a#
.....i...##
.....i...#t

( human )

.....i.....
.....i.....
...o.i.o...
.....i.....
...*.i.*...
.....i.....
.....i...h.
.....i.....
p....i....w

.....i.####
.....i.####
..o..i.#o##
.....i.####
.....i.####
.....i.####
..*..i..*h#
.....i.####
p....i.###t

(
......#....
......#....
......#....
..*...#..ii
....#..h.io
..*...#..ii
......#....
......#....
p.....#...w
)

( move holes )

###########
###########
##.......##
##....iii##
##.*..ioi##
##....iii##
##.......##
###########
s#########t

###########
###########
###########
###########
##c..*..o##
###########
###########
###########
##########t

###########
##...#...##
##.o.#.o.##
##...#...##
###########
###a###a###
##a*a#a*a##
###a###a###
s#########t

iiiiiiiiiii
iiiiiiiiiii
iiiiiiiiiii
iiiiiiiiiii
iii*.i.oiii
iiiiiiiiiii
iiiiiiiiiii
iiiiiiiiiii
ciiiiiiiiib

( move walls )

###########
###########
##.......##
##.*.....g#
##.......##
##.*.....g#
##.......##
###########
f#########t

(
#####i#####
....#i#....
..o.#i#.o..
....#i#....
..*.#i#.*..
....#i#....
....#i#.h..
....#i#....
f####i####t
)

#####.#####
....#.#....
..o.#.#.o..
....#.#....
..i.#h#.i..
.i*i#.#i*i.
..i.#.#.i..
....#.#....
f####.####t

message Thank you for playing!
(
message Bonus level warmup

...........
...........
....*.*....
...........
.....q.....
...........
....o.o....
...........
..........w
)

message Bonus level

...........
...........
...*****...
...........
.....q.....
...........
...ooooo...
...........
..........w

message Very thank you for playing!