title A port of Puzzle Wizard IQ 130+, level 18 of 72
author Cheeky Mammoth, PuzzleScript port by TGE

run_rules_on_level_start

========
OBJECTS
========

Background
grey
.....
.....
.....
.....
.....

target
black
.....
.0.0.
..0..
.0.0.
.....

player
grey
.....
.000.
.0.0.
.000.
.....

red
red

yellow
yellow

green
green

blue
blue

portal1
black
..0..
.0.0.
0...0
.0.0.
..0..

actiontemp
black
.....
.....
.....
.....
.....

leftblue
black blue
.....
.00..
010..
.00..
.....

rightyellow
black yellow
.....
..00.
..010
..00.
.....

rbswap
black red blue
.....
00000
01020
00000
.....

ryswap
black red yellow
.....
00000
01020
00000
.....

byswap
black blue yellow
.....
00000
01020
00000
.....

shield
black 
0...0
..0..
.000.
..0..
0...0

centprotect
lightgrey
....0
...0.
..0..
.0...
0....

midprotect
lightgrey
....0
...0.
..0..
.0...
0....

edgeprotect
lightgray
....0
...0.
..0..
.0...
0....

rbswapsign
black
.....
.....
.....
.....
.....

ryswapsign
black
.....
.....
.....
.....
.....

byswapsign
black
.....
.....
.....
.....
.....

rbswapsignstart
black
.....
.....
.....
.....
.....

ryswapsignstart
black
.....
.....
.....
.....
.....

byswapsignstart
black
.....
.....
.....
.....
.....
=======
LEGEND
=======

r = red
y = yellow
g = green
b = blue

1 = player and blue
2 = rbswap and blue
3 = portal1 and blue
4 = byswap and blue
5 = portal1 and yellow
6 = leftblue and yellow
7 = shield and blue
8 = ryswap and red
9 = rightyellow and yellow
0 = target and red

portals = portal1
colors = red or yellow or green or blue
directions = leftblue or rightyellow
swaps = rbswap or ryswap or byswap
protection = centprotect or midprotect or edgeprotect

swapsign = rbswapsign or ryswapsign or byswapsign
markers = rbswapsignstart or ryswapsignstart or byswapsignstart

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

background

actiontemp

swapsign

markers

colors

portals

swaps

shield

directions

target

player

protection

======
RULES
======

(Player cannot switch colours)
[> player red | colors no red] -> cancel
[> player yellow | colors no yellow] -> cancel
[> player green | colors no green] -> cancel
[> player blue | colors no blue] -> cancel

(teleports)
late [player portal1 no actiontemp][no player portal1] -> [portal1 no player][portal1 player actiontemp]

late [no player actiontemp] -> [no actiontemp]


(directions)
startloop
late left [player leftblue|...|colors no blue] -> [player leftblue|...|no colors]
late left [player leftblue|...|no colors] -> [player leftblue|...|blue]

late right [player rightyellow|...|colors no yellow] -> [player rightyellow|...|no colors]
late right [player rightyellow|...|no colors] -> [player rightyellow|...|yellow]
endloop




(shield activation)
startloop
late [player shield no centprotect] -> [player shield centprotect actiontemp]
late vertical [no midprotect|centprotect] -> [midprotect|centprotect]
late horizontal [no edgeprotect|midprotect] -> [edgeprotect|midprotect]
late horizontal [no edgeprotect|centprotect] -> [edgeprotect|centprotect]
endloop

(shield deactivation)
startloop
late [player shield centprotect no actiontemp] -> [player shield no centprotect]
late vertical [no centprotect| midprotect|no centprotect] -> [no centprotect|no midprotect|no centprotect]
late horizontal [no midprotect no centprotect| edgeprotect|no midprotect no centprotect] -> [no midprotect no centprotect| no edgeprotect|no midprotect no centprotect]
endloop

late [no player actiontemp] -> [no actiontemp]



(swapper)
late [player rbswap no actiontemp] -> [player rbswap actiontemp rbswapsignstart]
late [rbswapsignstart no player] -> [no rbswapsignstart]
startloop
late [rbswapsignstart][red no protection no rbswapsign] -> [rbswapsignstart][red rbswapsign]
late [rbswapsignstart][blue no protection no rbswapsign] -> [rbswapsignstart][blue rbswapsign]
endloop
startloop
late [red rbswapsign] -> [blue no rbswapsign]
late [blue rbswapsign] -> [red no rbswapsign]
late [no red no blue rbswapsign] -> [no rbswapsign]
endloop

late [player ryswap no actiontemp] -> [player ryswap actiontemp ryswapsignstart]
late [ryswapsignstart no player] -> [no ryswapsignstart]
startloop
late [ryswapsignstart][red no protection no ryswapsign] -> [ryswapsignstart][red ryswapsign]
late [ryswapsignstart][yellow no protection no ryswapsign] -> [ryswapsignstart][yellow ryswapsign]
endloop
startloop
late [red ryswapsign] -> [yellow no ryswapsign]
late [yellow ryswapsign] -> [red no ryswapsign]
late [no red no yellow ryswapsign] -> [no ryswapsign]
endloop

late [player byswap no actiontemp] -> [player byswap actiontemp byswapsignstart]
late [byswapsignstart no player] -> [no byswapsignstart]
startloop
late [byswapsignstart][blue no protection no byswapsign] -> [byswapsignstart][blue byswapsign]
late [byswapsignstart][yellow no protection no byswapsign] -> [byswapsignstart][yellow byswapsign]
endloop
startloop
late [blue byswapsign] -> [yellow no byswapsign]
late [yellow byswapsign] -> [blue no byswapsign]
late [no red no blue byswapsign] -> [no byswapsign]
endloop

late [no player actiontemp] -> [no actiontemp]

==============
WINCONDITIONS
==============

All player on target

=======
LEVELS
=======

message get to the x

1bb2bb3bb4
bbbbbbbbb5
bbbbbbbbb6
ybbbbbbbry
ybb7bbbbby
yb8bbbrbbb
9bbbbbbbbb
bbbb0bbbbb