title Twin Drop
author Jonah Ostroff
text_color #5FC
color_palette amiga
key_repeat_interval 2
background_color Black
realtime_interval .4
noundo

========
OBJECTS
========

Gem1 (Red)
#F31 #F84 #C10
.222.
20102
21012
20102
.222.

Gem2 (Yellow)
#EC3 #B64 #CA1
.222.
21002
20102
20012
.222.

Gem3 (Blue)
#42F #88F #31C
.222.
20002
21112
20002
.222.

Player
Pink
.....
.....
0...0
.0.0.
..0..

Wall
#DDFFDD #AADDAA #99BB99 #BBCCBB
01203
01201
21210
20113
20213

LSource
#DDFFDD #AADDAA #99BB99 #BBCCBB
0120.
012..
212..
201..
2021.

RSource
#DDFFDD #AADDAA #99BB99 #BBCCBB
.1203
..201
..210
..113
.0213

Disappearing
Blue
.....
.....
.....
.....
.....

Gone
#F0F
.....
..0..
.000.
..0..
.....

GoneWall
#F0F
.....
..0..
.000.
..0..
.....

Line
#CCC
.....
.....
.....
.....
.....

Push
Blue
.....
.....
.....
.....
.....

Background
#001 #012
00000
01000
00000
00100
00000

Sad
Black
.....
.0.0.
..0..
.0.0.
.....

Lose1
Black
.....
.0.0.
..0..
.0.0.
.....

Lose2
Orange
.....
.0.0.
..0..
.0.0.
.....

LetterI
White
.000.
..0..
..0..
..0..
.000.

LetterW
White
0...0
0...0
0.0.0
0.0.0
.0.0.

LetterN
White
0...0
00..0
0.0.0
0..00
0...0

LetterT
White
00000
..0..
..0..
..0..
..0..

LetterE
White
00000
0....
000..
0....
00000

LArrow
White
..0..
.0...
0000.
.0...
..0..

RArrow
White
..0..
...0.
.0000
...0.
..0..

FloorLeft
White
.....
.....
.....
.....
.....

FloorRight
White
.....
.....
.....
.....
.....

=======
LEGEND
=======

Gem = Gem1 or Gem2 or Gem3
Support = Gem or Wall
Lose = Lose1 or Lose2
Source = LSource or RSource
Letter = LetterT or LetterI or LetterE or LetterW or LetterN or Larrow or Rarrow
Floor = FloorLeft or FloorRight

# = Wall
L = LSource
R = RSource
- = Line
. = Background
P = Player
1 = FloorLeft and Wall
2 = FloorRight and Wall
=======
SOUNDS
=======
Player Move 49937507
Sfx1 13003907
Sfx2 51682104
Sfx3 33864103
Sfx4 57994108
================
COLLISIONLAYERS
================
Background
Letter Floor
Gem Wall Source Player Gone
Line Sad GoneWall
Lose Push Disappearing

======
RULES
======
[GoneWall] -> []
Down[Gone|...|Wall no Lose] -> [|...|GoneWall Wall]
[Gone] -> []
[Down Player] -> [Action Player]
[Up Player] -> Cancel
[Lose][Action Player] -> Cancel

[Lose1][Stationary Player] -> [Action Lose2][Player]
[Stationary Lose2][Stationary Player] -> [Lose1][Player]

[Source|Line] -> [> Push Source|Line]
[Moving Player][Push] -> [Moving Player][]
[> Push|Line no Gem] -> [|> Push Line]
[> Push|Gem] -> [|> Gem]
[> Push|Wall] -> [|Wall]
[> Gem Line|Gem Line] -> [> Gem Line|> Gem Line]
Horizontal[> Gem|Wall] -> [|Wall]

Down[Gem|...|Action Player] -> [> Gem|...|Action Player] Sfx1

[Action Player] -> [Player]

Down[> Gem|Player|no Gem] -> [|Player|Gem]
+ Down[Gem no Line|no Support] -> [|Gem]
+ Down[> Gem Line|Line no Gem] -> [Line|> Gem Line]
+ Down[> Gem|Player|Gem] -> [|Action Player|Gem] Sfx2

Down[Action Player|...|Gem] -> [Action Player|...|]
Down[Action Player|...|Wall] -> [Action Player|...|Sad Wall]

late[Gem1 no Line|Gem1 no Line|Gem1 no Line] -> [Disappearing Gem1|Disappearing Gem1|Disappearing Gem1]
late[Gem2 no Line|Gem2 no Line|Gem2 no Line] -> [Disappearing Gem2|Disappearing Gem2|Disappearing Gem2]
late[Gem3 no Line|Gem3 no Line|Gem3 no Line] -> [Disappearing Gem3|Disappearing Gem3|Disappearing Gem3]

late [Sad no Lose|Sad no Lose|Sad no Lose] -> [Sad|Sad|Sad] Sfx4
late [Sad no Lose2|Sad no Lose2|Sad no Lose2] -> [Sad Lose1|Sad Lose1|Sad Lose1]

late [Disappearing Gem] -> [Gone] Sfx3

late [Source|Line no Gem] -> [Source|Line random Gem]
late [Source|Gem][Lose] -> [Source|][Lose]

late Up[Lose][Wall|Line|Line] -> [Lose][Wall|Line|Line LetterI]
late Right[Lose FloorLeft][Lose FloorRight][no Letter|LetterI|no Letter] -> [Lose FloorLeft][Lose FloorRight][LetterT|LetterI|LetterE]
late Right[Lose FloorLeft][no Letter|LetterI|no Letter|no Letter] -> [Lose FloorLeft][LetterW|LetterI|LetterN|RArrow]
late Right[Lose FloorRight][no Letter|no Letter|LetterI|no Letter] -> [Lose FloorRight][LArrow|LetterW|LetterI|LetterN]

==============
WINCONDITIONS
==============
Some LetterI
No Gem on Line
=======
LEVELS
=======
Message Player 1 uses A/D, Player 2 uses Left/Right. You move together.

Message Press S, Down, or Space to drop gems.

Message If a column overflows, it is emptied and marked.

Message Match three gems to delete marks.

Message You lose if you have three marked columns in a row.

#...............#
L---------------#
#---------------R
#...P...#...P...#
#.......#.......#
#.......#.......#
#.......#.......#
#.......#.......#
#.......#.......#
#.......#.......#
#.......#.......#
#1111111#2222222#