title Elementals
author Joel Ryan
homepage www.puzzlescript.net


key_repeat_interval 0.25
(noundo)
again_interval .05

========
OBJECTS
========

Background
#000022 #002233
00011
01001
01100
10001
11000

(#002233 #000022
01010
10101
01010
10101
01010)

Lava
#990000 #770000 #550000 #330000 #FF5533 #FFCC00
34113
13351
04321
32110
22121

puddle
blue lightblue darkblue white
02110
12131
03101
02110
20121

PolsVoice
orange  white  #CC7700 #ffaa88
22.22
.2.2.
31013
.222.
32023

PolsVoiceStone
grey  white  darkgrey lightgrey
22.22
.2.2.
31013
.222.
32023

Slimes
green lightgreen white #003322 black
.001.
00011
32020
33300
.333.

Slimes2
#003322  green red black   
.001.
00011
32020
33300
.333.

Slimestatue
grey #666666 white black
.001.
00011
32020
33300
.333.

Wall
#cccccc #dddddd #eeeeee #ffffff #bbbbbb #aaaaaa #666666
.212.
62323
02120
62106
.606.

TrapWall
#cccccc #dddddd #eeeeee #ffffff #bbbbbb #aaaaaa #666666
.212.
62323
02120
62106
.606.


Ice
blue #dddddd lightblue #ffffff #bbbbbb #aaaaaa #dddddd
.212.
62323
02120
60106
.606.

Player1
blue yellow #FF9900 #FF8800 #FF5500 #336699 #0077EE
..6.1
6.0.6
..0.3
.0.02
.6.63

statue
#666666 #AAAAAA #444444 #CCCCCC
..1..
1.0.3
..0..
.2.2.
.2.2.

Player2
#666666 #AAAAAA #444444 #CCCCCC yellow #FF8800 #FF5500
..1.4
1.0.1
..0.5
.2.26
.2.25


Player3
red #CC6611 #FF7733 #CCCCCC yellow #FF8800 #FF5500
..1.4
1.0.1
..0.5
.2.26
.2.26

empty 
black
.....
.....
.....
.....
.....


BrokenStatue
#666666 #AAAAAA #444444
.....
...2.
1....
.20.1
.2.2.

Chest
#440000 #FFFF00 BLACK
.....
.000.
02220
00000
00000

crystal
blue lightblue white
.....
..0..
.021.
..0..
.....

waterbullet
blue
.....
.....
..0..
.....
.....

stonebullet
grey
.....
.....
..0..
.....
.....

firebullet
red
.....
.....
..0..
.....
.....

Ladder
Black White
.1.1.
.111.
01010
01110
.101.

LOCK
#cccccc #dddddd #eeeeee #ffffff #bbbbbb #aaaaaa BLACK
.212.
46663
06665
50605
.505.

KEY
#FFFF00
.....
0..0.
000.0
...0.
.....

Obsidian
#333333 #555555 #444444 #777777 #AAAAAA #CCCCCC
34113
13351
04321
32110
23131


trigger
#002233 #000022
01010
10101
01010
10101
01010

=======
LEGEND
=======

. = Background
, = Empty
# = Wall
o = Lock
L = Ladder
P = Player1
c = Chest
G = crystal
K = Key
D = PolsVoice
+ = Slimes
B = Player2
S = Statue
0 = Obsidian
q = BrokenStatue
1 = waterbullet
2 = stonebullet
3 = firebullet
M = Lava
z = ice
4 = polsvoicestone 
t = trigger
% = trapwall
e = empty

bullet = waterbullet or stonebullet or firebullet
Enemy = PolsVoice or slimes or slimes2
Object = PolsVoice or slimes or ladder or lock or key or statue or wall or BrokenStatue or bullet or ice or polsvoicestone or trapwall or slimes2 or slimestatue or crystal
Player = Player1 or Player2 or player3


=======
SOUNDS
=======
Player1 Move 75179507
Player1 CantMove 4306701
Player2 Move 75179507
Player2 CantMove 4306701
Sfx0 98562100
Sfx1 5663104
Sfx2 14588102
Sfx3 90582505
================
COLLISIONLAYERS
================

Background
Obsidian, Lava, BrokenStatue
Empty
Ladder, trigger, trapwall, empty, puddle, crystal
Player1, Wall, Chest, PolsVoice, Lock, Key, Statue, Player2, bullet, ice, player3,  polsvoicestone, trapwall, empty, slimes, slimestatue, slimes2


======
RULES	 
======  

(turning into other players)
[Action Player1 | statue  ] -> [Player2 |  statue ] Sfx1
[Action Player3 | statue  ] -> [Player2 |  statue ] Sfx1
[Action Player1 |  lava  ] -> [Player3 |  lava ] Sfx1
[Action Player2 | lava  ] -> [Player3 |  lava ] Sfx1
[Action Player2 | ice  ] -> [Player1 |  ice ] Sfx1
[Action Player3 | ice  ] -> [Player1 |  ice ] Sfx1

(shooting rules)
Up [Action Player1 |   ] -> [Player1 | waterbullet ] Sfx1
Up [Action Player2 |   ] -> [Player2 | stonebullet ] Sfx1
Up [Action Player3 |   ] -> [Player3 | firebullet ] Sfx1

(bullets move upwards)
Up [ bullet ] -> [ > bullet ] again

(pushing stuff)
[ > player | polsvoicestone] -> [ > player | > polsvoicestone]
[ > player | slimestatue] -> [ > player | > slimestatue]
[ > player | ice ] -> [ > player | > ice]
[ > player | key ] -> [ > player | > key]
[ > Player | Statue ] -> [ > Player | > Statue  ]
[ > Player | polsvoicestone | Lava] -> [ Player | > Obsidian | ]
[ > Player | ice | Lava] -> [ Player | > puddle | ]
[ > Player | Statue | Lava] -> [ Player | > Obsidian | ]
[ > Player | slimestatue | Lava] -> [ Player | > Obsidian | ]

(bullet effects on enemies)
[ stonebullet | polsvoice ] -> [ | polsvoicestone ]
[ stonebullet | slimes ] -> [ | slimes2 ]
[ firebullet | slimes ] -> [ | slimes2 ]
[ waterbullet | slimes ] -> [ | slimes2 ]
[ stonebullet | slimes2 ] -> [ | slimestatue ]
[ firebullet | slimes2 ] -> [ | lava]
[ waterbullet | slimes2 ] -> [ | ice ]
[ firebullet | enemy ] -> [ | lava ]
[ waterbullet | enemy ] -> [ | ice ]

(stone bullets break statues)
[ > stonebullet | statue ] -> [ | brokenstatue ]

(bullet hitting wall)
[ > bullet | object ] -> [ | object ]

(player triggers trapwall)
[ > player |  trigger ] -> [ trapwall |  player]

(enemy movement)
[ Stationary Enemy ] -> [randomDir Enemy]

(Player dies when he/she hits an enemy)
[ > Enemy | Player] -> restart

(key and lock rules)
[ > player | Key | Lock] -> [> Player||] Sfx3
[ > Player1 | Key] -> [> Player1 | > Key]

(collect gold)
[ player | crystal] -> [ player | ]

(lava rules)
[ > Player1 | Lava] -> Restart
[ > Player1 | Lava] -> Sfx2
[ > Player2 | Lava] -> Restart
[ > Player2 | Lava] -> Sfx2
[ > Enemy | Lava] -> [ | Lava ]

(Ice rules)
[> enemy | ice] -> [ | puddle ]



==============
WINCONDITIONS
==============

some player  on ladder
no crystal

=======	 
LEVELS
=======

message There are three elements- Stone, Fire, and Ice.  
message Each element abides by certain rules
message switch by pressing X next to an example of the element
message Gather crystals before climbing up the ladder

##################
##################
#..dd.dddd.d.dd.##
#....dddddd...d..#
#..dd.......dd...#
#......d.dddd....#
#...d....t....d..#
#mmmmmmmmommm#.d.#
#..S.z.m.....s...#
#.g.......k..s.d.#
#.l..........z.d.#
#.g...P......z.d.#
##################

message Choose Wisely

############################
############################
#....................d....##
#............d...g....d..###
#....d.....####.....d......#
#......d.....d..d..........#
#....####..d...d..d..d.....#
#....ddd......###..........#
#....g.............d.d.....#
#..........d..g......d.....#
#..dd......................#
#......d....d......####....#
#........t........d........#
#########o####........d....#
#..S.z.m.....#.......####..#
#.........k..#.............#
#............#........l....#
#.....P......#.............#
############################


#####################################
#####################################
#...............dd..................#
#.....gg.................l..g.......#
#.......########...mmm..............#
#........d..++.....mm.ddd.......dd..#
#..Z...d......z..ddd..#########.....#
#.#.....+++++................s#.....#
#.###########..+++...ddd......#.....#
#.#...g...............ddd.....#.....#
#...ddd......mmmmmm...........dd....#
#.......ddd....####.......d.........#
#.........ddd....+..........+....d..#
######........+++...##########..z...#
#......Z............++++.....g#.....#
#.............................#.....#
#.........########...........g......#
#.S.Z.M......z......zzz.............#
#..................zzzzz....+++.....#
#...P...............................#
#####################################


#####################################
#####################################
#g..............#...#.g#+.....++....#
#..+....+...+...#.k.#..#....g..+....#
#....+k.........#...#....#..........#
#.........+...+.ot..#.ddd#....#.dd..#
#....++.........#.#.#..+.#.+..#..+..#
#..+.........t..#...#....#...s#.....#
#############o###g..#ddd...dd.#.....#
#g......ddd.....#...#.ddd.....#..#.+#
#...ddd.........#g..#...+.....+..#..#
#s......ddd.....##..#.....d...+..#..#
#z..............#...#.......+....#..#
#m..k...........#...#....#.#z.......#
#.........t.....#.g.#g...#t#.g#...l.#
##########o######...######o##########
#..S.z.m.....#............k.SMZ.....#
#............#......................#
#..k.........#......................#
#.....P......#......................#
#####################################







