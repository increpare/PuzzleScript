title Zaap
author puzzlescript@googlegroups.com
homepage www.puzzlescript.net
run_rules_on_level_start
realtime_interval 0.1
color_palette 14

========
OBJECTS
========

Background _
DarkGray
.....
.....
..0..
.....
.....

Level0
Gray
.....
.....
..0..
.....
.....

Level1
LightGray DarkGray
00000
00000
00100
00000
00000

LeftInserter
Transparent

RightInserter
Transparent

UpInserter
Transparent

DownInserter
Transparent

Start @
Transparent

PlayerRed
Red

PlayerBlue
Blue

PlayerGreen
Green

PlayerPurple
Purple

PlayerRight
LightGray
..000
....0
.....
....0
..000

PlayerLeft
LightGray
000..
0....
.....
0....
000..

PlayerUp
LightGray
00.00
0...0
0...0
.....
.....

PlayerDown
LightGray
.....
.....
0...0
0...0
00.00

Red
Red

Blue
Blue

Green
Green

Purple
Purple

Timer
Transparent (Orange
.....
.....
.....
....1
...11)

TimerMax
Transparent (Brown
11...
1....
.....
.....
.....)

Insert
White
00.00
0...0
.....
0...0
00.00

Clear
Transparent

=======
LEGEND
=======

Level = Level0 or Level1

PlayerNonRed    =              PlayerGreen or PlayerBlue or PlayerPurple
PlayerNonGreen  = PlayerRed                or PlayerBlue or PlayerPurple
PlayerNonBlue   = PlayerRed or PlayerGreen               or PlayerPurple
PlayerNonPurple = PlayerRed or PlayerGreen or PlayerBlue

PlayerColor = PlayerRed or PlayerGreen or PlayerBlue or PlayerPurple
PlayerDirection = PlayerLeft or PlayerRight or PlayerUp or PlayerDown
Player = PlayerColor or PlayerDirection

Inserter = LeftInserter or RightInserter or UpInserter or DownInserter
Color = Red or Blue or Green or Purple

. = Level0
: = Level1
L = LeftInserter and Level0
R = RightInserter and Level0
U = UpInserter and Level0
D = DownInserter and Level0

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Level
PlayerColor
PlayerDirection
Color
Inserter, Start
Timer
TimerMax
Insert
Clear

======
RULES
======

[stationary Player] [Timer] -> [Player] [right Timer]
random [Timer TimerMax] [Insert] -> [Timer TimerMax] [action Insert]
left [action Insert] [Timer |] -> [action Insert] [| Timer]

[action Insert LeftInserter  Color] -> [action Insert LeftInserter  left  Color]
[action Insert RightInserter Color] -> [action Insert RightInserter right Color]
[action Insert UpInserter    Color] -> [action Insert UpInserter    up    Color]
[action Insert DownInserter  Color] -> [action Insert DownInserter  down  Color]
[> Color | Color] -> [> Color | > Color]
[> Color | no Color] -> [| Color]

[action Insert] -> [action Insert random Color]
random [action Insert LeftInserter ] [Inserter no LeftInserter ] -> [LeftInserter ] [Insert Inserter]
random [action Insert RightInserter] [Inserter no RightInserter] -> [RightInserter] [Insert Inserter]
random [action Insert UpInserter   ] [Inserter no UpInserter   ] -> [UpInserter   ] [Insert Inserter]
random [action Insert DownInserter ] [Inserter no DownInserter ] -> [DownInserter ] [Insert Inserter]

[ left PlayerDirection no PlayerLeft  ] -> [PlayerLeft]
[right PlayerDirection no PlayerRight ] -> [PlayerRight]
[   up PlayerDirection no PlayerUp    ] -> [PlayerUp]
[ down PlayerDirection no PlayerDown  ] -> [PlayerDown]
[moving PlayerColor stationary PlayerDirection] -> [PlayerColor PlayerDirection]

[> Player | Level0] -> cancel

left  [action PlayerLeft  |] -> [PlayerRight | > Clear]
right [action PlayerRight |] -> [PlayerLeft  | > Clear]
up    [action PlayerUp    |] -> [PlayerDown  | > Clear]
down  [action PlayerDown  |] -> [PlayerUp    | > Clear]

startloop
[> Clear no Color |] -> [| > Clear]
[PlayerRed   ] [> Clear] -> [PlayerRed   ] [> Clear no Red]
[PlayerGreen ] [> Clear] -> [PlayerGreen ] [> Clear no Green]
[PlayerBlue  ] [> Clear] -> [PlayerBlue  ] [> Clear no Blue]
[PlayerPurple] [> Clear] -> [PlayerPurple] [> Clear no Purple]
endloop

[Clear Green ] [PlayerRed   ] -> [Red   ] [PlayerGreen]
[Clear Blue  ] [PlayerRed   ] -> [Red   ] [PlayerBlue]
[Clear Purple] [PlayerRed   ] -> [Red   ] [PlayerPurple]
[Clear Red   ] [PlayerGreen ] -> [Green ] [PlayerRed]
[Clear Blue  ] [PlayerGreen ] -> [Green ] [PlayerBlue]
[Clear Purple] [PlayerGreen ] -> [Green ] [PlayerPurple]
[Clear Red   ] [PlayerBlue  ] -> [Blue  ] [PlayerRed]
[Clear Green ] [PlayerBlue  ] -> [Blue  ] [PlayerGreen]
[Clear Purple] [PlayerBlue  ] -> [Blue  ] [PlayerPurple]
[Clear Red   ] [PlayerPurple] -> [Purple] [PlayerRed]
[Clear Green ] [PlayerPurple] -> [Purple] [PlayerGreen]
[Clear Blue  ] [PlayerPurple] -> [Purple] [PlayerBlue]

[Clear] [PlayerRed   ] -> [] [random PlayerNonRed]
[Clear] [PlayerGreen ] -> [] [random PlayerNonGreen]
[Clear] [PlayerBlue  ] -> [] [random PlayerNonBlue]
[Clear] [PlayerPurple] -> [] [random PlayerNonPurple]

random [@] [Level1] -> [@] [Level1 random PlayerDirection]
random [@] [PlayerDirection] -> [@] [PlayerDirection random PlayerColor]
random [@] [Inserter] -> [@] [Inserter random Color]
random [@] [Inserter] -> [@] [Inserter Insert]
right [@ |||||||||] -> [Timer ||||||||| TimerMax]

==============
WINCONDITIONS
==============

=======
LEVELS
=======

_______DDDD_______
_______...._______
_______...._______
_______...._______
_______...._______
R......::::......L
R......::::......L
R......::::......L
R......::::......L
_______...._______
_______...._______
_______...._______
_______...._______
@______UUUU_______