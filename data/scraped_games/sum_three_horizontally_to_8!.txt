title sum three horizontally to 8!
author increpare
homepage www.increpare.com
color_palette 1
background_color darkblue
text_color pink

(anderer Quellcode für Level-Generation usw kann man hier finden
https://github.com/increpare/sum-three-horizontally-to-8-
)

run_rules_on_level_start

norepeat_action


========
OBJECTS
========

Background
black

wall
darkgray

spawn 
transparent

player p
white
00...
0....
.....
0....
00...


player_r 
white
...00
....0
.....
....0
...00


deadplayer 
gray
00...
0....
.....
0....
00...


deadplayer_r 
gray
...00
....0
.....
....0
...00

k1 1
red darkred
11111
11111
11011
11111
11111


k2 2
green darkgreen
11111
11101
11111
10111
11111


k3 3
yellow lightbrown
11111
11101
11011
10111
11111



k4 4
brown darkbrown
11111
10101
11111
10101
11111

k5 5
blue darkblue
11111
10101
11011
10101
11111

k6 6
pink purple
11111
10101
10101
10101
11111

spawnfall
transparent

dokill
black
00000
0...0
0...0
0...0
00000

dokill2
black
00000
00000
00.00
00000
00000

tanwas
purple


=======
LEGEND
=======

. = Background
# = Wall
? = spawn
k = k1 or k2 or k3 or k4 or k5 or k6
ko = k1 or k2 or k3 or k4 or k5 or k6

q = k1 and player
w = k2 and player
e = k3 and player
r = k4 and player
t = k5 and player
y = k6 and player

=======
SOUNDS
=======

sfx0 29634507 (schieben)
sfx1 93386709 (leeren)
startlevel 61503707

================
COLLISIONLAYERS
================

Background
wall,k
player, player_r
deadplayer, deadplayer_r
spawn
spawnfall
dokill
dokill2
tanwas

======
RULES
======


right [player deadplayer | player_r deadplayer_r ] -> [stationary player deadplayer | stationary player_r deadplayer_r ]

[player_r]->[]

[dokill][player]->[dokill][player spawnfall]
[dokill2][player]->[dokill2][player spawnfall]

[action player ] -> sfx0


right [action player |]->[action player deadplayer|deadplayer_r]

right [action player  k1|k1]->cancel
right [action player  k2|k2]->cancel
right [action player  k3|k3]->cancel
right [action player  k4|k4]->cancel
right [action player  k5|k5]->cancel
right [action player  k6|k6]->cancel
right [action player  no k|no k]->cancel

[> player | wall]->cancel
right [> player ||wall]->cancel

down [ k | no k ] -> [ > k | ] again
[> k | wall ] -> [ k | wall ]
[> k | stationary ko ] -> [ k | ko ]

[> k ] [ player ] -> [ > k ] [ player spawnfall]

[moving k] [player no spawnfall]-> [ moving k ] [ player spawnfall]

[player] [? no k]->[player spawnfall] [ ? random k] again

right [action player k | ko ]->[tanwas player spawnfall ko | k] again
right [action player k | no ko ]->[tanwas player spawnfall| k] again
right [action player no ko | k ]->[tanwas player spawnfall k | ] again



[dokill2 k]->[] again

[dokill k]->[dokill2 k] again


horizontal [player no spawnfall] [ k1 | k1 | k6 ] -> [player no spawnfall] [ k1 dokill | k1 dokill | k6 dokill ]
horizontal [player no spawnfall] [ k1 | k2 | k5 ] -> [player no spawnfall] [ k1 dokill | k2 dokill | k5 dokill ]
horizontal [player no spawnfall] [ k1 | k3 | k4 ] -> [player no spawnfall] [ k1 dokill | k3 dokill | k4 dokill ]
horizontal [player no spawnfall] [ k1 | k4 | k3 ] -> [player no spawnfall] [ k1 dokill | k4 dokill | k3 dokill ]
horizontal [player no spawnfall] [ k1 | k5 | k2 ] -> [player no spawnfall] [ k1 dokill | k5 dokill | k2 dokill ]
right [player no spawnfall] [ k1 | k6 | k1 ] -> [player no spawnfall] [ k1 dokill | k6 dokill | k1 dokill ]
horizontal [player no spawnfall] [ k2 | k1 | k5 ] -> [player no spawnfall] [ k2 dokill | k1 dokill | k5 dokill ]
horizontal [player no spawnfall] [ k2 | k2 | k4 ] -> [player no spawnfall] [ k2 dokill | k2 dokill | k4 dokill ]
horizontal [player no spawnfall] [ k2 | k3 | k3 ] -> [player no spawnfall] [ k2 dokill | k3 dokill | k3 dokill ]
right [player no spawnfall] [ k2 | k4 | k2 ] -> [player no spawnfall] [ k2 dokill | k4 dokill | k2 dokill ]
horizontal [player no spawnfall] [ k3 | k1 | k4 ] -> [player no spawnfall] [ k3 dokill | k1 dokill | k4 dokill ]
right [player no spawnfall] [ k3 | k2 | k3 ] -> [player no spawnfall] [ k3 dokill | k2 dokill | k3 dokill ]

right [dokill][deadplayer|deadplayer_r]->[dokill][|] sfx1


[tanwas]->[]

[dokill]->again

late [ spawnfall]->[]

late right [ player | ] -> [ player|player_r]


==============
WINCONDITIONS
==============

no k

=======
LEVELS
=======

message If three numbers horizontally sum to eight, they vanish.
message Each level has a unique solution


message World 1/3 - One Move
message World 1/3 - Level 1/4

#############
#p24.2..23.3#
#############


message World 1/3 - Level 2/4


########
#p.133.#
#142352#
########

(
Lösung: (2,1) 
Verhältnis3: 9.000000
Stückzahl: 9
Erfolg: 1
Gesamt: 8
Interessante: 1
Lösungstiefe: 0
)

message World 1/3 - Level 3/4

#######
#p.2..#
#.132.#
#.4114#
#.4243#
#.4324#
#######

(
Lösung: (0,1) 
Verhältnis3: 16.000000
Stückzahl: 16
Erfolg: 1
Gesamt: 17
Interessante: 1
Lösungstiefe: 0
)


message World 1/3 - Level 4/4

###########
#p.15631..#
#.2121244.#
###########

(
Lösung: (3,1) 
Verhältnis3: 12.000000
Stückzahl: 12
Erfolg: 1
Gesamt: 13
Interessante: 1
Lösungstiefe: 0
)



message World 2/3 - Medium Land
message World 2/3 - Level 1/4

(easy but not 100% trivial)
######
#p...#
#.1..#
#13.1#
#61.4#
######

(
Lösung: (1,1) (2,3) 
Verhältnis2: 0.090909
Stückzahl: 7
Erfolg: 1
Gesamt: 32
Interessante: 11
Lösungstiefe: 1
)


message World 2/3 - Level 2/4

(not hard! but non-trivial)

###########
#p4.4.4.4.#
#.2.2.2.2.#
#42.2.2.24#
###########

(
uh, edited/doubled!
Lösung: (1,0) (2,2) 
Verhältnis2: 0.043478
Stückzahl: 7
Erfolg: 1
Gesamt: 61
Interessante: 23
Lösungstiefe: 1
)




message World 2/3 - Level 3/4



(pleasant to solve!)

############
#p..1..1124#
#22.41.6225#
############

(
Lösung: (2,1) (5,1) (6,1) (6,1) 
Verhältnis2: 0.040000
Stückzahl: 13
Erfolg: 1
Gesamt: 142
Interessante: 25
Lösungstiefe: 3
)


message World 2/3 - Level 4/4

(nice! not obvious but can be rationalized)
######
#p...#
#33.3#
#32.2#
#23.3#
######

(
Lösung: (2,1) (2,3) (2,3) 
Verhältnis2: 0.047619
Stückzahl: 9
Erfolg: 1
Gesamt: 69
Interessante: 21
Lösungstiefe: 2
)

message World 3/3 - HARD WORLD
message World 3/3 - Level 1/4


(hard, maybe for after)
############
#q.11.2....#
#6.15.14513#
############

(
Lösung: (6,1) (4,0) (0,1) 
Verhältnis2: 0.013333
Stückzahl: 12
Erfolg: 1
Gesamt: 420
Interessante: 75
Lösungstiefe: 2
)

message World 3/3 - Level 2/4


(I could figure it out!)

######
#p3.4#
#3114#
#2442#
######

(
Lösung: (2,1) (0,2) (2,2) 
Verhältnis2: 0.033333
Stückzahl: 10
Erfolg: 1
Gesamt: 76
Interessante: 30
Lösungstiefe: 2
)

message World 3/3 - Level 3/4


(hardish. try solve again yourself later)
##############
#p4.3.11.4..4#
#13.1441.61.3#
##############

(
Lösung: (1,1) (3,1) (7,1) (10,1) 
Verhältnis2: 0.002688
Stückzahl: 15
Erfolg: 1
Gesamt: 2585
Interessante: 372
Lösungstiefe: 3
)


message World 3/3 - Level 4/4

(I could figure it out :] ) 

######
#w.2.#
#3.32#
#3.33#
#2.33#
######
(
Lösung: (1,0) (0,3) (0,3) 
Verhältnis2: 0.013158
Stückzahl: 11
Erfolg: 1
Gesamt: 225
Interessante: 76
Lösungstiefe: 2
)



message You won!

message [On the next screen the goal is to remove as many sixes as possible.  How many can you get down to? ]


#####################
#11.1.1p.111..1.1.11#
#11.1.1..111..1.1.11#
#11.1.1..111..1.1.11#
#11.1.1..111..1.1.11#
#11.1.1..111..1.1.11#
#11.1.1..111..1.1.11#
#1216.621666216.6211#
#12166622616216.6211#
#1112612166621666211#
#1122222222222222211#
#1126162612612666211#
#1126162612612616211#
#1126666612612616211#
#1122222222222222211#
#1111111111111111111#
#1443441443441443441#
#1443441443441443441#
#1443441443441443441#
#1443441443441443441#
#1111111111111111111#
#####################