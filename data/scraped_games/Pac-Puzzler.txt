title Pac-Puzzler
author Pancake Robot
homepage www.puzzlescript.net

========
OBJECTS
========

Background
darkblue black
11111
01111
11101
11111
10111


PacDot
yellow
.....
.....
..0..
.....
.....

Wall
#0000ff black
00010
11111
01000
11111
00010

PacRight
#ffff00 black
.000.
010..
00...
000..
.000.

PacDown
#ffff00 black
.000.
00000
00.00
0...0
.....

PacLeft
#ffff00 black
.000.
..010
...00
..000
.000.

PacUp
#ffff00 black
.....
0...0
00.00
00000
.000.

Crate
Orange Yellow
00000
01.10
0.1.0
01.10
00000


=======
LEGEND
=======

. = Background
# = Wall
P = PacRight
O = Crate
@ = Crate and PacDot
* = PacDot
PacMan = PacRight or PacLeft or PacDown or PacUp
Player = PacMan
=======
SOUNDS
=======

Crate MOVE 36772507

sfx0 92546706

================
COLLISIONLAYERS
================

Background
PacDot
PacMan, Wall, Crate

======
RULES
======

(PacMan turning visual)
[ right PacMan ] -> [ right PacRight ]
[ up PacMan ] -> [ up PacUp ]
[ left PacMan ] -> [ left PacLeft ]
[ down PacMan ] -> [ down PacDown ]
(end)

[ >  Player | Crate ] -> [  >  Player | > Crate  ]

late [PacMan PacDot] -> [PacMan] sfx0

==============
WINCONDITIONS
==============

no PacDot

=======
LEVELS
=======

message You know what to do.

#########
#***#***#
#..*#*..#
#*#...#*#
#*#.P.#*#
#########