title Flipush: An Exciting Square Game
author Taito
homepage www.puzzlescript.net
again_interval 0.05
run_rules_on_level_start
norepeat_action
key_repeat_interval 0.1

========
OBJECTS
========

Background .
Black

Wall #
Brown DarkBrown
00000
00001
00001
00001
01111

1
Brown
000.0
000.0
000.0
000.0
000.0

2
Brown
0...0
000.0
0...0
0.000
0...0

3
Brown
0...0
000.0
0...0
000.0
0...0

4
Brown
0.0.0
0.0.0
0...0
000.0
000.0

5
Brown
0...0
0.000
0...0
000.0
0...0

6
Brown
0...0
0.000
0...0
0.0.0
0...0

7
Brown
0...0
000.0
000.0
000.0
000.0

8
Brown
0...0
0.0.0
0...0
0.0.0
0...0

9
Brown
0...0
0.0.0
0...0
000.0
000.0

0
Brown
0...0
0.0.0
0.0.0
0.0.0
0...0

Pipe %
Brown DarkBrown
00000
0...1
0...1
0...1
01111

Heart -
Red
.....
..00.
.0..0
.0..0
..00.

NextHeart
Red
.....
.....
..00.
..00.
.....

Ladder1 |
DarkBrown
00000
0...0
0...0
00000
0...0

Ladder2
DarkBrown
0...0
0...0
00000
0...0
0...0

Ladder3
DarkBrown
0...0
00000
0...0
0...0
00000

NormalPlayer
Orange Black
..0..
.000.
01010
00000
.000.

ActionPlayer
Orange Black
.0...
000..
1010.
00000
.000.

AnimationTimer
Transparent

PowerUp PWR Z
White LightGray Red Green Blue Black
00000
00251
00301
05401
11111

BlackX BLK X
Gray DarkGray White
00000
02021
00201
02021
01111

RedCircle RED O
Red DarkRed White
00000
00201
02221
00201
01111

GreenSquare GRN S
Green DarkGreen White
00000
02221
02221
02221
01111

BlueTriangle BLU T
Blue DarkBlue White
00000
00021
00221
02221
01111

Marker
Brown
..0..
..0..
.000.
..0..
.....

Leftward
Transparent (White
.....
.....
1....
.....
.....)

Downward
Transparent (White
.....
.....
.....
.....
..1..)

Matched
Transparent

Hand
Transparent

Only
Transparent

NoWin
Transparent

=======
LEGEND
=======

Block = PWR or BLK or RED or GRN or BLU
PWRBLK = PWR or BLK
PWRRED = PWR or RED
PWRGRN = PWR or GRN
PWRBLU = PWR or BLU

Player = NormalPlayer or ActionPlayer
Ladder = Ladder1 or Ladder2 or Ladder3
LadderBase = Ladder3 or Wall

OtherBlock = Block
Solid = Wall or Block
Deflector = Wall or Pipe
InMotion = Leftward or Downward
Number = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or 0

@ = NormalPlayer and Ladder1
* = Heart and NextHeart

=======
SOUNDS
=======

Player move   16417107
ActionPlayer create 90453905
Sfx0 29560900
Sfx1 35028302
Sfx2 14655702
endlevel 32226908

================
COLLISIONLAYERS
================

Background
Ladder, Hand
Player, Wall, Block, Heart
Pipe

Marker, NextHeart, Number

Only
InMotion
Matched
NoWin
AnimationTimer

======
RULES
======

[NoWin] -> []

(Blocks fall, everyone dies.)
down [Block no InMotion no Hand | no Solid] -> [> Block |]


((( Player movement )))

(Left can be used instead of action.
 Disable rightward-movement.)
[left Player] -> [action Player]
[right Player] -> cancel

(Deduct one from health.
 Cancel the move if we couldn't do so.)
[action Player] [NextHeart] -> [Player] [action NextHeart]
[action Player] -> cancel
[action NextHeart Heart] [Player] -> [] [action Player]
down [no Heart | Heart] -> [| NextHeart Heart]

(Move the block in the player's hand with the player.)
[moving Player | Block] -> [moving Player | moving Block]

(Fire at will, commander.)
[action Block] -> [Leftward Block]

(Player leans left for 5 ticks.)
[action NormalPlayer] -> [ActionPlayer AnimationTimer]
[AnimationTimer] -> [left AnimationTimer] again
[ActionPlayer |||| AnimationTimer] -> [NormalPlayer ||||]


((( Block movement )))

(Apply motion to the moving block.
 Left-bound blocks that can't continue are redirected down.)
left [Leftward Block | Deflector] -> [Downward Block | Deflector]
[Leftward Block] -> [left Leftward left Block]
[Downward Block] -> [down Downward down Block]
[moving Block] -> again

(A moving block clears anything of its own color.
 Note in the player's hand that we've matched something.
 Powerups match anything, then convert to the color of the thing matched.)
[> PWRBLK InMotion | BLK] [Hand] -> [| BLK InMotion] [Matched Hand] Sfx0
[> PWRRED InMotion | RED] [Hand] -> [| RED InMotion] [Matched Hand] Sfx0
[> PWRGRN InMotion | GRN] [Hand] -> [| GRN InMotion] [Matched Hand] Sfx0
[> PWRBLU InMotion | BLU] [Hand] -> [| BLU InMotion] [Matched Hand] Sfx0

(The moving block hit a non-matching block, but previously matched.
 Move the target block to the player's hand.
 Put the moving block in its place.)
[> BLK InMotion | Block] [Matched Hand] -> [| BLK] [Block Hand] Sfx1
[> RED InMotion | Block] [Matched Hand] -> [| RED] [Block Hand] Sfx1
[> GRN InMotion | Block] [Matched Hand] -> [| GRN] [Block Hand] Sfx1
[> BLU InMotion | Block] [Matched Hand] -> [| BLU] [Block Hand] Sfx1

(The moving block hit a non-matching block first.
 Beep angrily and move it back to the player's hand.)
[> Block InMotion | OtherBlock] [Hand] -> [| OtherBlock] [Block Hand] Sfx2

(The moving block hit the floor, but previously matched.
 Move it back to the player's hand.)
[> Block InMotion | Wall] [Matched Hand] -> [| Wall] [Block Hand] Sfx1

(The moving block hit the floor first.
 Beep angrily and move it back to the player's hand.)
[> Block InMotion | Wall] [Hand] -> [| Wall] [Block Hand] Sfx2


((( Late bookkeeping )))

(Keep the player's hand next to the player.)
late [Hand] -> []
late left [Player | ] -> [Player | Hand]

(The random trick:
 Each rule will be applied to exactly one block of each color.
 If there is exactly one, then all blocks will be marked as only.
 If there are more, then one will remain unmarked (we don't care which).)
late [Only] -> []
random late [BLK] -> [Only BLK]
random late [RED] -> [Only RED]
random late [GRN] -> [Only GRN]
random late [BLU] -> [Only BLU]

(Blocks want to fall.
 Run another tick, and mark that the player shouldn't win yet.)
late down [Block no InMotion no Hand | no Solid] -> [NoWin Block |] again

(Put a marker where a downward block will go.)
late [Marker] -> []
late [Player | Block |] -> [Player | Block | Marker]
late left [Marker | no Deflector no Block] -> [| Marker]
late left [Marker | Block] -> [| Block]
late down [Marker | no Wall no Block] -> [| Marker]

(Make ladder pretty.)
late up [LadderBase || Ladder1 | Ladder1] -> [LadderBase || Ladder2 | Ladder3]

==============
WINCONDITIONS
==============

no NoWin
some Block on Hand
all Block on Only

=======
LEVELS
=======

#############.
#####......|#*
####.......|#-
###........|#-
##.........|#-
#..........|#-
#..........|#-
#..........|#-
#..........|#-
#xsss......|#-
#oxxx......|#-
#tttt......|#-
#sooo.....t@1-
#############-

#############.
#####......|#*
####.......|#-
###........|#-
##.........|#-
#..........|#-
#..........|#-
#..........|#-
#..........|#-
#xooo......|#-
#sxxx......|#-
#tooo......|#-
#oxxx.....z@2-
#############-

#############.
#####......|#*
####.......|#-
###........|#-
##.........|#-
#..........|#-
#..........|#-
#..........|#-
#..........|#-
#oooo......|#-
#ttto......|#-
#oooo......|#-
#txso.....z@3-
#############-

#############.
#####......|#*
####.......|#-
###........|#-
##.........|#-
#..........|#-
#..........|#-
#..........|#-
#..........|#-
#xoxo......|#-
#xoxo......|#-
#oxox......|#-
#stox.....z@4-
#############-

#############.
#####......|#*
####.......|#-
###........|#-
##.........|#-
#..........|#-
#..........|#-
#..........|#-
#..........|#-
#xsto......|#-
#stox......|#-
#stox......|#-
#stox.....z@5-
#############-

#############.
######.....|#*
###........|#-
##.........|#-
#..........|#-
#..........|#-
#..........|#-
#..........|#-
#......%...|#-
#ttss......|#-
#oott..%...|#-
#txxx......|#-
#ostx.....s@6-
#############-

message That's all for now!