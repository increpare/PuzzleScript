title Possibility Space
run_rules_on_level_start
verbose_logging
noaction
require_player_movement

(
Thanks for digging through the source code! Sadly, how possibility space is displayed in here is super faked and hacky. So be warned!

Based of the Microban example. To see the original game, open the puzzlescript editor, open the "Load Example" dropdown, and select Microban.)
)

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

OriginalPlayer
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

CopyPlayer D
Black Orange Yellow Orange
.000.
.111.
22222
.333.
.3.3.


OriginalCrate
Orange Yellow
00000
0...0
0...0
0...0
00000

CopyCrate Y
Orange Yellow
00000
0...0
0...0
0...0
00000

FuturePos F
TRANSPARENT

FutureCrate R
TRANSPARENT

RightArrow >
black
..0..
..00.
00000
..00.
..0..

LeftArrow <
black
..0..
.00..
00000
.00..
..0..

UpArrow U
black
..0..
.000.
00000
..0..
..0..

DownArrow K
black
..0..
..0..
00000
.000.
..0..

=======
LEGEND
=======

. = Background
# = Wall
P = OriginalPlayer
* = OriginalCrate
@ = OriginalCrate and Target
O = Target

Player = OriginalPlayer or CopyPlayer
Crate = OriginalCrate or CopyCrate

=======
SOUNDS
=======

OriginalCrate MOVE 36772507
endlevel 83744503
startgame 92244503

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate
FuturePos
FutureCrate
UpArrow, DownArrow, LeftArrow, RightArrow

======
RULES
======

(Clearing up from last turn)
[CopyPlayer] -> []
[CopyCrate] -> []
[FuturePos] -> []
[FutureCrate] -> []

(Push boxes and stop movement for main player)
[ >  Player | Crate ] -> [  >  Player | > Crate  ]
[ > Player | Crate | Crate] -> [Player | Crate | Crate]
[ > Player | Wall] -> [Player | Wall]
[ > Player | Crate| Wall] -> [Player | Crate| Wall]

(Copy crates)
[ > OriginalCrate | ] -> [ OriginalCrate | FutureCrate]
[ OriginalCrate] -> [OriginalCrate FutureCrate]
up [FutureCrate |||||||| ] -> [FutureCrate |||||||| CopyCrate]
down [FutureCrate |||||||| ] -> [FutureCrate|||||||| CopyCrate]
left [FutureCrate ||||||| ] -> [FutureCrate |||||||  CopyCrate]
right [FutureCrate ||||||| ] -> [FutureCrate ||||||| CopyCrate]

(Copy Player)
[ > OriginalPlayer | ] -> [ > OriginalPlayer | FuturePos]
[ stationary OriginalPlayer] -> [OriginalPlayer FuturePos]
up [FuturePos |||||||| ] -> [FuturePos |||||||| up CopyPlayer]
down [FuturePos |||||||| ] -> [FuturePos |||||||| down CopyPlayer]
left [FuturePos ||||||| ] -> [FuturePos ||||||| left CopyPlayer]
right [FuturePos ||||||| ] -> [FuturePos ||||||| right CopyPlayer]

(Move copied entities)
[ >  Player | Crate ] -> [  >  Player | > Crate  ]
[ > Player | Crate | Crate] -> [Player | Crate | Crate]
[ > Player | Wall] -> [Player | Wall]
[ > Player | Crate| Wall] -> [Player | Crate| Wall]


==============
WINCONDITIONS
==============

All OriginalCrate on Target

=======
LEVELS
=======

message level 1 of 3


.......####.........
.......#.o#.........
.......#..###.......
.......#o...#.......
.......#....#.......
.......#..###.......
.......####.........
.........U..........
####...####...####..
#.o#...#.o#...#.o#..
#..###.#..###.#..###
#o...#<#@p..#>#o...#
#....#.#..*.#.#....#
#..###.#..###.#..###
####...####...####..
.........K..........
.......####.........
.......#.o#.........
.......#..###.......
.......#o...#.......
.......#....#.......
.......#..###.......
.......####.........

message level 2 of 3

.......######.......
.......#....#.......
.......#....#.......
.......#..o.#.......
.......#.oo.#.......
.......#....#.......
.......######.......
.........U..........
######.######.######
#....#.#....#.#....#
#....#.#.#p.#.#.#..#
#..o.#<#.*@.#>#..o.#
#.oo.#.#.O@.#.#.oo.#
#....#.#....#.#....#
######.######.######
..........K.........
.......######.......
.......#....#.......
.......#....#.......
.......#..o.#.......
.......#.oo.#.......
.......#....#.......
.......######.......

message level 3 of 3

.......########.......
.......#......#.......
.......#.OOO..#.......
.......#......#.......
.......#####..#.......
...........####.......
..........U...........
......................
######################
#......#......#......#
#.OOO..#.O@@*P#.OOO..#
#......#......#......#
#####..#####..#####..#
....###<...####>..####
......................
...........K..........
.......########.......
.......#......#.......
.......#.OOO..#.......
.......#......#.......
.......#####..#.......
...........####.......

message solved!
