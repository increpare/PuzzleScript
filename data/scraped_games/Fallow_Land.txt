title Fallow Land
author Le Slo
homepage www.puzzlescript.net

run_rules_on_level_start

========
OBJECTS
========

PlayerT
#8d8b90 #f4f0eb
.....
.....
.011.
01111
01111

Sprout
#dee4ad
.....
.....
.0.0.
..0..
..0..

CrateT
#ca4e9b #f54cac #80ab47
.....
.....
.....
.....
..1..

Crate
#ca4e9b #f54cac #80ab47
.011.
.001.
2.2.2
.2.2.
..2..

deadcrate
#ca4e9b #f54cac #807b47
...2.
..2..
2.2.2
.2.2.
..2..

Player
#8d8b90 #2b7f94 #2b4f84
.000.
.2..1
..12.
1....
.1.1.

PlayerToxic
#8d8b90 #bbcf14 #8b9f04
.000.
.2..1
..12.
1....
.1.1.

Background
#78735d #b0a987 #cec8bc
00000
11111
11111
11111
22222

fallowLandE
red #b0a987 red
00000
11111
11111
11111
22222

fallowLand
#78735d #b0a987 #cec8bc
00000
11111
11111
11111
22222


Target
#3f6a0d #9dc640 #cde640
00000
11111
11111
11111
22222

cross
#663c13 #e09d44 #f6d180
00000
11111
11111
11111
22222

ToxicLand
#3c5c3c #9dbd9d #daeada
00000
11111
11111
11111
22222



WallT
#4d702a
.....
.....
.....
.....
00000

Wall
#233216 #2e4b19 #4d702a
01112
01112
01112
00002
01110



Trail
transparent

flowerWall
transparent

cantMove
transparent



=======
LEGEND
=======

. = Background
# = Wall and flowerWall
P = Player
I = Player and fallowLandE
* = Crate
@ = Crate and Target
O = Target
x = cross
f = fallowLandE
t = ToxicLand
r = crate and cross

crates = crate or deadcrate
=======
SOUNDS
=======

Crate MOVE 36772507

sfx0 70832502
sfx1 67827703
sfx2 61743103

restart 87115708

endlevel 75839708

startgame 81871308
================
COLLISIONLAYERS
================

Background
cantMove
fallowLand, fallowLandE, ToxicLand
trail
Sprout
Target

cross
Crates, flowerWall
Player, Wall


crateT
PlayerT, wallT

PlayerToxic

======
RULES
======

[player PlayerToxic no ToxicLand] -> [player]


[ >  Player PlayerToxic ] -> [> Player > PlayerToxic]
[ >  Player ] -> [> Player trail]
([ >  Player ToxicLand| crate ] -> [  >  Player ToxicLand| deadcrate]
[ >  Player ToxicLand| sprout ] -> [  >  Player ToxicLand| ])
[ >  Player no ToxicLand| Crates ] -> [  >  Player | > Crates  ]
[ >  Crates | Crates ] -> [  >  Crates | > Crates  ]


Startloop

[ > crates | wall ] 	-> [ > crates cantMove| wall  ]
[ > crates no cantMove| cantMove ] 	-> [  > crates cantMove| cantMove]
[ > player no playerToxic| cantMove ]	-> [ player | cantMove]

endloop
late [cantMove] -> []

late [trail Sprout no player fallowLand] -> [Crate no fallowLand toxicLand] sfx2
late [trail Sprout no player no deadcrate] -> [Crate] sfx2
late [trail no player no target no cross no ToxicLand no deadcrate] -> [Sprout] sfx1
late [trail] -> []



late [player ToxicLand no PlayerToxic] -> [player ToxicLand PlayerToxic]
late[PlayerToxic no Player] -> []

late [crate playerToxic] -> [deadcrate PlayerToxic] AGAIN sfx0
late [sprout playerToxic] -> [playerToxic] AGAIN sfx0

late[fallowLandE] -> [fallowLand]
late[player playerToxic no ToxicLand] -> [player playerToxic no ToxicLand] AGAIN 


late down[playerT | no player] -> [ | ]
late up [player | no playerT] -> [player | playerT]

late down[wallT | no wall] -> [ | ]
late up [wall | no wallT] -> [wall | wallT]

late down[crateT | no crate] -> [ | ]
late up [crate | no crateT] -> [crate | crateT]
==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

message Part I: A field 

message ...Y
####
#.o#
#..#
#..#
#op#
####

message .Y.Y

########
#....###
#..#...#
#.oooop#
#...#..#
###....#
########

message .YYY

######
#....#
#.oo.#
#.op.#
#....#
######

message YYYY

######
#o.###
#o..##
#op..#
#oooo#
######

message Part II : A tired field

message .Y...

#######
##.####
##.p.##
#ooxoo#
##...##
###.###
#######

message .Y..Y

########
###..###
###..###
#ooxxoo#
###.p###
###..###
########

message YY..Y

########
#....###
#xx#p.x#
#xoooox#
#x..#xx#
###....#
########

message YYY.Y

######
######
#....#
#.p..#
##.x##
##ooo#
##ooo#
######

message YYYYY

######.
#ooo.#.
#opo.##
#oooxx#
#..xxx#
###xxx#
..#####

message Part III : A resented field

message Y....

########
#ffffff#
#ffffxf#
#fifxof#
#ffffff#
########

message YY...

######
##ofo#
#ofif#
##off#
######

message YY.Y.

####
#oo#
#if#
#ff#
#ff#
#oo#
####

message YY.YY

.########
.###xxx##
###xfxxx#
#oxoffix#
###xfxxx#
####xxx##
.########

message YYYYY

##########
#xxxfffxx#
#xrrfixox#
####fxxxx#
####xoxox#
####xxxxx#
##########







