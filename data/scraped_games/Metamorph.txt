title Metamorph
author tim-timman
homepage www.puzzlescript.net

key_repeat_interval 0.2
norepeat_action

(we want realtime to be able to handle gravity without infinite loops)
realtime_interval 0.2
throttle_movement

(just for debugging)
(verbose_logging)

========
OBJECTS
========

Background 
Grey

Wall 
DarkGrey

Goal
Black White
01010
10101
01010
10101
01010

Metamorph
Green

(Fire Element)
FireBlock
Red
Fire
Red
.....
..0..
.000.
..0..
.....

(Water element)
WaterBlock
Blue
Water
Blue
.....
..0..
.000.
..0..
.....
WaterTemp
DarkBlue

(Air element)
AirBlock
LightBlue
Air
LightBlue
.....
..0..
.000.
..0..
.....

(Earth Element)
EarthBlock
Brown
Earth
Brown
.....
..0..
.000.
..0..
.....

Shadow
Transparent

Teleporting
White Black
0.0.0
.111.
0.1.0
..1..
0.0.0

Dashing
White Black
.....
011.0
01.10
011.0
.....
DashTemp
lightgrey

Ghost
LightGreen
00000
0...0
0...0
0...0
00000

Skull
White
.000.
0.0.0
00.00
.000.
.0.0.

Spike
Black
.....
..0..
..0..
.000.
00000

=======
LEGEND
=======

Element = Fire or Water or Air or Earth
Block = FireBlock or WaterBlock or AirBlock or EarthBlock

Player = Metamorph or Ghost or Skull

Temp = WaterTemp or DashTemp

Object = Wall or Block or Player or Temp

. = Background
# = Wall
F = FireBlock
W = WaterBlock
A = AirBlock
E = EarthBlock
P = Metamorph
G = Goal
S = Spike

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Goal
Shadow
Metamorph, Wall, Block, Spike
Element
Dashing, Teleporting
Ghost
Temp
Skull

======
RULES     
======

(Winning)
[ Metamorph Goal ] -> Win
(Loosing)
[ MOVING Skull ] -> Restart

[ Player ] -> [ Player Shadow ]
(
(Don't allow to move when falling)
DOWN [ MOVING Metamorph | NO Object ] -> Cancel
)
(Handling falling)
DOWN [ STATIONARY Metamorph | NO Object ] -> [ DOWN Metamorph | ]


( Toggling: Dash -> Teleport -> Move )
[ ACTION Metamorph No Dashing No Teleporting ] -> [ Metamorph Dashing ]
[ ACTION Metamorph Dashing ] -> [ Metamorph Teleporting Ghost]
[ ACTION Metamorph Teleporting Ghost ] -> [ Metamorph ]

(Handling dash mode)
[ VERTICAL Metamorph Dashing ] -> Cancel
[ HORIZONTAL Metamorph Dashing ] -> [ HORIZONTAL Metamorph DashTemp ] again
[ Metamorph | ... | DashTemp ] -> [ < Metamorph | ... | DashTemp ] again
(Max dash distance is 5)
[ HORIZONTAL Metamorph | | | | | DashTemp ] -> [ Metamorph | | | | | ]


(Teleporting into a wall or block kills you)
[ ACTION Ghost Wall ] -> [ Wall Skull ]
[ ACTION Ghost Block ] -> [ Block Skull ]
(Teleporting moves you to the new location)
[ ACTION Ghost ] [ Metamorph Teleporting ] -> [ Metamorph Ghost ] [ ]

late [ Skull ] [ Metamorph Teleporting ] -> [ Skull ] [ ]

(Handling teleporting only in orthogonal directions up to max 4 spaces)
[ MOVING Metamorph ] [ Ghost ] -> [ Metamorph ] [ Ghost ]
ORTHOGONAL [ < Ghost | | | | Teleporting ] -> Cancel
ORTHOGONAL [ v Ghost | ... | Teleporting ] -> Cancel
ORTHOGONAL [ ^ Ghost | ... | Teleporting ] -> Cancel


(Disallow moving up, have this before effects)
[ UP Metamorph ] -> [ Metamorph ]

( +++ BLOCK EFFECTS +++ )

("Steam explosion reaction" - Send player flying in opposite direction
Setup like this to avoid lots of looping)
[ Metamorph Fire | WaterBlock ] -> [ < Metamorph Fire WaterTemp | WaterBlock ] again
[ Metamorph Fire | WaterTemp  ] -> [ < Metamorph Fire WaterTemp | WaterTemp  ] again
(Die if you can't move away from steam explosion)
late [ Metamorph Shadow WaterTemp | WaterBlock ] -> [ Skull | WaterBlock ]

(Transfering Elemental powers)
late [ Metamorph NO Element | FireBlock  ] -> [ Fire Metamorph  | FireBlock  ]
late [ Metamorph NO Element | WaterBlock ] -> [ Water Metamorph | WaterBlock ]
late [ Metamorph NO Element | EarthBlock ] -> [ Earth Metamorph | EarthBlock ]
late [ Metamorph NO Element | AirBlock   ] -> [ Air Metamorph   | AirBlock   ]

( --- BLOCK EFFECTS --- )
 
(Dying)
DOWN [ DOWN Metamorph | Spike ] -> [ Skull | Spike ]

(Elements should follow the Metamorph on moving)
[ MOVING Metamorph STATIONARY Element ] -> [ MOVING Metamorph MOVING Element ]
(Move elemment back onto metamorph in case our move was blocked, unless we teleported)
late [ Metamorph Shadow NO Ghost | Element] -> [ Metamorph Shadow Element | ]
(Clean up any Ghosts when we are no longer teleporting)
late [ Metamorph Ghost NO Teleporting ] -> [ Metamorph ]
(Clean up any elements that are not on the metamorph)
late [ Element NO Metamorph ] -> [ ]
(We've stopped auto moving. Remove any used temps)
late [ Player Shadow ] [ Temp ] -> [ Player Shadow ] [ ]
(Turn's over; remove the shadow)
late [ Shadow ] -> [ ]


==============
WINCONDITIONS
==============

=======     
LEVELS
=======

##########
#...######
#.P.######
###.######
###.######
###.######
###.....G#
##########

###########
#.........#
#P........#
###SSSS##.#
########..#
##.......##
##S#.#S####
####.######
#....######
#.####....#
#........G#
##SS#S#S###

############
#...###....#
#P..###...G#
############

################
#.P..A..#F.....#
###..F..#......#
#....E..#......#
#....W..#......#
#.......#.....G#
##########W#####
