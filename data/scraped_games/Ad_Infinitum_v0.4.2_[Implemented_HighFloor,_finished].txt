title Ad Infinitum v0.4.2 [Implemented HighFloor, finished]
author Tom Hermans @Auroriax
homepage auroriax.itch.io
background_color #151515
text_color #FBC403
(run_rules_on_level_start)

(verbose_logging)

noaction

(You have a bunch of mirrors. Create infinities.)

(https://www.schemecolor.com/black-gold-color-palette.php for color palette)

========
OBJECTS
========

Background .
#353535

HighFloor ,
#777777

WallTop 1
#0A0A0A #252525
10101
01010
00000
00000
00000

WallDef 0
#0A0A0A #252525
00000
00000
00000
00000
00000

WallBottom 2
#0A0A0A #252525
00000
00000
00000
00000
01010


Cliff H
Black #28253C transparent
11111
00000
00000
00000
20002

Player P
white black blue
.000.
00000
00210
00110
.000.

LaserR
#DBA403 (#FBC403)
.....
.....
..000
.....
.....

LaserU
#FBC403
..0..
..0..
..0..
.....
.....

LaserL
#DBA403 (#FBC403)
.....
.....
000..
.....
.....

LaserD
#FBC403
.....
.....
..0..
..0..
..0..

Mirror1 /
#FBFCD4 transparent
...1.
..101
.101.
101..
.1...

VictoryMirror1
#FFDD03 #FFDD03
...0.
..010
.010.
010..
.0...

Mirror2 \
#FBFCD4 transparent
.1...
101..
.101.
..101
...1.

VictoryMirror2
#FFDD03 #FFDD03
.0...
010..
.010.
..010
...0.

MirrorVert I
black gray
..0..
..0..
..0..
..0..
..0..

MirrorHor _
black gray
.....
.....
00000
.....
.....

(SplitterR >
gray
0....
000..
00000
000..
0....)

EmitR 6
yellow
0....
000..
00000
000..
0....

TeleportBlue b
blue white
.0.1.
10100
.101.
00101
.1.0.

Citizen C
white
..0..
.000.
..0..
.0.0.
.0.0.

Statue S
black
..0..
.000.
..0..
.0.0.
.0.0.

GameOver
gray black blue
.000.
00000
00210
00110
.000.

BulbOff +
Gray Yellow
.000.
0...0
0...0
.0.0.
.000.

BulbOn
Gray Orange
.000.
01110
01110
.010.
.000.

=======
LEGEND
=======

(UnlockedPlayer = PlayerDown or PlayerRight or PlayerLeft or PlayerUp)
(LockedPlayer = LockedPlayerDown or LockedPlayerRight or LockedPlayerLeft or LockedPlayerUp)
Laser = LaserR or LaserU or LaserL or LaserD
Mirror = Mirror1 or Mirror2 or VictoryMirror1 or VictoryMirror2 or MirrorVert or MirrorHor (or SplitterR) (or FrozenMirror1 or FrozenMirror2 or FrozenSplitterR)
(FrozenMirror = FrozenMirror1 or FrozenMirror2 or FrozenSplitterR)
Teleport = TeleportBlue

M/ = Mirror1 or VictoryMirror1
M\ = Mirror2 or VictoryMirror2
(SR = SplitterR or FrozenSplitterR)

Shootable = Mirror or Teleport

NormalMirror = Mirror1 or Mirror2
VictoryMirror = VictoryMirror1 or VictoryMirror2

Emit = EmitR

Wall = WallDef or WallTop or WallBottom

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
HighFloor
Cliff
Player, Wall, Mirror, Statue, Citizen, BulbOff, BulbOn, GameOver, Emit
Teleport
LaserU
LaserR
LaserD
LaserL

======
RULES     
======  

down [no Wall no WallTop | Wall] -> [ | WallTop]
up [no Wall no WallBottom | Wall] -> [ | WallBottom ]

[Laser] -> []
[BulbOn] -> [BulbOff]
[VictoryMirror1] -> [Mirror1]
[VictoryMirror2] -> [Mirror2]
[GameOver] -> cancel

(Absorb player movement so I don't have to mess with late rules)
[> Player | Mirror] -> [> Player | > Mirror]
[> Mirror | Mirror] -> [> Mirror | > Mirror]
[> Mirror | no wall no BulbOff no BulbOn no Mirror no Wall no Player no HighFloor] -> [ | Mirror]
[> Mirror] -> [Mirror]
[> Player | no wall no BulbOff no BulbOn no Mirror no Wall no Player] -> [ | Player]
[> Player] -> [Player]

(Dump stuff off cliffs)
[Mirror Cliff] -> [Cliff]
[Player Cliff] -> [Cliff]

(Mirrors: Fire lasers first, then check for valid combinations.)
up [Shootable no LaserU | ... | Shootable] -> [Shootable action LaserU| ... | Shootable]
down [Shootable no LaserD | ... | Shootable] -> [Shootable action LaserD| ... | Shootable]
left [Shootable no LaserL | ... | Shootable] -> [Shootable action LaserL| ... | Shootable]
right [Shootable no LaserR | ... | Shootable] -> [Shootable action LaserR| ... | Shootable]

[M/ LaserU LaserL no LaserR no LaserD] -> [VictoryMirror1 LaserU LaserL]
[M/ no LaserU no LaserL LaserR LaserD] -> [VictoryMirror1 LaserR LaserD]
[M/ LaserU LaserL LaserR LaserD] -> [VictoryMirror1 LaserU LaserL LaserR LaserD]

[M\ LaserU LaserR no LaserL no LaserD] -> [VictoryMirror2 LaserU LaserR]
[M\ no LaserU no LaserR LaserL LaserD] -> [VictoryMirror2 LaserL LaserD]
[M\ LaserU LaserL LaserR LaserD] -> [VictoryMirror2 LaserU LaserL LaserR LaserD]

(Other mirror types here QQQ)

[Laser no VictoryMirror] -> []

(Continue laser)
right [action LaserR | no Shootable] -> [LaserR | action LaserR]
left [action LaserL | no Shootable ] -> [LaserL | action LaserL]
up [action LaserU | no Shootable ] -> [LaserU | action LaserU]
down [action LaserD | no Shootable] -> [LaserD | action LaserD]
[action Laser TeleportBlue] [TeleportBlue] -> [TeleportBlue] [action Laser TeleportBlue]

(Activate bulbs)
[Laser BulbOff] -> [Laser BulbOn]

(Stone citizens)
[Laser Citizen] -> [Laser Statue]

==============
WINCONDITIONS
==============  

No BulbOff
No NormalMirror
All Player on Laser

=======     
LEVELS
=======

message You have a bunch of mirrors. Create infinities.

message ALPHA
000000
0..\.0
0/p..0
0.../0
0.\..0
000000

message BETA
00000000000
00...\00000
0.\....0000
0.\....0000
0.....\//p0
0......0000
00000000000

message GAMMA
000000000
0.......0
0./\....0
0.\.0p..0
0..0\0..0
0...0.\.0
0....\/.0
0.......0
000000000

message DELTA
00000000
0/0000\0
00..p000
0.//.000
0.\..000
0.\\.000
0....0/0
00000000

message EPSILON
00000000
0....\.0
0.0.\p.0
0/0...\0
0....\.0
0...00.0
0./....0
00000000

message ZETA
00000000
000....0
0...00.0
0......0
0.\.p..0
0./\0..0
0.\00..0
0.\/\.00
0.....00
00000000

message THETA

(000000000
0000...\0
00...p2.0
0..///..0
0..///..0
0.2....00
0\...0000
000000000)
(lddll urddr urrll uurdl dllul drrd) (Usable somewhat elegant version)

000000000
00000..\0
00.0./..0
00.././.0
00..p..00
0././.000
0../...00
0\..00000
000000000
(SOLVABLE , 8 mirrors, one loop, 6/2)

message Unfinished - 8 mirrors, one loop, 6/2
..../\.
./\....
.......
.00000.
.\.../.
..\./..
.....p.

...0...
./\0/\.
.......
.00000.
.\.../.
..\0/..
...0.p.
(Trivial)

...0...
.0.0.0.
...0...
./\0/\.
.......
.00000.
.\.../.
..\0/..
...0.p.
(Unsolvable - This puzzle would need one-directional tiles to work)

000000000
0,,,0,,,0
0./\0/\.0
0.......0
0.00000.0
0.\.../.0
0..\0/..0
0...0.p.0
000000000

message IOTA
(- 8 mirrors, two loops, 4/4)
.../\
...\/
/.\..
.....
\./.p

.....
./.\.
/.\..
.\./.
\./.p

000000000
0.......0
0.////..0
0..0....0
0.0.0...0
0.\\\\p.0
0.......0
000000000

0222222
.......
..////.
...2...
..2\2..
..\\\\.
...p...
0111111
(Interesting but unsolvable?)

0222222
.......
..//\\.
...2...
..2.2..
..\\//.
...p...
0111111
(Way to easy!)

.........
.0000000.
.........
0../\\\.0
0...2...0
0..2.2..0
0..\///.0
....p....
.0111111.
.........
(Interesting, solvable but can be cheated, would also need one-directional blocks)

........
.000000.
........
0./\\\.0
0...2..0
0..2...0
0.\///.0
....p...
.011111.
........
(Little but more elegant but not sure if solvable)

00000000000
000,,,,,000
00,,,,,,,00
0,,./\\\,,0
0,,..2..,,0
0,,.2.2.,,0
0,,p\///,,0
00,,,,,,,,0
00000,,,,,0
00000000000
(Added new gimmick to prevent cheating, solvable)

message Unfinished - 7 mirrors, two loops, 4/3

../.\
.....
/.\./
.....
\./.p

message Unfinished - 11 mirrors, one loop, 7/4

./..\
../\.
/../.
.\/./
\./.p

message Unfinished - 10 mirrors, three loops, 5/5
.......
.......
../\...
./\/.\.
.\/....
...\./.
.....p.

message Unfinished
../\.
./\\\
.\./.
.p\./

.p.....
..////.
.\...00
.\...\0
.\....0
..0\./0
..00000

message Unordered - 12 mirrors, one loop, 6/6

000000000
0.......0
0.p.....0
0.../\..0
0..//\\.0
0..\\//00
0000\/000
000000000 (I2, 12 mirrors, one loop)

message Unfinished - 13 mirrors, four loops, ?/?
..........
...../.\..
..........
.../.\./..
..........
../\./..\.
..\/......
......p...
.....\../.

message Fin.

(----------------------------------)

(Testing)
///.......
./.....p..
..../.....
...//.....
..../.....
....../...
.....///..
......../.
........//
......../.

(Pill-shaped mirror setups)
../\.../\.../\
./././../././.
/./...//..././
\/..p\/...\/..