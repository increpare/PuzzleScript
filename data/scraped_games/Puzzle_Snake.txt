title Puzzle Snake
author Thordur Agust
homepage www.puzzlescript.net
run_rules_on_level_start
require_player_movement

========
OBJECTS
========

background 
yellow

wall 
brown

wrap
black white
00000
01010
00000
01010
00000

wrapUp
green

wrapDown
green

wrapLeft
green

wrapRight
green


playerUp
#D8AD6D #946F48 yellow
.111.
10001
12021
10001
10001

playerDown
#D8AD6D #946F48 yellow
10001
10001
12021
10001
.111.

playerLeft
#D8AD6D #946F48 yellow
.1111
10200
10000
10200
.1111

playerRight
#D8AD6D #946F48 yellow
1111.
00201
00001
00201
1111.

bodyUp
#D8AD6D #946F48
10101
10001
10001
10001
10001

bodyDown
#D8AD6D #946F48
10001
10001
10001
10001
10101

bodyLeft
#D8AD6D #946F48
11111
00000
10000
00000
11111

bodyRight
#D8AD6D #946F48
11111
00000
00001
00000
11111

bodyConLeftDown
#D8AD6D #946F48
1111.
00001
00001
00001
10001

bodyConLeftUp
#D8AD6D #946F48
10001
00001
00001
00001
1111.

bodyConRightDown
#D8AD6D #946F48
.1111
10000
10000
10000
10001

bodyConRightUp
#D8AD6D #946F48
10001
10000
10000
10000
.1111

bodyEndUp
#D8AD6D #946F48
.111.
10001
10001
10001
10001

bodyEndDown
#D8AD6D #946F48
10001
10001
10001
10001
.111.

bodyEndLeft
#D8AD6D #946F48
.1111
10000
10000
10000
.1111

bodyEndRight
#D8AD6D #946F48
1111.
00001
00001
00001
1111.

oldPosUp
green

oldPosDown
green

oldPosLeft
green

oldPosRight
green

crate 
orange
00000
00000
00.00
00000
00000

food
red

newBody
pink

hasMoved
green

=======
LEGEND
=======

. = background
# = wall
w = playerUp
s = playerDown
a = playerLeft
d = playerRight
1 = bodyUp
2 = bodyDown
3 = bodyLeft
4 = bodyRight
F = food
* = crate
O = wrap

player = playerLeft or playerRight or playerUp or playerDown
playerBody = bodyLeft or bodyRight or bodyUp or bodyDown or bodyConLeftDown or bodyConLeftUp or bodyConRightDown or bodyConRightUp or bodyEndUp or bodyEndDown or bodyEndLeft or bodyEndRight
oldPos = oldPosUp or oldPosDown or oldPosLeft or oldPosRight
wrapPos = wrapUp or wrapDown or wrapLeft or wrapRight
thing = playerBody or wall or food or wrap

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

background
wrap
wrapUp, wrapDown
wrapLeft, wrapRight
food
oldPos
player, wall
crate
playerBody
newBody
hasMoved

======
RULES     
======     

up [ wrap | no thing ] -> [ wrap | wrapUp ]
down [ wrap | no thing ] -> [ wrap | wrapDown ]
left [ wrap | no thing ] -> [ wrap | wrapLeft ]
right [ wrap | no thing ] -> [ wrap | wrapRight ]

[ > player | crate ] -> [ > player | > crate ]
[ > player | > crate | wall ] -> [ player | crate | wall ]

[ up playerDown | bodyDown ] -> [ playerDown | bodyDown ]
[ down playerUp | bodyUp ] -> [ playerUp | bodyUp ]
[ left playerRight | bodyRight ] -> [ playerRight | bodyRight ]
[ right playerLeft | bodyLeft ] -> [ playerLeft | bodyLeft ]

[ up player ] -> [ up playerUp ]
[ down player ] -> [ down playerDown ]
[ left player ] -> [ left playerLeft ]
[ right player ] -> [ right playerRight ]

[ up player ] -> [ up player oldPosUp ]
[ down player ] -> [ down player oldPosDown ]
[ left player ] -> [ left player oldPosLeft ]
[ right player ] -> [ right player oldPosRight ]

startloop
up [ bodyUp no hasMoved | oldPosUp ] -> [ oldPosUp | bodyUp hasMoved ]
up [ bodyUp no hasMoved | oldPosLeft ] -> [ oldPosUp | bodyLeft hasMoved ]
up [ bodyUp no hasMoved | oldPosRight ] -> [ oldPosUp | bodyRight hasMoved ]
down [ bodyDown no hasMoved | oldPosDown ] -> [ oldPosDown | bodyDown hasMoved ]
down [ bodyDown no hasMoved | oldPosLeft ] -> [ oldPosDown | bodyLeft hasMoved ]
down [ bodyDown no hasMoved | oldPosRight ] -> [ oldPosDown | bodyRight hasMoved ]
left [ bodyLeft no hasMoved | oldPosLeft ] -> [ oldPosLeft | bodyLeft hasMoved ]
left [ bodyLeft no hasMoved | oldPosUp ] -> [ oldPosLeft | bodyUp hasMoved ]
left [ bodyLeft no hasMoved | oldPosDown ] -> [ oldPosLeft | bodyDown hasMoved ]
right [ bodyRight no hasMoved | oldPosRight ] -> [ oldPosRight | bodyRight hasMoved ]
right [ bodyRight no hasMoved | oldPosUp ] -> [ oldPosRight | bodyUp hasMoved ]
right [ bodyRight no hasMoved | oldPosDown ] -> [ oldPosRight | bodyDown hasMoved ]
endloop

[ > player | > crate | playerBody ] -> [ player | crate | playerBody ]

late [ player playerBody ] -> [ player newBody ]
(
late left [ playerLeft crate | no wall ] -> [ playerLeft | crate ]
late right [ playerRight crate | no wall ] -> [ playerRight | crate ]
late up [ playerUp crate | no wall ] -> [ playerUp | crate ]
late down [ playerDown crate | no wall ] -> [ playerDown | crate ]
)

late [ playerUp wrap | ... | wrapUp ] -> [ wrap | ... | wrapUp playerUp ]
late [ playerDown wrap | ... | wrapDown ] -> [ wrap | ... | wrapDown playerDown ]
late [ playerLeft wrap | ... | wrapLeft ] -> [ wrap | ... | wrapLeft playerLeft ]
late [ playerRight wrap | ... | wrapRight ] -> [ wrap | ... | wrapRight playerRight ]

late [ crate wrap | ... | wrapPos ] -> [ wrap | ... | wrapPos crate ]

late [ oldPosUp newBody ] -> [ bodyUp ]
late [ oldPosDown newBody ] -> [ bodyDown ]
late [ oldPosLeft newBody ] -> [ bodyLeft ]
late [ oldPosRight newBody ] -> [ bodyRight ]
late [ player food ] -> [ player newBody ]

late [ oldPos ] -> []
late [ hasMoved ] -> []

==============
WINCONDITIONS
==============

=======     
LEVELS
=======

#########
#.......#
#.......#
#...*...#
#.......#
#.......#
#########
