title Lime Richard
author Franklin P. Dyer
again_interval 0.1

========
OBJECTS
========

Background .
#9cc4dd

Exit #
Red
.000.
00000
00000
00000
.000.

Apple $
Blue
.000.
00000
00000
00000
.000.

PlayerBodyH
Green
.000.
00000
0...0
00000
.000.

PlayerBodyV
Green
.000.
00.00
00.00
00.00
.000.

Crate +
Orange
00000
0...0
0...0
0...0
00000

Target o
Red
.....
.000.
.000.
.000.
.....

Verttarget |
Red
.....
..0..
..0..
..0..
.....

Horitarget -
Red
.....
.....
.000.
.....
.....

PlayerHead1 @
LightGreen Green
.000.
01010
00000
00000
.000.

PlayerHead2
LightGreen Yellow
.000.
01010
00000
00000
.000.

PlayerHead3
LightGreen Orange
.000.
01010
00000
00000
.000.


PlayerHead4
LightGreen Red
.000.
01010
00000
00000
.000.

Dirt /
Brown

Topground _
Brown Lightgreen
11111
01010
00000
00000
00000

=======
LEGEND
=======

Wall = Dirt or Topground
Player = PlayerHead1 or PlayerHead2 or PlayerHead3 or PlayerHead4
Obstacle = PlayerBodyH or PlayerBodyV or Wall or Crate or Player
PlayerBody = PlayerBodyH or PlayerBodyV

=========
SOUNDS
=========
sfx0  3295707 (player jump)
sfx1 3538707 (player jump to max)
sfx2 42451307 (player move horizontally)
endlevel 96434300
startgame 49875902

================
COLLISIONLAYERS
================

Background
Exit, Apple, Target, Verttarget, Horitarget
PlayerBody
Player, Wall, Crate


======
RULES
======

UP [ UP PlayerHead4 ] -> [ PlayerHead4 ]
UP [ UP PlayerHead3 | No Obstacle ] -> [ PlayerBodyV | PlayerHead4 ] sfx1
UP [ UP PlayerHead2 | No Obstacle ] -> [ PlayerBodyV | PlayerHead3 ] sfx0
UP [ UP PlayerHead1 | No Obstacle ] -> [ PlayerBodyV | PlayerHead2 ] sfx0

[ > Player | Crate | No Obstacle ] -> [ PlayerBodyH | PlayerHead1 | Crate ] sfx2

Horizontal [ > Player | No Obstacle ] -> [ PlayerBodyH  | PlayerHead1 ] sfx2

[ Player Apple ] [ PlayerBody ] -> [ Player Apple ] [ ]
[ Player Apple ] -> [ Player ]

[ > Player ] -> [ Player ]

DOWN [ Player | No Obstacle ] -> [ PlayerBodyV > PlayerHead1 |  ] again
DOWN [ Crate | No Obstacle ] -> [ > Crate | ] again

==============
WINCONDITIONS
==============

All player on exit
All target on crate
All Verttarget on PlayerBodyV
All Horitarget on PlayerBodyH

=======
LEVELS
=======

message Move Lime Richard to the target.

.....
@...#
_____

message Make sure that all boxes are on their targets.

......
..+o..
..__..
..//..
@....#
______
//////

message I wonder what that fruit does?

......
.....@
....__
......
$_.._#
_/__/_

......
.....#
...___
...///
......
___...
///..@
///___
//////

......
.....#
...___
...///
......
......
___...
///..@
///___
//////

........
.......#
......__
......//
........
...+..+@
__._o.__
//./..//
//....//
////////

........
........
.._....#
._/.____
./..////
...+....
_..@....
/_______.

message Cover up red lines by moving over them in the right direction with your tail.

........
#.......
__......
//......
........
........
..__....
.-//-...
........
....__..
...|//..
.......@
________

.........
.....+.@.
.....___.
.........
#...._...
_...+....
/____....
////.....
//......_
......../
....___./
.-__///./
__/////_/

..........
..........
........_#
....+.../_
...____o//
...///..//
.._///....
........@.
__....____
//_.......
///_......
////______

message You win!


