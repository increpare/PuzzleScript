title Colour Chained Endless
author Aarex Tiaokhiao
run_rules_on_level_start
youtube _VzrmRkLfxM


========
OBJECTS
========
Background
Black

Wall -
DarkGrey
.....
.....
0...0
.....
.....

VBorder |
DarkGrey
..0..
.....
.....
.....
..0..

Colour1 1
Red LightGrey Black
.000.
00010
00000
00000
.000.

C1P !
Red LightGrey Black
.000.
00010
00000
00000
.000.

Colour2 2
Green LightGrey Black
.000.
00010
00000
00000
.000.

C2P @
Green LightGrey Black
.000.
00010
00000
00000
.000.

Colour3 3
Blue LightGrey Black
.000.
00010
00000
00000
.000.

C3P #
Blue LightGrey Black
.000.
00010
00000
00000
.000.

Colour4 4
Yellow LightGrey Black
.000.
00010
00000
00000
.000.

C4P $
Yellow LightGrey Black
.000.
00010
00000
00000
.000.

Playera
Black

Playerb
Black

Released
Grey

hasN
Black

Remove
Black


np1 n
Black
.....
.....
.....
.....
.....

np2 m
Black
.....
.....
.....
.....
.....

LoseFlag
Red

PieceGen
Black

Digit1
White
..0..
.00..
..0..
..0..
.000.

Digit2
White
.00..
0..0.
..0..
.0...
0000.

Digit3
White
000..
...0.
.00..
...0.
000..

Digit4
White
..0..
.00..
0.0..
0000.
..0..

Digit5
White
0000.
0....
000..
...0.
000..

Digit6
White
.00..
0....
000..
0..0.
.00..

Digit7
White
0000.
...0.
..0..
.0...
0....

Digit8
White
.00..
0..0.
.00..
0..0.
.00..

Digit9 9
White
.00..
0..0.
.000.
...0.
.00..

Digit0 0
White
.00..
0..0.
0..0.
0..0.
.00..

Inc
Black

=======
LEGEND
=======
. = Background
a = Playera
b = Playerb
x = PieceGen
+ = Inc

Player = Playera or Playerb
Piece = Colour1 or Colour2 or Colour3 or Colour4
Preview = C1P or C2P or C3P or C4P
Np = Np1 or Np2
Border = Wall or VBorder
Score = Digit1 or Digit2 or Digit3 or Digit4 or Digit5 or Digit6 or Digit7 or Digit8 or Digit9 or Digit0

=======
SOUNDS
=======
sfx0 45317502
sfx1 530500
startlevel 17589703

================
COLLISIONLAYERS
================
Background
Wall, Released, LoseFlag
VBorder, Player, Np
Piece, Preview, Score, Inc
hasN, Remove, PieceGen


======
RULES
======
(match 3 rules)
Right [Colour1|Colour1] -> [Colour1 hasN|Colour1 hasN]
Vertical [Colour1 hasN|Colour1] -> [Colour1 Remove| Colour1 Remove]
[Colour1 Remove| Colour1] -> [Colour1 Remove| Colour1 Remove]
[Colour1 | Colour1 | Colour1] -> [Colour1 Remove|Colour1 Remove|Colour1 Remove]

Right [Colour2|Colour2] -> [Colour2 hasN|Colour2 hasN]
Vertical [Colour2 hasN|Colour2] -> [Colour2 Remove| Colour2 Remove]
[Colour2 Remove| Colour2] -> [Colour2 Remove| Colour2 Remove]
[Colour2 | Colour2 | Colour2] -> [Colour2 Remove|Colour2 Remove|Colour2 Remove]

Right [Colour3|Colour3] -> [Colour3 hasN|Colour3 hasN]
Vertical [Colour3 hasN|Colour3] -> [Colour3 Remove| Colour3 Remove]
[Colour3 Remove| Colour3] -> [Colour3 Remove| Colour3 Remove]
[Colour3 | Colour3 | Colour3] -> [Colour3 Remove|Colour3 Remove|Colour3 Remove]

Right [Colour4|Colour4] -> [Colour4 hasN|Colour4 hasN]
Vertical [Colour4 hasN|Colour4] -> [Colour4 Remove| Colour4 Remove]
[Colour4 Remove| Colour4] -> [Colour4 Remove| Colour4 Remove]
[Colour4 | Colour4 | Colour4] -> [Colour4 Remove|Colour4 Remove|Colour4 Remove]

[Piece Remove][Playera][Inc] -> [][Playera][left Inc] sfx1
[Piece Remove][Playera] -> [][Playera] sfx1
[Piece Remove][LoseFlag] -> [][LoseFlag]
[hasN] -> []


(release)
Down [> Player Piece] -> [> Player Released Piece]
Down [Player Released Piece|No Piece] -> [Player |> Piece]
Down [> Player] -> [Player]
[Released] -> []

(gravity)
Down [Piece No Player No NP|No Piece No Preview No Score] -> [|> Piece] again

(rotation)
[Action Playera|Playerb] -> [> Playera|v Playerb]
[Moving Player Piece] -> [Moving Player Moving Piece]
[> Player| Border] -> cancel


(preview area)
Up [Preview| No Piece No Preview] -> [|Preview] again

[NP C1P] -> [NP Colour1]
[NP C2P] -> [NP Colour2]
[NP C3P] -> [NP Colour3]
[NP C4P] -> [NP Colour4]
[Playera no Piece][NP1 Piece] -> [Playera Piece][NP1]
[Playerb no Piece][NP2 Piece] -> [Playerb Piece][NP2]
[PieceGen No Preview] -> [PieceGen Random Preview] again


(explicit loss if piece ends on border)
[Piece Border] -> [LoseFlag] sfx0 again
[LoseFlag][Border] -> [LoseFlag][]
[LoseFlag][Player] -> [LoseFlag][]
[LoseFlag][No Piece] -> [LoseFlag][Random Piece]

(scoring rules)

(up down right left [> inc] -> [left inc])

left [> inc|score] -> [> inc| > score]
left [> digit9| score] -> [digit0|> score]
left [> digit9| border] -> [digit0| border]

left [> digit0] -> [digit1]
left [> digit1] -> [digit2]
left [> digit2] -> [digit3]
left [> digit3] -> [digit4]
left [> digit4] -> [digit5]
left [> digit5] -> [digit6]
left [> digit6] -> [digit7]
left [> digit7] -> [digit8]
left [> digit8] -> [digit9]

==============
WINCONDITIONS
==============
No Piece
No Preview

=======
LEVELS
=======

nm|..ab..|
..|......|
..|------|
..|......|
..|......|
..|......|
..|......|
..|......|
..|......|
xx|000000+




