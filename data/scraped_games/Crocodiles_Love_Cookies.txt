title Crocodiles Love Cookies
author Ethan Clark
homepage www.puzzlescript.net
again_interval 0.1



========
OBJECTS
========

Background 
#f2e4cd  

Croc
green

tounge
pink

cookie
brown 
.000.
00000
00000
00000
.000.

wall
black

toungeP
red

goal
#a3a300
0.0.0
.....
0...0
.....
0.0.0

prevToungePosition
transparent

goDown
transparent

destroyTounge
transparent

originalToungeP
transparent

noOriginalTOungeP
transparent

=======
LEGEND
=======

. = Background
a = Croc
c = cookie
d = wall
e = toungeP
f = goal
g = tounge

moveable = toungeP or cookie
solid = wall or tounge or moveable or croc

player = toungeP

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
goal
croc,tounge,cookie,wall,toungeP
prevToungePosition,goDown,destroyTounge
originalToungeP,noOriginalToungeP

======
RULES     
======     

[toungeP no noOriginalTOungeP] -> [toungeP originalToungeP]
[no originalToungeP] -> [noOriginalTOungeP]

[moving toungeP] -> [moving toungeP prevToungePosition]

[> moveable | moveable] -> [> moveable | > moveable]

down [cookie goDown | ] -> [ | cookie]

late [prevToungePosition toungeP] -> cancel
late [prevToungePosition] -> [tounge]

late [toungeP goal] -> [destroyTounge goal]

late [destroyTounge | no destroyTounge] -> [destroyTounge | destroyTounge]


late [destroyTounge tounge] -> [destroyTounge]
late [destroyTounge originalToungeP] -> [toungeP]
late [destroyTounge] -> []


late down [cookie | no solid] -> [cookie goDown | ]
late [goDown] -> again

==============
WINCONDITIONS
==============

all goal on cookie

=======     
LEVELS
=======

message I want a cookie!

..........
..........
.aaa......
.a........
.af....c..
.ae....d..
.aaa......

..........
..........
.aaa......
.a.....c..
.af....d..
.ae.......
.aaa......

..........
..........
.......c..
.aaa...d..
.a........
.af.......
.ae.......
.aaa......

..........
........c.
........d.
.aaa......
.a......d.
.af.......
.ae.......
.aaa......

...........
...........
...........
...........
......c....
.....aaa...
.....a.....
.....af....
.....ae....
.....aaa...

...........
..c........
..d........
...........
...........
.....aaa...
.....a.....
.....af....
.....ae....
.....aaa...

message Two Cookies? Eh, just one will do.

..........
..........
.aaa......
.a........
.af.......
.ae.......
.aaa......
.aa....c..
.aa....c..
.aa....d..
.aa.......

message I wonder what happens if I eat my own tounge?

..........
..c.......
..d.......
......d...
..........
d...aaa...
....a.....
....af....
....ae....
....aaa...

..............
..............
.aaa..........
.a............
.af....dd.ddd.
.ae....ddcddd.
.aaa...d.c.dd.
.aa....dddcdd.
.aa.......c...
.aa.......d...
.aa...........

message "Hey, can I have a cookie?"
message Sure thing!

.............
.............
.............
.............
.............
.aaa.....aaa.
.a....c....a.
.a....d...fa.
.ae.......aa.
.aaa.....aaa.

...............
...............
...............
...............
......c.c......
.aaa..d.d..aaa.
.a...........a.
.af.........fa.
.ae.........aa.
.aaa.......aaa.

...............
...............
...............
...............
......c....c...
.aaa..ddd..aaa.
.a......d....a.
.af.....d...fa.
.ae.........aa.
.aaa.......aaa.

message "That was weird."
message Yeah, let's never do that again.