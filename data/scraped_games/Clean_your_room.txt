title Clean your room
author bregehr
homepage www.puzzlescript.net

========
OBJECTS
========

Background 
darkgrey 

basketempty
white gray
.000.
01110
10001
10001
.000.

basketfull
white gray red orange lightgreen
.000.
02340
10341
10001
.0224

dirtybasketfull
white gray #510909 #7f4d18 #4f7c1b
.000.
02340
10341
10001
.0224

wall1
#3a2711 #7a4f1d
11111
11111
11111
11111
00000

wall2
#7a4f1d

Player 
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3. 

laundry 
Orange red lightgreen green
.0001
00111
00122
01122
33332

dirtylaundry
#7f4d18 #510909 #4f7c1b #19260b #2c3324
.0044
04411
44122
01144
43442

washer
white #848383 black green red
00000
11113
12224
12221
11111

dirt
#352410 #68451d #472f13
21001
10022
22201
20121
01200

mop
white gray brown
..2..
..2..
.111.
10101
01010

rubble
black #323233
.....
..1..
.001.
01001
10010

dumpright
#848484 #595959
.0000
00000
00000
01111
11111

dumpdown
#848484 #595959 black
00000
11111
12221
12221
11111

dumpleft
#848484 #595959
0000.
00000
00000
11110
11111

dumpup
#848484 #595959
00000
11111
00000
00000
11111

vacuum
white gray brown
....2
...2.
...2.
.101.
10001

upvacuum
white gray brown
....2
...2.
...2.
.101.
10001

leftvacuum
white gray brown
....2
...2.
...2.
.101.
10001

rightvacuum
white gray brown
....2
...2.
...2.
.101.
10001

downvacuum
white gray brown
....2
...2.
...2.
.101.
10001

pebbles
#313438
0..0.
..0..
.0.00
0.0..
.0..0

crusher
gray red darkred white
11111
10301
11111
22222
22222



=======
LEGEND
=======

. = Background
- = wall1
# = wall2
P = Player
* = laundry
K = dirtylaundry
O = basketempty
@ = washer
% = dirt
& = pebbles
! = pebbles and dirt
m = mop
a = dumpleft
s = dumpdown
d = dumpright
f = dumpup
J = vacuum
G = rubble
X = crusher
dump = dumpleft or dumpright or dumpup or dumpdown
basket = basketempty or basketfull or dirtybasketfull
wall = wall1 or wall2
solid = wall or dump or basket or laundry or dirtylaundry or washer or rubble or mop
allvac = vacuum or upvacuum or downvacuum or leftvacuum or rightvacuum
movvac = upvacuum or downvacuum or leftvacuum or rightvacuum

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
dirt
pebbles
Player, Wall, laundry, dirtylaundry, basket, washer, mop, rubble, dump, allvac, crusher
======
RULES     
======     

(basic pushing)
[ >  Player | laundry ] -> [  >  Player | > laundry  ] 
[ >  Player | dirtylaundry ] -> [  >  Player | > dirtylaundry  ] 
[ >  Player | basket ] -> [  >  Player | > basket  ]
[ >  Player | mop ] -> [ >  Player | > mop]
[ >  Player | rubble ] -> [  >  Player | > rubble  ] 
[ >  Player | vacuum ] -> [  >  Player | > vacuum  ]

(laundry into basket)
[ > laundry | basketempty] -> [ | basketfull]

(basket into dryer)
[ > basketfull | washer] -> [ | washer]
[ > dirtybasketfull | washer ] -> [ laundry | washer ]

(mop)
late [mop dirt] -> [mop]

(dirtylaundry)
[> dirtylaundry | basketempty ] -> [ | dirtybasketfull ]
late [laundry dirt] -> [dirtylaundry dirt]
late [laundry pebbles] -> [dirtylaundry pebbles]

(rubble)
[> rubble | rubble] -> [> rubble | > rubble]
(dumps)
[> rubble | dump] -> [ | dump]

(vacuum pebbles)
[vacuum pebbles] -> [vacuum]

(vacuum slide)
[ UP vacuum ] -> [ UP upvacuum ]
[ DOWN vacuum ] -> [ DOWN downvacuum ]
[ LEFT vacuum ] -> [ LEFT leftvacuum ]
[ RIGHT vacuum ] -> [ RIGHT rightvacuum ]

[ upvacuum ] -> [ Up upvacuum ]again
[ leftvacuum ] -> [ Left leftvacuum ]again
[ rightvacuum ] -> [ Right rightvacuum ]again
[ downvacuum ] -> [ Down downvacuum ]again


(vacuum slide with advanced pebble detection technology)
[ UP vacuum pebbles] -> [ UP upvacuum ]
[ DOWN vacuum pebbles] -> [ DOWN downvacuum ]
[ LEFT vacuum pebbles] -> [ LEFT leftvacuum ]
[ RIGHT vacuum pebbles] -> [ RIGHT rightvacuum ]

[ upvacuum pebbles] -> [ Up upvacuum ]again
[ leftvacuum pebbles] -> [ Left leftvacuum ]again
[ rightvacuum pebbles] -> [ Right rightvacuum ]again
[ downvacuum pebbles] -> [ Down downvacuum ]again


(vacuum stop)
[ > movvac | solid ] -> [ > vacuum | solid ]

(crusher)
[> basketempty | crusher | ] -> [ | crusher | rubble]







==============
WINCONDITIONS
==============

No basket
No laundry
No dirt
No dirtylaundry
No rubble
No pebbles

=======     
LEVELS
=======


#---##---#
#@..--...#
#.p..*o..#
#...##...#
##########


#######
##----#
##.p..#
#-*-o.#
#@....#
#######
-------

#####-#####
#---#@#---#
#...-.-...#
#.o..p..*.#
#...#..#..#
-----------

#-----#
#....@#
#.om..#
#...%.#
#p%%%*#
#.%%%.#
-------

#------#
#......#
#.-%%%.#
#pm%%-##
#.%%%%##
#..#..##
--------

###---##
#--...##
#p.*%o##
#--.%.-#
#.o%%..#
#.m..@.#
#......#
--------

#-------####
#.......---#
#..-..&....#
#.j.&.p.&..#
#....&.&&-.#
###........#
------------

#-------#
#.......#
#.p.&...#
#..j.&o.#
#..&....#
#....*.@#
---------

#####----#
##---....#
##%&!.mj.#
##..%%%!.#
#-..--.!.#
#..&&&...#
#p..##...#
#...##...#
----------

#---s-#
#...g.#
#..g..#
#pg.-.#
###...#
-------

#-----#
#@..*.a
#p.x..#
#.oo.##
#....##
-------

#--####
#..s--#
#.o*.@#
#.xo..#
#...p.#
###...#
-------








