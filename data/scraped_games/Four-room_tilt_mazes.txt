title Four-room tilt mazes
author Andrea Gilbert
homepage http://www.clickmazes.com
again_interval 0.05
flickscreen 16x16

========
OBJECTS
========

Background
white

Edge
darkgrey 
00000
00000
00000
00000
00000

Bar
darkgrey
00000
00000
00000
00000
00000

P1
red #ff4040 #990000
...20
.2200
.2000
22001
22001

P2
red #ff4040 #ff8080
00...
0000.
1100.
12100
11100

P3
red #990000 #660000
21000
21100
.1110
.2111
...22

P4
red #ff4040 #990000
11000
00002
0002.
2222.
22...


T1
blue
.....
.....
.....
...00
...00

T2
blue
.....
.....
.....
00...
00...

T3
blue
...00
...00
.....
.....
.....

T4
blue
00...
00...
.....
.....
.....

vBar 
#f0f0f0
..0..
..0..
..0..
..0..
..0..

hBar 
#f0f0f0
.....
.....
00000
.....
.....

xBar 
#f0f0f0
..0..
..0..
00000
..0..
..0..

Cell
white
00000
00000
00000
00000
00000

Cursor
transparent

=======
LEGEND
=======

. = Cell
x = Bar
o = Edge
! = vBar
- = hBar
+ = xBar
1 = T1
2 = T2
3 = T3
4 = T4
a = P1
b = P2
c = P3
d = P4
Wall = Bar or Edge
Target = T1 or T2 or T3 or T4
Grid = Cell or Wall or vBar or hBar or xBar
Player = P1 or P2 or P3 or P4

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Grid
Target
Player, Cursor

======
RULES
======

[ Player | Player | Cursor ] -> [ > Player | > Player | ]

[ > Player | > Player | Wall ] -> cancel
[ > Player | > Player | no Wall | ] -> [ > Player | > Player | | Cursor ] again
late [Player Target | Player Target] -> [Player | Player] 
late [ Cursor Edge ] -> [ Edge ]
late [ Cursor Bar ] -> [ Bar ]

==============
WINCONDITIONS
==============

No Target

=======
LEVELS
=======

Message To the blue target

Message Maze 1 of 3

oooooooooooooooooooooooooooooooo
oab!..x..!..!..oo..!..!..x..!12o
ocd!..x..!..!..oo..!..!..x..!34o
oxxx--x--x--+--ooxxx--+--x--xxxo
o..x..!..x..!......!..!..!..!..o
o..x..!..x..!......!..!..!..!..o
o--+--+--xxxx--oo--xxxx--+--x--o
o..!..!..!..x..oo..!..!..!..x..o
o..!..!..!..x..oo..!..!..!..x..o
o--xxxx--x--x--ooxxx--x--xxxx--o
o..!..!..x..!......x..x..!..x..o
o..!..!..x..!......x..x..!..x..o
o--x--xxxx--+--oo--x--x--+--x--o
o..x..!..!..!..oo..!..!..!..!..o
o..x..!..!..!..oo..!..!..!..!..o
oooo..oooo..oooooooo..oooo..oooo
oooo..oooo..oooooooo..oooo..oooo
o..!..!..!..!..oo..!..!..!..!..o
o..!..!..!..!..oo..!..!..!..!..o
o--xxxx--+--+--oo--+--+--x--xxxo
o..x..!..!..!......!..!..x..!..o
o..x..!..!..!......!..!..x..!..o
o--x--+--xxxx--ooxxx--+--xxxx--o
o..!..!..x..!..oo..x..!..!..!..o
o..!..!..x..!..oo..x..!..!..!..o
oxxx--+--x--xxxoo--x--+--+--x--o
o..!..!..!..!......!..!..!..x..o
o..!..!..!..!......!..!..!..x..o
o--x--xxxx--xxxoo--+--x--+--x--o
o..x..!..!..!..oo..!..x..!..!..o
o..x..!..!..!..oo..!..x..!..!..o
oooooooooooooooooooooooooooooooo

Message Maze 2 of 3

oooooooooooooooooooooooooooooooo
oab!..!..x..!..oo..!..!..x..!..o
ocd!..!..x..!..oo..!..!..x..!..o
oxxx--+--x--xxxoo--+--+--xxxx--o
o..x..!..!..!......!..!..!..!..o
o..x..!..!..!......!..!..!..!..o
o--x--xxxx--x--ooxxx--+--x--xxxo
o..!..!..!..x......!..!..x..!..o
o..!..!..!..x......!..!..x..!..o
o--x--+--+--x--oo--+--xxxx--+--o
o..x..!..!..!......!..!..!..!..o
o..x..!..!..!......!..!..!..!..o
o--x--+--+--xxxoo--+--xxxx--+--o
o..!..!..!..!12oo..!..!..!..!..o
o..!..!..!..!34oo..!..!..!..!..o
oooo..o..o..oooooooo..o..o..oooo
oooo..o..o..oooooooo..o..o..oooo
o..!..!..!..!..oo..x..x..!..!..o
o..!..!..!..!..oo..x..x..!..!..o
o--+--+--xxxx--oo--x--x--+--+--o
o..!..!..!..!......x..!..!..!..o
o..!..!..!..!......x..!..!..!..o
o--xxxx--x--+--oo--x--+--xxxx--o
o..x..!..x..!......!..!..!..x..o
o..x..!..x..!......!..!..!..x..o
oxxx--x--x--x--oo--+--xxxx--x--o
o..!..x..!..x......!..x..!..!..o
o..!..x..!..x......!..x..!..!..o
o--+--x--+--xxxoo--+--x--+--xxxo
o..!..!..!..!..oo..!..!..!..!..o
o..!..!..!..!..oo..!..!..!..!..o
oooooooooooooooooooooooooooooooo

Message Maze 3 of 3

oooooooooooooooooooooooooooooooo
o..x..!..!..!..oo..!..x..!..!..o
o..x..!..!..!..oo..!..x..!..!..o
o--xxxx--+--+--oo--+--x--xxxx--o
o..!..!..!..!......!..!..!..!..o
o..!..!..!..!......!..!..!..!..o
o--+--xxxx--+--oo--xxxx--+--xxxo
o..!..!..!..!......x..!..!..x..o
o..!..!..!..!......x..!..!..x..o
oxxx--+--+--+--oo--x--+--x--x--o
o..!..!..!..!......!..!..x..!..o
o..!..!..!..!......!..!..x..!..o
o--+--x--+--xxxoo--+--+--x--+--o
o..!..x..!..!aboo..!..!..!..!..o
o..!..x..!..!cdoo..!..!..!..!..o
oooo..o..o..oooooooo..o..o..oooo
oooo..o..o..oooooooo..o..o..oooo
o..!..!..!..!..oo12!..!..!..!..o
o..!..!..!..!..oo34!..!..!..!..o
o--+--xxxx--+--ooxxx--x--xxxx--o
o..!..!..!..!......!..x..!..x..o
o..!..!..!..!......!..x..!..x..o
o--xxxx--+--+--ooxxx--x--+--x--o
o..x..!..!..!......x..!..!..!..o
o..x..!..!..!......x..!..!..!..o
oxxx--xxxx--+--oo--x--+--+--xxxo
o..!..x..!..!......!..!..!..!..o
o..!..x..!..!......!..!..!..!..o
o--+--x--+--x--oo--+--xxxx--x--o
o..!..!..!..x..oo..!..!..!..x..o
o..!..!..!..x..oo..!..!..!..x..o
oooooooooooooooooooooooooooooooo

(oooooooooooooooooooooooooooooooo
oab!..!..!..!..oo..!..!..!..!12o
ocd!..!..!..!..oo..!..!..!..!34o
o--+--+--+--+--oo--+--+--+--+--o
o..!..!..!..!......!..!..!..!..o
o..!..!..!..!......!..!..!..!..o
o--+--+--+--+--oo--+--+--+--+--o
o..!..!..!..!..oo..!..!..!..!..o
o..!..!..!..!..oo..!..!..!..!..o
o--+--+--+--+--oo--+--+--+--+--o
o..!..!..!..!......!..!..!..!..o
o..!..!..!..!......!..!..!..!..o
o--+--+--+--+--oo--+--+--+--+--o
o..!..!..!..!..oo..!..!..!..!..o
o..!..!..!..!..oo..!..!..!..!..o
oooo..oooo..oooooooo..ooox..oooo
oooo..oooo..oooooooo..oooo..oooo
o..!..!..!..!..oo..!..!..!..!..o
o..!..!..!..!..oo..!..!..!..!..o
o--+--+--+--+--oo--+--+--+--+--o
o..!..!..!..!......!..!..!..!..o
o..!..!..!..!......!..!..!..!..o
o--+--+--+--+--oo--+--+--+--+--o
o..!..!..!..!..oo..!..!..!..!..o
o..!..!..!..!..oo..!..!..!..!..o
o--+--+--+--+--oo--+--+--+--+--o
o..!..!..!..!......!..!..!..!..o
o..!..!..!..!......!..!..!..!..o
o--+--+--+--+--oo--+--+--+--+--o
o..!..!..!..!..oo..!..!..!..!..o
o..!..!..!..!..oo..!..!..!..!..o
oooooooooooooooooooooooooooooooo)

MESSAGE Thank you for playing! :)




