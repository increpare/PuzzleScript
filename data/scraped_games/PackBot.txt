title PackBot
author Max
run_rules_on_level_start
key_repeat_interval 0.2

========
OBJECTS
========

Background
#9DF


Wall
#c45 #ddd
00010
11111
01000
11111
00010


PlayerRight
#111 #fff #ea0 #e70 #a50 #666 #bbb
10610
..2..
.4244
.433.
.5.5.


PlayerLeft
Black #fff #ea0 #e70 #a50 #666 #bbb
01601
..2..
4424.
.334.
.5.5.


Block
#37a #058 #036
00000
01112
01112
01112
02222


Target
#3b3
.000.
0...0
0...0
0...0
.000.


StackPosition
Transparent


GroundPosition
Transparent

=======
LEGEND
=======

. = Background
# = Wall
P = PlayerRight
q = PlayerLeft
* = Block
$ = Target
% = Target and Block

Player = PlayerRight or PlayerLeft
CanFall = Player or Block
Surface = Player or Block or Wall
StackObject = Player or Block

=======
SOUNDS
=======

Player MOVE 40653507
Player CANTMOVE 75399904
sfx0 99541303
EndLevel 85029903

================
COLLISIONLAYERS
================

Background
StackPosition
GroundPosition
Block
Target
Player, Wall


======
RULES
======

( Face direction )
[ left PlayerRight ] -> [ PlayerLeft ]
[ right PlayerLeft ] -> [ PlayerRight ]


( Pick up blocks )
[ StackPosition ] -> [ ]
[ GroundPosition ] -> [ ]
right [ up PlayerRight | ] -> [ PlayerRight StackPosition | GroundPosition ]
left [ up PlayerLeft | ] -> [ PlayerLeft StackPosition | GroundPosition ]
( You can pick up from a one-high wall)
up [ stationary GroundPosition Wall | ] -> [ Wall | action GroundPosition ]
up [ StackPosition StackObject | ] -> [ StackObject | StackPosition ]
[ StackPosition no Surface ] [ Block GroundPosition ] -> [ Block ] [ ]


( Put down blocks )
[ StackPosition ] -> [ ]
[ GroundPosition ] -> [ ]
right [ down PlayerRight | ] -> [ PlayerRight StackPosition | GroundPosition ]
left [ down PlayerLeft | ] -> [ PlayerLeft StackPosition | GroundPosition ]
( You can place onto one-high wall)
up [ stationary GroundPosition Wall | ] -> [ Wall | action GroundPosition ]
( You can stack blocks )
up [ GroundPosition StackObject | ] -> [ StackObject | GroundPosition ]
( Grab the highest block from your stack )
up [ StackPosition StackObject | StackObject ] -> [ StackObject | StackObject StackPosition ]
[ StackPosition Block ] [ GroundPosition no Surface ] -> [ ] [ Block ]


( Mark stack position so that blocks will follow you )
[ StackPosition ] -> [ ]
up [ Player | ] -> [ Player | StackPosition ]


( You can climb )
[ GroundPosition ] -> [ ]
horizontal [ > Player | Surface ] -> [ Player | Surface GroundPosition ]
( Can't climb if there is a wall directly above you )
up [ Player | Wall ] [ GroundPosition ] -> [ Player | Wall ] [ ]
up [ stationary GroundPosition | no Surface ] -> [ | action GroundPosition ]
[ Player ] [ GroundPosition no Surface ] -> [ ] [ Player ]


( Can't move into surfaces )
[ > Player | Surface ] -> [ Player | Surface ]


( Player gravity )
late down [ Player | no Surface ] -> [ | Player ]

( Move blocks to where you ended up. From: StackPosition. To: GroundPosition )
late [ GroundPosition ] -> [ ]
late up [ Player | ] -> [ Player | GroundPosition ]
late up [ StackPosition Block | ] [ GroundPosition no Surface | ] -> [ | StackPosition ] [ Block | GroundPosition ]

( Block gravity )
late down [ Block | no Surface ] -> [ | Block ]


==============
WINCONDITIONS
==============

All Block on Target

=======
LEVELS
=======

Message Press Up to pick up boxes. Press Down to drop them.
Message Pack boxes into green targets.


#########
#.......#
#.$*q*$.#
#########


#######
#$...*#
#$...*#
#$.p.*#
#######


#########
#.......#
#......*#
#.....###
#p...*###
#$..#####
#$.######
#########


########
#*.....#
#....*.#
#$..####
#$.....#
####.q.#
########


#########
#p.**...#
#..**...#
#####...#
#.......#
#$$.....#
#$$.....#
#########


#######
#*....#
#.....#
###.*.#
#$$.*.#
#$$.*q#
#######


#########
#.......#
#.....**#
#....p**#
#...#####
#..#..$.#
#.....$$#
#......$#
#########


###########
#...*.....#
#...*.....#
#p.###....#
##....#...#
#......#.$#
#..#$$...*#
###########


#########
#***....#
#.p....$#
####..###
#.....*.#
#.......#
#$..##$$#
#########


#############
#$....*....$#
##..*.*.*..##
#$..#p#.#..$#
#############


#################
#...............#
#...............#
#p*.............#
###.##.........*#
#..$#....#.....##
#..##...#.......#
#.....##.#....$%#
#*....**$.*...$%#
#################


#################
#...............#
#.....*.........#
#....###........#
#.........#.....#
#..#*....#.*....#
#..##...##.##...#
#$.#.......#.#..#
#$.*$##....*.$p$#
#################