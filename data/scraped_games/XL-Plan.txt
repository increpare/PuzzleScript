title XL-Plan
author Mark Richardson
flickscreen 9x11
key_repeat_interval 0.5
noaction
require_player_movement
run_rules_on_level_start
(verbose_logging)


========
OBJECTS
========

Background .
Black

Player
#800

Next $
#008

Refill &
Transparent (#066)

Goal {
#660

Cursor1
#080

Cursor2
#080

Cursor3
#080

Wall #
#222

Z
White
.....
.....
..0..
.....
.....

I
White
.....
..0..
..0..
..0..
.....

U
White
.....
.0.0.
.0.0.
..0..
.....

X
White
.....
.0.0.
..0..
.0.0.
.....

L
White
.....
.0...
.0...
.000.
.....

C
White
.....
..00.
.0...
..00.
.....

D
White
.....
.00..
.0.0.
.00..
.....

M
White Gray
.....
.000.
.010.
.0.0.
.....

Hyphen -
White
.....
.....
00000
.....
.....

P
White
.00..
.0.0.
.00..
.0...
.....

A
White
.....
..00.
.0.0.
..00.
.....

N
White
.....
.00..
.0.0.
.0.0.
.....


=======
LEGEND
=======

@ = Player  and Z
1 = Cursor1 and Z
2 = Cursor2 and Z
3 = Cursor3 and Z

Numeral = I or U or X or L or C or D or M or Z
NotI    = U or X or L or C or D or M or Z
NotU    = I or X or L or C or D or M or Z
NotX    = I or U or L or C or D or M or Z
NotL    = I or U or X or C or D or M or Z
NotC    = I or U or X or L or D or M or Z
NotD    = I or U or X or L or C or M or Z
NotM    = I or U or X or L or C or D or Z
NotZ    = I or U or X or L or C or D or M
Text    = P or A or N or Hyphen
Cursor = Cursor1 or Cursor2 or Cursor3

=======
SOUNDS
=======

Player Move 73969707
Cancel      87888304
Undo        87888304
Restart     38699508
EndLevel    16741708


================
COLLISIONLAYERS
================

Background
Player, Wall, Next, Refill, Cursor1, Cursor2, Cursor3
Numeral, Text, Goal


======
RULES
======

[Next I][> Player|NotI]->cancel
[Next U][> Player|NotU]->cancel
[Next X][> Player|NotX]->cancel
[Next L][> Player|NotL]->cancel
[Next C][> Player|NotC]->cancel
[Next D][> Player|NotD]->cancel
[Next M][> Player|NotM]->cancel
[Next Z][> Player|NotZ]->cancel

late[Next Numeral][Player]->[Next][Player Numeral]
late right[Next no Numeral|Numeral]->[Next Numeral|]
late right[Next|...|no Numeral|Numeral]->[Next|...|Numeral|]

startloop
late random down[Refill no Numeral][Cursor1|]->[Refill][|Cursor1]
late down[||||||||||Cursor1][Cursor2|]->[Cursor1||||||||||][|Cursor2]
late down[||||||||||Cursor2][Cursor3|]->[Cursor2||||||||||][|Cursor3]
late down[||||||||||Cursor3]->win
late right[Refill no Numeral|...|no Numeral][Cursor3 Numeral|]->[Refill|...|Numeral][Numeral|Cursor3]
late right[Refill no Numeral|...|no Numeral][Cursor2 Numeral|]->[Refill|...|Numeral][Numeral|Cursor2]
late right[Refill no Numeral|...|no Numeral][Cursor1 Numeral|]->[Refill|...|Numeral][Numeral|Cursor1]
late right[Numeral|Cursor]->[Numeral Cursor|]
late right[Refill no Numeral|...|Z]->[Refill|...|]
late right[Refill no Numeral|...|no Numeral|Numeral]->[Refill no Numeral|...|Numeral|]
late random right[Refill no Numeral|...|no Numeral]->[Refill|...|Z]
  late right[Next no Numeral|Numeral]->[Next Numeral|]
+ late right[Next|...|no Numeral|Numeral]->[Next|...|Numeral|]
endloop

late right[Goal|Cursor1][Goal|Cursor2][Goal|Cursor3][Refill]->[|][|][|][]
late[Next no Numeral]->win


==============
WINCONDITIONS
==============


=======
LEVELS
=======

message XL-Plan is a variant of the XL-Up maze.
message The goal of XL-Plan is to build and walk through a grid of figures, writing out roman numerals in ascending order.
message The final roman numeral is shown at the top of the screen.  The next figures are shown at the bottom of the screen.
message Each step you take must be on to a space showing the next figure, or an empty space that will become the next figure.

UII--PLAN{3ZZZ{2ZZZ.1ZZZ.
..........CZZZ.XZZZ.IZZZ.
..........CCZZ.XXZZ.IIZZ.
..#####...CCCZ.XXXZ.IIIZ.
..#...#...CDZZ.XLZZ.IUZZ.
..#.@.#...DZZZ.LZZZ.UZZZ.
..#...#...DCZZ.LXZZ.UIZZ.
..#####...DCCZ.LXXZ{UIIZ.
..........DCCC.LXXX.UIII.
..........CMZZ.XCZZ.IXZZ.
$.......&................

XXI--PLAN{3ZZZ.2ZZZ.1ZZZ.
..........CZZZ{XZZZ.IZZZ.
.#######..CCZZ.XXZZ.IIZZ.
.#.....#..CCCZ.XXXZ.IIIZ.
.#.....#..CDZZ.XLZZ.IUZZ.
.#..@..#..DZZZ.LZZZ.UZZZ.
.#.....#..DCZZ.LXZZ.UIZZ.
.#######..DCCZ.LXXZ.UIIZ.
..........DCCC.LXXX.UIII.
..........CMZZ.XCZZ{IXZZ.
$.......&................

XXU--PLAN{3ZZZ.2ZZZ.1ZZZ.
..........CZZZ.XZZZ.IZZZ.
.#######..CCZZ{XXZZ.IIZZ.
.#.....#..CCCZ.XXXZ.IIIZ.
.#.....#..CDZZ.XLZZ.IUZZ.
.#..@..#..DZZZ.LZZZ{UZZZ.
.#.....#..DCZZ.LXZZ.UIZZ.
.#.....#..DCCZ.LXXZ.UIIZ.
.#######..DCCC.LXXX.UIII.
..........CMZZ.XCZZ.IXZZ.
$.......&................

.L--PLAN.{3ZZZ.2ZZZ{1ZZZ.
#########.CZZZ.XZZZ.IZZZ.
#.......#.CCZZ.XXZZ.IIZZ.
#.......#.CCCZ.XXXZ.IIIZ.
#.......#.CDZZ.XLZZ.IUZZ.
#...@...#.DZZZ{LZZZ.UZZZ.
#.......#.DCZZ.LXZZ.UIZZ.
#.......#.DCCZ.LXXZ.UIIZ.
#.......#.DCCC.LXXX.UIII.
#########.CMZZ.XCZZ.IXZZ.
$.......&................
