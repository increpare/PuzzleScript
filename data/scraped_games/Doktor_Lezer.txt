title Doktor Lezer
author Ax
homepage axelceard.fr

background_color gray
text_color red

run_rules_on_level_start
norepeat_action

========
OBJECTS
========

background
#639bff #86b2ff
10001
01110
01110
01110
10001

wall
gray

wallSide
darkgray
.....
.....
.....
00000
00000

playerU
#EEC39A #222034 red #FFFFFF 
.000.
00000
.000.
.333.
.1.1.

playerD
#EEC39A #222034 red #FFFFFF 
.000.
02020
.000.
.333.
11.11


playerL
#EEC39A #222034 red #FFFFFF 
.000.
20200
.000.
.333.
11.1.

playerR
#EEC39A #222034 red #FFFFFF 
.000.
00202
.000.
.333.
.1.11

grabbing
#EEC39A
.....
.....
.....
0...0
.....
laserL
red
.....
.....
..000
.....
.....

laserD
red
..0..
..0..
..0..
.....
.....

laserR
red
.....
.....
000..
.....
.....

laserU
red
.....
.....
.....
..0..
..0..

emitR
red
.....
.....
..000
.....
.....

emitU
red
..0..
..0..
..0..
.....
.....

emitL
red
.....
.....
000..
.....
.....

emitD
red
.....
.....
.....
..0..
..0..

frontL
red
.....
.....
000..
.....
.....

frontR
red 
.....
.....
..000
.....
.....


pewU
darkblue blue
.....
.....
.111.
.111.
.000.

pewD
darkblue blue red
.111.
.111.
.020.
.....
.....

pewR
darkblue blue
.....
111..
111..
000..
.....

pewL
darkblue blue
.....
..111
..111
..000
.....

mirrorZ
white darkgreen 
...1.
..10.
.100.
.001.
.01..

mirrorS
white darkgreen
.1...
.01..
.001.
.100.
..10.

grabbed
transparent

targetH
#EEC39A #524B24 #222034 #DF7126 #663931 #AC3232 
.....
..4..
.000.
02020
.000.

target
#EEC39A #524B24 #222034 #DF7126 #663931 #AC3232 
13331
.111.
.333.
.040.
..4..

targetHDead
#663931
.....
..0..
..0..
..0..
..0..

targetDead
#847E87 #000000 #222034 #663931
.03.0
0.30.
.02.0
.122.
12212

playerDead
#847E87 #000000 #222034 
.0..0
0..0.
.02..
.122.
12212

targetHMouth
#AC3232 
.....
.....
.....
.....
..0..



grabMirrorNotLitV
transparent
grabMirrorLitV
transparent
grabMirrorNotLitH
transparent
grabMirrorLitH
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = PlayerD and grabMirrorNotLitV and grabMirrorNotLitH
u = pewU
d = pewD
l = pewL
r = pewR
@ = target
/ = mirrorZ
\ = mirrorS

player = playerU or playerD or playerL or playerR
mirror = mirrorZ or mirrorS
emit = emitR or emitL or emitU or emitD
laser = laserL or laserD or laserR or laserU
laserV = laserU or laserD
laserH = laserL or laserR
pew = pewU or pewD or pewR or pewL

laserObstacle = pew or wall
playerObstacle = laserObstacle or target or mirror
alive = player or target
movable = mirror

deco = frontL or frontR or targetH

=======
SOUNDS
=======
player move 40664307
player create 40664307
movable move 29090307
playerDead create 99288701 (5076501)
target destroy 99288701  
grabbing create 9239704
grabbing destroy 17724501
sfx0 75572300(87063501)(6483701)(44611502)
(48607701)

================
COLLISIONLAYERS
================

Background

laserD (')
emitU

emitR (- )
laserL

emitL ( -)
laserR

player, playerDead, wall, target, targetDead, mirror, pewU, pewD, pewR, pewL

grabbing
wallSide

frontL (- )
frontR ( -)

emitD (,)
laserU

targetH, targetHDead
targetHMouth

(transparent)
grabbed
grabMirrorNotLitV
grabMirrorLitV
grabMirrorNotLitH
grabMirrorLitH

======
RULES
======

(GAMEPLAY)

(grab)
up [action playerU no grabbing | movable] -> [grabbing playerU | grabbed movable]
down [action playerD no grabbing | movable] -> [grabbing playerD | grabbed movable]
left [action playerL no grabbing | movable] -> [grabbing playerL | grabbed movable]
right [action playerR no grabbing | movable] -> [grabbing playerR | grabbed movable]
(let go)
[action player grabbing | grabbed] -> [player|]

(turn)
[up player no playerU no grabbing] -> [playerU]
[down player no playerD no grabbing] -> [playerD]
[left player no playerL no grabbing] -> [playerL]
[right player no playerR no grabbing] -> [playerR]

(move stuff)
[ moving player grabbing | grabbed movable ] -> [ moving player moving grabbing | moving grabbed moving movable ]

[ > grabbed | playerObstacle] -> cancel
[ > grabbing | playerObstacle no grabbed] -> cancel


(LASERS)
late [emit] -> []
late [laser] -> []

late [pewU] -> [pewU emitU]
late [pewD] -> [pewD emitD]
late [pewR] -> [pewR emitR]
late [pewL] -> [pewL emitL]

late up [emitU|no laserObstacle] -> [emitU|laserU]
+ late down [emitD|no laserObstacle] -> [emitD|laserD]
+ late left [emitL|no laserObstacle] -> [emitL|laserL]
+ late right [emitR|no laserObstacle] -> [emitR|laserR]

+ late up [laserU no mirror] -> [laserU emitU]
+ late down [laserD no mirror] -> [laserD emitD]
+ late left [laserL no mirror] -> [laserL emitL]
+ late right [laserR no mirror] -> [laserR emitR]

+ late up [laserU mirrorZ] -> [laserU mirrorZ emitR]
+ late down [laserD mirrorZ] -> [laserD mirrorZ emitL]
+ late left [laserL mirrorZ] -> [laserL mirrorZ emitD]
+ late right [laserR mirrorZ] -> [laserR mirrorZ emitU]

+ late up [laserU mirrorS] -> [laserU mirrorS emitL]
+ late down [laserD mirrorS] -> [laserD mirrorS emitR]
+ late left [laserL mirrorS] -> [laserL mirrorS emitU]
+ late right [laserR mirrorS] -> [laserR mirrorS emitD]


(DEATH)
late [laser target] -> [laser targetDead]
late [laser player] -> [laser playerDead]
late [grabbing playerDead] -> [playerDead]
(restart when press any key)
[playerDead] -> restart


(DECO)
up [no wall|wall] -> [|wall wallSide]

late [deco] -> []

late up [target|] -> [target|targetH]
late up [targetDead|] -> [targetDead|targetHDead]
late [playerDead] [targetH] -> [playerDead] [targetH targetHMouth]

(fix perspective)
late [mirrorS emitL] -> [mirrorS emitL frontL]
late [mirrorS laserR]-> [mirrorS laserR frontL]
late [mirrorZ emitR] -> [mirrorZ emitR frontR]
late [mirrorZ laserL]-> [mirrorZ laserL frontR]

(SOUND)
late [grabbed laserV] [grabMirrorNotLitV] -> [grabbed laserV] [grabMirrorLitV] sfx0
late [grabbed no laserV] [grabMirrorLitV] -> [grabbed no laserV] [grabMirrorNotLitV]
late [grabbed laserH] [grabMirrorNotLitH] -> [grabbed laserH] [grabMirrorLitH] sfx0
late [grabbed no laserH] [grabMirrorLitH] -> [grabbed no laserH] [grabMirrorNotLitH]


==============
WINCONDITIONS
==============

no playerDead
no target

=======
LEVELS
=======

#########
#...@...#
#.......#
#.......#
#r......#
#.......#
#./.p...#
#.......#
#########

#########
#...d...#
#@......#
#.......#
#r......#
#../....#
#.p.....#
#.......#
#########

#########
#.@.....#
#.../\..#
#.......#
#...\.\.#
#.......#
#..../..#
#.p...u.#
#########

#########
#.@.....#
#.../\..#
#.......#
#..#\.\.#
#.......#
#..../..#
#.p...u.#
#########

#########
#.@.....#
#.../\..#
#.......#
#..#\.\.#
#..#....#
#..#./..#
#.p#..u.#
#########

#########
#.@...d.#
#..../.l#
#...../.#
#.......#
#.......#
#.p/...\#
#.......#
#########

###########
#.........#
#.........#
#../\./\..#
#..\.../..#
#....@....#
#../...\..#
#..\/../..#
#..../....#
#.........#
#....pu...#
###########

###########
#.........#
#....#....#
#../\./\..#
#..\.../..#
#.#..@....#
#.#/...\#.#
#..\/../..#
#..../....#
#.........#
#....pu...#
###########

###########
#.........#
#..#####..#
#.#/\./\#.#
#.#\.../#.#
#.#..@..#.#
#.#/...\#.#
#..\/../..#
#..../....#
#.........#
#....pu...#
###########

###########
#...#/...\#
#.@.#.....#
#...#..#..#
#......#..#
#\.../.#..#
########..#
#../...../#
#...p.....#
#..\.....l#
###########

###########
#.........#
#...d.d...#
#.........#
#.r.....l.#
#....@....#
#.r.....l.#
#.........#
#...upu...#
#.........#
###########

###########
#.....#...#
#.p.d.#...#
#.........#
###.....l.#
#....@....#
#.r.....###
#.........#
#...#.u...#
#...#.....#
###########

###########
####..#####
#.../.#...#
#.p.d.#...#
#...\.....#
###.....l.#
#....@....#
#.r.....###
#.........#
#...#.u...#
#...#.....#
###########

###########
#....d....#
#.p/......#
#.........#
#..../....#
#r..\@\..l#
#..../....#
#.........#
#.........#
#....u....#
###########

###########
#./.d.....#
#.p.......#
#.........#
#.../.\..l#
#....@....#
#r..\./...#
#.........#
#......\..#
#.....u...#
###########

#########
#/.....\#
#./...\.#
#...@...#
#.../\..#
#...u...#
#.\../..#
#\..../.#
##...../#
#...p...#
#########