title Mr. Dwarf
author Guilherme Stutz Tows
zoomscreen 11x10
run_rules_on_level_start
again_interval 0.01

========
OBJECTS
========

Background .
DarkBrown

PDU
Black

PDD 
Black

PDL
Black

PDR
Black

ProtoDirt X
Brown

ProtoDirt2 Y
Brown

DirtX
Brown Orange Yellow Black
11101
10111
11110
11011
01111

DirtU
Brown Orange Yellow Black
22222
10111
11110
11011
01111

DirtR
Brown Orange Yellow Black
11100
10110
11110
11010
01110

DirtD
Brown Orange Yellow Black
11101
10111
11110
11011
33333

DirtL
Brown Orange Yellow Black
01101
00111
01110
01011
01111

DirtUR
Brown Orange Yellow Black
2222.
10112
11112
11012
01112

DirtUL
Brown Orange Yellow Black
.2222
20111
21110
21011
21111

DirtDR
Brown Orange Yellow Black
11103
10113
11113
11013
3333.

DirtDL
Brown Orange Yellow Black
31101
30111
31110
31011
.3333

DirtRL
Brown Orange Yellow Black
01100
00110
01110
01010
01110

DirtURL
Brown Orange Yellow Black
.222.
20112
21112
21012
21112

DirtDRL
Brown Orange Yellow Black
31103
30113
31113
31013
.333.

DirtUD
Brown Orange Yellow Black
22222
10111
11110
11011
33333

DirtUDR
Brown Orange Yellow Black
2222.
10112
11112
11012
3333.

DirtUDL
Brown Orange Yellow Black
.2222
20111
21110
21011
.3333

DirtUDRL
Brown Orange Yellow Black
.222.
20112
21112
21012
.333.

DirtCracked
Brown Orange Yellow Black
.022.
03112
21330
21312
.333.

PlayerGround @
Gray LightGray Red Black
.000.
13131
22222
22222
.1.1.

PlayerAir
Gray LightGray Red Black
.000.
13131
22222
21212
.....

PlayerAir2
Gray LightGray Red Black
.000.
.111.
13131
22222
12221

PlayerDead
Gray LightGray Red Black
.000.
13131
22322
23232
.1.1.


Rock #
DarkBlue

Generator O
Black

Hole
Red

Marker5 5
DarkBlue White
01110
01000
01100
00010
01100

Marker4 4
DarkBlue White
01010
01010
01110
00010
00010

Marker3 3
DarkBlue White
01110
00010
01100
00010
01100

Marker2 2
DarkBlue White
01100
00010
00110
01000
01110

Marker1 1
DarkBlue White
01100
00100
00100
00100
01110

Marker0 0
DarkBlue White
01100
01010
01010
01010
00110

FirstTurn !
White

Brace %
Gray DarkGray White
0...0
12.21
1.2.1
12.21
0...0

StatusBrace
Gray DarkGray White
0...0
12.21
1.2.1
12.21
0...0

Status _
Black

DetectPlayer
Black

Sky :
Blue

=======
LEGEND
=======

Dirt = DirtX or DirtL or DirtR or DirtU or DirtD or DirtUL or DirtUR or DirtDL or DirtDR or DirtUD or DirtRL or DirtUDL or DirtUDR or DirtURL or DirtDRL or DirtUDRL or DirtCracked

Falls = Dirt or Brace

Marker = Marker0 or Marker1 or Marker2 or Marker3 or Marker4 or Marker5
Built = Rock or ProtoDirt or Dirt or Marker or Brace
SolidToPlayer = Rock or ProtoDirt or Dirt or Marker 
Support = DirtX or DirtL or DirtR or DirtU or DirtD or DirtUL or DirtUR or DirtDL or DirtDR or DirtUD or DirtRL or DirtUDL or DirtUDR or DirtURL or DirtDRL or Rock

DirtDown = DirtD or DirtDL or DirtDR or DirtUD or DirtDRL or DirtUDR or DirtUDL or DirtUDRL or DirtCracked or Brace
DirtUp = DirtU or DirtUL or DirtUR or DirtUD or DirtURL or DirtUDR or DirtUDL or DirtUDRL or DirtCracked
DirtLeft = DirtL or DirtDL or DirtUL or DirtRL or DirtDRL or DirtURL or DirtUDL or DirtUDRL or DirtCracked
DirtRight = DirtR or DirtDR or DirtUR or DirtRL or DirtDRL or DirtURL or DirtUDR or DirtUDRL or DirtCracked

DirtND = DirtX or DirtL or DirtR or DirtU or DirtUL or DirtUR or DirtRL or DirtURL or Brace
DirtNU = DirtX or DirtL or DirtR or DirtD or DirtDL or DirtDR or DirtRL or DirtDRL or Brace
DirtNR = DirtX or DirtL or DirtU or DirtD or DirtUL or DirtDL or DirtUD or DirtUDL or Brace
DirtNL = DirtX or DirtR or DirtU or DirtD or DirtUR or DirtDR or DirtUD or DirtUDR or Brace

Player = PlayerGround or PlayerAir or PlayerDead or PlayerAir2

$ = Status and StatusBrace
* = Status and DetectPlayer and StatusBrace
=======
SOUNDS
=======

SFX0 65719302 (dead)
SFX1 26152302 (dig)
SFX2 71766100 (support)
SFX3 70521902 (crumble)
SFX4 82348702 (crumble2)
SFX5 22613705 (jump)

================
COLLISIONLAYERS
================

Background
Generator, Sky
ProtoDirt, ProtoDirt2, Dirt, Rock, Marker
PDU
PDR, DetectPlayer
PDD, Status
PDL, StatusBrace
Player, PlayerDead, Hole, FirstTurn
Brace

======
RULES
======

down [ FirstTurn ] [ Generator | ... | Generator ] [ moving Player ] -> [ FirstTurn ] [ Generator | ... | ] [ moving Player ]

[ FirstTurn ] [ moving Player ] -> [ ] [ moving Player ]

[ stationary Generator Dirt ] -> [ action Generator Dirt ]

random [ stationary Generator ] -> [ randomDir ProtoDirt Generator ]
random [ > ProtoDirt | no Built ] -> [ randomDir ProtoDirt | randomDir ProtoDirt ]
random [ > ProtoDirt | no Built ] -> [ randomDir ProtoDirt | randomDir ProtoDirt ]
random [ > ProtoDirt | no Built ] -> [ randomDir ProtoDirt | randomDir ProtoDirt ]
random [ > ProtoDirt | no Built ] -> [ randomDir ProtoDirt | randomDir ProtoDirt ]

[ moving ProtoDirt ] -> [ stationary ProtoDirt ]

up    [ ProtoDirt no PDU | no ProtoDirt ] -> [ ProtoDirt PDU | ]
right [ ProtoDirt no PDR | no ProtoDirt ] -> [ ProtoDirt PDR | ]
down  [ ProtoDirt no PDD | no ProtoDirt ] -> [ ProtoDirt PDD | ]
left  [ ProtoDirt no PDL | no ProtoDirt ] -> [ ProtoDirt PDL | ]

[ PDU PDR PDD PDL ProtoDirt ] -> [ ]


[ DirtUDRL ] -> [ PDU PDR PDD PDL ProtoDirt ]  
[ DirtUDR ]	-> [ PDU PDR PDD ProtoDirt ]  
[ DirtURL ]	-> [ PDU PDR PDL ProtoDirt ] 
[ DirtUDL ]	-> [ PDU PDD PDL ProtoDirt ] 
[ DirtDRL ]	-> [ PDR PDD PDL ProtoDirt ] 
[ DirtUL ]	-> [ PDU PDL ProtoDirt ] 		
[ DirtUD ]	-> [ PDU PDD ProtoDirt ] 		
[ DirtUR ]	-> [ PDU PDR ProtoDirt ] 		
[ DirtDR ]	-> [ PDR PDD ProtoDirt ] 		
[ DirtRL ]	-> [ PDR PDL ProtoDirt ] 		
[ DirtDL ]	-> [ PDD PDL ProtoDirt ] 		
[ DirtU ]	-> [ PDU ProtoDirt ] 		
[ DirtD ]	-> [ PDD ProtoDirt ] 		
[ DirtL ]	-> [ PDL ProtoDirt ] 		
[ DirtR ]	-> [ PDR ProtoDirt ] 		
[ DirtX ]	-> [ ProtoDirt ]			

[ moving PlayerDead ] -> cancel
[ > Player | DirtCracked ] -> [ Player | ] sfx1
[ moving Player ] [ DirtCracked ] -> [ moving Player ] [ ] SFX4
[ down Player ] -> [ stationary Player ]

( dig )

[ > Player | Rock ] -> [ Player | Rock ]
[ > Player | ProtoDirt ] -> [ Player | Hole ] sfx1
up    [ no PDU ProtoDirt | Hole ] -> [ PDU ProtoDirt | Hole ]
down  [ no PDD ProtoDirt | Hole ] -> [ PDD ProtoDirt | Hole ]
left  [ no PDL ProtoDirt | Hole ] -> [ PDL ProtoDirt | Hole ]
right [ no PDR ProtoDirt | Hole ] -> [ PDR ProtoDirt | Hole ]


[ Hole ] -> [ ]

[ PDU PDR PDD PDL ProtoDirt ] -> [ DirtUDRL ]
[ PDU PDR PDD ProtoDirt ] -> [ DirtUDR ]
[ PDU PDR PDL ProtoDirt ] -> [ DirtURL ]
[ PDU PDD PDL ProtoDirt ] -> [ DirtUDL ]
[ PDR PDD PDL ProtoDirt ] -> [ DirtDRL ]
[ PDU PDL ProtoDirt ] -> [ DirtUL ]
[ PDU PDD ProtoDirt ] -> [ DirtUD ]
[ PDU PDR ProtoDirt ] -> [ DirtUR ]
[ PDR PDD ProtoDirt ] -> [ DirtDR ]
[ PDR PDL ProtoDirt ] -> [ DirtRL ]
[ PDD PDL ProtoDirt ] -> [ DirtDL ]
[ PDU ProtoDirt ] -> [ DirtU ]
[ PDD ProtoDirt ] -> [ DirtD ]
[ PDL ProtoDirt ] -> [ DirtL ]
[ PDR ProtoDirt ] -> [ DirtR ]
[ ProtoDirt ] -> [ DirtX ]

[ PDU ] -> [ ]
[ PDD ] -> [ ]
[ PDL ] -> [ ]
[ PDR ] -> [ ]

[ ProtoDirt2 ] -> [ ProtoDirt ] again



up [ action Player Brace ] [ StatusBrace | Status no StatusBrace ] -> [ Player ] [ StatusBrace | Status StatusBrace ] sfx2
up [ action Player no Brace ] [ StatusBrace | StatusBrace ] -> [ Player Brace ] [ StatusBrace | ] sfx2

horizontal [ > Player | no SolidToPlayer ] -> [ | stationary Player ]
horizontal [ > Player | SolidToPlayer ] -> [  stationary Player | SolidToPlayer ]
down [ > Player | no SolidToPlayer ] -> [ | stationary Player ]
down [ > Player | SolidToPlayer ] -> [ stationary Player | SolidToPlayer ]
down [ PlayerAir2 | SolidToPlayer ] -> [ PlayerGround | SolidToPlayer ]
down [ PlayerAir2 | no SolidToPlayer ] -> [ | stationary PlayerAir ]
[ PlayerAir ] -> [ PlayerAir2 ]
down [ PlayerGround | no SolidToPlayer ] -> [ PlayerAir | ] SFX5
up [ > PlayerAir ] -> [ stationary PlayerAir ]
up [ > PlayerAir2 ] -> [ stationary PlayerAir2 ]
up [ > PlayerGround | no SolidToPlayer ] -> [ | stationary PlayerAir ] SFX5
down [ PlayerAir | SolidToPlayer ] -> [ PlayerGround | SolidToPlayer ]
down [ PlayerAir2 | SolidToPlayer ] -> [ PlayerGround | SolidToPlayer ]

( things fall down )

[ stationary Falls ] -> [ down Falls ]

down [ > DirtDown | Support ] -> [ stationary DirtDown | Support ]
[ down Brace | Rock ] -> [ stationary Brace | Rock ]

up [ stationary DirtNU | down DirtND ] -> [ stationary DirtNU | stationary DirtND ]
+ down [ stationary DirtND | down DirtNU ] -> [ stationary DirtND | stationary DirtNU ]
+ left [ stationary DirtNL | down DirtNR ] -> [ stationary DirtNL | stationary DirtNR ]
+ right [ stationary DirtNR | down DirtNL ] -> [ stationary DirtNR | stationary DirtNL ]
+ [ stationary Dirt | down Brace ] -> [ stationary Dirt | stationary Brace ]
+ [ stationary Brace | down Dirt ] -> [ stationary Brace | stationary Dirt ]

down [ > DirtDown | stationary DirtCracked ] -> [ stationary DirtDown | stationary DirtCracked ]
down [ > DirtDown | stationary DirtUDRL ] -> [ stationary DirtDown | stationary DirtCracked ] sfx3

up [ stationary DirtNU | down DirtND ] -> [ stationary DirtNU | stationary DirtND ]
+ down [ stationary DirtND | down DirtNU ] -> [ stationary DirtND | stationary DirtNU ]
+ left [ stationary DirtNL | down DirtNR ] -> [ stationary DirtNL | stationary DirtNR ]
+ right [ stationary DirtNR | down DirtNL ] -> [ stationary DirtNR | stationary DirtNL ]
+ [ stationary Dirt | down Brace ] -> [ stationary Dirt | stationary Brace ]
+ [ stationary Brace | down Dirt ] -> [ stationary Brace | stationary Dirt ]

down [ > Dirt | Player | no SolidToPlayer ] -> [ > Dirt | > Player | ] again
down [ > Dirt | no Built | < Player ] -> [ > Dirt | | Player ] again
down [ > Dirt | no Built ] -> [ > Dirt | ] again

[ Dirt Player ] -> [ Dirt PlayerDead ] sfx0

[ DetectPlayer ] -> [ action DetectPlayer ]
left [ DetectPlayer | no DetectPlayer ] -> [ DetectPlayer | left DetectPlayer ]

[ left DetectPlayer up Player ] [ action DetectPlayer ] ->  [ left DetectPlayer up Player ] [ up DetectPlayer ]
[ left DetectPlayer down Player ] [ action DetectPlayer ] ->  [ left DetectPlayer down Player ] [ down DetectPlayer ]

up [ left DetectPlayer | Player ] [ action DetectPlayer ] ->  [ left DetectPlayer | Player ] [ up DetectPlayer ]
down [ left DetectPlayer | Player ] [ action DetectPlayer ] ->  [ left DetectPlayer | Player ] [ down DetectPlayer ]

up [ left DetectPlayer | down Player ] [ up DetectPlayer ] ->  [ left DetectPlayer | Player ] [ DetectPlayer ]
down [ left DetectPlayer | up Player ] [ down DetectPlayer ] ->  [ left DetectPlayer | Player ] [ DetectPlayer ]

[ left DetectPlayer ] -> [ ]

[ up DetectPlayer ] [ stationary Status ] -> [ up DetectPlayer ] [ up Status ]
[ down DetectPlayer ] [ stationary Status ] -> [ down DetectPlayer ] [ down Status ]
[ up DetectPlayer ] [ stationary StatusBrace ] -> [ up DetectPlayer ] [ up StatusBrace ]
[ down DetectPlayer ] [ stationary StatusBrace ] -> [ down DetectPlayer ] [ down StatusBrace ]

==============
WINCONDITIONS
==============

any Player on Sky

=======
LEVELS
=======

message LEFT/RIGHT to walk, UP to jump. Push onto dirt to dig it out. Press X to place/remove a support. Press DOWN to wait a turn.

##########.
#!.......#.
#........#.
#........#.
#........#.
#.OOOOOO.#.
#........#.
#........#.
#........#.
#........#.
:::::::::::
:::::::::::
:::::::::::
:::::::::::
:::::::::::
:::::::::::
:::::::::::
:::::::::::
:::::::::::
#.OOOOOO.#.
#........#.
#........#.
#........#.
#........#.
#.OOOOOO.#.
#........#.
#........#.
#........#.
#........#.
1.OOOOOO.0.
#........#.
#........#.
#........#.
#........#.
#.OOOOOO.#.
#........#.
#........#.
#........#.
#........#.
2.OOOOOO.0.
#........#.
#........#.
#........#.
#........#.
#.OOOOOO.#.
#........#.
#........#.
#........#.
#........#.
3.OOOOOO.0.
#........#.
#........#.
#........#.
#........#.
#.OOOOOO.#.
#........#.
#........#.
#........#.
#........#.
4.OOOOOO.0.
#........#.
#........#.
#........#.
#........#.
#.OOOOOO.#$
#........#$
#........#$
#YYYYYYYY#$
#XX....XX#$
5.XX.@XX.0*
##########$
##########$
##########$
##########$
##########$
