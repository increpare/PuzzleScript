title Flippin' Ladybug
author Chris Zorg
homepage https://chriszorg.itch.io

require_player_movement



========
OBJECTS
========

Background
Lightgreen

Blank
black

Target0
#73AB20

Target1
#CDD849

Wall
Brown Darkbrown
00010
11111
01000
11111
00010

Player
transparent

Playerv01
Red Black White
.010.
00000
10101
00100
.....

Playerh01
Red Black White
.010.
00000
10121
00011
.....

Playerv10
Red Black White
.010.
00000
12121
01110
.....

Playerh10
Red Black White
.010.
00000
12101
11000
.....

Card000
Green Yellow
.000.
00000
00000
10001
.111.

Card111
Green Yellow
.111.
11111
11111
01110
.000.

Cardh01
Green Yellow
..10.
.100.
.100.
.100.
..10.

Cardh10
Green Yellow
..01.
.011.
.011.
.011.
..01.

Cardv01
Green Yellow
.....
.000.
01110
11111
.....

Cardv10
Green Yellow
.....
.111.
10001
00000
.....


Temp000
Transparent

Temp111
Transparent

Temph01
Transparent

Temph10
Transparent

Tempv01
Transparent

Tempv10
Transparent






=======
LEGEND
=======

0 = Blank
. = Background
# = Wall
p = Player and Playerv10
a = Card000
b = Card111
x = Target0
y = Target1

Obj = Card000 or Card111 or Cardh01 or Cardh10 or Cardv01 or Cardv10 or Temp000 or Temp111 or Temph01 or Temph10 or Tempv01 or Tempv10 


=======
SOUNDS
=======



================
COLLISIONLAYERS
================

Background
Blank, Target0, Target1 
Playerv01, Playerh01, Playerv10, Playerh10
Player, Wall, Card000, Cardv01, Cardh01, Cardv10, Cardh10, Card111, Cardv10, Cardv10, Cardv01, Cardv01, Temp000, Tempv01, Temph01, Tempv10, Temph10, Temp111, Tempv10, Temph10, Tempv01, Temph01





======
RULES
======

right [ right Player ] -> [ right Player right Playerh01 ]
left [ left Player  ] -> [ left Player left Playerh10 ]
up [ up Player ] -> [ up Player up Playerv01 ]
down [ down Player ] -> [ down Player down Playerv10 ]

[ Player Blank ] -> [ Blank ]
[ Playerh01 Blank ] -> [ Blank ]
[ Playerh10 Blank ] -> [ Blank ]
[ Playerv01 Blank ] -> [ Blank ]
[ Playerv10 Blank ] -> [ Blank ]


right [ right Player | Card000 ] -> [ right Player | right Temph01 ]
right [ right Player | Card111 ] -> [ right Player | right Temph10 ]
right [ right Player | Cardh01 ] -> [ right Player | right Temp111 ]
right [ right Player | Cardh10 ] -> [ right Player | right Temp000 ]
right [ right Player | Cardv01 ] -> [ right Player | right Tempv01 ]
right [ right Player | Cardv10 ] -> [ right Player | right Tempv10 ]

left [ left Player | Card000 ] -> [ left Player | left Temph10 ]
left [ left Player | Card111 ] -> [ left Player | left Temph01 ]
left [ left Player | Cardh01 ] -> [ left Player | left Temp000 ]
left [ left Player | Cardh10 ] -> [ left Player | left Temp111 ]
left [ left Player | Cardv01 ] -> [ left Player | left Tempv01 ]
left [ left Player | Cardv10 ] -> [ left Player | left Tempv10 ]

up [ up Player | Card000 ] -> [ up Player | up Tempv01 ]
up [ up Player | Card111 ] -> [ up Player | up Tempv10 ]
up [ up Player | Cardh01 ] -> [ up Player | up Temph01 ]
up [ up Player | Cardh10 ] -> [ up Player | up Temph10 ]
up [ up Player | Cardv01 ] -> [ up Player | up Temp111 ]
up [ up Player | Cardv10 ] -> [ up Player | up Temp000 ]

down [ down Player | Card000 ] -> [ down Player | down Tempv10 ]
down [ down Player | Card111 ] -> [ down Player | down Tempv01 ]
down [ down Player | Cardh01 ] -> [ down Player | down Temph01 ]
down [ down Player | Cardh10 ] -> [ down Player | down Temph10 ]
down [ down Player | Cardv01 ] -> [ down Player | down Temp000 ]
down [ down Player | Cardv10 ] -> [ down Player | down Temp111 ]




startloop

right [ right Obj | Card000 ] -> [ right Obj | right Temp000 ]
right [ right Obj | Card111 ] -> [ right Obj | right Temp111 ]
right [ right Obj | Cardh01 ] -> [ right Obj | right Temp111 ]
right [ right Obj | Cardh10 ] -> [ right Obj | right Temp000 ]
right [ right Obj | Cardv01 ] -> [ right Obj | right Tempv01 ]
right [ right Obj | Cardv10 ] -> [ right Obj | right Tempv10 ]

left [ left Obj | Card000 ] -> [ left Obj | left Temp000 ]
left [ left Obj | Card111 ] -> [ left Obj | left Temp111 ]
left [ left Obj | Cardh01 ] -> [ left Obj | left Temp000 ]
left [ left Obj | Cardh10 ] -> [ left Obj | left Temp111 ]
left [ left Obj | Cardv01 ] -> [ left Obj | left Tempv01 ]
left [ left Obj | Cardv10 ] -> [ left Obj | left Tempv10 ]

up [ up Obj | Card000 ] -> [ up Obj | up Temp000 ]
up [ up Obj | Card111 ] -> [ up Obj | up Temp111 ]
up [ up Obj | Cardh01 ] -> [ up Obj | up Temph01 ]
up [ up Obj | Cardh10 ] -> [ up Obj | up Temph10 ]
up [ up Obj | Cardv01 ] -> [ up Obj | up Temp111 ]
up [ up Obj | Cardv10 ] -> [ up Obj | up Temp000 ]

down [ down Obj | Card000 ] -> [ down Obj | down Temp000 ]
down [ down Obj | Card111 ] -> [ down Obj | down Temp111 ]
down [ down Obj | Cardh01 ] -> [ down Obj | down Temph01 ]
down [ down Obj | Cardh10 ] -> [ down Obj | down Temph10 ]
down [ down Obj | Cardv01 ] -> [ down Obj | down Temp000 ]
down [ down Obj | Cardv10 ] -> [ down Obj | down Temp111 ]

endloop


[ Card000 Blank ] -> [ Blank ]
[ Card111 Blank ] -> [ Blank ]
[ Cardh01 Blank ] -> [ Blank ]
[ Cardh10 Blank ] -> [ Blank ]
[ Cardv01 Blank ] -> [ Blank ]
[ Cardv10 Blank ] -> [ Blank ]
[ Temp000 Blank ] -> [ Blank ]
[ Temp111 Blank ] -> [ Blank ]
[ Temph01 Blank ] -> [ Blank ]
[ Temph10 Blank ] -> [ Blank ]
[ Tempv01 Blank ] -> [ Blank ]
[ Tempv10 Blank ] -> [ Blank ]



late [ Temp000 ] -> [ Card000 ]
late [ Temp111 ] -> [ Card111 ]
late [ Temph01 ] -> [ Cardh01 ]
late [ Temph10 ] -> [ Cardh10 ]
late [ Tempv01 ] -> [ Cardv01 ]
late [ Tempv10 ] -> [ Cardv10 ]


==============
WINCONDITIONS
==============

All Target0 On Card000 
All Target1 On Card111 




=======
LEVELS
=======

message 1
00000000000
00000000000
0000######0
#####.....#
#.......b.#
#a..p..y..#
#.........#
#y###.....#
###00######
00000000000
00000000000



message 2
000000000000
000000000000
000000#####0
0###......#0
0#....p.a.#0
0#..a..y..#0
0#........#0
0#..x.#####0
00#####00000
000000000000
000000000000



message 3
000000000
00#####00
###.x.###
#y..p..y#
##.#.#.##
0.b.a.a.0
0.......0
0##...##0
00#####00
000000000
000000000



message 4
000000000000
000000000000
000000000###
0##0000..#y#
0#...##..b.#
0..p.......0
#.a..##...#0
#x#..0000##0
###000000000
000000000000
000000000000



message 5
000000000000
000000000000
###000000###
#x#..00..#y#
#.a..##..b.#
0..p.......0
0##..##..##0
000000000000
000000000000
000000000000
000000000000



message You are a winner!



message 6 - for movement testing purposes only - someone told me there is at least one bug in the game.
##############
#............#
#..........b.#
#.p.ab.a.....#
#.....#.b.b..#
#.......b....#
#......a...a.#
#.....b......#
#..0..a.a.####
#.........#xy#
##############





