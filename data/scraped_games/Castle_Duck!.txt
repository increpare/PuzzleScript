title Castle Duck!
author Daniel 'Sheepolution' Haazen
homepage www.puzzlescript.net
zoomscreen 15x15
========
OBJECTS
========

Background
gray lightgray
11111
01000
11111
00010
11111

skydark
darkblue

star
darkblue yellow
00000
00000
00100
00000
00000

fire (The torches on the wall)
orange yellow darkbrown
..0..
.010.
.010.
.222.
..2..

crate
darkbrown brown
00000
01110
01110
01110
00000

ladder
brown
0...0
0...0
00000
0...0
0...0

Topground (Grass)
Green orange brown
00000
11111
12112
22122
22222

MidGround (dirt)
 Brown orange
00010
01000
00010
00000
00100

bricks (castlestone)
gray darkgray
10110
01101
10110
01011
01101

fakebricks (Walk-through stone)
gray darkgray
10110
01101
10110
01011
01101

Player
Black Orange green Blue
.000.
.111.
22222
.333.
.3.3.

Enemy (Wizard)
purple white
.000.
.111.
.000.
.000.
.0000

Princess
pink Orange Yellow
.222.
.111.
.000.
.000.
.0000

Button
Red
.....
.....
.....
.000.
.000.

GateClosed
black
0.0.0
00000
0.0.0
00000
0.0.0

GateOpen
black
.....
.....
.....
.....
.....

CableOff
white
.....
.....
.....
.....
.....

CableGoOff
white
.....
.....
.....
.....
.....

CableOn
Red
.....
.....
.....
.....
.....

Goal
black
.....
.....
.....
.....
.....

=======
LEGEND
=======
gate = gateclosed or gateopen
cable = cableon or cableoff or cablegooff
ground = topground or midground or bricks
solid = gateclosed or ground or crate or player or ladder or princess
moveable = player or crate or enemy
P = Player
C = crate
# = MidGround
~ = topground
. = Background
, = skydark
x = cableoff
O = Button
% = gateclosed
; = player and skydark
{ = button and skydark
" = Cableoff and skydark
U = crate and skydark
! = goal
@ = bricks
* = star
^ = fire
& = bricks and cableoff
$ = ladder
€ = fakebricks
1 = princess
2 = enemy

=======
SOUNDS
=======
SFX0 77700504 (Press button)
SFX1 89729307 (Move crate)
sfx2 17138307 (Walk)
StartGame 8892900
EndLevel 83345102
EndGame 34629702
CloseMessage 44855507
================
COLLISIONLAYERS
================
Background
skydark star fire
cable goal
Button gateopen ladder fakebricks
Ground Player crate gateclosed enemy princess

======
RULES
======

late [Button no player no crate | CableOn] -> [Button | CableGoOff]
late [CableGoOff | CableOn] -> [CableGoOff | CableGoOff]
late [CableGoOff | Button] -> [CableOff | Button]
late [CableOff | CableGoOff] -> [CableOff | CableOff]
late [moveable Button | Cableoff] -> [moveable Button | Cableon] SFX0
Late [CableOn | CableOff ] -> [CableOn | CableOn]

late [gateopen no moveable | CableOff] -> [gateclosed | CableOff ]
late [gateclosed | CableOn] -> [gateopen | CableOn ]


[ | | |cable|button enemy| ] -> [ | | |cable|button | enemy ]

horizontal [> moveable | crate] -> [> moveable | > Crate ] SFX1
horizontal [> moveable | no solid] -> [  | moveable] sfx2

[player| | |cable|button|enemy] -> [player| | |cable|button | < enemy ]


down [player | no solid ] -> [  | player]
down [enemy | no solid ] -> [  | enemy]



late down [crate | no solid ] -> [  | crate]

[> player | ladder] -> [> player | ladder]

[> player goal] -> [goal]


==============
WINCONDITIONS
==============
no player

=======
LEVELS
=======
message I need to save her!
,,,,,*,,*,,,,,*@@
,,,,,,,"""*""",@@
*,,*"""","""*",@@
,*"""*,*,,,*,"*@@
,""*,,,,,*,,,",@@
,{,,;,,,,,u,,""%!
~~~~~~~~~~~~~~~@@
###############@@
message  I will find you!

@@@@@@@@@@@@@@@@
@.@.@.@.@.@@@@@@
..........@@@@@@
........co&@@@@@
.....c.@@@&@@@@@
.^...c^...&^....
p....c....%....!
@@@@@@@@@@@@.@@@
@@@@@@@@@@@@.@@@
@@@@@@@@@@@@.@@@
@@@@@@@@@@@@.@@@
@@@@@@@@@@@@@@@@
message I must.. something something!

@xxxxxxxxxxxxxxxxxxxxx...
@x^....^....^....^...x^..
@x.........c.........xo..
!x...c....@@@@@@@@@@@@@@@
@&@%&@@@@.c..............
@&@.&@@@@@%x.xxxx........
@&@.&@....^xxx^.xxx^....^
@&@@&@p...c..c...cxxxxo..
@&@@&&&&&&&&&&&&&&&@@@%&@
@&@@@@@@@@@@@@@@@@@@@@.&@
@&&&&&&&&&&&&&&&&&&&&&&&@

message I.. you know.. thing!

@...........@@@@@
@...........@@@@@
@...........@@@@@
@...........@@@@@
@$.o.pccc...@@@@@
@$@&&&&&&%@@@@@@@
@$..........@@@@@
@$.........o@@@@@
@$@@&@@@%%&&@@@@@
@$......xxx.@@@@@
@$..............!
@$........@@@@@@@
@$........@@@@@@@
@$........@@@@@@@
@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@

message I WILL SAVE YOU!

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@.......................@
@@@@@@@@@@@@@.^...^...^...^...^...^.@
@o€€€€€€€€€€€p....xo2..............!1
@&@@@@@@@@@@@@@@%&&@%&@@@@@@@@@@@@@@@
@&@@@@@@@@@@@@@@.@@@.&@@@@@@@@@@@@@@@
@&&&&&&&&&&&&&&&&&&&&&@@@@@@@@@@@@@@@

Message Wait, this isn't my rubber duck!

Message THE END!


