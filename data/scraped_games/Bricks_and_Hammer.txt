title Bricks and Hammer
author Strangecreation
homepage www.puzzlescript.net
again_interval 0.4
run_rules_on_level_start

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Black White Blue
.....
.111.
.222.
.333.
.3.3.

Crate
 Yellow Orange
.11..
11.11
00000
0...0
00000

Hammer
Black brown white
..222
...1.
...0.
...0.
.....

DownHammer
Black brown white
.....
.....
.....
..222
...1.

Brick
Orange
.....
.....
00.00
.....
.....

Rubble
 DARKBROWN
000.0
.....
0.000
.....
0.0.0

PlayerBrick
red
RobotBrick
red


Pit
black


RobotPlayer
Black Black White purple
.....
.111.
.222.
.333.
.3.3.

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target
H = Hammer
_ = Pit
R = RobotPlayer


=======
SOUNDS
=======

Crate MOVE 36772507
sfx0 99558108
sfx1 64605708
sfx2 51123508

================
COLLISIONLAYERS
================

Background
Rubble
Target
Player, Wall, Crate, PlayerBrick, Pit, RobotPlayer, RobotBrick
Hammer, DownHammer, Brick

======
RULES
======

[Rubble] -> [no Rubble]
[DownHammer] -> [no DownHammer]


(ROBOT)
right[RobotPlayer no brick] -> [> RobotPlayer] again
up[RobotPlayer brick] -> [> RobotPlayer > Brick] again
[ >  RobotPlayer | Crate ] -> [  >  RobotBrick | > Crate  ]
[ >  RobotPlayer  Hammer ] -> [  >  RobotPlayer  > Hammer  ]
[ >  RobotPlayer  Brick ] -> [  >  RobotPlayer  > Brick ]
late [RobotBrick no Hammer] -> [RobotPlayer Brick]
late [RobotBrick Hammer] -> [RobotPlayer Hammer]

(PLAYER)
[ >  Player | Crate ] -> [  >  PlayerBrick | > Crate  ]
[> Crate | Hammer] -> [Crate | Hammer]
[ >  Player  Hammer ] -> [  >  Player  > Hammer  ]
[ >  Player  Brick ] -> [  >  Player  > Brick ]

[ >  Brick | Hammer] -> [  Brick | Hammer ]

[> Brick | wall ] -> [ brick | Wall]
[> Brick | no wall ] -> [ | Wall] sfx1
late [Hammer Wall] -> [Hammer Rubble] sfx0

late [Hammer Pit] -> [DownHammer Pit] sfx2 again

late [PlayerBrick no Hammer] -> [Player Brick]
late [PlayerBrick Hammer] -> [Player Hammer]


late [Brick no Player no RobotPlayer] -> [Wall] sfx1
==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======


message These crates are overflowing with bricks

......
r*...o
......

message They can really get in my way

######
#..o.#
#..#.#
#....#
#....#
#.#..#
#.*..#
#.P..#
######


#######
#...#O#
#.*P*.#
#O....#
#######

#####
#O@O#
#.*.#
#.*.#
#.P.#
#...#
#####

#######
#o*.*o#
##.p.##
#o*.*o#
#######

##...##
#o.*.o#
..*p*..
#o.*.o#
##...##

message Luckily I've brought my hammer with me

######
rH##*o
######

message It can help me clear a path

####..
#..###
#..*PH
#o####
###...


o.H.*.
.*.P.o

...*Po
..H*.o






message I can use the bricks to fill in pits
......
______
r*...o
......
message Then use my hammer to get past them

#######
#..o..#
_______
#..*..#
#H.P..#
#######

p_#_
.o*#
h_#_

....
_.o_
#**#
_o_#
.ph.

#_oh_#
#_.._#
..**..
#_.._#
#_op_#

#######
#_____#
#_PO._#
#_H*._#
#_____#
#######

._..._.
._.o*_.
._____.
.......
._____.
._.@h_.
._.p._.

message Thanks for playing!

