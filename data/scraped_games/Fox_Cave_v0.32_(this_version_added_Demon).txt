(,.-•*^*•-.,.-•*^*•-.,.-•*^*•-.,.-•*^*•-.,.-•*^*•-.,)
(Original Puzzlescript Boulder Dash source code made)
(by Connor M (connorses.devianart.com) Please credit)
(him by pasting this in if you use the falling      )
(objects code! ^-^;                                 )
(,.-•*^*•-.,.-•*^*•-.,.-•*^*•-.,.-•*^*•-.,.-•*^*•-.,)

title Fox Cave v0.32 (this version added Demon)
author Connorses
homepage connorses.deviantart.com

color_palette arnecolors
background_color #160035
text_color white
realtime_interval 0.25

noundo
noaction
require_player_movement
zoomscreen 25x13
youtube slUuwvfXil0

========
OBJECTS
========
backg
black
dirtbg
black darkbrown
01010
10001
10000
00001
01100

player
Orange Red Darkblue
.0.0.
.202.
.111.
.0001
.0.0.

corpse
red
.0.0.
00000
.000.
00000
.0.0.

dirt
brown darkbrown
01011
10101
01010
01101
11011

brick
darkblue blue
00000
01110
00000
01110
01110
brick2
red lightred darkred
00020
01112
20222
21110
01102

rock
darkred red
.111.
11000
10101
00100
10000
rock1
darkblue blue
.111.
11000
10101
00100
10000


boulder
lightgrey grey
.000.
10101
01010
01101
.111.


diamond
green lightgreen darkgreen
..1..
.011.
02111
.220.
..2..

chest
brown yellow darkbrown
.....
.000.
00000
22122
00000

key
yellow
.....
.....
...00
000.0
00.00

gate
yellow brown
11111
10001
10011
10001
11111
opengate
yellow
00000
0...0
0...0
0...0
00000

fireflyd
yellow white darkblue
11.11
11.11
20002
.000.
.2.2.
fireflyu
yellow white darkblue
.2.2.
.000.
20002
11.11
11.11
fireflyl
yellow white darkblue
..211
20011
.00..
20011
..211
fireflyr
yellow white darkblue
112..
11002
..00.
11002
112..

explosion
red yellow
.10.1
01.10
10.10
.1001
0.1.0
explosion2
red yellow
.10.1
01.10
10.10
.1001
0.1.0
explosion3
red yellow
.10.1
01.10
10.10
.1001
0.1.0

nibblerr
grey white
.....
.0000
0.1.1
00000
0..0.
nibblerl
grey white
.....
0000.
1.1.0
00000
.0..0

demon
red white darkblue
.1.1.
1...1
12021
.000.
.0.0.

tele
blue white
00.0.
.1.10
01...
0..10
.0.00

l
black
.....
.....
.....
.....
.....
r
black
.....
.....
.....
.....
.....
m
green
.....
.....
.....
.....
.....
t
black
.....
.....
.....
.....
.....
=======
LEGEND
=======
. = backg
; = dirtbg

# = brick
1 = brick
2 = brick2
3 = brick2
4 = rock
5 = rock1
6 = rock

@ = dirt and dirtbg

p = player

o = boulder and backg
* = diamond and backg

c = chest and backg
k = key and backg

g = gate and m and backg

f = fireflyu and backg
n = nibblerr and backg
d = demon

! = explosion

0 = tele

wall = brick or brick or brick2 or rock or rock1

pushable = boulder or chest or key
round = boulder or diamond
falling = boulder or diamond or chest or key

firefly = fireflyl or fireflyr or fireflyu or fireflyd
nibbler = nibblerr or nibblerl
monster = firefly or nibbler or demon

splosion = explosion or explosion2 or explosion3

obstacle = round or player or dirt or wall or chest or key or t or monster

destructible = player or rock1 or rock or monster or key or dirt or boulder

required = diamond or chest
marker = l or m or r

background = backg or dirtbg

=======
SOUNDS
=======
Sfx0 65945500 (pickup)
Sfx1 53087508 (death)
Sfx2 87691904 (boulder)
Sfx3 54502    (chest)
Sfx4 53046700 (diamond)
Sfx5 86310507 (walk)
Sfx6 90045707 (player move)
Sfx7 6157308   (win)
Sfx8 97434708 (teleport)

boulder move 87691904
diamond move 53046700
Explosion2 create 1261502
EndLevel 35743508

================
COLLISIONLAYERS
================
background
l
r
m,t,explosion,explosion2,explosion3
corpse,gate,opengate, tele
obstacle

======
RULES
======

(teleports)
[> player|tele|...|no obstacle] -> [|tele|...|player] Sfx8


[corpse] -> []

[l] -> []
[r] -> []
[t] -> []

[> player|dirt] -> [t|player] Sfx6
[> player |no obstacle] -> [t|player]
[player] -> [player]
[> player|diamond] -> [|player] Sfx0

(Explosions!!)
[explosion3] -> []
[explosion2 |destructible] -> [explosion2|explosion3]
[explosion2 |no obstacle] ->  [explosion2|explosion3]
[explosion2 |destructible] -> [explosion2|explosion3]
[explosion2 |no obstacle] ->  [explosion2|explosion3]
[explosion2 |destructible] -> [explosion2|explosion3]
[explosion2 |no obstacle] ->  [explosion2|explosion3]
[explosion2 |destructible] -> [explosion2|explosion3]
[explosion2 |no obstacle] ->  [explosion2|explosion3]
[explosion2] -> []
[explosion |destructible] -> [explosion|explosion2]
[explosion |no obstacle] ->  [explosion|explosion2]
[explosion |destructible] -> [explosion|explosion2]
[explosion |no obstacle] ->  [explosion|explosion2]
[explosion |destructible] -> [explosion|explosion2]
[explosion |no obstacle] ->  [explosion|explosion2]
[explosion |destructible] -> [explosion|explosion2]
[explosion |no obstacle] ->  [explosion|explosion2]
[explosion] -> []
[splosion|player] -> [splosion|corpse]Sfx1
late[destructible splosion] -> [splosion]
late[splosion dirtbg] -> [splosion backg]
(MONSTERS!)

[fireflyu] -> [up fireflyu]
[fireflyr] -> [right fireflyr]
[fireflyd] -> [down fireflyd]
[fireflyl] -> [left fireflyl]
[> firefly|obstacle] -> [v firefly|obstacle]
[up firefly] -> [up fireflyu]
[right firefly] -> [right fireflyr]
[down firefly] -> [down fireflyd]
[left firefly] -> [left fireflyl]

right [nibblerr|obstacle] -> [nibblerl|obstacle]
left  [nibblerl|obstacle] -> [nibblerr|obstacle]
[nibblerr] -> [right nibblerr]
[nibblerl] -> [left  nibblerl]

[demon|...|no obstacle|player] -> [ |...|demon|player]

[demon  |player] -> [demon  |corpse]Sfx1
[firefly|player] -> [firefly|corpse]Sfx1
horizontal [nibbler|player] -> [nibbler|corpse]Sfx1

(----V-V-Falling objects-V-V----)

[> diamond] ->[> diamond] Sfx4
[> boulder] ->[> boulder] Sfx2
(chest opens with key)
[stationary chest|stationary key] -> [diamond| ] Sfx3


[> falling|no obstacle] -> [t|falling]

left [stationary round|no obstacle] -> [round l| ]
right[stationary round|no obstacle] -> [round r| ]

down [round|round l] -> [left  round|round l]
down [round|round r] -> [right round|round r]
down [round|round l r] -> [round m|round l r]
horizontal [round m|no obstacle] -> [|round]
horizontal [> round|obstacle] -> [round|obstacle]

down [falling|no obstacle] -> [> falling|]

down[> falling|no obstacle|stationary player] -> [|falling|corpse] Sfx1

down[falling|firefly] -> [falling|explosion] Sfx3
down[falling|monster] -> [falling|corpse] Sfx3 (Rocks fall, everyone dies.)

(player pushes things LIKE A FOX)
horizontal [> player|pushable] -> [> player|> pushable] Sfx2




(gates)
[gate] -> opengate]
[required][opengate] -> [required][gate]
[opengate m] -> [opengate no m] Sfx7 ( message DIAMONDS COLLECTED - the EXIT is OPEN!)

==============
WINCONDITIONS
==============
all opengate on player
no required

=======
LEVELS
p*g

111111111111
1p@@@@o@@@@1
1@@@@@*@@@o1
1@@@@@@@@@@1
1@@@@@@@@@@1
1@@*;@d@;*@1
1@@@@@@@@@@1
1@@@@@;@@@@1
1@@@@@*@@@@1
1g@@@@@@@@@1
111111111111

...0551o55o5....
....1@@@@@@1;;;;
5555#......1@@*@
##51#.......@*@@
##0##.......@@@@
....1.......;;;;
.g.p1...........
...555.515550...

2222222222222222222
2@@6;g.;66@@@@4@@@2
2@@@;;;;6@@@@@6@@@2
2@@@@@@o@@@o@@4**@2
2@@@@@@@@@@@@@6o*@2
2@@p@@@@@@@@@@4@@@2
2@@@@@@@@@@@@@6@@*2
2@@@@@@@@@@@@@6*@@2
2@@@4@@@@@@@@@6@@@2
2@@@@......@@@4@@@2
2@@@4......@@@4@@o2
2@@44f.....4@@4@oo2
2222222222222222222

...........5o;*;
.p...o.....5o@*@
...5555....5o@@@
####n......;5;;;
...#5555........
.g.#;...n......5
..;@@;.........*
..;@@;........**

p@@@@@@
@@@@@*@
@@@@@@@
@@@@@@@
@@@*@@@
@@@@*o5
@@@@@5@
@@@@@@@
@@@@;;;
r;;;;;;
55n55;.
;;;;5..
g...5..
message Kill the fireflies!
##############
#@@@@@@@@@@@@#
#@@@@@@@@@@@@#
#@@@@@o@@@@@@#
#@@@@@6@@...@#
#@@@@@@@.....#
#6@@@@@......#
#@@@@@@......#
#@@@@@@;;;;;;#
#@@@@@@6;p;;6#
#@@@@@@@6666@#
#44...4@@@@@@#
#4*fg.44@@@@@#
#44...*4@@@@@#
#422222@@@@@@#
##############

message Dig up all diamonds!
#########
#4644o@g#
#44@@@55#
#*o@@@@@#
#o@@@@@*#
#@.@@o@@#
#@p@@*@@#
#4@@@@@6#
#########

message Unlock buried treasure!
############
#@@@...@@@@#
#@@@.k.@@@@#
#@p@@@o@@@@#
#@@@@@@@@@@g
#@o*@@@@@@@#
#@@@@@@oc@@#
############
message Beware of cave-ins.
############
#@.......@##
#2o.oo.*o2@#
#@2ooooo2o@#
#@@2ooo2@@@#
#@o@2*2@@@@#
#@*o.@.@o@p#
#@@o@...@@@#
#g@@......@#
#.....o....#
#;;;;;;;;;;#
#@@@@@@@@@@#
############

############
#@@@@@@@@@@#
#@o@@@@@@@@#
#@@@@@@;;;@#
#...@@5;k;@#
#....5@ooo@#
#g.c.@@@@@@#
#555...;;;;#
#@@....;;;;#
#@@@.......#
#@p@..5....#
#@@@@.5o;;;#
############

message ALL LEVELS COMPLETED!
