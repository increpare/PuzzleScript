title Line of Sight
author Joshua Minor
homepage pixelverse.org
run_rules_on_level_start 
color_palette gameboycolour              

========
OBJECTS
========

Background 
LIGHTGREEN GREEN
11111
01111
11101
11111    
10111 


Target 
DarkBlue 
..... 
.000.
.0.0.
.000.
.....     

Wall     
BROWN DARKBROWN     
00010
11111
01000
11111
00010

Human 
Black Orange White Blue     
.000.
.111.
22222
.333.
.3.3.

Crate
Orange brown
00000
01010
00100
01010
00000

Dirt 
orange brown
00100
00000
00010
10000
00000

Player 
white black
.0.0.
01010
00000
.000.
11.11

DeadPlayer 
white black red
.00.1
02011
.000.
02001
.00.1

Rock 
grey darkgrey
.100.
11000
11000
11000
11101

HGaze
yellow
.....
.....
.0.0.
.....
.....

VGaze
yellow
.....
..0..
.....
..0..
.....

UnbrushedSand 
yellow orange
00000
01000
00000
00010
00000

HBrushedSand
yellow orange
00000
01011
00000
11010
00000

VBrushedSand
yellow orange
01000
01010
00000
01010
00010

=======
LEGEND
=======

Obstacle = Wall or Crate or Human or Rock
Gaze = VGaze or HGaze
Sand = UnbrushedSand or HBrushedSand or VBrushedSand
. = Background
# = Wall
P = Player
* = Crate
O = Target
H = Human
R = Rock
S = UnbrushedSand

=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Dirt, Sand
Target
Player, DeadPlayer, Wall, Crate, Rock, Human
Gaze

======
RULES     
======     

(push & pull crates)
[ > Player | Crate ] -> [ > Player | > Crate ]
[ < Player | Crate ] -> [ < Player | < Crate ]

(kick a block)
([ Action Player | Crate ] -> [  Action  Player | > Crate  ])

(push a human)
[ > Crate | Human ] -> [ > Crate | > Human ]

(brush the sand as you walk on it)
Horizontal [ UnbrushedSand >  Player  ] -> [ HBrushedSand > Player ]
Vertical [ UnbrushedSand > Player ] -> [ VBrushedSand > Player ] 

(remove all gaze)
[ Gaze ] -> []
(compute gaze via line-of-sight - thanks @toph_wells)
late Horizontal [ Human | no Obstacle no HGaze ] -> [ Human | HGaze ]
late Vertical [ Human | no Obstacle no VGaze ] -> [ Human | VGaze ]
late Horizontal [ HGaze | no Obstacle no HGaze ] -> [ HGaze | HGaze ]
late Vertical [ VGaze | no Obstacle no VGaze ] -> [ VGaze | VGaze ]

(don't let the player move onto gaze)
(we do this last, so we use the final gaze computation)
late [ Player Gaze ] -> cancel

(if you're seen, that human rushes over and kills you)
(
late [ Human | ... | Gaze | Player ] -> [ | ... | Human | Player ]
late [ Player Gaze ] -> [ DeadPlayer Gaze ]
)

==============
WINCONDITIONS
==============

All Player on Target
No DeadPlayer

=======     
LEVELS
=======

message Don't let them see you

#########
#.......o
#.r.h...#
#.......#
#*.....r#
#.......#
p.....*.#
#########

(message Push 'em)

#########
p..*....#
#.*h....#
#.......#
#.......#
#...h...#
#.......o
#########

#########
#h......o
#.r.h...#
#.......#
#*.....r#
#..*....#
p.....*h#
#########

message Congratulations!


