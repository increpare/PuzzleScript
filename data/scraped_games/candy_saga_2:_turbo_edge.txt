title candy saga 2: turbo edge
author Mark Wonnacott
homepage http://twitter.com/ragzouken

youtube QlrJgbdtKcc
color_palette pastel

noundo
realtime_interval 0.15

========
OBJECTS
========

Background
black

Floor
brown darkbrown
00000
01000
00001
00100
00000

Wall
grey darkbrown black
01111
20001
20001
20001
22220

Sweet
purple white darkred
.000.
20110
20010
20000
.222.

RainbowH
#FF7F7F #E5FF7F #7FFFB2 #7FB4FF #E37FFF
00000
11111
22222
33333
44444

RainbowV
#FF7F7F #E5FF7F #7FFFB2 #7FB4FF #E37FFF
01234
01234
01234
01234
01234

StarU
white lightgray #FF7F7F #E5FF7F #7FFFB2 #7FB4FF #E37FFF
..0..
00000
21016
20106
03450

StarD
white lightgray #FF7F7F #E5FF7F #7FFFB2 #7FB4FF #E37FFF
23056
00000
21016
.010.
0...0

StarR
white lightgray #FF7F7F #E5FF7F #7FFFB2 #7FB4FF #E37FFF
220..
00000
4101.
5010.
066.0

StarL
white lightgray #FF7F7F #E5FF7F #7FFFB2 #7FB4FF #E37FFF
..022
00000
.1014
.0105
0.660

WhiteOn
yellow white brown
01111
20001
20001
20001
22220

WhiteOff
brown darkbrown yellow
01111
20001
20001
20001
22220

Temp
white

Exit
black
.0.0.
0.0.0
.0.0.
0.0.0
.0.0.

=======
LEGEND
=======

. = Background
, = Floor
# = Wall
* = StarU and Floor
@ = Sweet and Floor
/ = WhiteOn
_ = WhiteOff
+ = Exit and Floor
x = Exit
| = RainbowV

Rainbow = RainbowV or RainbowH

StarH = StarL or StarR
StarV_ = StarU or StarD

Star = StarV_ or StarH
Player = Star

Obstacle = Wall or WhiteOn or Star

Candy = Sweet
White = WhiteOff or WhiteOn

=======
SOUNDS
=======

sfx0 56581700 (turn)
sfx1 70597304 (switch)
sfx2 25844102 (crash)
Candy destroy 32939900
endlevel 92970503
restart 25844102

================
COLLISIONLAYERS
================

Temp
Background
Floor, Rainbow, WhiteOff
Exit
Sweet
Star, Wall, WhiteOn

======
RULES     
======

(input)
[ action Star ] [ WhiteOff no Temp Star ] -> restart

[ action Star ] [ WhiteOn  no Temp ] -> [ action Star ] [ WhiteOff Temp ]
[ action Star ] [ WhiteOff no Temp ] -> [ action Star ] [ WhiteOn  Temp ]

[ Temp ] -> [ ] sfx1

[ moving Star ] -> [ Star ]

(movement)
[ StarU ] -> [ up    StarU ]
[ StarD ] -> [ down  StarD ]
[ StarL ] -> [ left  StarL ]
[ StarR ] -> [ right StarR ]

(turning)
[ > Star | Obstacle ] -> [ Star Temp | Obstacle ]

horizontal [ no Obstacle | stationary StarV_ Temp | Obstacle ] -> [ | < StarV_ Temp | Obstacle ]
vertical   [ no Obstacle | stationary StarH  Temp | Obstacle ] -> [ | < StarH  Temp | Obstacle ]

horizontal [ Obstacle | stationary StarV_ Temp | Obstacle ] -> restart
vertical   [ Obstacle | stationary StarH  Temp | Obstacle ] -> restart

horizontal [ no Obstacle | stationary StarV_ Temp | no Obstacle ] -> [ | > StarV_ Temp | ] 
vertical   [ no Obstacle | stationary StarH  Temp | no Obstacle ] -> [ | > StarH  Temp | ]

[ up    Star Temp ] -> [ up    StarU ] sfx0
[ down  Star Temp ] -> [ down  StarD ] sfx0
[ left  Star Temp ] -> [ left  StarL ] sfx0
[ right Star Temp ] -> [ right StarR ] sfx0

(pickups)
[ Star Candy ] -> [ Star ] 

(trail)
horizontal [ > Star no White ] -> [ RainbowH > Star ]
vertical   [ > Star no White ] -> [ RainbowV > Star ]

[ > Star Exit ] -> [ Exit ]

==============
WINCONDITIONS
==============

no Candy
no Star

=======     
LEVELS
=======

message the chocolate kingdom has kidnapped the royal candies!
message they will not get away with this!
message candy commandoes, mobilise!

xxxxx
.....
.....
.....
.....
.....
.....
.....
.....
.....
.....
.....
..*..
.*#*.
*###*
#####

message door control established --
message split up and find the royal candies!

##+#+#+#+#+##
##@#,#,#,#@##
.#,#@#,#@#,#.
##/#,#@#,#/##
##,,/#,#/,,##
##,,,,_,,,,##
.##,,,,,,,##.
####,,,,,####
##x#,,,,,#x##
..##,,,,,##..
..##,,,,,##..
..##,,*,,##..
...#,,|,,#...
..##,*|*,##..
..##,|||,##..
....,|||,....
....*|||*....

message sweeping left sector

...#+#####
##.#,#####
####,#####
.#,,,,,/,#
##,#,#,#,#
##,,@#,,,#
##_###_#,#
.#,,,,,,,#
####,#####
##.#,#####
...#*#####

message sweeping right sector

#####+#...
#####@#.##
#####,####
#_//,/_/#.
#///,__/##
#,,,,,,,##
#,,//,__##
#/,__,__#.
#####,####
#####,#.##
#####*#...

..##.###.###.###
..##############
...#_/,,,,,,,###
...#/,,,,,,,,###
...#/,,,,,,#####
..###_####/#####
..#,,,/,,,,_,,,+
..#,,,####/#####
..##,#####/#####
...#,#,,,,,#####
##.#,#,,,,,,,###
####*#######*###

message left sector clear

###.###.###.##..
##############..
##,,,_,,,/,,#...
##/########,#...
##,,,_#,,,#_#...
#####,#_#/#,##..
+,,#,,,,#,#,##..
##,,,,,#,,#,##..
#####,,#_##/##..
##,,####,#,,#...
##,,/,,,,#,,#.##
###*#######*####

message right sector clear --
message all together now!

........#+#........
###x###x#,#.###.###
#########,#########
##@_,,,,#,#,,,,/@##
x#_#/##,#,#,##_#/#x
##,/@##,_,/,##@_,##
##,######/######,##
##,#,,,,#,,,,,,#,##
x#,#,##,######,#,#x
##/#,##,,@,,##,#_##
#,,#,######,##,#,,#
#,##,,,,,,#,,,,#*,#
#,,######*######|,#
##*|||||#|#||||||##
#######|#|#*#######
#######|#|#|#######
#######|#|#|#######
#######*#|#|#######

message the royal candies are rescued!

.........x.........
...................
...................
...................
...................
.......##.##.......
.......##*##.......
x.......*/*.......x
......##///##......
x.......*/*.......x
.......##|##.......
.......##|##.......
........#|#........
........#|#........
........#|#........

