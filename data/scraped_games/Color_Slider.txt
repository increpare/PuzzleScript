Title Color Slider
again_interval 0.05
run_rules_on_level_start
color_palette c64
========
OBJECTS
========
r
red
..00.
.0000
.0000
..00.
.....
o
orange
..00.
.0000
.0000
..00.
.....
y
yellow
..00.
.0000
.0000
..00.
.....
g
green
..00.
.0000
.0000
..00.
.....
b
blue
..00.
.0000
.0000
..00.
.....
p
purple
..00.
.0000
.0000
..00.
.....

background
black darkgreen
10000
10000
10000
10000
11111

player1
lightgreen
.0..0
.....
.....
.0..0
.....

pup
lightgreen
...0.
..000
...0.
...0.
.....
pdown
lightgreen
...0.
...0.
..000
...0.
.....
pleft
lightgreen
..0..
.0000
..0..
.....
.....
pright
lightgreen
...0.
.0000
...0.
.....
.....

clear
white

border
brown

=======
LEGEND
=======
color = r or o or y or g or b or p
color2 = r or o or y or g or b or p
. = background
+ = border and player1
# = border

player = player1 or pup or pdown or pleft or pright
=======
SOUNDS
=======

================
COLLISIONLAYERS
================
background
border, color
clear
player
======
RULES
======
[clear player] -> [player]

[up    player] -> [pup   ]
[down  player] -> [pdown ]
[left  player] -> [pleft ]
[right player] -> [pright]

up    [pup   ] [color|no border no color] -> [clear pup   ] [up    color| ] again
down  [pdown ] [color|no border no color] -> [clear pdown ] [down  color| ] again
left  [pleft ] [color|no border no color] -> [clear pleft ] [left  color| ] again
right [pright] [color|no border no color] -> [clear pright] [right color| ] again

[clear color] -> [ ] again

[player no clear] [r|r|r] -> [player] [clear r|clear r|clear r] again
[player no clear] [o|o|o] -> [player] [clear o|clear o|clear o] again
[player no clear] [y|y|y] -> [player] [clear y|clear y|clear y] again
[player no clear] [g|g|g] -> [player] [clear g|clear g|clear g] again
[player no clear] [b|b|b] -> [player] [clear b|clear b|clear b] again
[player no clear] [p|p|p] -> [player] [clear p|clear p|clear p] again

==============
WINCONDITIONS
==============

=======
LEVELS
=======
+#########
#........#
#........#
#........#
#r.......#
#br......#
#br......#
#gbg.....#
#yygy....#
##########

+#########
#........#
#...rr...#
#.br..g..#
#..bbg.y.#
#.....g..#
#....yy..#
#........#
#........#
##########

+#########
#..#..#..#
#.....#..#
#........#
#.......y#
#......#.#
##.y#....#
##.....#y#
##..####.#
##########

+#########
#...#....#
#........#
##.......#
#.#..#...#
#....#...#
#...p#...#
#....gp..#
#...gpg..#
##########

+#########
#ppg#....#
#gp......#
#g.......#
#....#...#
#....#...#
#....#...#
###......#
#........#
##########