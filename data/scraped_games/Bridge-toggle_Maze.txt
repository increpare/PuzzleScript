title Bridge-toggle Maze
author Andrea Gilbert
homepage www.clickmazes.com

========
OBJECTS
========

Background
white

Xroad
black
0...0
.....
.....
.....
0...0

redEW
black red
00000
.....
..1..
.....
00000

greenNS
black green
0...0
0...0
0.1.0
0...0
0...0

greenEW
black green
00000
.....
..1..
.....
00000

redNS
black red
0...0
0...0
0.1.0
0...0
0...0


Player
Blue 
.....
..0..
.000.
..0..
.....

StartPos
lightblue 
.....
..0..
.000.
..0..
.....

prevPos
transparent

=======
LEGEND
=======

. = Xroad
i = greenNS
z = redEW
1 = greenEW
2 = redNS
P = Player and StartPos and Xroad
OnlyEW = redEW or greenEW
OnlyNS = redNS or greenNS
OkNS = OnlyNS or Xroad 
OkEW = OnlyEW or Xroad 
Roads = Xroad or OnlyEW or OnlyNS

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Roads
StartPos
Player, prevPos

======
RULES
======
(cancel side-step from a bridge)
vertical [ > player onlyEW ] -> cancel
horizontal [ > player onlyNS ] -> cancel
(If there is a reachable position - move there)
vertical [ > player | ... | OkNS ] -> [ prevPos | ... | player OkNS ]
horizontal [ > player | ... | OkEW ] -> [ prevPos | ... | player OkEW ]
(Else if first step is blocked - ignore)
vertical [ > player | onlyEW ] -> cancel
horizontal [ > player | onlyNS ] -> cancel

(Update previous position )
late [ prevPos greenNS ] -> [ redEW ] 
late [ prevPos redNS ] -> [ greenEW ] 
late [ prevPos greenEW ] -> [ redNS ] 
late [ prevPos redEW ] -> [ greenNS ] 
late [ prevPos ] -> [ ] 

==============
WINCONDITIONS
==============

No redNS
No redEW
All Player on StartPos

=======
LEVELS
=======

MESSAGE Flip all red bridges to green and then return home 

pz.z.

.p.2
2.1.

(solved)
.zz.
z.zp
.z.z
z.zz

(solved)
.222.2
2.2p2.
22.222
2.22.2
.2..22
2.222.

MESSAGE Let's mix it up a bit...

(solved)
.zp2z
2.2z.
.zz.2
2z.z.
z.z.z

(solved)
.2.2.z
2pz.22
z2z2.z
.2.22.
2.zz.2
.z2.z.

(solved)
.2z.2.z
z.zz.2.
.2.222z
2.zpz2.
z2.22.z
.zz2.z2
z2.2z2.

MESSAGE Thank you for playing! :)



