title Turn-based Pac-man
author Rabbit From Hell
homepage rabbitfromhellhome.appspot.com

key_repeat_interval 0.2
require_player_movement

========
OBJECTS
========

Background 
Black

(
WallH 
Blue
.....
00000
00000
00000
.....

WallV 
Blue
.000.
.000.
.000.
.000.
.000.
)

Wall
Blue

(
Player 
Yellow
.0000
0000.
000..
0000.
.0000
)

PlayerClosedN
Yellow
.0.0.
00.00
00.00
00000
.000.

PlayerClosedS
Yellow
.000.
00000
00.00
00.00
.0.0.

PlayerClosedE
Yellow
.000.
00000
00...
00000
.000.

PlayerClosedW
Yellow
.000.
00000
...00
00000
.000.

PlayerE
Yellow
.0000
0000.
000..
0000.
.0000

PlayerW
Yellow
0000.
.0000
..000
.0000
0000.

PlayerN
Yellow
0...0
00.00
00000
00000
.000.

PlayerS
Yellow
.000.
00000
00000
00.00
0...0

DeadPlayer
Yellow
0...0
..00.
00..0
0..00
.0.0.

Ghost
Red White
.000.
01010
00000
00000
0.0.0

Pellet
White DarkGrey
.....
..1..
.101.
..1..
.....

=======
LEGEND
=======
Player = PlayerN or PlayerS or PlayerE or PlayerW or PlayerClosedN or PlayerClosedS or PlayerClosedE or PlayerClosedW
Mover = Player or Ghost

. = Background
# = Wall
* = Pellet
G = Ghost
O = Ghost and Pellet
n = PlayerN
s = PlayerS
e = PlayerE
w = PlayerW

(
P = Player
T = Player and Pellet
)

=======
SOUNDS
=======

CloseMessage 91412900
EndGame 42707303
EndLevel 29754303
TitleScreen 22546103
Undo 155504
Restart 29704903
Player move 90139507

Sfx0 36551100 (Player eats pellet)
Sfx1 71069504 (Player dies)

================
COLLISIONLAYERS
================

Background
Pellet
Player, DeadPlayer, Wall, Ghost

======
RULES     
======     

[> Mover | Wall] -> [Mover | Wall]
late [PlayerN Pellet] -> [PlayerClosedN] Sfx0
late [PlayerS Pellet] -> [PlayerClosedS] Sfx0
late [PlayerE Pellet] -> [PlayerClosedE] Sfx0
late [PlayerW Pellet] -> [PlayerClosedW] Sfx0
[right Player] -> [right PlayerE]
[left Player] -> [left PlayerW]
[up Player] -> [up PlayerN]
[down Player] -> [down PlayerS]

[Ghost | ... | Player] -> [> Ghost | ... | Player]
([Ghost | ... | perpendicular Player] -> [perpendicular Ghost | ... | perpendicular Player])
[> Ghost | Ghost] -> [Ghost | Ghost]
[> Ghost | stationary Player] -> [Ghost | DeadPlayer] Sfx1
[> Player | Ghost] -> [DeadPlayer | Ghost] Sfx1
[> Player | no Wall | < Ghost] -> [ | DeadPlayer | Ghost] Sfx1

==============
WINCONDITIONS
==============

No Pellet

=======     
LEVELS
=======

message Eat all the yummy white pellets. Beware of ghosts!

message Level 1

#################
#O.*.*.*.*.*.*.*#
#.######*######.#
#*#....#n#....#*#
#.#....#*#....#.#
#O#....#.#....#*#
#.######*######.#
#*.*.*.*.*.*.*.*#
#################

message Level 2

#################
#O.*.*.*.*.*.*.O#
#.######*######.#
#*#....#n#....#*#
#.#....#*#....#.#
#*#....#.#....#*#
#.######*######.#
#*.*.*.*.*.*.*.*#
#################

message Level 3

(a)
#################
#*.*.*.*.*.*.*.O#
#.######*######.#
#*#....#n#....#*#
#.#....#*#....#.#
#*#....#.#....#*#
#.######*######.#
#O.*.*.*.*.*.*.*#
#################

message Level 4
(b)
#################
#O.*.*.*.*.*.*.O#
#.######*######.#
#*#....#.#....#*#
#.#....#*#....#.#
#*#....#.#....#*#
#.######*######.#
#*.*.*.*.*.*.*.*#
#.######*######.#
#*#....#n#....#*#
#.#....#*#....#.#
#*#....#.#....#*#
#.######*######.#
#O.*.*.*.*.*.*.O#
#################

message Level 5
(c)
#################
#o.*.*.*.*.*.*.o#
#.######*######.#
#*#....#.#....#*#
#.#....#*#....#.#
#*#....#n#....#*#
#.######*######.#
#*.*g*.*.*.*g*.*#
#.######*######.#
#*#....#.#....#*#
#.#....#*#....#.#
#*#....#.#....#*#
#.######*######.#
#*.*.*.*.*.*.*.*#
#################

message Level 6
(d)
#################
#*.*.*.*.*.*.*.*#
#.###.#####.###.#
#*#*.*.*#*.*.*#*#
#.#.###.#.###.#.#
#*.*#O.*.*.O#*.*#
#.#.#.##.##.#.#.#
#*#*.*#...#*.*#*#
#.###..e....###.#
#*#*.*#...#*.*#*#
#.#.#.##.##.#.#.#
#*.*#O.*.*.O#*.*#
#.#.###.#.###.#.#
#*#*.*.*#*.*.*#*#
#.###.#####.###.#
#*.*.*.*.*.*.*.*#
#################

message Level 7
(e)
#################
#*.*.*.*.*.*.*.*#
#.###.#####.###.#
#*#*.*.*#*.*.*#*#
#.#.###.#.###.#.#
#*.*#*.*.*.*#*.*#
#.#.#.##.##.#.#.#
#*#*.*#*g*#*.*#*#
#.###..g*g..###.#
#*#*.*#*g*#*.*#*#
#.#.#.##.##.#.#.#
#*.*#*.*.*.*#*.*#
#.#.###.#.###.#.#
#*#*e*.*#*.*.*#*#
#.###.#####.###.#
#*.*.*.*.*.*.*.*#
#################

message Last level
(f)
#################
#o.*.*.*.*.*.*.o#
#.###.#####.###.#
#*#*.*.*#*.*.*#*#
#.#.###.#.###.#.#
#*.*#*.*.*.*#*.*#
#.#.#.##.##.#.#.#
#*#*.*#*g*#*.*#*#
#.###..g*g..###.#
#*#*.*#*g*#*.*#*#
#.#.#.##.##.#.#.#
#*.*#*.*.*.*#*.*#
#.#.###.#.###.#.#
#*#*e*.*#*.*.*#*#
#.###.#####.###.#
#o.*.*.*.*.*.*.o#
#################


message Congratulations! You beat the game.
message Thanks to Stephen Lavelle (www.increpare.com) for providing the pac-man eating animation.
