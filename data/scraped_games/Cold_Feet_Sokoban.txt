title Cold Feet Sokoban
author Franklin P. Dyer
again_interval 0.1

========
OBJECTS
========

Background .
Gray Lightgray
00010
00000
01000
00000
00001

Wall /
Darkgray Black
00010
11111
01000
11111
00010

Player @
Brown #d2b48c #6666ff Darkblue Purple
.000.
.111.
42224
.333.
.3.3.

PlayerUp
Brown #d2b48c #6666ff Darkblue Purple
.000.
.000.
.222.
.333.
.3.3.

PlayerDown
Brown #d2b48c #6666ff Darkblue Purple
.000.
.111.
.424.
.333.
.3.3.

PlayerLeft
Brown #d2b48c #6666ff Darkblue Purple
.000.
.110.
4222.
.333.
..3..

PlayerRight
Brown #d2b48c #6666ff Darkblue Purple
.000.
.011.
.2224
.333.
..3..

Ice a
Lightblue White
00010
00000
01000
00000
00001

Crate b
Orange
00000
0...0
0...0
0...0
00000

Target c
Darkblue
.....
.000.
.0.0.
.000.
.....

=======
LEGEND
=======

& = Crate and Target

Solid = Wall
Mover = PlayerUp or PlayerDown or PlayerLeft or PlayerRight

=======
SOUNDS
=======

Mover Create 87812508
Crate Move 30575307
Endlevel 5530903

================
COLLISIONLAYERS
================

Background
Ice
Target
Wall, Player, PlayerUp, PlayerDown, PlayerLeft, PlayerRight, Crate

======
RULES
======

[> Player|Crate] -> [> Player|> Crate]

[Player No Ice] -> [Player Ice]

RIGHT[> Player|Ice] -> [PlayerRight Ice|Ice]
LEFT[> Player|Ice] -> [PlayerLeft Ice|Ice]
DOWN[> Player|Ice] -> [PlayerDown Ice|Ice]
UP[> Player|Ice] -> [PlayerUp Ice|Ice]

startloop
[PlayerLeft Ice] -> [LEFT PlayerLeft Ice] again
[PlayerRight Ice] -> [RIGHT PlayerRight Ice] again
[PlayerUp Ice] -> [UP PlayerUp Ice] again
[PlayerDown Ice] -> [DOWN PlayerDown Ice] again

[> Mover|Solid] -> [Player|Solid]
[> Mover|Crate] -> [Player|> Crate]
[Mover No Ice] -> [Player]
endloop

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

message Level 1 of 3

//////
/..../
/./@./
/.b&./
/.c&./
/..../
//////

message Level 2 of 3

///////
/c..../
////../
/@..../
/..b&./
/...../
///////

message Level 3 of 3

//////
/.c///
/..///
/&..@/
/..b./
/...//
/..///
//////

message Congratulations! You win!
