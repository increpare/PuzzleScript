title Boats Cars & Trains
author James ah ha
homepage www.puzzlescript.net
flickscreen 11X9
realtime_interval 0.25


========
OBJECTS
========
background :
green

home £
red lightbrown blue lightgrey
.000.
00000
11111
12131
12111

YtrBbl ╚ (200)
grey blue brown white
.0000
12030
11200
11120
1111.

YtlBbr ╝ (188)
grey brown blue white
0000.
03012
00122
01222
.2222

YbrBtl ╔ (201)
blue brown grey white
0000.
00012
00122
01232
.2222

YblBtr ╗ (187)
blue grey brown white
.0000
12000
11200
13120
1111.

YHxBV ╩ (202)
blue grey brown
.000.
12021
11211
12021
.000.

YVxBH ╠ (204)
grey blue brown
.000.
12021
11211
12021
.000.

YThroV  ║ (186)
grey white 
.010.
.000.
.010.
.000.
.010.
YThroH ═ (205)
grey white 
.....
00000
10101
00000
.....

BthroH ─ (196)
blue 
.....
00000
00000
00000
.....

BthroV │ (179)
blue 
.000.
.000.
.000.
.000.
.000.

YHoBV o
grey blue brown white
21112
22222
03030
22222
21112

YVoBH u
grey blue brown white
22022
12321
12021
12321
22022

set
transparent
was
red
.....
.0.0.
..0..
.0.0.
.....
swapable1
purple
0...0
.....
.....
.....
0...0
swapable2
purple
0...0
.....
.....
.....
0...0
swap1
orange
0...0
.....
.....
.....
0...0
swap2
orange
0...0
.....
.....
.....
0...0
temp q
transparent

(ANIMATION)
YcarH
yellow black
.....
.000.
00000
.1.1.
.....

YcarV
yellow black 
..0..
.001.
.00..
.001.
..0..

BboatH
yellow darkbrown 
.010.
00100
11111
.111.
.....

BboatV
yellow darkbrown 
.01..
0011.
1111.
0011.
.01..

RailBR ┌
black darkbrown 
.....
...00
..0.1
.0..0
.010.

RailH =
black darkbrown 
.....
00000
.1.1.
00000
.....

RailBL ┐
black darkbrown 
.....
00...
1.0..
0..0.
.010.

RailTR └
black darkbrown 
.010.
.0..0
..0.1
...00
.....

RailV █
black darkbrown 
.0.0.
.010.
.0.0.
.010.
.0.0.


RailTL ┘
black darkbrown 
.010.
0..0.
1.0..
00...
.....

RailStat S
darkred brown darkgrey 
.....
.000.
.1.1.
22222
.....

TrainH
white red black 
0.0..
11.1.
.1111
11111
2.2.2

TrainV
red black white 
..001
.000.
2.001
.000.
20.01

(music)
m_End
#000
m_rest
#000
m_next_line
#000
m_start
#000
m_temp
#000
m_new_line
#000
m_C4
#001
m_D4
#003
m_E4
#005
m_F4
#007
m_G4
#009
m_A5
#00B
m_B5
#00D
m_C5
#00F
m_sound
white
...0.
...0.
.000.
.0.0.
.000.
m_hold
white red
1..0.
.100.
.010.
..01.
...01


=======
LEGEND
=======
(amination)

(main routes)
junc = YtrBbl or YtlBbr or YbrBtl or YblBtr or YHxBV or YVxBH or RailStat
over = YHoBV or YVoBH
Ythro = YthroV or YthroH
Bthro = BthroV or BthroH
RailTurn = RailTL or RailTR or RailBL or RailBR
RailThro = RailTurn or RailH or RailV
thro = Ythro or Bthro or over or RailThro
routes = junc or thro or home
swap = swap1 or swap2
swapi = over or YHxBV or YVxBH
swapable = swapable1 or swapable2

(vehicles)
car = YcarH or YcarV
boat = BboatH or BboatV
train = trainH or trainV

Y = YcarH and YthroH
Z = YcarV and YthroV
W = BboatH and BthroH
X = BboatV and BthroV
player = car or boat or train

(swapping)
K = YHoBV and swapable1
L = YHxBV and swapable1
M = YVoBH and swapable2
N = YVxBH and swapable2
 
(music)
« = m_End
. = m_rest
C = m_C4
D = m_D4
E = m_E4
F = m_F4
G = m_G4
A = m_A5
B = m_B5
H = m_C5
m_note = C or D or E or F or G or A or B or H
» = m_sound and m_start
& = m_next_line
% = m_new_line
m_ender = m_next_line or m_end
=======

=======
SOUNDS
=======
sfx0 72400106 (C-4)
sfx1 52564906 (D-4) 
sfx2 31758506 (E-4)
sfx3 67624706 (F-4)
sfx4 41694506 (G-4)
sfx5 34551106 (A-5)
sfx6 71346306 (B-5)
sfx7 10280506 (C-5)
================
COLLISIONLAYERS
================
background
set, m_note, m_start, m_end, m_next_line, m_new_line, m_rest
temp, m_temp, m_hold
routes, home
swapable
swap
was
player, m_sound

======
RULES
======
(stop playing music if player moves)
[> player][m_sound] -> [> player][m_hold]
[action player][m_sound] -> [action player][m_hold]
(stop moving off routes)
[> player routes | no routes] -> [player routes | no routes]
(stop player going back to last junction)
[> player | was] -> [player | was]
(if going to a new junction then remove 'was')
[> player | junc][was] -> [> player | junc][no was]
(if car moving onto station then change to train & visa versa)
[> car | railStat] -> [> TrainH | railStat set was]
[> train | railStat no set] -> [> YcarH | railStat set was]
(if on junction then change player type and stop returning)
late [car junc no set] -> [BboatH junc set was]
late [boat junc no set] -> [YcarH junc set was]
(set vert if only verts possible)
late [YthroV | YcarH | YthroV] -> [YthroV | YcarV | YthroV]
late [BthroV | BboatH | BthroV] -> [BthroV | BboatV | BthroV]
(if moving onto thro route then stop if not same colour as player)
[> car | Bthro] -> [car | Bthro]
[> car | RailThro] -> [car | RailThro]
[> boat | Ythro] -> [boat | Ythro]
[> boat | RailThro] -> [boat | RailThro]
[> train | Ythro] -> [train | Ythro]
[> train | Bthro] -> [train | Bthro]

(if on thro route then remove set)
late [player thro][set] -> [player thro][no set]
(if in thro route then correct player orientaition)
late [YcarV YthroH] -> [YcarH YthroH]
late [YcarH YthroV] -> [YcarV YthroV]
late [BboatV BthroH] -> [BboatH BthroH]
late [BboatH BthroV] -> [BboatV BthroV]
late [TrainV RailH] -> [TrainH RailH]
late [TrainH RailV] -> [TrainV RailV]

(stop player actioning on junc already marked for swap)
[action player swapable swap] -> [player swapable swap]
(if action on swappable juncn and another has been marked for swap then swap)
[action player swapable1 swapi][swap1][temp] -> [action player swapable1 no swapi][swap1][temp swapi set]
[action player swapable1][swap1 swapi] -> [action player swapable1 swapi][swap1 no swapi]
[action player swapable1][swap1][temp swapi] -> [action player swapable1][swap1 swapi][temp no swapi]
(tidy up)
[action player swapable1][swap1][temp set][was] -> [player swapable1][no swap1][temp no set][no was]
(if action on swappable juncn and another has been marked for swap then swap)
[action player swapable2 swapi][swap2][temp] -> [action player swapable2 no swapi][swap2][temp swapi set]
[action player swapable2][swap2 swapi] -> [action player swapable2 swapi][swap2 no swapi]
[action player swapable2][swap2][temp swapi] -> [action player swapable2][swap2 swapi][temp no swapi]
(tidy up)
[action player swapable2][swap2][temp set][was] -> [player swapable2][no swap2][temp no set][no was]

(if action on swappable juncn but no swap mark then mark for swap)
[action player swapable1] -> [player swapable1 swap1]
[action player swapable2] -> [player swapable2 swap2]



(aminate)

(MUSIC)
(move sounder)
right [m_sound] -> [> m_sound]
down[m_sound m_next_line|...|m_ender] -> [m_next_line|...|m_temp m_ender]
left [m_temp m_ender |...| | m_new_line] -> [ m_ender |...| m_sound | m_new_line]
[m_sound m_end][m_start ] -> [m_end] [m_start m_sound]
(play notes)
late [m_C4 m_sound] -> sfx0
late [m_D4 m_sound] -> sfx1
late [m_E4 m_sound] -> sfx2
late [m_F4 m_sound] -> sfx3
late [m_G4 m_sound] -> sfx4
late [m_A5 m_sound] -> sfx5
late [m_B5 m_sound] -> sfx6
late [m_C5 m_sound] -> sfx7
(switch music back on)
late [m_hold] -> [m_sound]

==============
WINCONDITIONS
==============

all player on home

=======
LEVELS
=======

message Xcross 1.1 - your yellow car can follow the road until it meets a river then you have change to a boat and follow the river, you transfer back to a car at the next road.
message Find your way home (you can't return to junction you've just left, so don't get stuck on routes that go no where)
::│:│:║:║::
::│:│:║:║::
══╩═╗─╠─╠──
::│:║:║:║::
£═╩═╝─╠─╚══
::│:│:║:│::
══╩═╩═╝─╔══
::│:│:│:║::
::│:X:│:║::
»GAGFEFG.&.
%DEFEFG..&.
%GAGFEFG.&.
%DGEC....«.

message Xcross 1.2 - new starting point, rember you can't go back to the junction you have just left
::│:│:║:║::
::│:│:║:║::
══╩═╗─╠─╠─W
::│:║:║:║::
£═╩═╝─╠─╚══
::│:│:║:│::
══╩═╩═╝─╔══
::│:│:│:║::
::│:│:│:║::
»DGGGBAGA&.
%BGGB..DE&.
%EDBBGAGA&.
%BAGEEDG.«.


message Xcross 1.3 - use one bridge
::│:│:║:║::
::│:│:║:║::
══╩═╗─╠─╠──
::│:║:║:║::
£═╩═╝─u─╚══
::│:│:║:│::
══╩═╩═╝─╔═y
::│:│:│:║::
::│:│:│:║::
»GG.FG.AD.&
%BA.B.CBA.&
%GG.F.E.FG&
%A.B.FEDD.&
%D.C.BC.B.&
%AB.GA.FED«

message Xcross 1.4 - you'll need to cross four bridges
::│:│:║:║::
::│:│:║:║::
══╩═╗─u─╠──
::│:║:║:║::
£═o═╝─u─╚══
::│:│:║:│::
══╩═o═╝─╔═y
::│:│:│:║::
::│:│:│:║::
»GABGGABG&.
%BCD.BCD.&.
%DEDCBG&...
%DEDCBG&...
%GDG.GDG.«.

message Xcross 1.5 - you can swap purple dotted junctions and bridges
message when you are on the first one press X to select it then move to second and press X again - they will then swap 
::│:│:║:║:q
::│:│:║:║::
£═L═╗─╠─╠─W
::│:║:║:║::
══╩═╝─╠─╚══
::│:│:║:│::
══╩═K═╝─╔══
::│:│:│:║::
::│:│:│:║::
»GABABBB.&.
%AAA.BBB.&.
%GABABBB.&.
%AABAG...«.

message Xcross 1.6 - different swaping
::│:│:║:║:q
::│:│:║:║::
£═╩═╗─N─M──
::│:║:║:║::
══O═╝─╠─╚══
::│:│:║:│::
══╩═╩═╝─╔══
::│:│:│:║::
::│:│:X:║::
»GAGFEFG.&.
%DEFEFG..&.
%GAGFEFG.&.
%DGEC....«.

message Xcross 1.7 - you can only swap pairs where the directions of the routes match
::│:│:║:║:q
::│:│:║:║::
£═L═╗─M─N──
::│:║:║:║::
══╩═╝─╠─╚══
::│:│:║:│::
══╩═K═╝─╔═Y
::│:│:│:║::
::│:│:│:║::
»DGGGBAGA&.
%BGGB..DE&.
%EDBBGAGA&.
%BAGEEDG.«.

message Xcross 1.8 - tough swapping
::│:│:║:║:q
::│:│:║:║::
£═L═╗─M─╠─W
::│:║:║:║::
══╩═╝─N─╚══
::│:│:║:│::
══K═╩═╝─╔══
::│:│:│:║::
::│:│:│:║::
»GG.FG.AD.&
%BA.B.CBA.&
%GG.F.E.FG&
%A.B.FEDD.&
%D.C.BC.B.&
%AB.GA.FED«

message Xcross 1.9 - they have built a railway, get to the right station to catch the train to home
q:│:│:║:║:£
::│:│:║:║:█
══╩═╗─M─N─█
::│:║:║:║:█
══╩═╝─N─╚═S
::│:│:║:│:█
══╩═╩═╝─╔═S
::│:│:│:║::
::X:│:│:║::
»GABGGABG&.
%BCD.BCD.&.
%DEDCBG&...
%DEDCBG&...
%GDG.GDG.«.

