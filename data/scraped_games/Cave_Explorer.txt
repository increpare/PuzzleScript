title Cave Explorer
(run_rules_on_level_start)
flickscreen 17x17
(noundo)
========
OBJECTS
========
Player
#732714 #ff6538
.....
.00..
.11..
.00..
.00..

Enemy
red pink white
.110.
11110
11000
01000
.000.

sightu
blue
.....
.....
..0..
.....
.....

sightd
yellow
.....
.....
..0..
.....
.....

sightl
red
.....
.....
..0..
.....
.....

sightr
green
.....
.....
..0..
.....
.....

sighttemp
black

Breakable
#8d919e #ced4e8 #f6fafe #58565b 
.111.
12210
12100
11003
10033

(inflow
red
.....
.....
..0..
.....
.....

New
black

New2
yellow)

Wall
#645853 #a07f75 #ffd4d5 #f6effc 
33221
31110
21110
21110
10000

Door
#f3de06 #3d2a03 #ece4d3 #e0b400 
22222
02120
00100
03130
33333

Chest
#9ba6bc #eff2fc #7298b6 #3f4547 
00000
02220
13331
11111
22222

Box
#ff793a #ffc1a3 #b06555
11110
11002
10002
10022
02222

Pit
#3a0100

Background
#4f0200 #5f0300
01000
00010
11000
00000
00010

item1
#444238 #b3b7ce white 
.....
.221.
.210.
.100.
.....

item11
#444238 #b3b7ce white 
21.21
10.10
.....
21.21
10.10

item2
#b3b7ce white  
..1..
.1.0.
..0..
..0..
..00.

item22
#b3b7ce white  
.1.1.
1.0.0
.1.0.
.1.0.
.1100

item3
red pink
.....
.1.0.
10000
.000.
..0..

bomb
#451a18 #cf5651 #ffd5d6
.....
.221.
.210.
.100.
.....

bomb2
#451a18 #cf1f00 #f2fafe
.....
.221.
.210.
.100.
.....

bomb3
#650802 #ff2a02 #fef4f3
.....
.221.
.210.
.100.
.....

explosion
yellow
.0.0.
0.0.0
.0.0.
0.0.0
.0.0.

explosion2
red orange
.0.1.
1.0.1
.1.0.
0.1.0
.0.1.

negitem
red
.....
.000.
.0.0.
.000.
.....

Stair
black orange brown
00002
00012
00212
01212
21212

Counter1
purple

Counter2
pink

Counter3
red

=======
LEGEND
=======
P = player
& = enemy
# = Wall
% = pit
n = door
u = door and item1
m = chest
s = stair
@ = box
$ = breakable and sighttemp
o = item1
k = item2
l = item3
. = background
c = Counter1
d = Counter2
e = Counter3
1 = Counter1 and wall
2 = Counter2 and wall
3 = Counter3 and wall
sight = sightu or sightd or sightl or sightr or sighttemp
counter = counter1 or counter2 or counter3
Litbomb = bomb or bomb2 or bomb3
doubleitem = item11 or item22 or item3
item = item1 or item2 or doubleitem
items = item1 or item2 or item11 or item22
drops = item1 or item2 or item3 or bomb3
hidden = item1 or item2
(anywall = new or wall or pit or player
anywall2 = new2 or breakable or box
special = chest or item)
Pushable = box or chest
Blastable = box or breakable
Solid = box or breakable or wall or door or pit
Blast = explosion or explosion2
(Solid = box or breakable or wall or Player)
=======
SOUNDS
=======
SFX1 18045307
SFX2 6330700
SFX3 18837707
SFX4 18301508
SFX5 49258302
SFX6 47498308
SFX7 19397105
SFX8 61426704
((roar)
SFX8 44821508
(scream)
SFX9 62041508)
================
COLLISIONLAYERS
================
sight
Background
(inflow)
stair
item, counter, negitem, bomb, bomb2, bomb3
Player, enemy, door, chest (, new), Wall, pit, breakable, pushable
explosion, explosion2 (, new2)
======
RULES
======

(setup)
startloop
random [ breakable sighttemp ] -> [ breakable ]
random [ breakable sighttemp ] -> [ breakable ]
random [ breakable sighttemp ] -> [ breakable ]
random [ breakable sighttemp ] -> [ breakable ]
random [ breakable sighttemp ] -> [ breakable ]
random [ breakable sighttemp ] -> [ breakable random hidden ]
endloop
((specific level setup)
(have player's last door entrance be seed for this process)
(then check that the next exit is in a contiguous region with the player, with respect to pits only)
[ Player no inflow ] [ item inflow ] -> [ Player no inflow ] [ inflow ]
[ Player no inflow ] [ inflow ] -> [ Player no inflow ] [ inflow new new2 ]

[ Player | New ] -> [ Player | ]
[ Player | New2 ] -> [ Player | ]

random [New]->[pit]
random [New]->[pit]

[ pit | New ] -> [ pit | pit ]
+[ pit | New ] -> [ pit | pit ]
+[ pit | New ] -> [ pit | pit ]
+[ pit | New ] -> [ Pit | ]
[new]->[ ]

(trim dense)
[new2][pit]->[new2][new]
[ new | no anywall ] -> [ pit | ]
[new]->[ ]
((pinch bridges)
[ new2 ] [ no anywall | pit | no anywall | pit | no anywall ] -> [ new2 ] [ | | pit | | ])

(stage 2)
startloop
random [New2 no pit]->[breakable]

[ blastable | New2 no pit ] -> [ blastable | random blastable ]
+[ blastable | New2 no pit ] -> [ breakable | random blastable ]
+[ blastable | New2 no pit | New2 ] -> [ breakable | | ]
+[ blastable | New2 no pit ] -> [ breakable | ]
endloop

[ new2 ] [ breakable ] -> [ new2 ] [ new2 ]
[ new2 no pit | no anywall2 ] -> [ breakable | ]

startloop
random [ new2 no pit ] -> [ random special ]
random [ new2 no pit ] -> [ random special ]
random [ new2 no pit ] -> [ random special ]
random [ new2 no pit ] -> [ ]
random [ new2 no pit ] -> [ ]
random [ new2 no pit ] -> [ ]
random [ new2 no pit ] -> [ ]
random [ new2 no pit ] -> [ ]

endloop

random [ new2 ] [ chest ] -> [ new2 ] [ door item1 ]

[new2]->[]

((trim sparse)
startloop
random Horizontal [ no anywall | breakable | no anywall ] -> [ | new | ]
random Vertical [ no anywall | new | no anywall ] -> [ |  | ]
endloop
[new]->[ breakable ]))



(bomb)
[explosion2]->[]
[explosion]->[explosion2] again
[bomb3]->[explosion] SFX5 again
[ explosion | blastable ] -> [ explosion | > blastable explosion2]
[ > blastable | pushable ] -> [ > blastable | > pushable ]
[ > Player | Pushable ] -> [ > Player | > Pushable ] SFX1
[ > blastable | pit ] -> [ | explosion2 ] again SFX6

[ explosion | ] -> [ explosion | explosion2 ]
[bomb2]->[bomb3]
[bomb]->[bomb2]

 [ explosion2 blastable ] -> [ explosion2 ]
 [ blast enemy ] -> [ explosion2 random drops ]

(enemy AI)
[ stationary player ] [ sight ] -> [ stationary player ] []
[ enemy sightu ] -> [ down enemy ]
[ enemy sightd ] -> [ up enemy ]
[ enemy sightl ] -> [ right enemy ]
[ enemy sightr ] -> [ left enemy ]
[ sight ] -> [ ]
[ > enemy | Player ] -> [ enemy | Player explosion2 ]
[ > enemy | Solid ] -> [ randomdir enemy | Solid ]


(using stuff ( bombs))
right [ action Player ] [ Counter1 no wall | no item | no item | no item | no item | no item | no item | Counter1 ] -> cancel
[ action Player no litbomb ] -> [ player bomb ]
right [ Player bomb ] [ Counter1 no wall | no item no negitem ] -> [ Player bomb ] [ Counter1 | negitem ] SFX4
right [ Player bomb ] [ Counter1 no wall | item ] -> [ Player bomb ] [ Counter1 | ] SFX4

(keys)
[ > Player | door no item ] -> [ > Player | door item3 ]
[ > Player | door item1 ] -> [ Player | door item2 ]
right [ door item2 ] [ Counter2 no wall | no item | no item | no item | no item | no item | no item | Counter2 ] -> cancel
right [ door item3 ] [ Counter2 no wall | no item | no item | no item | no item | no item | no item | Counter2 ] -> cancel
right [ door item3 ] [ Counter2 no wall | no item ] -> [ door item3 ] [ Counter2 | negitem ] SFX6
right [ door item3 ] [ Counter2 no wall | item ] -> [ door item3 ] [ Counter2 | ] SFX6

right [ door item2 ] [ Counter2 no wall | no item ] -> [ door item2 ] [ Counter2 | negitem ] SFX6
right [ door item2 ] [ Counter2 no wall | item ] -> [ door item2 ] [ Counter2 | ] SFX6
[ door item2 ] -> [ random doubleitem ]
[ door | door item3 ] -> [ door item3 | door item3 ]
[ door item3 ] -> [ ]

(hurt)
right [ Player blast ] [ Counter3 no wall | no item | no item | no item | no item | no item | no item | Counter3 ] -> [ blast ] [ Counter3 | | | | | | | Counter3 ] message game over
right [ Player blast ] [ Counter3 no wall | no item ] -> [ Player blast ] [ Counter3 | negitem ] SFX8
right [ Player blast ] [ Counter3 no wall | item ] -> [ Player blast ] [ Counter3 | ] SFX8

right [ negitem | no item ] -> [ negitem | negitem ]
right [ negitem | item | no negitem ] -> [ item | negitem | ]
right [ negitem | item ] -> [ item | item ]
[negitem]->[]
(chests)
[ > Player | chest ] -> [ Player | random items ] SFX6

(line of sight)
late right [ Player | no solid ] -> [ Player | sightr ]
late random down [ sightr | no Solid ] -> [ sightr | sightr ]

late right [ sightr | no solid no sight ] -> [ sightr | sighttemp ]
+ late vertical [ sighttemp | no solid no sight ] -> [ sighttemp | sightr ]
+ late [ sighttemp ] -> [ sightr ]

late left [ Player | no solid ] -> [ Player | sightl ]
late random up [ sightl | no Solid ] -> [ sightl | sightl ]

late left [ sightl | no solid no sight ] -> [ sightl | sighttemp ]
+ late vertical [ sighttemp | no solid no sight ] -> [ sighttemp | sightl ]
+ late [ sighttemp ] -> [ sightl ]

late up [ Player | no solid ] -> [ Player | sightu ]
late random right [ sightu | no Solid ] -> [ sightu | sightu ]

late up [ sightu | no solid no sight ] -> [ sightu | sighttemp ]
+ late horizontal [ sighttemp | no solid no sight ] -> [ sighttemp | sightu ]
+ late [ sighttemp ] -> [ sightu ]

late down [ Player | no solid ] -> [ Player | sightd ]
late random left [ sightd | no Solid ] -> [ sightd | sightd ]

late down [ sightd | no solid no sight ] -> [ sightd | sighttemp ]
+ late horizontal [ sighttemp | no solid no sight ] -> [ sighttemp | sightd ]
+ late [ sighttemp ] -> [ sightd ]

late up [ no sightl no sightu no sightd | sightr | no sight no solid ] -> [ | sightu | ]
late down [ no sightl no sightu no sightd | sightr | no sight no solid ] -> [ | sightd | ]

late up [ no sightr no sightu no sightd | sightl | no sight no solid ] -> [ | sightu | ]
late down [ no sightr no sightu no sightd | sightl | no sight no solid ] -> [ | sightd | ]

late left [ no sightl no sightr no sightd | sightu | no sight no solid ] -> [ | sightl | ]
late right [ no sightl no sightr no sightd | sightu | no sight no solid ] -> [ | sightr | ]

late left [ no sightl no sightr no sightu | sightd | no sight no solid ] -> [ | sightl | ]
late right [ no sightl no sightr no sightu | sightd | no sight no solid ] -> [ | sightr | ]

(pickup and counting)
late right [ Player item1 ] [ Counter1 no wall | item1 ] -> [ Player item1 ] [ Counter1 | negitem ] SFX3
late right [ Player item1 ] [ Counter1 no wall | no item no negitem ] -> [ Player item1 ] [ Counter1 | item1 ] SFX3
late right [ Player item11 ] [ Counter1 no wall | | | item1 ] -> [ Player item11 ] [ Counter1 | | | negitem ] SFX3
late right [ Player item11 ] [ Counter1 no wall | | | no item no negitem ] -> [ Player item11 ] [ Counter1 | | | item1 ] SFX3
late right [ negitem | item1 ] -> [ | negitem ]
late right [ negitem | no item no negitem no Counter ] -> [  | item1 ]


late right [ Player item2 ] [ Counter2 no wall | item2 ] -> [ Player item2 ] [ Counter2 | negitem ] SFX2
late right [ Player item2 ] [ Counter2 no wall | no item no negitem ] -> [ Player item2 ] [ Counter2 | item2 ] SFX2
late right [ Player item22 ] [ Counter2 no wall | | item2 ] -> [ Player item22 ] [ Counter2 | | negitem ] SFX2
late right [ Player item22 ] [ Counter2 no wall | | no item no negitem ] -> [ Player item22 ] [ Counter2 | | item2 ] SFX2
late right [ negitem | item2 ] -> [ | negitem ]
late right [ negitem | no item no negitem no Counter ] -> [  | item2 ]

late right [ Player item3 ] [ Counter3 no wall | item3 ] -> [ Player item3 ] [ Counter3 | negitem ] SFX7
late right [ Player item3 ] [ Counter3 no wall | no item no negitem ] -> [ Player item3 ] [ Counter3 | item3 ] SFX7
late right [ negitem | item3 ] -> [ | negitem ]
late right [ negitem | no item no negitem no Counter ] -> [  | item3 ]

late [ player item ] -> [ player ]
late [ player doubleitem ] -> [ player ]

late [ negitem ] -> [ ]

==============
WINCONDITIONS
==============
all stair on player
=======
LEVELS
=======
message Good luck, traveler...
message I hope you can read binary...
message Remember, press x to set bombs.
#####e......3#########e......3####
#####c......1#########c......1####
#####d......2#########d......2####
##################################
.%%......%.%%%@@##..$....&.......#
.%%..........%%@##..$....@.....$.#
.%%...........%%##%%$.@.&....$%%%#
.%%.....k......%##...$$$@.@@$%...#
.%%.............##...%.......%...#
.%%..m...p.m....nn.@.%.......%...#
.%%%............##...%.......%...#
.%%%...ooo......##..%........%...#
..%%...lll......##%%..........%%%#
..%%............##...........$...#
..%%............##..........$.m..#
..%%...........@##..........$....#
..%%##########################n###
#####e......3#########e......3n###
#####c......1#########c......1n###
#####d......2#########d......2n###
##############################n###
#.$.......&.....##......&.$%.....#
#$#$#.$.$.$.$@$.##....$..$.%@.%%%#
#.$u$oo...@.@&@.##.....$$..%%%...#
#$#$$$#.$.$@$@$.##........%$.$...#
#..%@...@o@u@..@##..$$$$..%....m.#
#u#$#$$.$@$@$.$.nn.$&.....%......#
#..%@.....@.@..@##.....%%%%%%%$.$#
#$#$@.#.$@$@$@$.##%%%%%%%%%%%%%%%#
#.$.......@m@...##%%.........$.$$#
#$#$#.$.$@$@$@$@##......%%%..$$$$#
#.$.$.......@.@.##@@...%%%%%..$lk#
#.#$#.#.$.$@$.$.##l@@..%%%%%%$$.&#
###n##############################
###n#e......3#########e......3####
###n#c......1#########c......1####
###n#d......2#########d......2####
###n##############################
#@..............##...............#
#.@.............##...............#
#...............##...............#
#%%.%...........##...............#
#....%%%%%%@....##...............#
#...............nn.......s.......#
#.......&...%...##...............#
#..&.&.......%..##...............#
#@..@........%..##...............#
%%%%%%.....&.%%.##...............#
%ummu%.&......%%##...............#
%muum%.......%%%##...............#
%%%%%%#######%%%##################