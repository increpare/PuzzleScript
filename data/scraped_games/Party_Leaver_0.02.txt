title Party Leaver 0.02
background_color Black
debug
run_rules_on_level_start

========
OBJECTS
========

Exit
Yellow
00000
0...0
0...0
0...0
00000

Floor
DarkBlue DarkGreen
11111
00001
00001
00001
00001

DanceFloor
DarkBrown Brown
11111
00001
00001
00001
00001

Wall
Red DarkRed
00010
11111
01000
11111
00010

PlayerU
Orange Green Black
.....
.....
0...0
.1221
..22.

PlayerR
Orange Green Black
..0..
.1...
22...
22...
.10..

PlayerD
Orange Green Black
.22..
1221.
0...0
.....
.....

PlayerL
Orange Green Black
..01.
...22
...22
...1.
..0..

PersonD
Blue Black
.....
.....
.....
.0110
..11.

PersonR
Blue Black
...0.
...11
...11
...0.
.....

PersonU
Blue Black 
.11..
0110.
.....
.....
.....

PersonL
Blue Black
.....
.0...
11...
11...
.0...

Dance
Black
.....
.....
.....
.....
.....

RPerson
Black
.....
..00.
..0..
..0..
.....

=======
LEGEND
=======
Background = Floor or DanceFloor
Player = PlayerU or PlayerR or PlayerD or PlayerL
Person = PersonU or PersonR or PersonD or PersonL
Obstacle = Person or Wall

# = Wall
P = PlayerR
U = PersonU and Floor
R = PersonR and Floor
D = PersonD and Floor
L = PersonL and Floor
E = Exit

. = Floor
, = DanceFloor
N = RPerson and DanceFloor
1 = PersonU and DanceFloor
2 = PersonR and DanceFloor
3 = PersonD and DanceFloor
4 = PersonL and DanceFloor

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
Background
Exit
Player, Wall
RPerson, Person
Dance

======
RULES
======

[RPerson] -> [Random Person]

(spin 'round and 'round)
[Action PlayerU No Person] -> [PlayerR]
[Action PlayerR No Person] -> [PlayerD]
[Action PlayerD No Person] -> [PlayerL]
[Action PlayerL No Person] -> [PlayerU]

(politely ask for room to move past)
Up [> PlayerU PersonU| No Obstacle] -> [> PlayerU > PersonU|]
Right [> PlayerR PersonR| No Obstacle] -> [> PlayerR > PersonR|]
Down [> PlayerD PersonD| No Obstacle] -> [> PlayerD > PersonD|]
Left [> PlayerL PersonL| No Obstacle] -> [> PlayerL > PersonL|]

(can't push a person into an obstacle)
[> Player|Wall] -> [Player |Wall]
Up [> PlayerU PersonU| PersonU] -> [PlayerU PersonU|PersonU]
Right [> PlayerR PersonR| PersonR] -> [PlayerR PersonR|PersonR]
Down [> PlayerD PersonD| PersonD] -> [PlayerD PersonD|PersonD]
Left [> PlayerL PersonL| PersonL] -> [PlayerL PersonL|PersonL]

(can't move backward into another person)
Down [> PlayerU | PersonU] -> [PlayerU| PersonU]
Left [> PlayerR | PersonR] -> [PlayerR| PersonR]
Up [> PlayerD | PersonD] -> [PlayerD| PersonD]
Right [> PlayerL | PersonL] -> [PlayerL| PersonL]

(can slide past a person or go up to them)
[> PlayerU| PersonU] -> [|PlayerU PersonU]
[> PlayerR| PersonR] -> [|PlayerR PersonR]
[> PlayerD| PersonD] -> [|PlayerD PersonD]
[> PlayerL| PersonL] -> [|PlayerL PersonL]

(otherwise, can't move into the same square)
[> Player| Person] -> [Player| Person]

late [Person No Player DanceFloor] -> [Person Dance DanceFloor]
late [Dance PersonU DanceFloor] -> [random PersonR random PersonL DanceFloor]
late [Dance PersonR DanceFloor] -> [random PersonD random PersonU DanceFloor]
late [Dance PersonD DanceFloor] -> [random PersonL random PersonR DanceFloor]
late [Dance PersonL DanceFloor] -> [random PersonU random PersonD DanceFloor]

==============
WINCONDITIONS
==============
Some Player on Exit

=======
LEVELS
=======

message Everyone's gone to the party. I better go too.

##########
#........#
#p.......#
#........e
##########

message That's right, I forgot. I hate parties. Get me outta here.
message [Note: Press x to turn]

########e#
#...##.rl#
#...##...#
#...d#...#
#..r.l.d.#
#.p.u..u.#
#...#....#
##########

message Excuse me, coming through.

########
#..r...#
#..d##.#
#pr.l#.#
#..u.#le
########

message What is this, some weird conga line?

######e##
######r##
#.dllll##
#pr.....#
#########

message Oh great. Now I have to get past the dance floor.

#########
#p.nnnn.#
#..nnnn.#
#..nnnn.#
#..nnnn.#
#..nnnn.e
#########

message How awkward.

#######e#
#r.r,nrl#
#pdr,nd.#
#########