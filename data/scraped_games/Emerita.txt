title Emerita
author Aaron Steed
homepage www.robotacid.com

again_interval 0.05
background_color Black
text_color yellow
(youtube LkYhVE9QuOo) (bah, can't use stock music for Ludum Dare)

========
OBJECTS
========

background
#bcbcbc #00195a #1b1252 #01126e
13333
33311
11222
22222
22232

downplayer
#372804 black #00fbbe black 
.103.
10330
10333
13333
.232.

upplayer
#372804 black #00fbbe black 
.232.
10330
10333
10331
.113.

rightplayer
#372804 black #00fbbe black 
.131.
10002
01333
11332
.113.

leftplayer
#372804 black #00fbbe black 
.131.
20001
11330
21331
.113.

deadplayer
#bcbcbc #d3eeff #a2d5dc #ffffff 
.020.
02000
02000
00000
.000.

mine
#002538 #d3eeff #247cfe #ffffff 
..1..
.030.
13321
.020.
..1..

spike
#002538 #d3eeff #247cfe #ffffff
..3..
.030.
.132.
01320
21320

drill
#002538 #d3eeff #247cfe #ffffff
21320
01320
.132.
.030.
..3..

drilling
#002538 #d3eeff #247cfe #ffffff
21320
01320
.132.
.030.
..3..

sand
#ebb42f #ffc82d #e2fefb #fff0aa 
01010
30131
11011
13111
01103

mud
#84710d #cb6e00 #ffa308 #b69811
01010
30131
11011
13111
01103

rock
#342604 #473606 #fffbe5 #5d573c 
.110.
12320
12330
01310
.000.

wall
#2c2003 #1e1915 #43423c #867e5b 
33231
22200
30201
20000
10011

plinkton
#044400 #8de800 #00fbbe #867e5b 
.212.
01110
.010.
0.0.0
..0..


=======
LEGEND
=======

. = background
# = wall
@ = downplayer and sand
m = downplayer and mud
* = rock
p = plinkton and sand
o = sand
u = mud
x = mine
a = spike
w = drill

medium = mud or sand
heavy = rock or mine or drill or drilling
player = upplayer or downplayer or leftplayer or rightplayer
drills = drill or drilling

=======
SOUNDS
=======

sfx0 35032108 (eat plinkton)
sfx1 89671908 (mine death)
sfx2 13358907 (move)

EndLevel 69038308
EndGame 28032108

================
COLLISIONLAYERS
================

background
sand, mud
plinkton
player, rock, wall, mine, deadplayer, drill, drilling, spike

======
RULES
======

(draw direction travelled)
[ up player ] -> [ up upplayer ]
[ down player ] -> [ down downplayer ]
[ right player ] -> [ right rightplayer ]
[ left player ] -> [ left leftplayer ]

(block moving out of medium)
[ > player | no medium ] -> [ player |  ]

(prepare drills)
[ > player ] [ drill ] -> [ > player ] [ drilling ] sfx2

(dig)
[ > player sand | medium ] -> [ | player medium ]

(move drills)
random down [ drilling | medium no plinkton] -> [ | drill ]

(drop sand)
down [ sand | no wall no sand no heavy no mud no drill no spike ] -> [ down sand | ] again
[ down sand plinkton ] -> [ down sand down plinkton ]

(convert hanging spikes to drills)
down [ spike | no heavy no medium no wall ] -> [ drill | ]

(drop heavies)
late down [ heavy | no spike no drill no drilling no mud no sand no upplayer no downplayer no leftplayer no rightplayer no heavy no wall no deadplayer ] -> [ no spike no drill no drilling no sand no mud no upplayer no downplayer no leftplayer no rightplayer no heavy no wall  no deadplayer | heavy ] again

(eat plinkton)
[ player plinkton ] -> [ player ] sfx0
late [ player plinkton ] -> [ player ] sfx0

(kill player)
late [ player | mine ] -> [ deadplayer | mine ] sfx1
late down [ drills | player ] -> [ drill | deadplayer ] sfx1

==============
WINCONDITIONS
==============

no plinkton
some player

=======
LEVELS
=======

.........
.o.....o.
.o....ooo
ooo.o.opo
o@o.o.ooo
ooooooooo
oopoooooo
ooooopooo
ooooooooo

..o....
..oo...
..oooo.
..oooo.
..ooo@o
o.ooo##
ooo#p#o
poo###o
ooooopo
ooooooo

..o.......
o@oo......
oooooo....
#o#ooo...o
#p#poooooo
#o###oooop
#oooooo###
#ooo###op#
##ooooooo#
##########

...oo....
.ooo@o...
.poooo...
.ooooo...
.ooooooo.
#ooooo#o.
#ooopo#o#
#o#####oo
#opo#opoo
#ooo#ooo#
##oo##o##
#########

.ooop.......
.oouuuo.....
ooo..#o.....
uom##oooo...
uuuoooooo.p.
#u.#ouuoooo.
.o.oopuuuuu.
oopoooo###..
poouuooop...

........o.
........o.
.......pop
..p..o.oou
..oo.p.uo.
..uo.o..o.
.ouo.oooo.
#o.o.o#oo#
#o#o#o#o##
#o#uuuooo#
#ouuuuuoo#
#ouumuuo##
##uuuuu###
##########

.........
p*oo**o*o
ooooooo*p
o#oo#oo*o
o@o#poooo
ooo#ooooo

.........
*********
popopopop
ooooooooo
#oooo##oo
##ooooooo
###ooooo#
###o@oo##

*x*x*x*
ooooooo
opopopo
ooooooo
o#o@o#o
ooooooo

xx.....xx
oo**x**oo
pooooooop
oooopoooo
uuxuouxuu
uoouuuoou
opoouoopo
#ooo@ooo#
##ooooo##

aaaaaaa
ooooooo
pooooop
uooooou
up#p#pu
uu.u.uu
uuumuuu

**.......
oo****...
o@oooo***
ooooopooo
***pooooo
oop*o**po
pooooop**
oooopoooo
ooooooooo

xxxxxxx
ooooooo
ooooooo
opppppo
ooooooo
aaaaaaa
ooooooo
ooooooo
ooooooo
opppppo
ooooooo
ooo@ooo

...........
ooooooooooo
ooooooooooo
ooxoxoxoxoo
ooooooooooo
opooopooopo
o#opooopo#o
ooooooooooo
ooo#ooo#ooo
#ooooooooo#
##ooo@ooo##

.x.x.x.x.
xoxoxoxox
opopopopo
ooooooooo
ooooooooo
oooo@oooo
ooooooooo

p.......p
o.p...p.o
o.o.p.o.o
*.*.*.*.*
ooooooooo
ooooooooo
ooooooooo
oooo@oooo
ooooooooo

