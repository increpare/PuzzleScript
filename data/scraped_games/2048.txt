( PuzzleScript port of "2048"

  Original at:
  http://gabrielecirulli.github.io/2048/ )

title 2048
noundo

========
OBJECTS
========

Background
lightblue

1
white red 
00000
00000
00100
00000
00000

2
white green
00000
00100
00000
00100
00000

3
white darkblue
00000
01000
00100
00010
00000

4
white yellow
00000
01010
00000
01010
00000

5
white brown
00000
01010
00100
01010
00000

6
white purple
00000
01010
01010
01010
00000

7
white darkred
00000
01010
01110
01010
00000

8
white orange
00000
01110
01010
01110
00000

9
white blue
00110
00100
01110
00100
01100

A
white lightgreen
01000
01110
01010
01110
00010

B
white pink
10001
01010
10101
01010
10001

=======
LEGEND
=======

. = Background
Player = 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or A or B

=======
SOUNDS
=======

(All sounds are annoying, so I'm not using them.)

================
COLLISIONLAYERS
================

Background
Player

======
RULES
======

(Disable the "action" button.  You must make a move!)
[ Action Player ] -> CANCEL

(First, shove boxes in the direction of movement, as far as they will go.)
[ > Player | No Player ] -> [ | > Player ]

(Combine equal-valued boxes adjacent in the direction of movement.)

[ > A | > A ] -> [ | > B ]
[ > 9 | > 9 ] -> [ | > A ]
[ > 8 | > 8 ] -> [ | > 9 ]
[ > 7 | > 7 ] -> [ | > 8 ]
[ > 6 | > 6 ] -> [ | > 7 ]
[ > 5 | > 5 ] -> [ | > 6 ]
[ > 4 | > 4 ] -> [ | > 5 ]
[ > 3 | > 3 ] -> [ | > 4 ]
[ > 2 | > 2 ] -> [ | > 3 ]
[ > 1 | > 1 ] -> [ | > 2 ]


(Note that the above rule might leave a gap when two combinations occur
 on the same line, so apply the movement rule again to close the gap.)
[ > Player | No Player ] -> [ | > Player ]

(Finally, spawn a new box at a random empty stop:)
random [ No Player ] -> [ 1 ]

==============
WINCONDITIONS
==============

Some B

=======
LEVELS
=======

....
2...
...2
....

MESSAGE Congratulations, you won!!
