title Towers of Hanoi
author Daniel Sherlock
(Note: The game itself is traditional.)
homepage https://en.wikipedia.org/wiki/Tower_of_hanoi
(I [the author] am temorarily lacking in webpage, so you can read all about the Towers of Hanoi instead. It's good fun.)

background_color DarkGreen
run_rules_on_level_start
key_repeat_interval 0.2

========
OBJECTS
========

(As you can probably tell, I've struggled to get reasonable colours and shapes for all the things. Suggestions welcome.)

Background
#6bb745 #45b758 #93b745
10201
00000
20102
00000
10201

Base
#ad914a #937b3e
01000
10000
00001
00010
00100

Base_Top
#ad914a #937b3e Yellow
22222
10000
00001
00010
00100

Base_Bottom
#ad914a #937b3e #8d712a
01000
10000
22222
.....
.....

Base_Left
#ad914a #937b3e DarkBrown #8d712a
31000
30000
20001
20010
20100

Base_Right
#ad914a #937b3e Yellow Lightbrown
01002
10002
00003
00013
00103

Base_Top_Left
#ad914a #937b3e #8d712a Yellow Lightbrown DarkBrown
...43
.2200
20001
20010
50100

Base_Top_Right
#ad914a #937b3e Yellow LightBrown
22...
1022.
00002
00012
00103

Base_Bottom_Left
#ad914a #937b3e DarkBrown #8d712a
21000
.2000
..233
.....
.....

Base_Bottom_Right
#ad914a #937b3e LightBrown #8d712a
01002
1002.
332..
.....
.....

Peg
#937b3e #ad914a
.001.
.001.
.001.
.001.
.001.

Peg_Top
#937b3e #ad914a
.11..
.001.
.001.
.001.
.001.

Peg_Bottom
#937b3e #ad914a DarkBrown Yellow
.001.
.001.
.201.
.201.
.231.

StartMarker
Transparent

FinishMarker
Transparent

FullBlock
DarkRed Red LightRed
22222
11111
11111
11111
00000

FullBlock_Left
DarkRed Red LightRed
.2222
01111
01111
00111
.0000

FullBlock_Right
DarkRed Red LightRed
2222.
11122
11112
11112
0000.

HalfBlock_Left
DarkRed Red LightRed
...22
..011
..001
..001
...00

HalfBlock_Right
DarkRed Red LightRed
22...
122..
122..
112..
00...

EmptyBlock
Transparent

Player
LightBlue
..0..
..0..
0.0.0
.000.
..0..


=======
LEGEND
=======

. = Background

# = Base
! = Peg

S = StartMarker and Player
F = FinishMarker

0 = FullBlock and Peg

@ = FullBlock
{ = Halfblock_Left
} = Halfblock_Right
_ = EmptyBlock

AnyBase = Base or Base_Top or Base_Bottom or Base_Left or Base_Right or Base_Top_Left or Base_Top_Right or Base_Bottom_Left or Base_Bottom_Right
AnyPeg = Peg or Peg_Top or Peg_Bottom
AnyMarker = StartMarker or FinishMarker
AnyFullBlock = FullBlock or FullBlock_Left or FullBlock_Right
HalfBlock = HalfBlock_Left or HalfBlock_Right
Block = AnyFullBlock or HalfBlock or EmptyBlock

=======
SOUNDS
=======

(I considered doing some sounds for various things, but was listening to music at the time, so didn't. As with above, suggestions are welcome.)


================
COLLISIONLAYERS
================

Background
AnyBase, AnyPeg, AnyMarker
Block
Player


======
RULES
======

horizontal [> Player no Block | ... | AnyMarker] -> [| ... | Player AnyMarker]

startloop

down [< Player no Block | ... | Block] -> [< Player | ... | < Block]
down [action Player no Block | ... | Block] -> [< Player | ... | < Block]

down [> Player Block] -> [> Player > Block]
down [action Player Block] -> [> Player > Block]
down [> Player | ... | Block | no Block no AnyBase] -> [> Player | ... | > Block |]

down [Block | ... | vertical Block] -> [Block | ... | Block]

horizontal [> Player Block | ... | AnyMarker] -> [> Player > Block | ... | AnyMarker]

horizontal [Block | moving Block] -> [moving Block | moving Block]

vertical [> Block |] -> [| Block]
horizontal [> Player > Block | no Block] -> [| > Player Block]
+ horizontal[no Player > Block | no Block] -> [| Block]

horizontal [> Player AnyMarker] -> [Player AnyMarker]

endloop

[> Player] -> [Player]

down [AnyFullBlock | HalfBlock] -> cancel
down [AnyFullBlock | EmptyBlock] -> cancel
down [HalfBlock | EmptyBlock] -> cancel

down [FinishMarker | | | Block] -> win

late right [no Block | FullBlock] -> [| FullBlock_Left]
late right [EmptyBlock | FullBlock] -> [EmptyBlock | FullBlock_Left]
late right [FullBlock | no Block] -> [FullBlock_Right |]
late right [FullBlock | EmptyBlock] -> [FullBlock_Right | EmptyBlock]

late down [no AnyPeg | Peg] -> [| Peg_Top]
late down [Peg | no AnyPeg] -> [Peg_Bottom |]

late down [no AnyBase | Base] -> [| Base_Top]
late down [Base | no AnyBase] -> [Base_Bottom |]
late right [no AnyBase | Base] -> [| Base_Left]
late right [Base | no AnyBase] -> [Base_Right |]

late right [no AnyBase | Base_Top] -> [| Base_Top_Left]
late right [Base_Top | no AnyBase] -> [Base_Top_Right |]
late right [no AnyBase | Base_Bottom] -> [| Base_Bottom_Left]
late right [Base_Bottom | no AnyBase] -> [Base_Bottom_Right |]


==============
WINCONDITIONS
==============

(see above)


=======
LEVELS
=======

(
--Tutorial:
--Comment/Uncomment this as necessary.
)

message Hi.
message "The Towers of Hanoi" is a well-known mathematical puzzle.
message In it, you move discs between pegs one at a time - trying to recreate the whole tower on a new peg.
message Like this...

...........
...S...F...
...........
...!...!...
..{0}..!...
.#########.
.#########.
...........

message Ok, so you can move a disc.
message But, there's only one problem: you're not allowed to place a larger disc on top of a smaller one.
message Still, you should be able to solve this next one just fine...

...............
...S...F...F...
...............
...!...!...!...
..{0}..!...!...
..@0@..!...!...
.#############.
.#############.
...............

message See? That wasn't so bad! Now, the real thing:

(
--End of tutorial.
)

.....................
....S.....F.....F....
.....................
....!.....!.....!....
...{0}....!.....!....
.._@0@_...!.....!....
..{@0@}...!.....!....
..@@0@@...!.....!....
.###################.
.###################.
.....................

message So, how many moves did you manage to complete that in? At best, it's possible in 15 moves!
message Try completing this next one in 31 moves - the shortes number that it takes to complete a puzzle with 5 rings and 3 pegs...

...........................
.....S.......F.......F.....
...........................
.....!.......!.......!.....
....{0}......!.......!.....
..._@0@_.....!.......!.....
...{@0@}.....!.......!.....
.._@@0@@_....!.......!.....
..{@@0@@}....!.......!.....
.#########################.
.#########################.
...........................

message Congratulations.
message That said, they're pretty much all like that, no matter how many discs you add...
message ... so I think I'll stop here, and you can enjoy the rest of your day.

message However, if you still (inexplicably) want more Towers of Hanoi, that's easy...
message 1. Find the source code for all this at https://gist.github.com/DanielSherlock/8177199aab5b525f027cebe254bff163
message 2. Copy and paste it into https://puzzlescript.net/editor.html
message 3. Remove the brackets before and after the final super-level.
message 4. Hit "Run", and play.
message Fair warning though, that one will take longer to finish than the current age of the universe, ...
message ... so I suppose don't start it if you're busy?

( TO UNLOCK BONUS LEVEL DELETE THIS ENTIRE LINE ...

message Seriously, just to make sure you know what you're getting into, ...
message ... I want you to take a look at these calulations:

message 2 turns per second
message = 120 turns per minute
message = 7,200 turns per hour
message = 172,800 turns per day
message = 63,115,200 turns per year
message = 870,989,760 billion turns per 13.8 billion years

message The Towers of Hanoi you're about to see would take 1,152,921,504,606,846,999 moves if you did it the quickest possible way.
message Which works out as:
message Too many.

.............................................................................................................................................................................................
................................S.............................................................F.............................................................F................................
.............................................................................................................................................................................................
................................!.............................................................!.............................................................!................................
...............................{0}............................................................!.............................................................!................................
.............................._@0@_...........................................................!.............................................................!................................
..............................{@0@}...........................................................!.............................................................!................................
............................._@@0@@_..........................................................!.............................................................!................................
.............................{@@0@@}..........................................................!.............................................................!................................
............................_@@@0@@@_.........................................................!.............................................................!................................
............................{@@@0@@@}.........................................................!.............................................................!................................
..........................._@@@@0@@@@_........................................................!.............................................................!................................
...........................{@@@@0@@@@}........................................................!.............................................................!................................
.........................._@@@@@0@@@@@_.......................................................!.............................................................!................................
..........................{@@@@@0@@@@@}.......................................................!.............................................................!................................
........................._@@@@@@0@@@@@@_......................................................!.............................................................!................................
.........................{@@@@@@0@@@@@@}......................................................!.............................................................!................................
........................_@@@@@@@0@@@@@@@_.....................................................!.............................................................!................................
........................{@@@@@@@0@@@@@@@}.....................................................!.............................................................!................................
......................._@@@@@@@@0@@@@@@@@_....................................................!.............................................................!................................
.......................{@@@@@@@@0@@@@@@@@}....................................................!.............................................................!................................
......................_@@@@@@@@@0@@@@@@@@@_...................................................!.............................................................!................................
......................{@@@@@@@@@0@@@@@@@@@}...................................................!.............................................................!................................
....................._@@@@@@@@@@0@@@@@@@@@@_..................................................!.............................................................!................................
.....................{@@@@@@@@@@0@@@@@@@@@@}..................................................!.............................................................!................................
...................._@@@@@@@@@@@0@@@@@@@@@@@_.................................................!.............................................................!................................
....................{@@@@@@@@@@@0@@@@@@@@@@@}.................................................!.............................................................!................................
..................._@@@@@@@@@@@@0@@@@@@@@@@@@_................................................!.............................................................!................................
...................{@@@@@@@@@@@@0@@@@@@@@@@@@}................................................!.............................................................!................................
.................._@@@@@@@@@@@@@0@@@@@@@@@@@@@_...............................................!.............................................................!................................
..................{@@@@@@@@@@@@@0@@@@@@@@@@@@@}...............................................!.............................................................!................................
................._@@@@@@@@@@@@@@0@@@@@@@@@@@@@@_..............................................!.............................................................!................................
.................{@@@@@@@@@@@@@@0@@@@@@@@@@@@@@}..............................................!.............................................................!................................
................_@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@_.............................................!.............................................................!................................
................{@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@}.............................................!.............................................................!................................
..............._@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@_............................................!.............................................................!................................
...............{@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@}............................................!.............................................................!................................
.............._@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@_...........................................!.............................................................!................................
..............{@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@}...........................................!.............................................................!................................
............._@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@_..........................................!.............................................................!................................
.............{@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@}..........................................!.............................................................!................................
............_@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@_.........................................!.............................................................!................................
............{@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@}.........................................!.............................................................!................................
..........._@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@_........................................!.............................................................!................................
...........{@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@}........................................!.............................................................!................................
.........._@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@_.......................................!.............................................................!................................
..........{@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@}.......................................!.............................................................!................................
........._@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@_......................................!.............................................................!................................
.........{@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@}......................................!.............................................................!................................
........_@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@_.....................................!.............................................................!................................
........{@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@}.....................................!.............................................................!................................
......._@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@_....................................!.............................................................!................................
.......{@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@}....................................!.............................................................!................................
......_@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@_...................................!.............................................................!................................
......{@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@}...................................!.............................................................!................................
....._@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@_..................................!.............................................................!................................
.....{@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@}..................................!.............................................................!................................
...._@@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@@_.................................!.............................................................!................................
....{@@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@@}.................................!.............................................................!................................
..._@@@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@@@_................................!.............................................................!................................
...{@@@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@@@}................................!.............................................................!................................
.._@@@@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@@@@_...............................!.............................................................!................................
..{@@@@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@@@@}...............................!.............................................................!................................
..@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@0@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@...............................!.............................................................!................................
.###########################################################################################################################################################################################.
.###########################################################################################################################################################################################.
.............................................................................................................................................................................................

... AND THEN ALSO DELETE THIS ONE.)

message Oh, and I should say that...
message ... I wouldn't have written any of this without the amazing PuzzleScript by https://www.increpare.com/
