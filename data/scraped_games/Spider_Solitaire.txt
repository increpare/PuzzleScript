title Spider Solitaire
author Aaron Dobbe
homepage nintendooutcast.tumblr.com

key_repeat_interval 0.3
again_interval 0.05
run_rules_on_level_start

noundo (otherwise it's very easy to cheese the new deals)

========
OBJECTS
========

Background
Green

RAce
White Red Lightgray
22122
01010
01110
01010
01010

RTwo
White Red Lightgray
21112
00010
01110
01000
01110

RThree
White Red Lightgray
21112
00010
01110
00010
01110

RFour
White Red Lightgray
21212
01010
01110
00010
00010

RFive
White Red Lightgray
21112
01000
01110
00010
01110

RSix
White Red Lightgray
21222
01000
01110
01010
01110

RSeven
White Red Lightgray
21112
00010
00010
00010
00010

REight
White Red Lightgray
21112
01010
01110
01010
01110

RNine
White Red Lightgray
21112
01010
01110
00010
01110

RTen
White Red Lightgray
12111
10101
10101
10101
10111

RJack
White Red Lightgray
22212
00010
00010
01010
01110

RQueen
White Red Lightgray
21112
01010
01010
01010
01101

RKing
White Red Lightgray
21212
01010
01100
01010
01010

Player
Blue
00.00
0...0
.....
0...0
00.00

PlayerShadow
Yellow
00.00
0...0
.....
0...0
00.00

PlayerBlock
Red
.....
.....
.....
.....
.....

StackMover
Pink

PlayerBlockSpawn
Black

CardBack
Blue White
01110
10001
00100
10001
01110

DealCounter
Blue White
01110
10001
00100
10001
01110

DealMarker
Green

ResetDealMarker
Green

NewDealMarker
Green

NewDealSeed
Green

NewDealPlace
Green

NewDealChecker
Yellow

DealEndMarker
Green

CardFlipFlag
Green

SPlayEdge
Black

OtherPlayEdge
Black

Mask
Black

=======
LEGEND
=======

M = DealMarker
N = NewDealPlace
E = DealEndMarker
X = OtherPlayEdge
Y = SPlayEdge
D = DealCounter
P = Player
. = Background
# = Mask
A = RAce and Mask
2 = RTwo and Mask
3 = RThree and Mask
4 = RFour and Mask
5 = RFive and Mask
6 = RSix and Mask
7 = RSeven and Mask
8 = REight and Mask
9 = RNine and Mask
T = RTen and Mask
J = RJack and Mask
Q = RQueen and Mask
K = RKing and Mask
Card = RAce or RTwo or RThree or RFour or RFive or RSix or RSeven or REight or RNine or RTen or RJack or RQueen or RKing
PlayEdge = SPlayEdge or OtherPlayEdge

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
DealMarker, PlayEdge, DealEndMarker, ResetDealMarker, CardFlipFlag, DealCounter
PlayerBlockSpawn, NewDealChecker
Card, NewDealPlace
CardBack, NewDealSeed, NewDealMarker, PlayerShadow, StackMover
Player, PlayerBlock, Mask

======
RULES
======

(Check for new deal)
[ action Player DealCounter ] [ NewDealPlace ] -> [ Player DealCounter ] [ NewDealPlace NewDealChecker ]
(Make sure there are no empty piles first)
up [ NewDealChecker | ... | | PlayEdge ] -> [ | ... | NewDealChecker | PlayEdge ]
right [ NewDealChecker | Card ] -> [ | Card NewDealChecker ]
right [ NewDealChecker | no Card no PlayEdge ] -> [ | ]
[ Player DealCounter ] [ NewDealPlace ] [ NewDealChecker ] -> [ Player ] [ NewDealPlace NewDealSeed ] [ ]
(Start new deal)
random right [ NewDealSeed | PlayEdge ] -> [ NewDealMarker | PlayEdge ] again
random right [ NewDealSeed | no PlayEdge ] -> [ NewDealMarker | NewDealSeed ] again
up [ NewDealMarker | no Card no PlayEdge ] -> [ | NewDealMarker ]
random right [ NewDealMarker ] [ PlayEdge | PlayEdge | ... | Card ] -> [ Card ] [ PlayEdge | PlayEdge | ... | ]

(Check for card release)
down [ PlayerShadow | ... | action Player ] -> [ | ... | Player ]
up [ action Player | RTwo ] [ PlayerShadow RAce ] -> [ StackMover | RTwo ] [ PlayerShadow RAce ]
up [ action Player | RThree ] [ PlayerShadow RTwo ] -> [ StackMover | RThree ] [ PlayerShadow RTwo ]
up [ action Player | RFour ] [ PlayerShadow RThree ] -> [ StackMover | RFour ] [ PlayerShadow RThree ]
up [ action Player | RFive ] [ PlayerShadow RFour ] -> [ StackMover | RFive ] [ PlayerShadow RFour ]
up [ action Player | RSix ] [ PlayerShadow RFive ] -> [ StackMover | RSix ] [ PlayerShadow RFive ]
up [ action Player | RSeven ] [ PlayerShadow RSix ] -> [ StackMover | RSeven ] [ PlayerShadow RSix ]
up [ action Player | REight ] [ PlayerShadow RSeven ] -> [ StackMover | REight ] [ PlayerShadow RSeven ]
up [ action Player | RNine ] [ PlayerShadow REight ] -> [ StackMover | RNine ] [ PlayerShadow REight ]
up [ action Player | RTen ] [ PlayerShadow RNine ] -> [ StackMover | RTen ] [ PlayerShadow RNine ]
up [ action Player | RJack ] [ PlayerShadow RTen ] -> [ StackMover | RJack ] [ PlayerShadow RTen ]
up [ action Player | RQueen ] [ PlayerShadow RJack ] -> [ StackMover | RQueen ] [ PlayerShadow RJack ]
up [ action Player | RKing ] [ PlayerShadow RQueen ] -> [ StackMover | RKing ] [ PlayerShadow RQueen ]
up [ action Player | PlayEdge ] [ PlayerShadow Card ] -> [ StackMover | PlayEdge ] [ PlayerShadow Card ]
up [ | StackMover ] [ | PlayerShadow Card ] -> [ StackMover | Card ] [ PlayerShadow | ]
[ StackMover ] [ PlayerShadow ] -> [ Player CardFlipFlag ] [ ]

(Check for card grab)
[ action Player Card ] -> [ Player PlayerShadow Card ]

(Deal the initial cards)
random right [ DealMarker ] [ PlayEdge | PlayEdge | ... | Card ] -> [ DealMarker Card ] [ PlayEdge | PlayEdge | ... | ] again
[ DealMarker ] -> [DealMarker CardBack ]
[ DealMarker Card ] -> [ right DealMarker Card ] again
right [ right DealMarker | PlayEdge ] -> [ down ResetDealMarker | PlayEdge ]
right [ right DealMarker | DealEndMarker ] -> [ Player | CardFlipFlag ]
late right [ PlayEdge | | ... | ResetDealMarker | PlayEdge ] -> [ PlayEdge | DealMarker | ... | | PlayEdge ]
horizontal [ PlayerShadow ] [ PlayEdge | < Player ] -> [ PlayerShadow ] [ PlayEdge | Player ]
horizontal [ PlayEdge | PlayEdge | < Player ] -> [ PlayEdge | PlayEdge | Player ]
up [ PlayEdge | | ... | horizontal Player ] -> [ PlayEdge | horizontal Player | ... | ]
horizontal [ no PlayEdge | PlayEdge | < Player ] -> [ Player | PlayEdge | ]

( Clear complete stack )
up [ RAce no CardBack | RTwo no CardBack | RThree no CardBack | RFour no CardBack | RFive no CardBack | RSix no CardBack | RSeven no CardBack | REight no CardBack | RNine no CardBack | RTen no CardBack | RJack no CardBack | RQueen no CardBack | RKing no CardBack ] -> [ |||||||||||| CardFlipFlag ]

down [ CardFlipFlag ] [ CardBack | no CardBack no Card ] -> [ CardFlipFlag ] [ | ]
[ CardFlipFlag ] -> [ ]

(Set Player boundaries)
late [ PlayerBlock ] -> [ ]
late up [ SPlayEdge | ] -> [ SPlayEdge | PlayerBlockSpawn ]
late [ DealMarker ] [ PlayerBlockSpawn ] -> [ DealMarker ] [ ]
late [ NewDealMarker ] [ PlayerBlockSpawn ] -> [ DealMarker ] [ ]
late up [ no Card | PlayerBlockSpawn | ] -> [ | | PlayerBlockSpawn ]
late up [ RAce | RTwo PlayerBlockSpawn | ] -> [ RAce | RTwo | PlayerBlockSpawn ]
late up [ RTwo | RThree PlayerBlockSpawn | ] -> [ RTwo | RThree | PlayerBlockSpawn ]
late up [ RThree | RFour PlayerBlockSpawn | ] -> [ RThree | RFour | PlayerBlockSpawn ]
late up [ RFour | RFive PlayerBlockSpawn | ] -> [ RFour | RFive | PlayerBlockSpawn ]
late up [ RFive | RSix PlayerBlockSpawn | ] -> [ RFive | RSix | PlayerBlockSpawn ]
late up [ RSix | RSeven PlayerBlockSpawn | ] -> [ RSix | RSeven | PlayerBlockSpawn ]
late up [ RSeven | REight PlayerBlockSpawn | ] -> [ RSeven | REight | PlayerBlockSpawn ]
late up [ REight | RNine PlayerBlockSpawn | ] -> [ REight | RNine | PlayerBlockSpawn ]
late up [ RNine | RTen PlayerBlockSpawn | ] -> [ RNine | RTen | PlayerBlockSpawn ]
late up [ RTen | RJack PlayerBlockSpawn | ] -> [ RTen | RJack | PlayerBlockSpawn ]
late up [ RJack | RQueen PlayerBlockSpawn | ] -> [ RJack | RQueen | PlayerBlockSpawn ]
late up [ RQueen | RKing PlayerBlockSpawn | ] -> [ RQueen | RKing | PlayerBlockSpawn ]
late up [ CardBack | PlayerBlockSpawn ] -> [ CardBack PlayerBlockSpawn | ]
late down [ PlayerBlockSpawn | DealCounter | DealCounter ] -> [ | PlayerBlockSpawn DealCounter | DealCounter ]
late [ PlayerBlockSpawn ] -> [ PlayerBlock ]

late up [ Player no Card no DealCounter | no PlayEdge ] -> [ | Player ]
late up [ PlayerShadow ] [ | Player Card ] -> [ PlayerShadow ] [ Player | Card ]

==============
WINCONDITIONS
==============

No Card

=======
LEVELS
=======

message Use X to grab/release cards. You may only stack cards in descending order.
message You can grab a whole stack of ordered cards by moving the cursor up to grab the top card of the stack.
message Move the cursor all the way to the left and press X to deal 10 new cards, as long as there are no empty stacks.
message A complete stack (from King to Ace) will vanish. Clear all cards to win.

XXXXXXXXXXXXXXXXXXXXXX
DXM.........XXAAAAAAAA
DX..........XX22222222
DX..........XX33333333
DX..........XX44444444
DX..........XX55555555
.X....E.....XX66666666
.X..........XX77777777
.X..........XX88888888
.X..........XX99999999
.X..........XXTTTTTTTT
.X..........XXJJJJJJJJ
.X..........XXQQQQQQQQ
.X..........XX########
.X..........XXKKKKKKKK
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.X..........XX########
.XN.........XX########
.X..........XX########
YXYYYYYYYYYYXXXXXXXXXX

message Hooray! You win!

