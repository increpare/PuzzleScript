Title Rocketman
Author Connorses
homepage https://sites.google.com/site/loneshipgames/
background_color darkbrown
again_interval 0.07

noundo
noaction
========
OBJECTS
========
background
lightblue
player1
black red
.....
.000.
10001
10001
.0.0.
player2
black red yellow
.000.
10001
10001
20.02
2...2

player3
black red yellow
2211.
.0000
..000
.0000
2211.
player4
black red yellow
.1122
0000.
000..
0000.
.1122

wall
brown

platu(form)
lightbrown
00000
0...0
.....
.....
.....
platl
lightbrown
00...
0....
0....
0....
00...
platr
lightbrown
...00
....0
....0
....0
...00
platd
lightbrown
.....
.....
.....
0...0
00000

goal
green
goalwin
lightgreen

=======
LEGEND
=======
. = background
p = player1
$ = wall
# = goal and platr and platl and platd
- = platu
_ = platd
[ = platl
] = platr
h = platd and platu
| = platl and platr
\ = platr and platu
r = platu and platl
J = platr and platl
L = platl and platd

player = player1
flying = player2 or player3 or player4
plat = platu or platl or platr or platd
=======
SOUNDS
=======
sfx0 20984702 (8300502 launch)
sfx1 12172702  (24686702 939502 flight)
================
COLLISIONLAYERS
================
background
platu
platd
platl
platr
player,flying,wall,goal,goalwin

======
RULES
======
[up    player1] -> [player2] sfx0
[right player1] -> [player3] sfx0
[left  player1] -> [player4] sfx0

[player2] -> [up    player2] sfx1 again
[player3] -> [right player3] sfx1 again
[player4] -> [left  player4] sfx1 again

[> flying |wall ] -> [player1|wall ] again
[> player3|platl] -> [player1|platl] again
[> player4|platr] -> [player1|platr] again
[> player2|platd] -> [player1|platd] again
down [> player1|platu] -> [player1|platu]

down [player1|no platu no wall] -> [> player1|] again

down [player|goal] -> [player|goalwin]
up [goalwin|no player] -> [goal|]
==============
WINCONDITIONS
==============
no goal
=======
LEVELS
=======
$$......$$
$........$
$........$
$........$
$p.......$
$$$....$#$
$$......$$

$$$$$$$
$.....$
$.....$
$....-$
$..$..$
$-.$..$
$..$..$
$..$..$
$..$.p$
$#$$$$$

$$.$$$..$$
$........$
$........$
$#$$-$$$$$
..........
$..].....$
$......$-$
....-$$$..
..........
$p.......$
$$$......$
$$$$$$$$$$

$$$$$$$$$$
$$...$...$
$$#-$$...$
$$$......$
$....]-.[$
$-.-.]...$
$$-...$$.$
$$.$-p...$
$$$$$$...$
$$$$$$$$$$

$$$$$$$$$$
$.p].....$
$$$$...-.$
$..$#....$
$-..[..[-$
$$]|-....$
$$.-....$$
$$$....-.$
$$$..-...$
$$$..$$$-$
$$$......$
$$$$$$$$$$