title Circuit Breaker
author Zithral
homepage www.puzzlescript.net

noaction
key_repeat_interval 0.15
run_rules_on_level_Start

========
OBJECTS
========

Background
BLACK #333333
00000
01100
01010
00110
00000

Wall
BLACK DARKGRAY
11111
11111
11111
11111
11111

Player
DARKBROWN Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
BLACK #333333 #555555 #666666
22222
23332
23332
23332
22222

EdgeU
#00aa00
..0..
..0..
..0..
.....
.....

EdgeD
#00aa00
.....
.....
..0..
..0..
..0..

EdgeL
#00aa00
.....
.....
000..
.....
.....

EdgeR
#00aa00
.....
.....
..000
.....
.....

MatchU
#00ff00
..0..
..0..
.....
.....
.....

MatchD
#00ff00
.....
.....
.....
..0..
..0..

MatchL
#00ff00
.....
.....
00...
.....
.....

MatchR
#00ff00
.....
.....
...00
.....
.....

FullMatch
WHITE
.....
.....
..0..
.....
.....


=======
LEGEND
=======

. = Background
# = Wall
P = Player
┼ = Crate
╨ = Crate and EdgeU
╥ = Crate and EdgeD
╡ = Crate and EdgeL
╞ = Crate and EdgeR
╚ = Crate and EdgeU and EdgeR
╗ = Crate and EdgeD and EdgeL
╝ = Crate and EdgeL and EdgeU
╔ = Crate and EdgeR and EdgeD
╫ = Crate and EdgeU and EdgeD
╪ = Crate and EdgeL and EdgeR
╦ = Crate and EdgeD and EdgeL and EdgeR
╩ = Crate and EdgeU and EdgeL and EdgeR
╠ = Crate and EdgeU and EdgeD and EdgeR
╣ = Crate and EdgeU and EdgeD and EdgeL
╬ = Crate and EdgeU and EdgeD and EdgeL and EdgeR

Blocker = Wall or Crate
Edge = EdgeU or EdgeD or EdgeL or EdgeR
Match = MatchU or MatchD or MatchL or MatchR or FullMatch

=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Player, Wall, Crate
EdgeU
EdgeD
EdgeL
EdgeR
MatchU
MatchD
MatchL
MatchR
FullMatch


======
RULES
======

[ > Player | Crate ] -> [ > Player | > Crate ]
[ > Crate | Blocker ] -> [ Crate | Blocker ]
[ > Crate Edge ] -> [ > Crate > Edge ]

late [ Match ] -> [ ]
late right [ EdgeR | EdgeL ] -> [ EdgeR MatchR | EdgeL MatchL ]
late down [ EdgeD | EdgeU ] -> [ EdgeD MatchD | EdgeU MatchU ]
late [ Crate ] -> [ Crate FullMatch ]
late [ Crate EdgeU FullMatch no MatchU ] -> [ Crate EdgeU ]
late [ Crate EdgeD FullMatch no MatchD ] -> [ Crate EdgeD ]
late [ Crate EdgeL FullMatch no MatchL ] -> [ Crate EdgeL ]
late [ Crate EdgeR FullMatch no MatchR ] -> [ Crate EdgeR ]


==============
WINCONDITIONS
==============

all EdgeU on MatchU
all EdgeD on MatchD
all EdgeL on MatchL
all EdgeR on MatchR


=======
LEVELS
=======

message Fix all the connections

message Level 1 of 5

#######
#...###
#╞...##
#..╔╡.#
#..╝.p#
#######


message Level 2 of 5

#######
#.....#
#..╔..#
#.╔╝╥.#
#.╝.╨.#
#..p..#
#######


message Level 3 of 5

##########
#....#...#
#.#..#.#.#
#.╥.╫....#
#..#╨#.#.#
#p...#...#
##########


message Level 4 of 5

#########
###...###
##.....##
#..╔╦╞..#
#..╠p╣..#
#..╡╩╝..#
##.....##
###...###
#########


message Level 5 of 5

#######
#..p..#
#.╗.╔.#
#.╬.╚.#
#.╝╔╝.#
#.....#
#######


message Good work!


