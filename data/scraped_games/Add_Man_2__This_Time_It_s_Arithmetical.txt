title Add Man 2: This Time It's Arithmetical
author Adam Gashlin
homepage gashlin.net

key_repeat_interval 0.5
noaction

========
OBJECTS
========

Background
Green

Zero
White
.....
.000.
.0.0.
.000.
.....

ZeroGoal
Blue DarkBlue
00000
01110
01010
01110
00000

One
White
.....
..0..
..0..
..0..
.....

OneGoal
Blue DarkBlue
00000
00100
00100
00100
00000

Player
LightBlue LightBrown Purple Pink
..3..
.111.
00000
12221
.2.2.

Wall
Grey

Two
Red
.....
0.000
0.0.0
0.000
.....

=======
LEGEND
=======

. = Background
+ = Player
0 = Zero
O = ZeroGoal
1 = One
! = OneGoal
# = Wall

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
ZeroGoal, OneGoal, Wall
Player, Two, Zero, One

======
RULES
======

[ > Player | Wall ] -> [ Player | Wall ]
[ > Player | Zero ] -> [ > Player | > Zero ]
[ > Player | One ] -> [ > Player | > One ]

[ > Zero | Zero ] -> [ | Zero ]
[ > Zero | One ] -> [ | One ]
[ > One | Zero ] -> [ | One ]
[ > One | One ] -> [ | Two ]

Right [ One | Two ] -> [ Two | Zero ]
Right [ No One | Two ] -> [ One | Zero ]

[ Two ] -> Cancel

==============
WINCONDITIONS
==============

All ZeroGoal on Zero
All OneGoal on One

=======
LEVELS
=======

MESSAGE Cover blue targets with white numbers

+#O
.#.
1#.
..0
!..

MESSAGE Numbers can go into walls
#####
#0.1#
#+#.#
###.#
0....
..0.O

MESSAGE 0 + 0 = 0

.#.....
+#.....
.#.....
.##0##.
0..0.#O
0....#O

MESSAGE 0 + 1 = 1

!#..#..
.#.010.
.#..#..
.#..#..
.#..#.#
0...#.#
O...#+#

MESSAGE 1 + 1 = 10

.!.#..+
....1#.
...#1#.
...#...
.O.....

...###...
.11...11.
!..#+#..!
...###..O
....#....

....+
...1.
#..1.
!##..

...###...
111...111
!..#+#..!
...###..O
.........

.......
..1001.
.+0011.
..!!OO.

......
+1.1..
....1.
###...
!##.1.
###...

+..
..1
.11
11!

...#+...
...####1
...11111
...#####
##11111!
111.####

##.......
#...1.1..
#1#####..
#1#......
..#####..
...!..#+.

#######
..!....
...##..
1111111
###..##
+......

........#
...+....#
..#####1#
11111111#
..####..#
..#!....#