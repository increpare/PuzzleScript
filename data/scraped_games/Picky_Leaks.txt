title Picky Leaks
author Xavier Direz
homepage www.lafaceobscuredupixel.fr
realtime_interval 0.6
noundo
========
OBJECTS
========

k
white
.000.
.0.0.
.0.0.
.000.
.....

1
white
..0..
.00..
..0..
.000.
.....
2
white
..00.
.000.
.0...
.000.
.....
3
white
.000.
..00.
...0.
.000.
.....
4
white
.0...
.0.0.
.000.
...0.
.....
5
white
.00..
.000.
...0.
.000.
.....
6
white
.000.
.00..
.0.0.
.000.
.....
7
white
.000.
...0.
..0..
.0...
.....
8
white
.000.
.000.
.0.0.
.000.
.....
9
white
.000.
.0.0.
..00.
.000.
.....
0
white
.000.
.0.0.
.0.0.
.000.
.....


N
WHITE
.00..
.0.0.
.0.0.
.0.0.
.....

E
WHITE
.000.
.00..
.0...
.000.
.....

X
WHITE
.0.0.
..0..
.0.0.
.0.0.
.....

T
WHITE
.000.
..0..
..0..
..0..
.....

Background 
GREEN  

growingflower0
green pink blue
00000
01000
00000
00020
00000

flower0
green pink yellow blue
01000
12100
01030
00323
00030

growingflower1
green blue pink
00000
00000
00000
00020
00000

flower1
green blue yellow pink
00000
00000
00030
00323
00030

growingflower2
green blue pink
00000
01000
00000
00000
00000

flower2
green blue yellow pink
01000
12100
01000
00000
00000

growingflower3
green pink blue
00000
00000
00000
00020
00000

flower3
green pink yellow blue
00000
00000
00030
00323
00030

growingflower4
green pink blue
00000
01000
00000
00000
00000

flower4
green pink yellow blue
01000
12100
01000
00000
00000

growingflower5
green blue pink
00000
01000
00000
00020
00000

flower5
green blue yellow pink
01000
12100
01030
00323
00030

Spawn s
GREEN RED darkbrown
00000
02000
00110
00110
00000

Current c
GREEN RED darkbrown
00000
01100
01100
00020
00000

Life €
#ff0000 #b90000 #ff5555 #ff9595  
.....
.0.2.
11032
.110.
..1..

TargetWest 
brown darkbrown
00000
01110
11110
01110
00000

TargetNorth 
brown darkbrown
00100
01110
01110
01110
00000

TargetEast
brown darkbrown
00000
01110
01111
01110
00000

TargetSouth 
brown darkbrown
00000
01110
01110
01110
00100

TargetFilledWest
brown blue
00000
01110
11110
01110
00000

TargetFilledNorth
brown blue
00100
01110
01110
01110
00000

TargetFilledEast
brown blue
00000
01110
01111
01110
00000

TargetFilledSouth
brown blue
00000
01110
01110
01110
00100

Wall 
#152b2b #408080 #336666 #abd6d6 
21113
02221
02221
02221
00002

desert0
#ff8000 #b95c00 #ffaa55 #ffca95 
22222
22222
22222
22222
22222

desert1 a
#ff8000 #e67300 #ffaa55 #ffca95 
22222
22232
22221
20222
22222

desert2 b
#ff8000 #e67300 #ffaa55 #ffca95 
22222
22022
22222
23222
22220

desert3 l
#ff8000 #e67300 #ffaa55 #ffca95 
20222
22222
12232
22212
22222

desert4 d
#ff8000 #e67300 #ffaa55 #ffca95 
22222
32122
22222
20221
22122

Player 
#ffae88 #ff0000 #ffc6aa #264a8c #1f3d74 #d20000
.551.
.002.
54331
04432
.4.4.

NorthSouthOFF i
brown darkbrown Blue
00100
00100
00100
00100
00100

NorthSouthON_1
brown darkbrown Blue
00200
00100
00100
00100
00100

NorthSouthON_2
brown darkbrown Blue
00200
00200
00100
00100
00100

NorthSouthON_3
brown darkbrown Blue
00200
00200
00200
00100
00100

NorthSouthON_4
brown darkbrown Blue
00200
00200
00200
00200
00100

NorthSouthON_5
brown darkbrown Blue
00200
00200
00200
00200
00200

SouthNorthON_1
brown darkbrown Blue
00100
00100
00100
00100
00200

SouthNorthON_2
brown darkbrown Blue
00100
00100
00100
00200
00200

SouthNorthON_3
brown darkbrown Blue
00100
00100
00200
00200
00200

SouthNorthON_4
brown darkbrown Blue
00100
00200
00200
00200
00200

WestEastOFF _
brown darkbrown Blue
00000
00000
11111
00000
00000

WestEastON_1
brown darkbrown Blue
00000
00000
21111
00000
00000

WestEastON_2
brown darkbrown Blue
00000
00000
22111
00000
00000

WestEastON_3
brown darkbrown Blue
00000
00000
22211
00000
00000

WestEastON_4
brown darkbrown Blue
00000
00000
22221
00000
00000

WestEastON_5
brown darkbrown Blue
00000
00000
22222
00000
00000

EastWestON_1
brown darkbrown Blue
00000
00000
11112
00000
00000

EastWestON_2
brown darkbrown Blue
00000
00000
11122
00000
00000

EastWestON_3
brown darkbrown Blue
00000
00000
11222
00000
00000

EastWestON_4
brown darkbrown Blue
00000
00000
12222
00000
00000

NorthEastOFF u
brown darkbrown Blue
00100
00100
00111
00000
00000

NorthEastON_1
brown darkbrown Blue
00200
00100
00111
00000
00000

NorthEastON_2
brown darkbrown Blue
00200
00200
00111
00000
00000

NorthEastON_3
brown darkbrown Blue
00200
00200
00211
00000
00000

NorthEastON_4
brown darkbrown Blue
00200
00200
00221
00000
00000

NorthEastON_5
brown darkbrown Blue
00200
00200
00222
00000
00000

EastNorthON_1
brown darkbrown Blue
00100
00100
00112
00000
00000

EastNorthON_2
brown darkbrown Blue
00100
00100
00122
00000
00000

EastNorthON_3
brown darkbrown Blue
00100
00100
00222
00000
00000

EastNorthON_4
brown darkbrown Blue
00100
00200
00222
00000
00000

SouthEastOFF z
brown darkbrown Blue
00000
00000
00111
00100
00100

SouthEastON_1
brown darkbrown Blue
00000
00000
00111
00100
00200

SouthEastON_2
brown darkbrown Blue
00000
00000
00111
00200
00200

SouthEastON_3
brown darkbrown Blue
00000
00000
00211
00200
00200

SouthEastON_4
brown darkbrown Blue
00000
00000
00221
00200
00200

SouthEastON_5
brown darkbrown Blue
00000
00000
00222
00200
00200

EastSouthON_1
brown darkbrown Blue
00000
00000
00112
00100
00100

EastSouthON_2
brown darkbrown Blue
00000
00000
00122
00100
00100

EastSouthON_3
brown darkbrown Blue
00000
00000
00222
00100
00100

EastSouthON_4
brown darkbrown Blue
00000
00000
00222
00200
00100





NorthWestOFF y
brown darkbrown Blue
00100
00100
11100
00000
00000

NorthWestON_1
brown darkbrown Blue
00200
00100
11100
00000
00000

NorthWestON_2
brown darkbrown Blue
00200
00200
11100
00000
00000

NorthWestON_3
brown darkbrown Blue
00200
00200
11200
00000
00000

NorthWestON_4
brown darkbrown Blue
00200
00200
12200
00000
00000

NorthWestON_5
brown darkbrown Blue
00200
00200
22200
00000
00000

WestNorthON_1
brown darkbrown Blue
00100
00100
21100
00000
00000

WestNorthON_2
brown darkbrown Blue
00100
00100
22100
00000
00000

WestNorthON_3
brown darkbrown Blue
00100
00100
22200
00000
00000

WestNorthON_4
brown darkbrown Blue
00100
00200
22200
00000
00000

SouthWestOFF w
brown darkbrown Blue
00000
00000
11100
00100
00100

SouthWestON_1
brown darkbrown Blue
00000
00000
11100
00100
00200

SouthWestON_2
brown darkbrown Blue
00000
00000
11100
00200
00200

SouthWestON_3
brown darkbrown Blue
00000
00000
11200
00200
00200

SouthWestON_4
brown darkbrown Blue
00000
00000
12200
00200
00200

SouthWestON_5
brown darkbrown Blue
00000
00000
22200
00200
00200

WestSouthON_1
brown darkbrown Blue
00000
00000
21100
00100
00100

WestSouthON_2
brown darkbrown Blue
00000
00000
22100
00100
00100

WestSouthON_3
brown darkbrown Blue
00000
00000
22200
00100
00100

WestSouthON_4
brown darkbrown Blue
00000
00000
22200
00200
00100

Start_1
brown darkbrown Blue
00000
00000
02111
00000
00000

Start_2
brown darkbrown Blue
00000
00000
02211
00000
00000

Start_3
brown darkbrown Blue
00000
00000
02221
00000
00000

Start_4
brown darkbrown Blue
00000
00000
02222
00000
00000

puddle
green Blue lightblue
00100
01110
11111
01110
00100

Bag
red

=======
LEGEND
=======

. = Background
# = Wall
% = desert0
P = Player
+ = Start_1
O = TargetWest
G = TargetNorth
H = TargetEast
F = TargetSouth
& = Bag and Wall

Target = TargetWest or TargetNorth or TargetEast or TargetSouth
TargetFilled = TargetFilledWest or TargetFilledNorth or TargetFilledEast or TargetFilledSouth

Piece = NorthSouthOFF or NorthSouthON_1 or NorthSouthON_2 or NorthSouthON_3 or NorthSouthON_4 or NorthSouthON_5 or SouthNorthON_1 or SouthNorthON_2 or SouthNorthON_3 or SouthNorthON_4 or NorthEastOFF or NorthEastON_1 or NorthEastON_2 or NorthEastON_3 or NorthEastON_4 or NorthEastON_5 or EastNorthON_1 or EastNorthON_2 or EastNorthON_3 or EastNorthON_4 or SouthEastOFF or SouthEastON_1 or SouthEastON_2 or SouthEastON_3 or SouthEastON_4 or SouthEastON_5 or EastSouthON_1 or EastSouthON_2 or EastSouthON_3 or EastSouthON_4 or NorthWestOFF or NorthWestON_1 or NorthWestON_2 or NorthWestON_3 or NorthWestON_4 or NorthWestON_5 or WestNorthON_1 or WestNorthON_2 or WestNorthON_3 or WestNorthON_4 or SouthWestOFF or SouthWestON_1 or SouthWestON_2 or SouthWestON_3 or SouthWestON_4 or SouthWestON_5 or WestSouthON_1 or WestSouthON_2 or WestSouthON_3 or WestSouthON_4 or Start_1 or Start_2 or Start_3 or Start_4 or Target or TargetFilled or WestEastOFF or WestEastON_1 or WestEastON_2 or WestEastON_3 or WestEastON_4 or WestEastON_5 or EastWestON_1 or EastWestON_2 or EastWestON_3 or EastWestON_4

SouthSource = NorthSouthON_5 or NorthEastON_5 or NorthWestON_5
NorthSource = NorthSouthON_5 or SouthEastON_5 or SouthWestON_5
WestSource = NorthEastON_5 or SouthEastON_5 or WestEastON_5 or Start_4
EastSource = NorthWestON_5 or SouthWestON_5 or WestEastON_5

Start = Start_1 or Start_2 or Start_3 or Start_4
SpawnPiece = NorthSouthOFF or NorthEastOFF or SouthEastOFF or NorthWestOFF or SouthWestOFF or WestEastOFF

EmptyPiece = NorthSouthOFF or NorthEastOFF or SouthEastOFF or NorthWestOFF or SouthWestOFF or WestEastOFF

Next = N or E or X or T

digits = 0 or 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or k

flower = flower0 or flower1 or flower2 or flower3 or flower4 or flower5
growingflower = growingflower0 or growingflower1 or growingflower2 or growingflower3 or growingflower4 or growingflower5

desert = desert1 or desert2 or desert3 or desert4

=======
SOUNDS
=======

TargetFilled Create 35120703
puddle Create 91785501
Endlevel 54070902
player cantmove 16703104
SFX0 20939902
SFX1 91195704

================
COLLISIONLAYERS
================

Background
Bag
flower, growingflower
Spawn, Current
Piece, puddle
Player, Wall, desert, desert0
Next, life, digits


======
RULES     
======     

( init )
[ stationary Player ][desert0] -> [player][random desert]

( The end )
[ stationary Player ][growingflower0] -> [player][flower0]
[ stationary Player ][growingflower1] -> [player][flower1]
[ stationary Player ][growingflower2] -> [player][flower2]
[ stationary Player ][growingflower3] -> [player][flower3]
[ stationary Player ][growingflower4] -> [player][flower4]
[ stationary Player ][growingflower5] -> [player][flower5]
[ stationary Player ][flower|desert] -> [player][flower|random growingflower]
[ stationary Player ][targetfilled|desert] -> [player][targetfilled|random growingflower]

( Fill Target )
RIGHT [ stationary Player ][WestSource | TargetWest] -> [ Player ][WestSource | TargetFilledWest]
DOWN [ stationary Player ][NorthSource | TargetNorth] -> [ Player ][NorthSource | TargetFilledNorth]
LEFT [ stationary Player ][EastSource | TargetEast] -> [ Player ][EastSource | TargetFilledEast]
UP [ stationary Player ][SouthSource | TargetSouth] -> [ Player ][SouthSource | TargetFilledSouth]

( North to South )
DOWN [ stationary Player ][NorthSouthON_5 | no Piece ] -> [ Player ][NorthSouthON_5 | puddle]

[ stationary Player ][NorthSouthON_4] -> [ Player ][NorthSouthON_5]
[ stationary Player ][NorthSouthON_3] -> [ Player ][NorthSouthON_4]
[ stationary Player ][NorthSouthON_2] -> [ Player ][NorthSouthON_3]
[ stationary Player ][NorthSouthON_1] -> [ Player ][NorthSouthON_2]

DOWN [ stationary Player ][NorthSource | NorthSouthOFF] -> [ Player ][ NorthSource | NorthSouthON_1]

( South to North )
UP [ stationary Player ][NorthSouthON_5 | no Piece] -> [ Player ][NorthSouthON_5 | puddle]

[ stationary Player ][SouthNorthON_4] -> [ Player ][NorthSouthON_5]
[ stationary Player ][SouthNorthON_3] -> [ Player ][SouthNorthON_4]
[ stationary Player ][SouthNorthON_2] -> [ Player ][SouthNorthON_3]
[ stationary Player ][SouthNorthON_1] -> [ Player ][SouthNorthON_2]

UP [ stationary Player ][SouthSource | NorthSouthOFF] -> [ Player ][ SouthSource | SouthNorthON_1]

( West to East )
RIGHT [ stationary Player ][WestEastON_5 | no Piece] -> [ Player ][WestEastON_5 | puddle]

[ stationary Player ][WestEastON_4] -> [ Player ][WestEastON_5]
[ stationary Player ][WestEastON_3] -> [ Player ][WestEastON_4]
[ stationary Player ][WestEastON_2] -> [ Player ][WestEastON_3]
[ stationary Player ][WestEastON_1] -> [ Player ][WestEastON_2]

RIGHT [ stationary Player ][WestSource | WestEastOFF] -> [ Player ][ WestSource | WestEastON_1]

( East to West )
LEFT [ stationary Player ][WestEastON_5 | no Piece] -> [ Player ][WestEastON_5 | puddle]

[ stationary Player ][EastWestON_4] -> [ Player ][WestEastON_5]
[ stationary Player ][EastWestON_3] -> [ Player ][EastWestON_4]
[ stationary Player ][EastWestON_2] -> [ Player ][EastWestON_3]
[ stationary Player ][EastWestON_1] -> [ Player ][EastWestON_2]

LEFT [ stationary Player ][EastSource | WestEastOFF] -> [ Player ][ EastSource | EastWestON_1]

( East to North )
UP [ stationary Player ][NorthEastON_5 | no Piece] -> [ Player ][NorthEastON_5 | puddle]

[ stationary Player ][EastNorthON_4] -> [ Player ][NorthEastON_5]
[ stationary Player ][EastNorthON_3] -> [ Player ][EastNorthON_4]
[ stationary Player ][EastNorthON_2] -> [ Player ][EastNorthON_3]
[ stationary Player ][EastNorthON_1] -> [ Player ][EastNorthON_2]

LEFT [ stationary Player ][EastSource | NorthEastOFF] -> [ Player ][ EastSource |EastNorthON_1]

( North to East )
RIGHT [ stationary Player ][NorthEastON_5 | no Piece] -> [ Player ][NorthEastON_5 | puddle]

[ stationary Player ][NorthEastON_4] -> [ Player ][NorthEastON_5]
[ stationary Player ][NorthEastON_3] -> [ Player ][NorthEastON_4]
[ stationary Player ][NorthEastON_2] -> [ Player ][NorthEastON_3]
[ stationary Player ][NorthEastON_1] -> [ Player ][NorthEastON_2]

DOWN [ stationary Player ][NorthSource | NorthEastOFF] -> [ Player ][ NorthSource |NorthEastON_1]

( West to South )
DOWN [ stationary Player ][SouthWestON_5 | no Piece] -> [ Player ][SouthWestON_5 | puddle]

[ stationary Player ][WestSouthON_4] -> [ Player ][SouthWestON_5]
[ stationary Player ][WestSouthON_3] -> [ Player ][WestSouthON_4]
[ stationary Player ][WestSouthON_2] -> [ Player ][WestSouthON_3]
[ stationary Player ][WestSouthON_1] -> [ Player ][WestSouthON_2]

RIGHT [ stationary Player ][WestSource | SouthWestOFF] -> [ Player ][ WestSource |WestSouthON_1]

( South to West )
LEFT [ stationary Player ][SouthWestON_5 | no Piece] -> [ Player ][SouthWestON_5 | puddle]

[ stationary Player ][SouthWestON_4] -> [ Player ][SouthWestON_5]
[ stationary Player ][SouthWestON_3] -> [ Player ][SouthWestON_4]
[ stationary Player ][SouthWestON_2] -> [ Player ][SouthWestON_3]
[ stationary Player ][SouthWestON_1] -> [ Player ][SouthWestON_2]

UP [ stationary Player ][SouthSource | SouthWestOFF] -> [ Player ][ SouthSource |SouthWestON_1]

( West to North )
UP [ stationary Player ][NorthWestON_5 | no Piece] -> [ Player ][NorthWestON_5 | puddle]

[ stationary Player ][WestNorthON_4] -> [ Player ][NorthWestON_5]
[ stationary Player ][WestNorthON_3] -> [ Player ][WestNorthON_4]
[ stationary Player ][WestNorthON_2] -> [ Player ][WestNorthON_3]
[ stationary Player ][WestNorthON_1] -> [ Player ][WestNorthON_2]

RIGHT [ stationary Player ][WestSource | NorthWestOFF] -> [ Player ][ WestSource |WestNorthON_1]

( North to West )
LEFT [ stationary Player ][NorthWestON_5 | no Piece] -> [ Player ][NorthWestON_5 | puddle]

[ stationary Player ][NorthWestON_4] -> [ Player ][NorthWestON_5]
[ stationary Player ][NorthWestON_3] -> [ Player ][NorthWestON_4]
[ stationary Player ][NorthWestON_2] -> [ Player ][NorthWestON_3]
[ stationary Player ][NorthWestON_1] -> [ Player ][NorthWestON_2]

DOWN [ stationary Player ][NorthSource | NorthWestOFF] -> [ Player ][ NorthSource |NorthWestON_1]

( East to South )
DOWN [ stationary Player ][SouthEastON_5 | no Piece] -> [ Player ][SouthEastON_5 | puddle]

[ stationary Player ][EastSouthON_4] -> [ Player ][SouthEastON_5]
[ stationary Player ][EastSouthON_3] -> [ Player ][EastSouthON_4]
[ stationary Player ][EastSouthON_2] -> [ Player ][EastSouthON_3]
[ stationary Player ][EastSouthON_1] -> [ Player ][EastSouthON_2]

LEFT [ stationary Player ][EastSource | SouthEastOFF] -> [ Player ][ EastSource |EastSouthON_1]

( South to East )
RIGHT [ stationary Player ][SouthEastON_5 | no Piece] -> [ Player ][SouthEastON_5 | puddle]

[ stationary Player ][SouthEastON_4] -> [ Player ][SouthEastON_5]
[ stationary Player ][SouthEastON_3] -> [ Player ][SouthEastON_4]
[ stationary Player ][SouthEastON_2] -> [ Player ][SouthEastON_3]
[ stationary Player ][SouthEastON_1] -> [ Player ][SouthEastON_2]

UP [ stationary Player ][SouthSource | SouthEastOFF] -> [ Player ][ SouthSource |SouthEastON_1]


( Start rules )

RIGHT [ stationary Player ][Start_4 | no Piece ]-> [ Player ][Start_4 | puddle]

( Player rules )
[ stationary Player ][Start_3] -> [ Player ][Start_4]
[ stationary Player ][Start_2] -> [ Player ][Start_3]
[ stationary Player ][Start_1] -> [ Player ][Start_2]

( init Bag )
right [stationary Player][Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece|Bag no SpawnPiece] -> [Player][Bag NorthSouthOFF|Bag NorthSouthOFF|Bag NorthSouthOFF|Bag NorthEastOFF|Bag NorthEastOFF|Bag NorthEastOFF|Bag SouthEastOFF|Bag SouthEastOFF|Bag SouthEastOFF|Bag NorthWestOFF|Bag NorthWestOFF|Bag NorthWestOFF|Bag SouthWestOFF|Bag SouthWestOFF|Bag SouthWestOFF|Bag WestEastOFF|Bag WestEastOFF|Bag WestEastOFF]

( next Piece )

random [Spawn no SpawnPiece][Bag SpawnPiece] -> [Spawn SpawnPiece][Bag]
[ Current no SpawnPiece ][Spawn SpawnPiece] -> [ Current SpawnPiece] [Spawn ]

( add piece )

[ Action Player no piece no puddle] [Current SpawnPiece]-> [ Player  SpawnPiece][Current] SFX1

( replace piece )

RIGHT [ Action Player EmptyPiece no puddle no start no target no targetFilled] [Current SpawnPiece][life]-> [ Player  SpawnPiece][Current][] SFX0

RIGHT[ no Wall no life |life]->[Life|]


( hundreds )


RIGHT [ stationary Player ][8 | 9|9| Wall] -> [Player][9|k |k | Wall]
RIGHT [ stationary Player ][7 | 9|9| Wall] -> [Player][8|k |k | Wall]
RIGHT [ stationary Player ][6 | 9|9| Wall] -> [Player][7|k |k | Wall]
RIGHT [ stationary Player ][5 | 9|9| Wall] -> [Player][6|k |k | Wall]
RIGHT [ stationary Player ][4 | 9|9| Wall] -> [Player][5|k |k | Wall]
RIGHT [ stationary Player ][3 | 9|9| Wall] -> [Player][4|k |k | Wall]
RIGHT [ stationary Player ][2 | 9|9| Wall] -> [Player][3|k |k | Wall]
RIGHT [ stationary Player ][1 | 9|9| Wall] -> [Player][2|k |k | Wall]
RIGHT [ stationary Player ][0 | 9|9| Wall] -> [Player][1|k |k | Wall]

( tens )

RIGHT [ stationary Player ][8 |9| Wall] -> [Player][9 |k | Wall]
RIGHT [ stationary Player ][7 |9| Wall] -> [Player][8 |k | Wall]
RIGHT [ stationary Player ][6 |9| Wall] -> [Player][7 |k | Wall]
RIGHT [ stationary Player ][5 |9| Wall] -> [Player][6 |k | Wall]
RIGHT [ stationary Player ][4 |9| Wall] -> [Player][5 |k | Wall]
RIGHT [ stationary Player ][3 |9| Wall] -> [Player][4 |k | Wall]
RIGHT [ stationary Player ][2 |9| Wall] -> [Player][3 |k | Wall]
RIGHT [ stationary Player ][1 |9| Wall] -> [Player][2 |k | Wall]
RIGHT [ stationary Player ][k |9| Wall] -> [Player][0 |9 | Wall]
RIGHT [ stationary Player ][0 |9| Wall] -> [Player][1 |k | Wall]

( units )

RIGHT [ stationary Player ][8 | Wall] -> [Player][9 | Wall]
RIGHT [ stationary Player ][7 | Wall] -> [Player][8 | Wall]
RIGHT [ stationary Player ][6 | Wall] -> [Player][7 | Wall]
RIGHT [ stationary Player ][5 | Wall] -> [Player][6 | Wall]
RIGHT [ stationary Player ][4 | Wall] -> [Player][5 | Wall]
RIGHT [ stationary Player ][3 | Wall] -> [Player][4 | Wall]
RIGHT [ stationary Player ][2 | Wall] -> [Player][3 | Wall]
RIGHT [ stationary Player ][1 | Wall] -> [Player][2 | Wall]
RIGHT [ stationary Player ][0 | Wall] -> [Player][1 | Wall]
RIGHT [ stationary Player ][k | Wall] -> [Player][0 | Wall]


==============
WINCONDITIONS
==============

no desert

=======     
LEVELS
=======
Message Put the right thing in the right place
&&&&&&&&&&&&&&&&&&
#+___i_od####next#
#.......b####.c..#
#...p...b####..s.#
#.......d#########
##################
###000######€€€€€#
##################

Message Avoid any leak 
&&&&&&&&&&&&&&&&&&
#+___._ob####next#
#......ab####.c..#
#...p..bd####..s.#
#......dd#########
##################
###000#######€€€€#
##################

Message Choose your path
&&&&&&&&&&&&&&&&&&
#+__...lb####next#
#....w.bl####.c..#
#.p.u._ob####..s.#
#......ba#########
##################
###000#######€€€€#
##################

Message Zero Heart, zero Tolerance
&&&&&&&&&&&&&&&&&&
#.zwi._od####next#
#.i._..ab####.c..#
#.i.p..ab####..s.#
#+y....dl#########
##################
###000#######....#
##################

Message Your own way
&&&&&&&&&&&&&&&&&&
#.....zol####next#
#.....yab####.c..#
#...p.bbl####..s.#
#+__.labb#########
##################
###000#######€€€€#
##################

Message Locked
&&&&&&&&&&&&&&&&&&&
#+_w...l.o####next#
#....p.lil####.c..#
#..g...bib####..s.#
#blblbblia#########
#+______yb#########
###################
###000########€€€€#
###################

&&&&&&&&&&&&&&&&&&&
#+__w...lb####next#
#.......bb####.c..#
#...p__oaa####..s.#
#.......ld#########
###################
###000########....#
###################

&&&&&&&&&&&&&&&&&&
#+__w..fa####next#
#...i..zb####.c..#
#...p..ia####..s.#
#....__.l#########
#aaablabd#########
##################
###000######€€€€€#
##################

&&&&&&&&&&&&&&&&&&
#+_____w#####next#
#...___y#####.c..#
#...p...#####..s.#
#.g.....##########
#adbdbbdblbdl#####
##################
###000#######€€€€#
##################

&&&&&&&&&&&&&&&&&&
#+_____w#####next#
#...___y#####.c..#
#...p...#####..s.#
#......o##########
#adbblbablbbb#####
#dbdbblabbblb#####
##################
###000#######€€€€#
##################

Message Snail
&&&&&&&&&&&&&&&&&&
#.z______..a#next#
#.iz____w..b#.c..#
#.iiz__wi..b#..s.#
#.iii+wiip.o######
#.iiu_yii..abbddb#
#.iu___yi..dblldd#
#.u_____y..bdbblb#
##################
###000######€€€€€#
##################

Message Spiral
&&&&&&&&&&&&&&&&&&
#..........l#next#
#..z____w..d#.c..#
#..iz__wi..o#..s.#
#..ii+_yip.#######
#..ii+__y..lbabld#
#..iu___._.lllabd#
#..u_____._ollbdd#
##################
###000######€€€€€#
##################

Message Looping
&&&&&&&&&&&&&&&&&&
#+_w......###next#
#+wi......#b#.c..#
#.iu__w##.ol#..s.#
#.i...ip..#d######
#.i...i##.########
#.u___y...oblbd###
##################
###000###€€€€€€€€#
##################

Message Take Your Time
&&&&&&&&&&&&&&&&&&
#+__i....#ld#next#
#+__i.p..obd#.c..#
#+__i....#db#..s.#
#...uw...#########
#..##g#..__oblblb#
#..#bl#....#lblbl#
#..#dl#....#lbblb#
##################
###000#€€€€€€€€€€#
##################

Message The End
&&&&&&&&&&&&&&&&&&
#+_opi.ibz_abbbbb#
#dibbi_iai_bbbbbb#
#bilaiaiau_bbdbbb#
#blallblllbbbbbbb#
#blbz_bzwibi_wblb#
#babi_liiibi.ibbb#
#bbbu_biuyli_ybbb#
#bbbbbbbbbbbbbbbb#
#bbbbbbbbbbbbbbbb#
##################

Message Well Done !!!
Message Thank you for playing