Title Runner 2
realtime_interval 0.01
key_repeat_interval 0.01
run_rules_on_level_start
flickscreen 20x14
color_palette 4


========
OBJECTS
========
Spot X
transparent yellow
.....
.0.0.
..0..
.0.0.
.....
Player P
transparent
Tick T
transparent
.....
.....
..0..
.....
.....

GoD
transparent
.....
.....
.....
.....
..0..

GoL
transparent
.....
.....
0....
.....
.....
GoR
transparent green
.....
.....
....0
.....
.....

GoU
transparent green
..0..
.....
.....
.....
.....
Act
transparent green
.....
..0..
.0.0.
..0..
.....

Wait32 W32
transparent
Wait16 W16
transparent
Wait8 W8
transparent
Wait4 W4
transparent
Wait2 W2
transparent
Wait1 W1
transparent
CD
transparent

Every32 E32
transparent
Every16 E16
transparent
Every8 E8
transparent
Every4 E4
transparent
Every2 E2
transparent

FallHeight FH
transparent red

Run1
green
Run2
lightgreen

Temp1
transparent
Temp2
transparent
Temp3
transparent


Background .
black
Wall #
darkgreen
Hidden ,
transparent
WallTxt1 WTxt1
darkgreen black
00000
00010
00000
01000
00000

WallTxt2 WTxt2
darkgreen black
00100
01001
10010
00100
01001

WallTxt3 WTxt3
darkgreen black
10110
01101
11010
10111
01101

WallTxt4 WTxt4
darkgreen black
11110
01111
11011
11110
01111
(
WallTxt6 WTxt6
darkblue black

.....
...0.
.....
.0...
.....
)
WallLeft WL
darkgreen
.....
.....
.....
0....
0....
WallRight WR
darkgreen
.....
.....
.....
....0
....0


WallCeiling
black
.....
.....
.....
0.0.0
00000

Abyss +
transparent
SafeZone SZ
transparent
Ground Gr
darkblue
00000
.....
.....
.....
.....
Platform PF "
darkbrown
00000
.....
.....
.....
.....
PlatformLoose PFL
darkbrown
00000
.....
.....
.....
.....
PlatformFalling PFF
darkbrown
.....
.00..
...00
.....
.....
PlatformScrap PFS
darkbrown
.....
.....
.....
.....
.00.0
SupportR SupR
darkbrown
.....
...0.
....0
.....
.....
SupportL SupL
darkbrown
.....
.0...
0....
.....
.....
SupportM SupM
darkbrown
.....
.0.0.
..0..
..0..
..0..
Pillar Pil
darkbrown
..0..
..0..
..0..
..0..
..0..


Ladder1 -
darkgrey
0...0
00000
0...0
00000
0...0
Ladder2
darkgrey
00000
0...0
00000
0...0
00000
LadderTop
darkgrey
.....
.....
.....
0...0
00000
FaceRight FR
transparent
FaceLeft FL
transparent
HeroR1
orange
...0.
..00.
..00.
...0.
...0.
HeroRLean
orange
....0
...00
...00
....0
...0.
HeroREdge
orange
....0
...00
...00
....0
...0.
HeroREdge2
orange
.....
.....
0....
.....
0....
HeroR2
orange
..0..
.00..
000..
.0.0.
0..0.
HeroRShoot HeroRS
orange
...0.
..00.
..000
...0.
...0.
HeroTurning HeroT
orange
..0..
.000.
.000.
..0..
..0..
HeroRJ1
orange
.....
.....
.....
....0
...00
HeroRJ1Feet
orange
..000
...00
..0..
.....
.....
HeroRJ2
orange
.....
.....
...0.
.000.
..000
HeroRJ2Feet
orange
.0..0
.....
.....
.....
.....
HeroRFall HeroRF
orange
...0.
.000.
..000
.0.0.
....0
(
0.0.0
.000.
..00.
00.0.
...0.
)

HeroRK
orange
.....
...0.
..00.
..000
.00.0

HeroRH 
orange
....0
....0
...00
...00
....0
HeroRHFeet 
orange
...0.
.....
.....
.....
.....
HeroRHS 
orange
..0.0
.000.
.00..
..00.
.0..0

HeroC1
orange
0.0..
.0000
..00.
.0.0.
.0...
HeroC2U
orange
.....
.....
..0.0
0000.
.00..
HeroC2D
orange
.....
.....
..0.0
0000.
.00..
HeroCFeet
orange
.0.0.
...0.
.....
.....
.....
HeroL1
orange
.0...
.00..
.00..
.0...
.0...
HeroLLean HeroLL
orange
0....
00...
00...
0....
.0...
HeroLEdge
orange
0....
00...
00...
0....
.0...
HeroLEdge2
orange
.....
.....
....0
.....
....0

HeroL2
orange
..0..
..00.
..000
.0.0.
.0..0
HeroLStand HeroLS
orange
.0...
.00..
000..
.0...
.0...
HeroLJ1
orange
.....
.....
.....
0....
00...
HeroLJ1Feet
orange
000..
00...
..0..
.....
.....
HeroLJ2
orange
.....
.....
.0...
.000.
000..
HeroLJ2Feet
orange
0..0.
.....
.....
.....
.....
HeroLFall HeroLF
orange
.0...
.000.
000..
.0.0.
0....
HeroLK
orange
.....
.0...
.00..
000..
0.00.
HeroLH 
orange
0....
0....
00...
00...
0....
HeroLHFeet 
orange
.0...
.....
.....
.....
.....

HeroLHS 
orange
0.0..
.000.
..00.
.00..
0..0.
HeroDead 
orange red
.....
.....
.....
.01..
01010
HeroDeadFalling
orange red
.....
.....
.0..0
0101.
.01..



Trunk1 1
darkgreen
000..
000..
000..
000..
000..

Trunk2 i
darkbrown
.000.
.000.
.000.
.000.
.000.

Trunk3 !
darkblue
..00.
..00.
..00.
..00.
..00.



Moon 0
lightgrey grey darkgrey darkblue
31113
11122
12112
01121
30013

Leaves z
darkred

Leafy1
darkred
00..0
.....
.....
.....
.....
Leafy2
darkred
00000
.0.00
.....
.....
.....
Leafy3
darkred
0000.
..0..
.....
.....
.....
LeavesL
darkred
00000
00000
0000.
000..
0....
LeavesR
darkred
00000
00000
00000
.0000
...00



WaterfallTop1 WT1 =
white lightblue
.....
00...
00000
00101
01101
WaterfallTop2 WT2
white lightblue
.....
...00
00000
11000
11011
WaterfallTop3 WT3
white lightblue
.....
.0.0.
00000
10001
10101
WaterRunning1 WR1 %
white lightblue
10110
10110
10111
11101
11101
WaterRunning2 WR2
white lightblue
11011
01111
11010
01011
01110
WaterRunning3 WR3
white lightblue
11101
10101
10111
10111
11101

Splash1
white lightblue
.....
.....
.0.0.
0000.
00000
Splash2
white lightblue
.....
.....
..0..
.00.0
00000
Splash3
white lightblue
.....
.....
.....
..00.
00000

FrogL F
green
.....
.....
.....
.0...
.00..

Pedestal g
darkgrey
.000.
..0..
.000.
.000.
0...0
PedestalLit
darkgrey
.000.
..0..
.000.
.000.
0...0
Flame1
yellow
.....
.....
.....
..0..
.000.
Flame2
yellow
.....
.....
.....
...0.
..00.
Flame3
yellow
.....
.....
.....
.....
..0..


Door d
darkgreen darkblue black
21112
21112
21112
21112
21112
DoorLeft
darkblue
.....
....0
....0
....0
....0
DoorRight
darkblue
.....
0....
0....
0....
0....
DoorTop
darkblue purple
..1..
.....
.....
00000
.....

Doorway y
darkbrown black darkgray darkblue darkgreen
.....
.....
...33
..444
.2222

=======
LEGEND
=======

Temp = Temp1 or Temp2 or Temp3

HeroWalking  = HeroR1 or HeroR2 or HeroRLean or HeroL1 or HeroL2 or HeroLLean or HeroREdge or HeroLEdge
HeroClimbing = HeroC1 or HeroC2U or HeroC1 or HeroC2D
HeroJumping  = HeroRJ1 or HeroRJ2 or HeroLJ1 or HeroLJ2
HeroFalling  = HeroRF or HeroLF
HeroKneeling = HeroRK or HeroLK
HeroStanding = HeroRS or HeroLS
HeroHanging  = HeroRH or HeroRHS or HeroLH or HeroLHS
Feet = HeroCFeet or HeroRJ1Feet or HeroRJ2Feet or HeroLJ1Feet or HeroLJ2Feet or HeroRHFeet or HeroLHFeet or HeroREdge2 or HeroLEdge2

HeroOnGrond = HeroStanding or HeroKneeling or HeroWalking

HeroC2 = HeroC2U or HeroC2D

Hero = HeroWalking or HeroJumping or HeroClimbing or HeroFalling or HeroKneeling or HeroStanding or HeroTurning or HeroHanging

HeroR = HeroR1 or HeroR2 or HeroRS or HeroREdge or HeroRK
HeroL = HeroL1 or HeroL2 or HeroLS or HeroLEdge or HeroLK

Go = GoU or GoD or GoL or GoR
GoV = GoU or GoD
GoH = GoL or GoR
GoUA = GoU or Act

Run = Run1 or Run2

E = Player and HeroR1
S = Spot and Tick
R = Wall and Spot and Tick


W = W1 or W2 or W4 or W8 or W16 or W32
SmallW = W1 or W2 or W4
MediumW =  W1 or W2 or W4 or W8

O = Wall

WallTxt = WTxt2 or WTxt3 or WTxt4

Cover = Wall or Hidden

Ladder = Ladder1 or Ladder2
Support = SupportL or SupportR or SupportM
' = Platform and PlatformLoose
¨ = PLatform and SupportM

Trunk = Trunk1 or Trunk2 or Trunk3
Treetops = Leaves or LeavesR or LeavesL or Leafy1 or Leafy2 or Leafy3 
Leafy = Leafy1 or Leafy2 or Leafy3 

WaterfallTop  = WT1 or WT2 or WT3
WaterRunning = WR1 or WR2 or WR3
Waterfall = WaterfallTop or WaterRunning
Splash = Splash1 or Splash2 or Splash3

Flame = Flame1 or Flame2 or Flame3

EveryX = E32 or E16 or E8 or E4 or E2

=======
SOUNDS
=======

HeroR1 create 89288107 (34320507) (21479507) 
HeroL1 create 89288107 (34320507) (21479507) 

HeroRJ1 create 82207507
HeroLJ1 create 82207507

(
WT1 create 38751308
4042108)


sfx0 5471508 (56002707) (46097907) (landing)
sfx1 5471508 (soft landing)

sfx2 70959507 (ladders)

sfx3 35367108 (fall too high)

sfx4 4893504 ( walking on a platform )

sfx5 51743308 (waterfall)



HeroLH create 9189904
HeroRH create 9189904

endlevel 75296308
restart 88486508


(50336308) (Flö-höu-laah)
(5046308 (whiup))
(50767508 (käkäkäkä))
(8799308) (pieni laukaus)
(49325908) ( valtava haukotus)
(71973108) (Kitarafanfaari)
(9298708) (jänteen näppäys)
( 3232508 tosi hiljainen ja matala ääni )



Abyss destroy  77494708
PlatformScrap create 52284308 
PlatForm destroy 45772107

HeroDead create 68550708

PedestalLit create 24252108 (35507308)


================
COLLISIONLAYERS
================

Background
Player
Go Act
FR FL
CD
W1
W2
W4
W8
W16
W32
EveryX
Temp1
Temp2
Temp3
Hidden
Abyss
DoorLeft DoorRight DoorTop
Moon Waterfall
PlatformScrap
Splash
WL
WR
Ladder LadderTop
Trunk3
Pedestal PedestalLit
Flame
FrogL
Trunk2
Doorway
Door
PlatformFalling
Feet
Wall Hero HeroDead HeroDeadFalling
Ground
WallTxt WTxt1
WallCeiling
Trunk1
Leaves LeavesL
LeavesR
Leafy
Pillar
Support
PlatformLoose
Platform
Run1
Run2
Spot
Tick
SZ
FallHeight

======
RULES
======

( Ticks and movement instructions )
[ X ] -> [ X T ]
[ left   P ][ X T ] -> [ P ][ X GoL no T ]
[ right  P ][ X T ] -> [ P ][ X GoR no T ]
[ up     P ][ X T ] -> [ P ][ X GoU no T ]
[ down   P ][ X T ] -> [ P ][ X GoD no T ]
[ action P ][ X T ] -> [ P ][ X Act no T ]

( Set universal wait tokens and fall height counter )
right [ T X | no E32 | | | | ] -> [ T X | E32 | E16 | E8 | E4 | E2 ]

[ T ][ FH ][ Hero no HeroFalling ] -> [ T ][ ][ Hero ]
right [ T X no FH | no FH | no FH | no FH | no FH | no FH ] -> [ T X | | | | | FH ]


( Safezone and Ground )
   [ T ][ SZ ] -> [ T ][ ]
up [ T ][ Wall | no Wall ] -> [ T ][ Wall Ground | SZ ]
up [ T ][ Platform | ] -> [ T ][ Platform | SZ ]
up [ T ][ PlatformLoose | ] -> [ T ][ PlatformLoose | SZ ]

( WallTexture )
[ T ][ Cover no WallTxt no WTxt1  ] -> [ T ][ Cover random WallTxt ]
down [ T ][ no Cover | WallTxt no WTxt2 ] -> [ T ][ | WTxt1 ]
[ T ][ WTxt1 | Cover no WTxt1 no WTxt2 ] -> [ T ][ WTxt1 | Wall WTxt2 ]
[ T ][ WTxt4 | Cover no WTxt3 ] -> [ T ][ WTxt4 | Cover WTxt3 ]
[ T ][ WTxt1 | Cover no WTxt1 no WTxt2 ] -> [ T ][ WTxt1 | Wall WTxt2 ]
[ T ][ WT3 | WallTxt no WT1 | WT3 ] -> [ T ][ WT3 | WT1 | WT3 ]

[ T ][ no Cover ] -> [ T ][ no Walltxt ]

left  [ Cover | SZ ] -> [ Cover | WR SZ ]
right [ Cover | SZ ] -> [ Cover | WL SZ ]
left  [ no Cover | SZ ] -> [ | no WR SZ ]
right [ no Cover | SZ ] -> [ | no WL SZ ]

up [ T ][ no Cover | Wall ] -> [ T ][ | Wall WallCeiling ]

[ T ][ Hero | Hidden ] -> [ T ][ Hero | ]


( Door sides )
right [ T ][ no DoorLeft | Door | ] -> [ T ][ DoorLeft | Door | DoorRight ]
up    [ T ][ Door | no DoorTop ] -> [ T ][ Door | DoorTop ]

up [ T ][ Wall | DoorLeft  |  ] -> [ T ][ Wall | DoorLeft  | DoorLeft ]
up [ T ][ Wall | DoorRight |  ] -> [ T ][ Wall | DoorRight | DoorRight ]



( Ladder )
down [ T ][ Platform | Ladder1   ] -> [ T ][ Platform Ladder1 | Ladder1 ]
down [ T ][ Ladder1  | Ladder1   ] -> [ T ][ Ladder1 | Ladder2 ]
up   [ T ][ Ladder1  | no Ladder ] -> [ T ][ Ladder1 | LadderTop ]

( Platform support )
left  [ Platform | Wall ] -> [ Platform SupportL | Wall ]
right [ Platform | Wall ] -> [ Platform SupportR | Wall ]
(      [ Platform no Support | Platform  no PFL | no Wall no Platform ] -> [ Platform | Platform SupportM Pillar | ] )
down  [ SupportM | no Wall ] -> [ SupportM | Pillar ] 
down  [ Pillar | no Wall ] -> [ Pillar | Pillar ] 

( Loose Platfrom falling )
up         [ T ][ PF PFL | Hero ] -> [ T ][ PFL W16 W8 | Hero ]
horizontal [ T ][ no PF PFL no W | PFL Platform ] -> [ T ][ PFL | PFL W32 ]


     [ T ][ no PF PFL no W ] -> [ T ][ PFF W8 ]


down [ T ][ PFF no W | no Wall ] -> [ T ][ | PFF W4 W2 ]
down [ T ][ PFF no W | Wall ] -> [ T ][ PFS | Wall ]
     [ T ][ PFF no W Abyss ] -> [ T ][ Abyss ]



( Trees and treetops )
up    [ T ][ Trunk | no Wall no Trunk ] -> [ T ][ Trunk | Trunk ]
left  [ T ][ Leaves | no Leaves ] -> [ T ][ Leaves | LeavesR ]
right [ T ][ Leaves | no Leaves ] -> [ T ][ Leaves | LeavesL ]
down  [ T ][ Leaves | no Leaves no Leafy ] -> [ T ][ Leaves | random Leafy ]


( Waterfall )
down [ T ][ Waterrunning no W no Splash | Wall ] -> [ T ][ WaterRunning random Splash | Wall ]
horizontal [ T ][ Splash Waterfall no W | no Waterfall ] -> [ T ][ Splash Waterfall | random Splash ]
horizontal [ T ][ no Waterfall | Waterfall | no Waterfall ] -> [ T ][ no Splash | Waterfall | no Splash ]
[ T ][ Splash Waterfall no W ] -> [ T ][ random Splash ]


down [ T ][ Waterfall | no Waterfall no wall ] -> [ T ][ Waterfall | random WaterRunning ]
[ T ][ WaterfallTop no W ] -> [ T ][ W4 W2 random WaterfallTop ]
[ T ][ WaterRunning no W ] -> [ T ][ W4 random WaterRunning ]

( Pedestal )
[ T ][ X no W no Go ][ HeroKneeling PedestalLit ] -> [ T ][ X ][ HeroKneeling PedestalLit ] checkpoint
[ T ][ X no W ][ HeroR no HeroJumping Pedestal ] -> [ T ][ X W32 ][ HeroRK PedestalLit W32 ] checkpoint
[ T ][ X no W ][ HeroL no HeroJumping Pedestal ] -> [ T ][ X W32 ][ HeroLK PedestalLit W32 ] checkpoint
up [ T ][ PedestalLit no W | ] -> [ T ][ PedestalLit W4 | random Flame ]


( HERO MOVEMENT )
[ T ][ X GoU no W ][ HeroStanding Doorway ]  -> win


( Climbing up )
[ T ][ X FR no W ][ HeroC2U LadderTop ]  -> [ T ][ X W8 W4 W1 ][ HeroRK LadderTop ]
[ T ][ X FL no W ][ HeroC2U LadderTop ]  -> [ T ][ X W8 W4 W1 ][ HeroLK LadderTop ]

[ T ][ X     no W ][ HeroC2U Ladder ]     -> [ T ][ X W8 W4 W1 ][    HeroC1  Ladder ] sfx2
[ T ][ X GoU no W ][ HeroC1  Ladder ]     -> [ T ][ X W8 W4 W1 ][ up HeroC2U Ladder ] sfx2

[ T ][ X GoU no W ][ HeroRS  Ladder ]     -> [ T ][ X W8 W4 FR ][ up HeroC2U Ladder ] sfx2
[ T ][ X GoU no W ][ HeroLS  Ladder ]     -> [ T ][ X W8 W4 FL ][ up HeroC2U Ladder ] sfx2


( Climbing down )
[ T ][ X FR  no W ][ HeroC1 Ladder SZ ]  -> [ T ][ X W8 W2 ][ HeroR1 Ladder SZ ]
[ T ][ X FL  no W ][ HeroC1 Ladder SZ ]  -> [ T ][ X W8 W2 ][ HeroL1 Ladder SZ ]

[ T ][ X GoD no W ][ HeroC1 Ladder ]     -> [ T ][ X W8 W2 ][      HeroC2D Ladder ] sfx2
[ T ][ X     no W ][ HeroC2D ]           -> [ T ][ X W8 W2 ][ down HeroC1 ] sfx2

[ T ][ X GoD no W ][ HeroRK  LadderTop  ] -> [ T ][ X W8 W2 FR ][ HeroC2D LadderTop ]
[ T ][ X GoD no W ][ HeroLK  LadderTop  ] -> [ T ][ X W8 W2 FL ][ HeroC2D LadderTop ]


( Falling off ladder )
right [ T ][ X GoR no W ][ HeroC1 | no O ] -> [ T ][ X W8 W2 ][ HeroRLean | ]
left  [ T ][ X GoL no W ][ HeroC1 | no O ] -> [ T ][ X W8 W2 ][ HeroLlean | ]


( Climbing on a small ledge )


right [ T ][ X Go no GoD no GoL no W ][ HeroR | Ground ] -> [ T ][ X W16 Run1 ][ HeroRHS | Ground ]
left  [ T ][ X Go no GoD no GoR no W ][ HeroL | Ground ] -> [ T ][ X W16 Run1 ][ HeroLHS | Ground ]


( JUMPING AND FALLING )

( Setting to fall )
[ T ][ X no W ][ HeroR no SZ ] -> [ T ][ X W4 W2 ][ HeroRF ]
[ T ][ X no W ][ HeroL no SZ ] -> [ T ][ X W4 W2 ][ HeroLF ]

( Standing up )

(
right [ T ][ X GoR no W ][ HeroRK SZ | no O SZ ] -> [ T ][ X W8 W4 ][ HeroRLean | SZ ]
left  [ T ][ X GoL no W ][ HeroLK SZ | no O SZ ] -> [ T ][ X W8 W4 ][ HeroLLean | SZ ]
)
[ T ][ X Go no GoD no W ][ HeroRK SZ ] -> [ T ][ X W8 ][ HeroRS ]
[ T ][ X Go no GoD no W ][ HeroLK SZ ] -> [ T ][ X W8 ][ HeroLS ]



( Catching ladder from falling )
[ T ][ X no W no FH ][ HeroRF no SZ Ladder ] -> [ T ][ X W16 FR ][ HeroC1 Ladder ] sfx2
[ T ][ X no W no FH ][ HeroLF no SZ Ladder ] -> [ T ][ X W16 FL ][ HeroC1 Ladder ] sfx2




( Catching the ledge )
right [ T ][ X no W no FH][ HeroRF no SZ no Platform | Ground ] -> [ T ][ X W16 W8 ][ HeroRH | Ground ]
left  [ T ][ X no W no FH ][ HeroLF no SZ no Platform | Ground ] -> [ T ][ X W16 W8 ][ HeroLH | Ground ]

right [ T ][ X no W ][ HeroRJ1 no SZ no PLatform | Ground ] -> [ T ][ X W16 W8 ][ HeroRH | Ground ]
left  [ T ][ X no W ][ HeroLJ1 no SZ no PLatform | Ground ] -> [ T ][ X W16 W8 ][ HeroLH | Ground ]
right [ T ][ X no W ][ HeroRJ2 no SZ no PLatform | Ground ] -> [ T ][ X W16 W8 ][ HeroRH | Ground ]
left  [ T ][ X no W no Run1 ][ HeroLJ2 no SZ no PLatform | Ground ] -> [ T ][ X W16 W8 ][ HeroLH | Ground ]


( Falling )
[ T ][ X no W ][ HeroFalling Abyss ] -> [ T ][ X ][  ]
[ T ][ X no W ][ HeroDeadFalling Abyss ] -> [ T ][ X ][ Abyss ]

[ T ][ X no W GoL ][ FH ][ HeroRF no SZ ] -> [ T ][ X W4 W2 ][ left FH ][ down HeroLF ] 
[ T ][ X no W GoR ][ FH ][ HeroLF no SZ ] -> [ T ][ X W4 W2 ][ left FH ][ down HeroRF ] 

[ T ][ X no W ][ FH ][ HeroRF no SZ ] -> [ T ][ X W4 W2 ][ left FH ][ down  HeroRF ] 
[ T ][ X no W ][ FH ][ HeroLF no SZ ] -> [ T ][ X W4 W2 ][ left FH ][ down  HeroLF ] 

[ T ][ X no W ][ HeroDeadFalling no SZ ] -> [ T ][ X W4 W2 ][ down HeroDeadFalling ] 
[ T ][ X no W ][ HeroDead no SZ ] -> [ T ][ X W4 W2 ][ down HeroDeadFalling ] 

[ T ][ > FH | X ] -> [ T ][ > FH | X ] sfx3


( Moving through the air )
      [ T ][ X no W ][ HeroRJ2 SZ ]   -> [ T ][ X W16 W2 ][ HeroRK ] sfx1
      [ T ][ X no W Run2 ][ HeroRJ2 no SZ ] -> [ T ][ X W4 W2 no Run ][ right HeroRJ2 ]

      [ T ][ X no W no Run2 ][ HeroRJ2 no SZ ] -> [ T ][ X ][ ]
      [ T ][ X no W no Run2 ][ HeroRJ2Feet   ] -> [ T ][ X W4 W2 ][ right HeroRF ]

(right [ T ][ X no W no Run ][ HeroRJ1 | | Ground ] -> [ T ][ X W8 W1 ][ left HeroRF | | Ground ])
      [ T ][ X no W no Run ][ HeroRJ1 ] -> [ T ][ X W8 W4 ][ HeroRF ]

      [ T ][ X no W Run ][ HeroRJ1 ]    -> [ T ][ X W4 W2 Run ][ right HeroRJ2 ]

      [ T ][ X no W ][ HeroLJ2 SZ ]    -> [ T ][ X W16 W2 ][ HeroLK ] sfx1
      [ T ][ X no W Run2 ][ HeroLJ2 no SZ ] -> [ T ][ X W4 W2 no Run ][ left HeroLJ2 ]
      [ T ][ X no W no Run2 ][ HeroLJ2 no SZ ] -> [ T ][ X ][ ]
      [ T ][ X no W no Run2 ][ HeroLJ2Feet   ] -> [ T ][ X W4 W2 ][ left HeroLF ]

(left  [ T ][ X no W no Run ][ HeroLJ1 | | Ground ] -> [ T ][ X W8 W1 ][ left HeroLF | | Ground ])
      [ T ][ X no W no Run ][ HeroLJ1 ] -> [ T ][ X W8 W4 ][ HeroLF ]

      [ T ][ X no W Run ][ HeroLJ1 ]    -> [ T ][ X W4 W2 Run ][ left HeroLJ2 ]


( Starting the jump )
up    [ T ][ X GoU no W no Run ][ HeroRS SZ | no O ]   -> [ T ][ X W4 W2 ][ up HeroRJ1 | ]
up    [ T ][ X GoU no W no Run ][ HeroLS SZ | no O ]   -> [ T ][ X W4 W2 ][ up HeroLJ1 | ]

up    [ T ][ X GoUA no W16 no W8  Run ][ HeroR SZ no HeroRS | no O ]   -> [ T ][ X W4 W2 Run ][ up HeroRJ1 | ]
up    [ T ][ X GoUA no W16 no W8  Run ][ HeroL SZ no HeroLS | no O ]   -> [ T ][ X W4 W2 Run ][ up HeroLJ1 | ]


( Auto jump )
(
right [ T ][ X GoR no W Run ][ HeroR SZ | no O no SZ ]   -> [ T ][ X W4 W2 Run ][ up HeroRJ1 | ]
left  [ T ][ X GoL no W Run ][ HeroL SZ | no O no SZ ]   -> [ T ][ X W4 W2 Run ][ up HeroLJ1 | ]
)


right [ T ][ X GoR no W ][ HeroR | Ground ] -> [ T ][ X W4 W2 ][ up HeroRJ1 | Ground ]
left  [ T ][ X GoL no W ][ HeroL | Ground ] -> [ T ][ X W4 W2 ][ up HeroLJ1 | Ground ]

( Landing ) 
[ T ][ X no W FH ][ HeroFalling SZ ] -> [ T ][ X W16 W4 ][ HeroDead SZ ] sfx0
[ T ][ X no W ][ HeroDeadFalling SZ ] -> [ T ][ X W16 W4 ][ HeroDead SZ ] sfx0


[ T ][ X no W ][ HeroRJ2 SZ ] -> [ T ][ X W4 W2 ][ HeroRK SZ ] sfx0
[ T ][ X no W ][ HeroRF  SZ ] -> [ T ][ X W16 ][ HeroRK SZ ] sfx0
[ T ][ X no W ][ HeroLJ2 SZ ] -> [ T ][ X W4 W2 ][ HeroLK SZ ] sfx0
[ T ][ X no W ][ HeroLF  SZ ] -> [ T ][ X W16 ][ HeroLK SZ ] sfx0



( Jumping from hanging )
[ T ][ X no W GoL ][ HeroRHS SZ ] -> [ T ][ X W8 ][ HeroRS ]
[ T ][ X no W GoR ][ HeroLHS SZ ] -> [ T ][ X W8 ][ HeroLS ]

[ T ][ X no W ][ HeroRHS ]     -> [ T ][ X W8 W1 ][ up   HeroRJ1 ]
[ T ][ X no W ][ HeroLHS ]     -> [ T ][ X W8 W1 ][ up   HeroLJ1 ]



[ T ][ X no W Go no GoD no GoL ][ HeroRH ]  -> [ T ][ X W8 W4 Run1 ][ HeroRHS ]
[ T ][ X no W Go no GoD no GoR ][ HeroLH ]  -> [ T ][ X W8 W4 Run1 ][ HeroLHS ]

( Letting go )
[ T ][ X GoD no W ][ HeroRH ]     -> [ T ][ X W4 W2 ][ down HeroRF ]
[ T ][ X GoL no W ][ HeroRH ]     -> [ T ][ X W4 W2 ][ down HeroLF ]
[ T ][ X GoD no W ][ HeroLH ]     -> [ T ][ X W4 W2 ][ down HeroLF ]
[ T ][ X GoR no W ][ HeroLH ]     -> [ T ][ X W4 W2 ][ down HeroRF ]


( Falling through platforms )
down [ T ][ X no W GoD ][ HeroRK | Platform no Ladder ] -> [ T ][ X W4 W2 ][ > HeroRF | Platform ]
down [ T ][ X no W GoD ][ HeroLK | Platform no Ladder ] -> [ T ][ X W4 W2 ][ > HeroLF | Platform ]


( Falling backwards )
right [ T ][ X no W GoD ][ no SZ no O | HeroRK ] -> [ T ][ X W8 W1 ][ HeroRF | ]
left  [ T ][ X no W GoD ][ no SZ no O | HeroLK ] -> [ T ][ X W8 W1 ][ HeroLF | ]

( Kneeling )
[ T ][ X no W GoD ][ HeroRS ] -> [ T ][ X W16 ][ HeroRK ]
[ T ][ X no W GoD ][ HeroLS ] -> [ T ][ X W16 ][ HeroLK ]





( On the edge )

[ T ][ X GoR no W16 no W8 ][ HeroREdge  ] -> [ T ][ X W4 W2 ][ right HeroRF ]
[ T ][ X GoL no W16 no W8 ][ HeroLEdge  ] -> [ T ][ X W4 W2 ][ left  HeroLF ]

[ T ][ X     no W  ][ HeroREdge ]  -> [ T ][ X W8 W4 no Run ][ HeroRS ]
[ T ][ X     no W  ][ HeroLEdge ]  -> [ T ][ X W8 W4 no Run ][ HeroLS ]

right [ T ][ X no W GoR ][ HeroRS SZ | no O no SZ ] -> [ T ][ X W16 W4 Run1 ][ HeroREdge | ]
left  [ T ][ X no W GoL ][ HeroLS SZ | no O no SZ ] -> [ T ][ X W16 W4 Run1 ][ HeroLEdge | ]





( Walking, running )
      [ T ][ X no W Run2 ][ HeroR2 SZ ]            -> [ T ][ X W4 Run2 ][ HeroR1 ]
      [ T ][ X no W ][ HeroR2 SZ ]                 -> [ T ][ X W4 W2 ][ HeroR1 ]
      [ T ][ X no GoR no W ][ HeroR1 SZ ]          -> [ T ][ X W4 W2 ][ HeroRS ]
      [ T ][ X no W ][ HeroRLean ]                 -> [ T ][ X W8 Run1 ][ right HeroR2 ]
right [ T ][ X GoR no W Run1 ][ HeroR1 SZ | no O ] -> [ T ][ X W4 Run2 ][ right HeroR2 | ]
right [ T ][ X GoR no W ][ HeroR1 SZ | no O ]      -> [ T ][ X W4 W2 ][ right HeroR2 | ]
right [ T ][ X GoR no W ][ HeroRS SZ | no O ]      -> [ T ][ X W8 W2 ][ HeroRLean | ] 

      [ T ][ X no W Run2 ][ HeroL2 SZ ]            -> [ T ][ X W4 Run2 ][ HeroL1 ]
      [ T ][ X no W ][ HeroL2 SZ ]                 -> [ T ][ X W4 W2 ][ HeroL1 ]
      [ T ][ X no GoL no W ][ HeroL1 SZ ]          -> [ T ][ X W4 W2 ][ HeroLS ]
      [ T ][ X no W ][ HeroLLean ]                 -> [ T ][ X W8 Run1 ][ left HeroL2 ]
left  [ T ][ X GoL no W Run1 ][ HeroL1 SZ | no O ] -> [ T ][ X W4 Run2 ][ left HeroL2 | ]
left  [ T ][ X GoL no W ][ HeroL1 SZ | no O ]      -> [ T ][ X W4 W2 ][ left HeroL2 | ]
left  [ T ][ X GoL no W ][ HeroLS SZ | no O ]      -> [ T ][ X W8 W2 ][ HeroLLean | ] 


[ T ][ X ][ HeroRS ] -> [ T ][ X no Run ][ HeroRS ]
[ T ][ X ][ HeroLS ] -> [ T ][ X no Run ][ HeroLS ]


( Turning )
[ T ][ X FR  no W ][ HeroT ]     -> [ T ][ X W4 ][ HeroRS ]
[ T ][ X FL  no W ][ HeroT ]     -> [ T ][ X W4 ][ HeroLS ]
[ T ][ X GoL no W ][ HeroRS SZ ] -> [ T ][ X W4 FL ][ HeroT ]
[ T ][ X GoR no W ][ HeroLS SZ ] -> [ T ][ X W4 FR ][ HeroT ]


( Clear run tokens for passive )
late [ T ][ Run ][ HeroFalling ]  -> [ T ][ ][ HeroFalling ]
late [ T ][ Run ][ HeroKneeling ] -> [ T ][ ][ HeroKneeling ]




( Feet )
[ T ][ X ][ Feet]  -> [ T ][ X ][  ]
late down [ T ][ HeroC2  | ] -> [ T ][ HeroC2  | HeroCFeet ]
late down [ T ][ HeroRJ1 | ] -> [ T ][ HeroRJ1 | HeroRJ1Feet ]
late down [ T ][ HeroRJ2 | ] -> [ T ][ HeroRJ2 | HeroRJ2Feet ]
late down [ T ][ HeroLJ1 | ] -> [ T ][ HeroLJ1 | HeroLJ1Feet ]
late down [ T ][ HeroLJ2 | ] -> [ T ][ HeroLJ2 | HeroLJ2Feet ]
late down [ T ][ HeroRH  | ] -> [ T ][ HeroRH  | HeroRHFeet ]
late down [ T ][ HeroLH  | ] -> [ T ][ HeroLH  | HeroLHFeet ]

late right [ T ][ HeroREdge | ] -> [ T ][ HeroRedge  | HeroREdge2 ]
late left  [ T ][ HeroLEdge | ] -> [ T ][ HeroLedge  | HeroLEdge2 ]




( Door open )
[ T ][ Door ] -> [ T ][ DoorWay ]
[ T ][ Doorway ][ Pedestal ] -> [ T ][ Door ][ Pedestal ]


( House keeping ) 
late [ T Go ] -> [ T ]
late [ T Act ] -> [ T ]

late [ T ][ P no Hero ][ Hero ] -> [ T ][ ][ P Hero ]


( Waiting )

late [ T ][ E32 no W32 ] -> [ T ][ E32 W32 ]
late [ T ][ E16 no W16 ] -> [ T ][ E16 W16 ]
late [ T ][ E8  no W8  ] -> [ T ][ E8  W8  ]
late [ T ][ E4  no W4  ] -> [ T ][ E4  W4  ]
late [ T ][ E2  no W2  ] -> [ T ][ E2  W2  ]

late [ T ][ W ] -> [ T ][ W CD ]
late [ T ][ W1 CD ] -> [ T ][ ]
late [ T ][ W2 CD no W1 ] -> [ T ][ W1 ]
late [ T ][ W4 CD no W2 no W1 ] -> [ T ][ W2 W1 ]
late [ T ][ W8 CD no W4 no W2 no W1 ] -> [ T ][ W4 W2 W1 ]
late [ T ][ W16 CD no W8 no W4 no W2 no W1 ] -> [ T ][ W8 W4 W2 W1  ]
late [ T ][ W32 CD no W16 no W8 no W4 no W2 no W1 ] -> [ T ][ W16 W8 W4 W2 W1 ]






==============
WINCONDITIONS
==============

=======
LEVELS

S.........zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz.......zzzzzzzz.........zzzzz.....
.............zzzz..zzz.....zzz.zzzzz...zzzz.............zz.....................
....0...................0...................0...................0...........###
.............................................................................##
..........................................................................g.###
.E.........!.1.!i.1..1!..!i.1..!.1i..1!i.1!..........!i.!.1...........!.i.#####
###"'''''"######################################""""###########......##########
###.......######################################====############.......########
####......#######,,,,,,#########################....###############......######
####.......###,,,,,,,,,,########################....###############......######
#####......,,,,,,,,,,,,,,,######################....###############""""¨....###
#####....##########,,,,,,,######################....###############..-......###
#####....#######################################....################.-......###
#####....#######################################....#################-##...####
####......###........############.####.#####...........#####...######-##.....##
###.......#................###.......#..###..............#........##.-.......##
##............................................................###....-.......##
###.....###......####....######.g......###...................###########"""""##
####....####...######.....########...########.............##############..-.###
##.......###...#######......#######..###..####""¨''¨""###################.-####
#.g......####..###..#............###..#...............########..........##-####
###.......###.............................##.............##...............-.###
##.................###"..¨""""¨.....#############.........................-..##
####.....##.......####......-.......#############.........................-.###
####....######....####......-.....#############........#######...g.d.g..#######
#####...######...######....###################.g...############################
####...######....######..########################..############################
####+++######+++++#####++########################++############################

(

r###########################################################
################.....####..#################################
######.##.......................############################
####..............................##########################
####e.......##¨""¨''¨''¨''¨'##..g.##########################
######.....###..............#..#############################
#############...............#.....##########################
##############..............####..##########################
##############......#.......####...##.######################
###############.....##......#...........####################
################...####....##.....###...####################
#########################.###..#..#####.####################
#############################..#.........###################
##############################.##...###.####################
#########################......##..####.####################
#############.................##...####.####################
##########...................###..#####...##################
###########....g......#########...#####....#########......##
###############################...########..######.....#...#
################################...########..##......#.#...#
################################...#########.#.....#.#.#...#
#################################.##########.....#.#.#.#.#.#
#############################################g.#.#.#.#.#.#.#
#########################################....#############.#
###################################................####....#
#################################.....................#.####
#################################..........................#
###############################...........................#.
################################.........................###
################################...........................#
################################...........................#
################################........######.........##..#
################################.......###################.#
############################################################
############################################################
############################################################
############################################################
############################################################
############################################################
############################################################
############################################################
############################################################
############################################################
############################################################
############################################################

)
