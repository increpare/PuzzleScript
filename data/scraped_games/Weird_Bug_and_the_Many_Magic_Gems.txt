title Weird Bug and the Many Magic Gems
author Jonah Ostroff et al.
text_color #F0F
color_palette amiga
background_color Black 
norepeat_action
run_rules_on_level_start

========
OBJECTS
========

Goal
Red LightGreen
.111.
00.10
0...0
01.00
.111.

Player
#F0F #F8F #80F
.1.1.
.000.
10201
.000.
11.11

DeadBug
#D8D #FCF #A5C
.1.1.
.000.
10201
.000.
11.11

SleepingAnt
Red #F75
..1..
.101.
..0..
.101.
.....

LiveAnt
Red #F75
..1..
.101.
..0..
.101.
.....

Background
#0B1104 #101804 #121808 #0D1D06
30222
31100
22223
00111
03330

Web
#F8FFFF
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

Wall
#BA9 #A98 #876 #987
01203
01201
21210
20113
20213

Permawall
#6A9 #698 #476 #487
01203
01201
21210
20113
20213

Pebble
#B84 #642
.111.
10001
10001
10001
.111.

Switch
#555 Yellow
.....
.000.
.010.
.000.
.....

Door
Yellow #A83 #555
22222
21012
20102
21012
22222

Gem1 (Connected to webs)
#99C #CCF
..0..
.010.
01110
.010.
..0..

Gem2 (Teleports with you)
#F08 #F8F
..0..
.010.
01110
.010.
..0..

Gem3 (Kills nearby bugs)
#0C0 #8F8
..0..
.010.
01110
.010.
..0..

Radioactive1
#0F0 #8F0
.....
..0..
.010.
..0..
.....

Radioactive2
#080 #0F0
.....
..0..
.010.
..0..
.....

Gem4 (Kills bugs in row/col.)
#F30 #F84
..0..
.010.
01110
.010.
..0..

Gem5 (Swaps wall and empty space.)
#C63 #FC8
..0..
.010.
01110
.010.
..0..

Gem6 (Clones, kills bugs.)
#33C #88F
..0..
.010.
01110
.010.
..0..

Gem7 (Charges webs.)
#CC0 #FF8
..0..
.010.
01110
.010.
..0..

Spark
#FF0 #FF8
.....
.0.0.
..1..
.0.0.
.....

Gem8 (Awakens ants.)
#639 #84F
..0..
.010.
01110
.010.
..0..

Gem9 (Move around corners, kill 2 away.)
#2B9 #8cf5f5
..0..
.010.
01110
.010.
..0..

Temp
White
.....
.....
.....
.....
.....

PDist0
Red
.....
.....
.....
.....
.....

PDist1
Yellow
.....
.....
.....
.....
.....

PDist2
Blue
.....
.....
.....
.....
.....

=======
LEGEND
=======

Gem = Gem1 or Gem2 or Gem3 or Gem4 or Gem5 or Gem6 or Gem7 or Gem9 (Does not actually include all gems; this is just the ones with normal movement properties.)
Radioactive = Radioactive1 or Radioactive2
SparkBlock = Pebble or Gem or Wall or Player or Door (Objects that block the spread of electricity.)
Ant = SleepingAnt or LiveAnt
PDist = PDist0 or PDist1 or PDist2

Blockable = Player or Pebble or Web or SleepingAnt or Gem (Objects that can be moved around and shouldn't overlap a wall.)
Blocking = Door or Wall or PermaWall (Objects that block movement.)
Weight = Player or Pebble or Gem or Ant (Objects that weigh down switches.)
Sticky = Pebble or Ant or Gem (Objects that stick to webs.)
Pushable = Pebble or SleepingAnt or Gem (Objects that can be pushed.)
Anything = Goal or Player or Ant or Web or Pebble or Switch or Door or Gem or Permawall or Temp (Objects that shouldn't be replaced with a wall.)
AntBlock = Blocking or Pebble or Gem or SleepingAnt (Things that block ant pathfinding.)

(Character equivalences for level creation.)
@ = Player
. = Background
# = Wall
$ = PermaWall
* = Pebble
O = Pebble and Web
~ = Web
+ = Door
! = Switch
G = Goal
A = SleepingAnt
1 = Gem1
2 = Gem2
3 = Gem3
4 = Gem4
5 = Gem5
6 = Gem6
% = Gem6 and Switch
7 = Gem7
8 = Gem8
9 = Gem9

=======
SOUNDS
=======

Pushable Move 80095707
Sfx1 73155703 (Web creation sound.)
Sfx2 56169902 (Web destruction sound.)
Sfx3 10616905 (Door open sound.)
Sfx4 85050702 (Death sound.)
Sfx5 39881109 (Teleport sound.)
Sfx6 21501302 (Kill ant sound.)
Sfx7 76430108 (Wall swap sound.)
Sfx8 26367505 (Ant awaken sound.)
sfx9 23117489 (Gem9 zapping Ant)
EndLevel 47272300

================
COLLISIONLAYERS
================

Background (Background.)
Goal Switch Gem8 (Level features you can step on.)
Web (Webbing only.)
Door Wall PermaWall Player Pebble Ant Gem DeadBug (Foreground: yourself, and other things you cannot step on.)
Temp PDist(Invisible technical stuff for unlocking doors, zapping ants, etc. Lots of rules use this, so don't keep it lying around longer than necessary.)
Radioactive Spark (Overlay effects.)

======
RULES
======

(Clean up effects from last turn.)
[Radioactive] -> []
[Spark] -> []

(Pathfinding stuff for awakened ants.)
[Player] -> [Player PDist0]
[PDist0 | No PDist No AntBlock] -> [PDist0 | PDist1]
+[PDist1 | No PDist No AntBlock] -> [PDist1 | PDist2]
+[PDist2 | No PDist No AntBlock] -> [PDist2 | PDist0]

startloop
random [stationary LiveAnt PDist2| PDist1] -> [> LiveAnt PDist2| PDist1]
random [stationary LiveAnt PDist1| PDist0] -> [> LiveAnt PDist1| PDist0]
random [stationary LiveAnt PDist0| PDist2] -> [> LiveAnt PDist0| PDist2]
endloop

[PDist] -> []

(Incineration gem.)
[Action Player][Gem4|...|Ant] -> [Action Player][Gem4|...|]
[Action Player][Gem4] -> [Player][] Sfx6

(Teleportation gem.)
[Action Player][Web Gem2] -> [Gem2][Player] Sfx5

(Player creates or destroys webs by pressing Action.)
[Action Player no Web] -> [Player Web] Sfx1
[Action Player Web] -> [Player] Sfx2

(Player pushes pushable objects.)
[> Player | Pushable] -> [> Player | > Pushable]

(Gem 9 turns corners. Implemented in a simple gross way because the robust way is horrible)
[> Player | Gem9 | Blocking] -> [> Player | ^ Gem9 | Blocking]
[> Player | Gem9 | Pushable | Blocking] -> [> Player | ^ Gem9 | Pushable | Blocking]
[> Player | Gem9 | Pushable | Pushable | Blocking] -> [> Player | ^ Gem9 | Pushable | Pushable | Blocking]

(Code for propagating movement between webs and pushed objects. The + signs indicate that these rules are "grouped": they'll all keep happening, even out of order, until none of them can happen anymore. That means you can transfer movement from one pebble to another pebble, then from the second pebble to a web, then from that web to some adjacent web, then from THAT web to another pebble, etc.)
[> Sticky Web] -> [> Sticky > Web]
+[> Web Sticky] -> [> Web > Sticky]
+[> Pushable|Pushable] -> [> Pushable|> Pushable]
+[Moving Web|Web] -> [Moving Web|Moving Web]
+[Moving Web][Gem1 Web] -> [Moving Web][Moving Gem1 Web]
+[Moving Gem1 Web][Web] -> [Moving Gem1 Web][Moving Web]
+[> Gem6][Gem6] -> [> Gem6][> Gem6]

(When the magic gem is pushed into a wall, change all walls to empty space and all empty space to walls, and then cancel movement.)
[ Player ] [ > Gem5 | Wall ] -> [ Player Temp ] [ > Gem5 | Wall ] Sfx7
[ Player Temp ] [ Wall ] -> [ Player Temp ] [ Wall Temp ]
[ Player Temp ] [ no Anything ] -> [ Player Temp ] [ Wall ]
[ Wall Temp ] -> [ ]
[ Player Temp ] [ > Anything ] -> [ Player Temp ] [ Anything ]
[ Temp no Gem6] -> [ ]


(If a moving object hits an immovable object, cancel all movement.)
[> Blockable|Blocking] -> Cancel

(Create the marker for pre-webbed gems!)
[Gem6 Web] -> [Gem6 Web Temp]

(Move the Marker!)
[> Gem6 Temp] -> [> Gem6 > Temp]

(Creating new gems - we are rich! :)
late[Web Gem6 no Temp][Web no Weight] -> [Web Gem6][Web Gem6]
late[Gem6 Temp] -> [Gem6]

(At end of turn, should we open doors? Put "Temp" on each door, then remove "Temp" if some switch is uncovered, then delete all switches and doors if Temp still remains.)
late[Door] -> [Temp Door]
late[Switch no Weight][Temp Door] -> [Switch][Door]
late[Temp Door][Switch] -> [Temp Door][]
late[Temp Door] -> [] Sfx3

(Spark propagation and ant-zapping.)
late[Gem7 Web|Web no SparkBlock] -> [Gem7 Web|Spark Web]
late[Spark Web|Web no SparkBlock] -> [Spark Web|Spark Web]
late VERTICAL[Spark|no SparkBlock] -> [Spark|Temp]
late VERTICAL[Gem7 Web|no SparkBlock] -> [Gem7 Web|Temp]
late VERTICAL[Temp|no SparkBlock] -> [Temp|Temp]
late [Temp Ant] -> [Temp] Sfx6
late [Temp] -> []
late HORIZONTAL[Spark|no SparkBlock] -> [Spark|Temp]
late HORIZONTAL[Gem7 Web|no SparkBlock] -> [Gem7 Web|Temp]
late HORIZONTAL[Temp|no SparkBlock] -> [Temp|Temp]
late [Temp Ant] -> [Temp] Sfx6
late [Temp] -> []

(Radioactive propagation.)
late[Gem3|] -> [Gem3|Radioactive1]
late[Radioactive1|no Gem3] -> [Radioactive1|Radioactive2]
late[Radioactive Player] -> [Radioactive Deadbug] Sfx4
late[Radioactive Ant] -> [Radioactive] Sfx6

(Killing stuff through adjacency.)
late[Gem6|Ant] -> [Gem6|] Sfx6
late[Player|Ant] -> [DeadBug|Ant] Sfx4

(Awakening ants with Gem8.)
late [Player Gem8][SleepingAnt] -> [Player Gem8][LiveAnt]
late [Gem8][LiveAnt] -> [][LiveAnt] Sfx8

(Webbed ants become dormant again.)
late [LiveAnt Web] -> [SleepingAnt Web]

(Personman's gem kills stuff)
late[Gem9 |  | Ant] -> [Gem9 |  |] sfx9


==============
WINCONDITIONS
==============

Some Player on Goal

=======
LEVELS
=======

Message Cauchy's Connection Gem telekinetically links its web to other webs.

#############
#.....#.#.#G#
#.###.#.#.#.#
#.#.....A.A.#
#.###########
#...........#
#.#.#.#.#.#.#
#....@.1....#
#.#.#.#.#.#.#
#...........#
#############

#############
#G.A..#...+1#
###.###!*.#+#
#!#.........#
#.#####...@!#
#*..........#
#############

Message Toph's Teleportation Gem will swap with the player, if it has webbing when X is pressed.

################
#.@..#a#.......#
#........!.!.*.#
#....#a#.......#
#.2..###.!.##+##
#....###...#...#
#....###.!.+.g.#
#.*..###...#...#
################

(#############
#.@...2.#...#
#.!.!.!.+.G.#
#...A...#...#
#############)

##########
##..###a##
#..*..@.g#
#....##a##
#..#######
##.a#.+.+.
##.##.+++.
##.##++!++
##2##.+++.
#####++.++

###########
##!.......#
##!.@...2.#
##!.......#
###...##.##
###...+a.g#
##....##.##
#..####...#
#.*...#*..#
#..#......#
#####.....#
###########

Message Jonah's Juggernaut Gem kills all nearby insects.

#############
#.....#.#.#G#
#.###.#.#.#.#
#.#.....A.A.#
#.###########
#...........#
#.#.#...#.#.#
#..*..@..3..#
#.#...#.#.#.#
#...........#
#############

###############
#G.A..#3+.....#
###.###+#..*..#
#!#..........!#
#.#####!....@.#
#*............#
###############

Message Increpare's Incineration Gem explodes when X is pressed, killing all ants in its row and column.

Message It's pretty powerful.

#############
#.....#.#.#G#
#.###.#.#.#.#
#.#.....A.A.#
#.###########
#...........#
#.#.#.#.#.#.#
#....@.4....#
#.#.#.#.#.#.#
#...........#
#############

#############
#G.A..#...+4#
###.###!*.#+#
#!#.........#
#.#####...@!#
#*..........#
#############

#############
#....4#.....#
#O~...#..A..#
#..@.....A..#
#...~.#...A.#
#...O.#.A..G#
#############

Message CHz's Changing Gem swaps all walls and empty spaces, whenever it's pushed into a wall.

$$$$$$$$$$$$$$$
$#############$
$#.....#.#.#G#$
$#.###.#.#.#.#$
$#.#.....A.A.#$
$#.###########$
$#...........#$
$#.#.#.#.#.#.#$
$#....@.5....#$
$#.#.#.#.#.#.#$
$#...........#$
$#############$
$$$$$$$$$$$$$$$

$$$$$$$$$$$$$
$.....#.#.#G$
$.###.#.#.#.$
$.#.....A.A.$
$.##########$
$...........$
$.#.#.#.#.#.$
$....@.5....$
$.#.#.#.#.#.$
$...........$
$$$$$$$$$$$$$

$$$$$$$$$$$$$
$G.A..#...+5$
$##.###!*.#+$
$!#.........$
$.#####...@!$
$*..........$
$$$$$$$$$$$$$

Message Lucas's Lucrative Gem is deadly to ants, and creates linked copies on all empty webbing the first time it moves onto a web.

#############
#.....#.#.#G#
#.###.#.#.#.#
#.#.....A.A.#
#.###########
#...........#
#.#.#.#.#.#.#
#....@.6....#
#.#.#.#.#.#.#
#...........#
#############

#############
#G.A..#...+6#
###.###!*.#+#
#!#.........#
#.#####...@!#
#*..........#
#############

##############
#!.#...#6..6.#
##...#.##...##
#..#!#%.#....#
#.####.##....#
#...##.......#
#.#######....#
#..........#+#
#!*.#AAA##@#G#
##############

Message Diribative's Dynamo Gem charges connected webbing. Charged webs fry all ants in sight!

#############
#.....#.#.#G#
#.###.#.#.#.#
#.#.....A.A.#
#.###########
#...........#
#.#.#.#.#.#.#
#....@.7....#
#.#.#.#.#.#.#
#...........#
#############

#############
#G.A..#...+7#
###.###!*.#+#
#!#.........#
#.#####...@!#
#*..........#
#############

###############
#7777777777777#
#7G+.........7#
#7+A.@!*..A!!7#
#7...........7#
#7777777777777#
###############

Message Dennis's Disturbance Gem awakens the ants!

#############
#.....#.#.#G#
#.###.#.#.#.#
#.#.....A.A.#
#.###########
#...........#
#.#.#.#.#.#.#
#....@.8....#
#.#.#.#.#.#.#
#...........#
#############

#############
#G.A..#...+8#
###.###!*.#+#
#!#.........#
#.#####...@!#
#*..........#
#############

############
#.!......+G#
#.#.#.#!.###
#.#.8......#
#.#.#.#!...#
#.#.#@.....#
#...!...AAA#
############

Message Personman's Pivoting Gem can be pushed counterclockwise around corners, and kills ants that are exactly two squares away.

#############
#.....#.#.#G#
#.###.#.#.#.#
#.#.....A.A.#
#.###########
#...........#
#.#.###.#.#.#
#...#@.9#...#
#.#.#.###.#.#
#...........#
#############

#############
#G.A..#...+9#
###.###!*.#+#
#!#.........#
#.#####...@!#
#*..........#
#############

##############
#@.....#....G#
##...9.#.#####
#.A..#.#A#...#
##.#.#A#.#.*.#
##...###.....#
#a9..A.......#
##############