title Ice Cubes

author Alexi
(Base game created by Increpare)

run_rules_on_level_start
key_repeat_interval 1

realtime_interval 0.3

noaction

========
OBJECTS
========

hot 
darkred

cold
darkblue

coldcratecold
blue
.....
.....
..0..
.....
.....

Grate g
darkgrey
..0..
.0.0.
..0..
..0..
.0.0.

ClosedDoor0 0
darkgrey darkgreen
00000
0.0.0
0.0.0
00000
11111

OpenDoor0 5
darkgrey darkgreen
00000
0...0
0...0
0...0
1...1

Button0 q
grey
.....
.000.
.000.
.000.
.....

ClosedDoor1 1
red darkred
00000
0.0.0
0.0.0
00000
11111

OpenDoor1 6
red darkred
00000
0...0
0...0
0...0
1...1

Button1 w
red
.....
.000.
.000.
.000.
.....

ClosedDoor2 2
green darkgreen
00000
0.0.0
0.0.0
00000
11111

OpenDoor2 7
green darkgreen
00000
0...0
0...0
0...0
1...1

Button2 e
green
.....
.000.
.000.
.000.
.....

ClosedDoor3 3
yellow orange
00000 
0.0.0
0.0.0
00000
11111

OpenDoor3 8
yellow orange
00000
0...0
0...0
0...0
1...1

Button3 r
yellow
.....
.000.
.000.
.000.
.....

HotSource h
red darkred white yellow lightgrey
..3..
.020.
.040.
.000.
.....

ColdHotSource c
red darkred white lightgrey
.....
.000.
.030.
.000.
.....

icecube i
lightblue blue white
.101.
00021
12000
10201
.110.


Background
black darkblue
11111
11111
11111
11111
11111


Target
darkgreen blue lightblue
0...0
.020.
.212.
.020.
0...0

PuddleTarget
darkgreen blue
00000
0.1.0
.111.
0.1.0
00000

Wall
darkbrown darkblue
00010
11111
01000
11111
00010

Player
Black Orange White green darkgreen lightgrey
.000.
.111.
52525
.233.
.3.3.

Crate
brown darkbrown 
10000
00110
01010
01100
00001

ColdCrate
brown blue darkbrown
20000
01110
01110
01110
00002

puddle u
blue
.....
.000.
00000
.000.
.....

tempclose0
transparent

tempopen0
transparent

tempclose1
transparent

tempopen1
transparent

tempclose2
transparent

tempopen2
transparent

tempclose3
transparent

tempopen3
transparent

tempbutton0
transparent

tempbutton1
transparent

tempbutton2
transparent

tempbutton3
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
$ = coldcrate
@ = Crate and Target
! = IceCube and PuddleTarget
O = Target
l = PuddleTarget

insulation = wall or crate or coldcrate or coldcratecold or closeddoor0 or closeddoor1 or closeddoor2 or closeddoor3

pushable = crate or hotsource or icecube or coldcrate or coldhotsource

movable = crate or hotsource or icecube or coldcrate or coldhotsource or player

=======
SOUNDS
=======

Crate MOVE 36772507
sfx0 87367102 (melt)
endlevel 87961509 

================
COLLISIONLAYERS
================
tempclose0, tempclose1, tempclose2, tempclose3, tempopen0, tempopen1, tempopen2, tempopen3, tempbutton0, tempbutton1,tempbutton2, tempbutton3
Background
Hot,Cold,coldcratecold
Target, PuddleTarget
puddle
opendoor0, opendoor1, opendoor2, opendoor3
button0, button1, button2, button3
Player, Wall, Crate, HotSource, icecube, coldcrate, coldhotsource, grate, closeddoor0, closeddoor1, closeddoor2, closeddoor3

======
RULES
======

[ icecube hot ] -> [ puddle ]

[ >  Player | pushable ] -> [  >  Player | > pushable  ]

late [ Player button0 ] [opendoor0] -> [Player button0] [tempclose0]
late [ Player button0 ] [closeddoor0] -> [Player button0] [tempopen0]
late [ Player button0] -> [ Player tempbutton0]

late [ Player button1 ] [opendoor1] -> [Player button1] [tempclose1]
late [ Player button1 ] [closeddoor1] -> [Player button1] [tempopen1]
late [ Player button1] -> [ Player tempbutton1]

late [ Player button2 ] [opendoor2] -> [Player button2] [tempclose2]
late [ Player button2 ] [closeddoor2] -> [Player button2] [tempopen2]
late [ Player button2] -> [ Player tempbutton2]

late [ Player button3 ] [opendoor3] -> [Player button3] [tempclose3]
late [ Player button3 ] [closeddoor3] -> [Player button3] [tempopen3]
late [ Player button3] -> [ Player tempbutton3]

late [tempclose0] -> [closeddoor0]
late [tempclose1] -> [closeddoor1]
late [tempclose2] -> [closeddoor2]
late [tempclose3] -> [closeddoor3]
late [tempopen0] -> [opendoor0]
late [tempopen1] -> [opendoor1]
late [tempopen2] -> [opendoor2]
late [tempopen3] -> [opendoor3]

late [no player tempbutton0] -> [no player button0]
late [no player tempbutton1] -> [no player button1]
late [no player tempbutton2] -> [no player button2]
late [no player tempbutton3] -> [no player button3]


late [hot]->[]
late [cold]->[]
late [coldcratecold]->[]
late [ no insulation ] -> [ cold ]

late [hot | coldcrate] -> [coldcratecold | coldcrate]
late [cold | coldcrate] -> [coldcratecold | coldcrate]

late [hotsource coldcratecold] -> [coldhotsource coldcratecold]
late [puddle coldcratecold] -> [icecube coldcratecold]
late [HotSource] -> [HotSource hot]

late [ hot | no insulation ] -> [ hot | hot ]
late [coldhotsource hot] -> [hotsource hot]
==============
WINCONDITIONS
==============
all icecube on target
all puddle on PuddleTarget

=======
LEVELS
=======

message Move the ice cube to the target

#########
#.......#
#.......#
#.p.i.o.#
#.......#
#.......#
#########

message very good

#########
#......o#
#.......#
#...p...#
#.i.....#
#.......#
#########

message very good

message Don't get the ice cube stuck

############
#......#...#
#..........#
#.p.i..#..o#
#......#...#
#......#...#
############

message very good

############
#..#....#..#
#.i#.......#
#..#..p..#.#
#........#.#
#...#....#o#
############

message very good

message Each ice cube needs to get to a target

###########
#.........#
#...o..o..#
#.p.......#
#...i..i..#
#.........#
###########

message very good

###########
#oo.......#
########..#
#.........#
#.p.i..i..#
#.........#
###########

message very good

message Not every target needs to be filled

###########
#........o#
#........o#
#.p.i....o#
#........o#
#........o#
###########

message very good

###########
#....p....#
#.i.....i.#
#.#...###.#
#.#.#.#o..#
#.o.#.#####
#.#......o#
#o..#.o#o.#
###########

message very good

message You can move crates

#############
#.....#.....#
#.....#...i.#
#..p..*.....#
#.....#...o.#
#.....#.....#
#############

message very good

#############
#.....*.....#
#......*....#
#.p.i...*..o#
#......*....#
#.....*.....#
#############

message very good

message You can't move two crates

##############
#.....**.....#
#.....**.....#
#..p.*...i.o.#
#.....**.....#
#.....**.....#
##############

message very good

##############
#*.....*.*...#
#.pi**.*..*..#
#...*...*.**.#
#..*..*.*....#
#..*...**.*.o#
##############

message very good

message Ice cubes melt in heat

###############
#.....#.......#
#.....#.......#
#.h.!.#.p.i.o.#
#.....#.......#
#.....#.......#
###############

message very good

message Crates block heat

###############
#.....*.......#
#.....*.......#
#..h..*.p.i.o.#
#.....*.......#
#.....*.......#
###############

message very good

###############
##......#.....#
##......#.....#
#h.p..o.*...i.#
##*.....#.....#
##......#.....#
###############

message very good

message You can move candles

###############
#.......#.....#
#..*....#.....#
#h.p..o.*...i.#
#..*....#.....#
#.......#.....#
###############

message very good

###################
#.....#.....#.....#
#.....#..h..#.....#
#..o..*.....*..i..#
#.....#..p..#.....#
#.....#.....#.....#
###################

message very good

message Doors block everything when they're closed

#############
#..0........#
#..0.....o..#
#h.0..p.....#
#..0.....i..#
#..0........#
#############

message very good

message Doors block nothing when they're open

#############
#.5.#...5...#
#.5.#...5.o.#
#h5.#.p.5...#
#.5.#...5.i.#
#.5.#...5...#
#############

message very good

message Switches open and close doors

################
#..#.....#.....#
#..#..q..#..i..#
#h.5.....0.....#
#..#..p..#..o..#
#..#.....#.....#
################

message very good

################
#..#..........q#
#.i0...5..p..###
#..0#55#.......#
####...######00#
#q.....0q....0o#
################

message very good

message Grates block you, but not heat

##############
#..g..#......#
#..g..#...o..#
#h.g..#p.....#
#..g..#...i..#
#..g..#......#
##############

message very good

message Cold crates create cold around themselves

################
#...g.#........#
#...g.#.....o..#
#.h.g.$..p.....#
#...g.#.....i..#
#...g.#........#
################

message very good

message Heat can't travel through cold

##############
#.g.$....p...#
#hg..........#
#hg....i...o.#
#.g.$........#
##############

message very good

##############
#..g.....#...#
#..g.....#.i.#
#h.g.p...#...#
#..g....*.o..#
#..g.$...#...#
##############

message very good

message Cold crates put out candles

###################
#...........#.....#
#........p..#..i..#
#..h........*.....#
#........$..#..o..#
#...........#.....#
###################

message very good

message Cold crates re-freeze puddles

###############
#.............#
#.....p....i..#
#..h..........#
#.....$....o..#
#.............#
###############

message very good

############
#.#..h..#..#
#p#ggggg#..#
#o......*i.#
#.#.....#..#
#.#$$$$$#..#
#..........#
############

message very good

###################
#.....#..h..#.....#
#.....#.....#..i..#
#p.$..5..q..0.....#
#.....#.....#..o..#
#.....#.....#.....#
###################

message very good

