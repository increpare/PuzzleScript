title Weird Bug II: Weird Bugger
author Jonah Ostroff and Toph Wells
text_color #0FF
color_palette amiga
background_color Black 

========
OBJECTS
========

Goal 
Red LightGreen
.000.
00011
00.11
00111
.111.

Player
#0FF #8FF #08F
.1.1.
.000.
10201
.000.
11.11

DeadBug
#D8D #FCF #A5C
.1.1.
.000.
10201
.000.
11.11

Ant
Red #F75
..1..
.101.
..0..
.101.
.....

Background
#0B1104 #101804 #121808 #0D1D06
30222
31100
22223
00111
03330


Web
#F8FFFF
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

Wall
#BA9 #A98 #876 #987
01203
01201
21210
20113
20213

Pebble
#B84 #642
.111.
10001
10001
10001
.111.

Switch
#555 Yellow
.....
.000.
.010.
.000.
.....

Door
Yellow #A83 #555
22222
21012
20102
21012
22222

Gem
#F08 #F8F
..0..
.010.
01110
.010.
..0..

Temp
White
.....
.....
.....
.....
.....

=======
LEGEND
=======

Blockable = Player or Pebble or Web or Ant or Gem (Objects that can be moved around and shouldn't overlap a wall.)
Blocking = Door or Wall (Objects that block movement.)
Weight = Player or Pebble or Gem or Ant (Objects that weigh down switches.)
Sticky = Pebble or Ant or Gem (Objects that stick to webs.)
Pushable = Pebble or Ant or Gem (Objects that can be pushed.)

(Character equivalences for level creation.)
@ = Player
. = Background
# = Wall
* = Pebble
O = Pebble and Web
~ = Web
+ = Door
! = Switch
G = Goal
A = Ant
? = Gem

=======
SOUNDS
=======

Pushable Move 80095707
Sfx1 73155703 (Web creation sound.)
Sfx2 56169902 (Web destruction sound.)
Sfx3 60494109 (Door open sound.)
Sfx4 85050702 (Death sound.)
Sfx5 39881109 (Teleport sound)
EndLevel 47272300

================
COLLISIONLAYERS
================

Background (Background.)
Goal Switch(Level features you can step on.)
Web (Webbing only.)
Door Wall Player Pebble Ant Gem DeadBug  (Foreground: yourself, and other things you cannot step on.)
Temp (Invisible technical stuff for unlocking doors.)

======
RULES
======

(Magic gem teleports player if there's a web on it - this overrides and replaces normal web placement)
[Action Player][Web Gem] -> [Gem][Player] Sfx5

(Player creates or destroys webs by pressing Action.)
[Action Player no Web] -> [Player Web] Sfx1
[Action Player Web] -> [Player] Sfx2

(Player pushes pushable objects.)
[> Player | Pushable] -> [> Player | > Pushable]

(Code for propagating movement between webs and pushed objects. The + signs indicate that these rules are "grouped": they'll all keep happening, even out of order, until none of them can happen anymore. That means you can transfer movement from one pebble to another pebble, then from the second pebble to a web, then from that web to some adjacent web, then from THAT web to another pebble, etc.)
[> Sticky Web] -> [> Sticky > Web]
+[> Web Sticky] -> [> Web > Sticky]
+[> Pushable|Pushable] -> [> Pushable|> Pushable]
+[Moving Web|Web] -> [Moving Web|Moving Web]

(If a moving object hits an immovable object, cancel all movement.)
[> Blockable|Blocking] -> Cancel

(Win if the player moves onto the goal)
[> Player|Goal]-> Win

(At end of turn, should we open doors? Put "Temp" on each door, then remove "Temp" if some switch is uncovered, then delete all switches and doors if Temp still remains.)
late[Door] -> [Temp Door]
late[Switch no Weight][Temp Door] -> [Switch][Door]
late[Temp Door][Switch] -> [Temp Door][]
late[Temp Door] -> [] Sfx3

(At end of turn, kill player if adjacent to ant.)
late[Player|Ant] -> [DeadBug|Ant] Sfx4


==============
WINCONDITIONS
==============
Some Player
All Player on Goal

=======
LEVELS
=======

(Helpful reminder: you can command-click any level in the editor to immediately skip to that level.)

Message Make sure to play the original game first! http://www.puzzlescript.net/play.html?p=9174930


################
#.@..#a#.......#
#........!.!.*.#
#....#a#.......#
#.?..###.!.##+##
#....###...#...#
#....###.!.+.g.#
#.*..###...#...#
################

Message This next level is by Jonan Ostroff. Thanks, Jonan!

#############
#.@...?.#...#
#.!.!.!.+.G.#
#...A...#...#
#############

##########
##..###a##
#..*..@.g#
#....##a##
#..#######
##.a#.+.+.
##.##.+++.
##.##++!++
##?##.+++.
#####++.++

Message Last level. Don't give up now!

###########
##!.......#
##!.@...?.#
##!.......#
###...##.##
###...+a.g#
##....##.##
#..####...#
#.*...#*..#
#..#......#
#####.....#
###########


Message Well done! You've beaten the game!
(Once you've made all the necessary changes, send me your fixed version!)

(Email to [firstname].[lastname][classified punctuation]gmail.com)