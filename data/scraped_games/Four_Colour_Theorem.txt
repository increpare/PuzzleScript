title Four Colour Theorem
author Dennis Au
background_color Black
run_rules_on_level_start

========
OBJECTS
========

HBorder
DarkBlue
....0
....0
....0
....0
....0

VBorder
DarkBlue
.....
.....
.....
.....
00000

Corner
DarkBlue
.....
.....
.....
.....
....0

Player
White
.....
.00..
.00..
.....
.....

C1
LightGreen

C2
Yellow


C3
Green

C4
DarkGreen

Background
Grey

Error
Red
.....
.....
.....
.....
.....

RegionSeed
LightBlue
.....
.0...
.....
.....
.....

Seeding
Green
0....
.....
.....
.....
.....

=======
LEGEND
=======

Border = HBorder or VBorder

Colour = C1 or C2 or C3 or C4
- = VBorder
| = HBorder
+ = HBorder and VBorder
. = Background
P = Seeding and RegionSeed
o = RegionSeed

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Colour
HBorder
VBorder, Corner
Player, Seeding
Error, RegionSeed


======
RULES
======

(================
 Level Generation
 ================)
 
(create a new region for each seed)
startloop
Random [RegionSeed][No C1] -> [][C1 HBorder VBorder]
Up [C1|][Seeding] -> [C1|VBorder][Seeding]
Left [C1|][Seeding] -> [C1|HBorder][Seeding]
endloop

(expand regions until no free space)
startloop
(expand one region by one square)
Random [C1| No C1][Seeding] -> [Action C1| Action C1][Seeding]

(use borders to find extent of current region)
Left [Action C1|C1 No HBorder] -> [Action C1| Action C1]
+ Right [Action C1 No HBorder|C1] -> [Action C1| Action C1]
+ Up [Action C1|C1 No VBorder] -> [Action C1| Action C1]
+ Down [Action C1 No VBorder|C1] -> [Action C1| Action C1]

(clear and redraw borders to include new square)
(clear borders)
[Action C1 Border] -> [Action C1]
Left [Action C1| HBorder] -> [Action C1|]
Up [Action C1|VBorder] -> [Action C1|]

(redraw borders)
Right [Action C1|No C1] -> [Action C1 HBorder|]
Right [Action C1|Stationary C1] -> [Action C1 HBorder| C1]

Down [Action C1|No C1] -> [Action C1 VBorder|]
Down [Action C1|Stationary C1] -> [Action C1 VBorder| C1]

Up [Action C1|No C1] -> [Action C1|VBorder]
Up [Action C1|Stationary C1] -> [Action C1|VBorder C1]

Left [Action C1|No C1] -> [Action C1|HBorder]
Left [Action C1|Stationary C1] -> [Action C1| HBorder C1]

[action C1] -> [Stationary C1]
endloop

(draw corners)
left [VBorder| No Border] -> [VBorder| Corner]

(apply right hand level border)
Right [C1|] -> [Action C1|]
[Stationary C1][Seeding] -> [C1 HBorder][Seeding]
[C1] -> [Stationary C1]

(apply bottom level border)
Down [C1|] -> [Action C1|]
[Stationary C1][Seeding] -> [C1 VBorder][Seeding]
[C1] -> [Stationary C1]


[Seeding][C1] -> [Seeding][]
[Seeding] -> [Player]

(==============
 Main Game Code
 ==============)

(clear error)
[Error] -> []

(action toggles colour)
[Action Player No Colour] -> [Player Action C1]
[Action Player C1] -> [Player Action C2]
[Action Player C2] -> [Player Action C3]
[Action Player C3] -> [Player Action C4]
[Action Player C4] -> [Player Action C1]

(colours fill areas)
Left [Action C1|No Colour No HBorder] -> [Action C1| Action C1]
+ Right [Action C1 No HBorder|No Colour] -> [Action C1| Action C1]
+ Up [Action C1|No Colour No VBorder] -> [Action C1| Action C1]
+ Down [Action C1 No VBorder|No Colour] -> [Action C1| Action C1]

Left [Action C2|C1 No HBorder] -> [Action C2| Action C2]
+ Right [Action C2 No HBorder|C1] -> [Action C2| Action C2]
+ Up [Action C2|C1 No VBorder] -> [Action C2| Action C2]
+ Down [Action C2 No VBorder|C1] -> [Action C2| Action C2]

Left [Action C3|C2 No HBorder] -> [Action C3| Action C3]
+ Right [Action C3 No HBorder|C2] -> [Action C3| Action C3]
+ Up [Action C3|C2 No VBorder] -> [Action C3| Action C3]
+ Down [Action C3 No VBorder|C2] -> [Action C3| Action C3]

Left [Action C4|C3 No HBorder] -> [Action C4| Action C4]
+ Right [Action C4 No HBorder|C3] -> [Action C4| Action C4]
+ Up [Action C4|C3 No VBorder] -> [Action C4| Action C4]
+ Down [Action C4 No VBorder|C3] -> [Action C4| Action C4]

Left [Action C1|C4 No HBorder] -> [Action C1| Action C1]
+ Right [Action C1 No HBorder|C4] -> [Action C1| Action C1]
+ Up [Action C1|C4 No VBorder] -> [Action C1| Action C1]
+ Down [Action C1 No VBorder|C4] -> [Action C1| Action C1]

(======
 Errors
 ======)
 
(can't have uncoloured squares)
[No Colour] -> [Error]

(can't have same colour neighbours)
Up [C1| VBorder C1] -> [C1| Error VBorder C1]
Up [C2| VBorder C2] -> [C2| Error VBorder C2]
Up [C3| VBorder C3] -> [C3| Error VBorder C3]
Up [C4| VBorder C4] -> [C4| Error VBorder C4]

Left [C1| HBorder C1] -> [C1| Error HBorder C1]
Left [C2| HBorder C2] -> [C2| Error HBorder C2]
Left [C3| HBorder C3] -> [C3| Error HBorder C3]
Left [C4| HBorder C4] -> [C4| Error HBorder C4]

==============
WINCONDITIONS
==============

No Error

=======
LEVELS
=======

Poooo
.....
.....
.....
.....


Poooooooo
.........
.........
.........
.........
.........

Pooooooooooooooo
oooooooooooooooo
................
................
................
................
................
................
................
................
................
................

