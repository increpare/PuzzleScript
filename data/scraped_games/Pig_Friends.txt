title Pig Friends
author Hayden Scott-Baron
homepage www.twitter.com/docky

(noaction)

background_color #272524

(Holfix Happy Moment)
youtube GtkpKST3GiU

(should I have realtime water?)
realtime_interval 0.3

(imcompetech Move Ahead)
(youtube WhvfKl8wQ1s)  

========
OBJECTS
========


Wall 
#BF4CC7

Counter1
#FF8600 #BF4CC7
11111
11111
11011
11111
11111

Counter2
#FF8600 #BF4CC7 #FFFFFF 
11111
11211
12021
11211
11111


Background 
#00E332 #29ADFF
00100
10000
00010
01000
00000

Rock 
#FFFF27 #586D86
.....
.11..
1111.
.11..
.....


Temp
Black
.....
.....
.....
.....
.....


Shadow
#00842E
.....
00000
00000
00000
00000


Tree 
#AF4100 #00842E 
.111.
11111
11111
.111.
..0..

Player 
#BF4CC7 #FFCFD0 #FFFF27 
.1.1.
.121.
10001
.000.
.1.1.

Exit 
#FF8600 #FFCFD0 #00E332 #FFFF27 
.1.1.
.131.
10001
.000.
.1.1.

Crate 
#FFFF27 #FF8600
0000.
0110.
0000.
1111.
.....

HoleBG
#29ADFF

Water1
#29ADFF #ffffff
.....
.1...
.1...
...1.
...1.

Water2
#29ADFF #ffffff
...1.
.....
.1...
.1...
...1.


Filled
#AF4100 #29ADFF #80004D
11111
00001
02201
00001
22221

FilledPart
#586D86 #29ADFF #ffffff
11111
00001
00001
00001
00001

Heart1
#BF4CC7 #BF4CC7 #FFFF27 
....1
...11
...11
....1
.....

Heart2
#BF4CC7 #BF4CC7 #FFFF27 
.1...
111..
111..
11...
1....

Final
#000000
.....
.....
.....
.....
.....

HeartSpawn
#000000
.....
.....
.....
.....
.....






=======
LEGEND
=======

. = Background
r = Rock
# = Wall
T = Tree
P = Player
* = Crate and Shadow
$ = Water1 and HoleBG 
w = FilledPart and Water1
1 = Counter1 
5 = Counter2 

H = HeartSpawn
F = Final

E = Exit
Hole = HoleBG
Part = FilledPart
Gap = Hole or Part
Box = Crate 
Counter = Counter1 or Counter2
Water = Water1 or Water2 or Temp
FilledHole = Filled

=======
SOUNDS
=======

SFX0 7980109 (splash)
SFX1 68520703 (pig1)
SFX2 39929304 (fill hole)
SFX3 2778707 (rock in the way)
SFX5 50540302 (feel hole fully)
SFX4 76001503 (sad pig level skip)
SFX6 34293309 (happy pig end)
RESTART 25172503 (teleport)
EndLevel 68520703 (pig1)

(push 84183307)
(pushSound : 84183307)
Crate MOVE 84183307

================
COLLISIONLAYERS
================

Background
Shadow, HoleBG, Part, Exit
Rock, 
Water1, Water2, Temp, Filled,
Player, Tree, Crate, Wall, Counter, HeartSpawn 
Heart1, Heart2, Final

======
RULES     
======     

(animate water)
[stationary Player][Water1] -> [Player][ temp ]
[stationary Player][Water2] -> [Player][ Water1 ]
[stationary Player][temp] -> [Player][ Water2 ]

(skip level)
[ Action Player] [ Final ] -> [ Player] [ Final ]  
[ Action Player no Exit] -> [ SFX4 Player Exit  ] Message Okay, let's skip this level
[ Player Exit ] -> Win

(Box pushing. can't push over rocks, can't walk over gaps) 
[ > Player | Gap ] -> [ Player | Gap ]
[ > Player | Box ] -> [  >  Player | > Box  ]
[ > Box | Rock ] -> [ Box | Rock SFX3 ]
[ > Box | Exit ] -> [ Box | Exit SFX3 ]


(special rules for the final level)
DOWN [ Heart1 | MOVING Player ] -> [ Heart1 | Player ] Win
DOWN [ HeartSpawn | Player ] -> [Heart1 | Player SFX6] 
DOWN [ HeartSpawn | Exit ] -> [Heart2 | Exit]

(limit watlking, and move exit towards player)
[> Player | Final] -> [ Player | Final] 
[Final][> Player | Exit] -> [Final][Player | Exit]
[Final][RIGHT Player | ... | Exit] -> [Final][RIGHT Player Final | ... | < Exit Final ]


(win the game if not the final level)
[ > Player | Exit ] -> [ Player | Exit ] Win


(fill holes with crates)
LATE [ Box Part ]  -> [ FilledHole SFX5 ]
Late [ FilledHole HoleBG ] -> [FilledHole]
Late [ FilledHole ] -> [FilledHole]
LATE [ Box Hole ]  -> [ Part SFX0 ]

(kill old shadows)
LATE [ Shadow ] -> [ Background ]
(draw new shadows for crates on grass)
LATE [ Crate ] -> [ Crate Shadow ]


==============
WINCONDITIONS
==============

(all conditions in rules)

=======     
LEVELS
=======


message 'pig, can you help?'

#############	( 01 )
#...........#	( TUTORIAL )
#......TT...#
#.....w$$$..#
#.*..*$.E$..#
#..P..$$$$..#
#T.....T....#
1############

message 'pig, can you reach me?'

#############	( 02)
#T.....E..TT#	( TUTORIAL )
#...........#
#$$$$$$$$$w$#
#..*........#
#........T..#
#T.P.......T#
11###########


message 'pig, I need you'

#############	( 03 )
#..ww..TTT.P#	( TUTORIAL )
#.T$w..TTT..#
#E.w$..TT.*.#
#..$$T...**.#
#..ww...T.*.#
#..w$..TTTT.#
111##########


message 'such a clever pig'

#############	( 04 )
#..T$..TTTT.#	( TUTORIAL )
#...$..$w...#
#...$..ww..E#
#T**$$.TTT..#
#$.......TT.#
#$...$.P.TT.#
1111#########

message 'you tackle obstacles, pig'


############# 	( 05 )
#.TTETTTTTT.#	( EASY )
#..$$$..*...#
#..$$$..**..#
#...*......T#
#..P........#
#T......T...#
5############

message 'there's nothing you can't overcome'

#############	( #06 - EASY )
#..$......TT#
#..$w*.T.TTT#
#..ww..T.*.P#
#.$$.....TT.#
#..$..T.....#
#E.$$$$.....#
51###########

message 'even rocks don't stop you'

############# 	( 07 )
#..$..E.w.r.#	( TUTORIAL )
#..$$$$$$...#
#....Tr.Pr.r#
#.r....$$$..#
#.*.r.......#
#....T....r.#
511##########

message 'I feel trapped without you'

#############	( #8)
#$.**......$#
#$p*e*****.$#
#$.*.rr.*.t$#
#$***r*.*..$#
#$..r*r*.t.$#
#$t.r.....r$#
5111#########

message 'don't leave me, pig'

#############	( #09 )
#$$$$$$T....#
#$T...$T*.**#
#$..*.$...T.#
#$.*.*$...T$#
#$T...$.P.T$#
#$$$$$$TTTTE#
51111########

message 'I'll tell you a secret'

#############	( #10 )
#..$$.E.....#
#.*$$....T..#
#.*.$$......#
#.P*.$$...T.#
#..*.$$.....#
#..$$$$.T.TT#
55###########

message 'sometimes I run away from you, pig'

#############	( #11 )
#...........#
#..*...$$w$$#
#.*P*.$$....#
#..*..$TT..T#
#$$$w$w...E.#
#......T...T#
551##########

message 'sometimes I'm scared'

#############	( #12 )
#......r.T..#
#..*r.....w$#
#.r....w$$$$#
#.P.T.w$w.T.#
#.**r.$$..E.#
#.....$wT.TT#
5511#########

message 'the reason I run...'

#############	( #13 )
#........w$.#
#...T..*..$.#
#T.....T$w..#
#..T.TT.w$.E#
#T....*P$w..#
#..T..*.$w..#
55111########

message 'the reason I hide...'

#############	( #14)
#r...$w.E..T#
#T.T.$$..$$$#
#....w$$$$..#
#.T......*.P#
#...Tr*$$*..#
#rTT...ww*.r#
551111#######

message 'the reason I'm scared...'
#############	( #15 ) 
#w*...w$$$$$#
#w**r*ww...$#
#$*p.w$....$#
#$*..w$.t.t$#
#w.r.w$..e.$#
#$$$$$$....$#
555##########

message 'I'm scared you won't save me'
#############	( #16 )
#....ttt...t#
#.**.....tpt#
#.*.w$$....t#
#...$$w$$w$$#
#.tt$...T.$$#
#T.T$.T..E..#
5551#########

message 'but you always save me'
#############	( #17 )
#TTTT.*.$...#
#...T..*..TT#
#.$.$$*.*.$.#
#.$.$$$.Tw.w#
#P.*....T$w.#
#$$$$$$$E...#
55511########

message 'pig hold my hand'
#############	( #18)
#T...*...P.T#
#....w$ww$$w#
#$w..$..*.*.#
#e$..w......#
#.$*.w...*.T#
#.$..$$$$$$$#
555111#######

message 'pig, let's be together'
#############	( #19)
#$$$$$$.E..$#
#$$.T..TT$$$#
#.$*.*..P.r$#
#.$r*....T.$#
#.$$T.TTTT.$#
#.$$T.*....$#
5551111######

message 'I'll never forget you'	
#############	( ending )
#$$$$$$$$$$$#	
#TTrFFFFFFTF#
#FFFFHHFFFFF#
#P........E.#
#FFFFFFFFrTF#
#$$$$$$$$$$$#	
5555#########

message 'you are my pig friend'
message Pig Friends
message Game by Hayden Scott-Baron
message Music by Holfix
message Thanks for playing!


( unused levels )
(
#############	(14)
#..t......$$#
#..*.......$#
#.rttrtT.Tww#
#.**..tT*.w$#
#p.t.....T..#
#.....t..T.E#
551111#######

#############	(#19)
#T...$*..T$$#
#T*T.$.T.T$$#
#T.......T$$#
#$$TTT.TTe$$#
#$$.P**.$.$$#
#w$.....ww$$#
5551111######

#############	( 11 )
#.....$$r...#	
#r...$$.....#
#..**w$...E.#
#.rT.$w.....#
#.P..*$T...T#
#r..r.$$...T#
551##########
)