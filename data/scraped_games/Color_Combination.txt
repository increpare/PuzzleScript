title Color Combination
author Pancake Robot
homepage crouton.net

========
OBJECTS
========

Background
DARKBLUE #00000A
11111
01111
11101
11111
10111


RedTarget
#990000
.....
.000.
.0.0.
.000.
.....

GreenTarget
#009900
.....
.000.
.0.0.
.000.
.....

BlueTarget
#000099
.....
.000.
.0.0.
.000.
.....

CyanTarget
#009999
.....
.000.
.0.0.
.000.
.....

MagentaTarget
#990099
.....
.000.
.0.0.
.000.
.....

YellowTarget
#999900
.....
.000.
.0.0.
.000.
.....

WhiteTarget
#999999
.....
.000.
.0.0.
.000.
.....

Wall
black white
00010
11111
01000
11111
00010

Player
white
.0.0.
00000
00000
.000.
..0..

Red
#ff0000 #990000
00010
11111
01000
11111
00010

Green
#00ff00 #009900
00010
11111
01000
11111
00010

Blue
#0000ff #000099
00010
11111
01000
11111
00010

Cyan
#00ffff #009999
00010
11111
01000
11111
00010

Magenta
#ff00ff #990099
00010
11111
01000
11111
00010

Yellow
#ffff00 #999900
00010
11111
01000
11111
00010

White
#ffffff #999999
00010
11111
01000
11111
00010

FilledTarget
white
.....
.000.
.000.
.000.
.....

=======
LEGEND
=======

. = Background
# = Wall
P = Player
R = Red
G = Green
B = Blue
C = Cyan
M = Magenta
Y = Yellow
W = White
@ = FilledTarget
1 = RedTarget
2 = GreenTarget
3 = BlueTarget
4 = CyanTarget
5 = MagentaTarget
6 = YellowTarget
0 = WhiteTarget
ColoredTargets = RedTarget or GreenTarget or BlueTarget or CyanTarget or MagentaTarget or YellowTarget or WhiteTarget
Block = Red or Green or Blue or Cyan or Magenta or Yellow or White
=======
SOUNDS
=======
 
Block MOVE 10958307
sfx0  63019702
sfx1 71103704

================
COLLISIONLAYERS
================

Background
ColoredTargets, FilledTarget
Player, Wall, Block

======
RULES
======

[ >  Player | Block ] -> [  >  Player | > Block  ]

[ > Red | RedTarget ] -> [ | FilledTarget ] sfx1
[ > Green | GreenTarget] -> [ | FilledTarget ] sfx1
[ > Blue | BlueTarget] -> [ | FilledTarget ] sfx1
[ > Cyan | CyanTarget] -> [ | FilledTarget ] sfx1
[ > Magenta | MagentaTarget] -> [ | FilledTarget ] sfx1
[ > Yellow | YellowTarget] -> [ | FilledTarget ] sfx1
[ > White | WhiteTarget] -> [ | FilledTarget ] sfx1

[ > Red | Green ] -> [ | Yellow ] sfx0
[ > Green | Red ] -> [ | Yellow ] sfx0
[ > Red | Blue ] -> [ | Magenta ] sfx0
[ > Blue | Red ] -> [ | Magenta ] sfx0
[ > Green | Blue ] -> [ | Cyan ] sfx0
[ > Blue | Green ] -> [ | Cyan ] sfx0

[ > Red | Cyan ] -> [ | White ] sfx0
[ > Cyan | Red ] -> [ | White ] sfx0
[ > Green | Magenta ] -> [ | White ] sfx0
[ > Magenta | Green ] -> [ | White ] sfx0
[ > Blue | Yellow ] -> [ | White ] sfx0
[ > Yellow | Blue ] -> [ | White ] sfx0

==============
WINCONDITIONS
==============

No ColoredTargets

=======
LEVELS
=======


##########
#........#
#........#
#..R..P..#
#..1.....#
#........#
#........#
##########

message Push a block into another block to combine them RGB-style.

##########
#1.6.....#
#........#
#.....P..#
#.R.R..G.#
#........#
#........#
##########

message Try combining all three!

##########
#0.......#
#........#
#.R.G....#
#..P.....#
#..B.....#
#........#
##########


########
#....6.#
#.wr...#
#..w.g.#
#...p..#
########

#########
#....g..#
#..p.g..#
#.r..g.1#
#....g..#
#########