title Ultimate Sokoban Supreme
author ncrecc
homepage www.puzzlescript.net

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000

Wrap
Black

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target
/ = Wrap

=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate
Wrap

======
RULES
======

startloop
[ >  Player | Wall ] -> [  >  Player | > Wall  ]
[> Player | Crate] -> [> Player|> Crate]
[> Wall|Wall] -> [> Wall|> Wall]
(wrap)
[Wrap|...|> Player|Wrap] -> [> Player Wrap|...||Wrap]
[Wrap|...|> Wall|Wrap] -> [> Wall Wrap|...||Wrap]
[Wrap|...|> Crate|Wrap] -> [> Crate Wrap|...||Wrap]
endloop

late [player wrap] -> cancel
late [wall wrap] -> cancel
late [crate wrap] -> cancel

==============
WINCONDITIONS
==============

All Target on Wall

=======
LEVELS
=======

message All targets must be covered by bricks.

message Level 1 of 10

////////
/####../
/#.O#../
/#..###/
/#@P..#/
/#..*.#/
/#..###/
/####../
////////

message Level 2 of 10

////////
/######/
/#....#/
/#.#P.#/
/#.*@.#/
/#.O@.#/
/#....#/
/######/
////////

message level 3 of 10

///////////
/..####.../
/###..####/
/#.....*.#/
/#.#..#*.#/
/#.O.O#P.#/
/#########/
///////////

message level 4 of 10

//////////
/########/
/#......#/
/#.O@@*P#/
/#......#/
/#####..#/
/....####/
//////////

message level 5 of 10

//////////
/.#######/
/.#.....#/
/.#.O*O.#/
/##.*P*.#/
/#..O*O.#/
/#......#/
/########/
//////////

message level 6 of 10

//////////////
/######.#####/
/#....###...#/
/#.**.....#P#/
/#.*.#OOO...#/
/#...########/
/#####......./
//////////////

message level 7 of 10

/////////
/#######/
/#.....#/
/#.O*O.#/
/#.*O*.#/
/#.O*O.#/
/#.*O*.#/
/#..P..#/
/#######/
/////////

message level 8 of 10

//////////
/..######/
/..#.OOP#/
/..#.**.#/
/..##.###/
/...#.#../
/...#.#../
/####.#../
/#....##./
/#.#...#./
/#...#.#./
/###...#./
/..#####./
//////////

message level 9 of 10

////////
/#####./
/#O..##/
/#P**.#/
/##...#/
/.##..#/
/..##O#/
/...###/
////////

message level 10 of 10

/////////////
/......#####/
/......#O..#/
/......#O#.#/
/#######O#.#/
/#.P.*.*.*.#/
/#.#.#.#.###/
/#.......#../
/#########../
/////////////

message Happy New Year's!