Title Crate Game Five
Author Connorses [Loneship Games]
background_color #170036
again_interval 0.04
scanline
run_rules_on_level_start
========
OBJECTS
========
grass1
green
grass2
lightgreen

player
lightblue

body
blue

crate1
orange
crate2
#FFA500

target
black

wall1
brown
wall2
darkbrown

moveto
transparent
.....
.000.
.000.
.000.
.....

stop
transparent
.000.
00000
00000
00000
.000.

grid
darkgrey
.....
.....
..0..
.....
.....

on1
yellow
on2
#FFFF00
=======
LEGEND
=======
. = grass1
, = grass2
p = body and grass1
q = body and grass2
d = player and grass1
1 = wall1
2 = wall2
x = target
o = crate1 and grass1
0 = crate1 and grass2
u = crate2 and grass1
n = crate2 and grass2

@ = crate1 and target and on1

g = grid and wall1

wall = wall1 or wall2
crate = crate1 or crate2
background = grass1 or grass2
moveable = player or body or crate
=======
SOUNDS
=======
crate move 40949707
moveto create 14901307
restart 9643709
startgame 17441908 (6337508)
startlevel 16855508
endlevel 47758508(48095708)
endgame 11918308
(57768908)
================
COLLISIONLAYERS
================
grass1, grass2
target
player, wall, crate, body
moveto
on1, on2
stop
grid
======
RULES
======
[on1 ] -> [ ]
[on2 ] -> [ ]
[stop] -> [ ]

[> player no moveto| | | | | ] -> [player|moveto|moveto|moveto|moveto|moveto] again
[> player no moveto| | | | ] -> [player|moveto|moveto|moveto|moveto] again
[> player no moveto| | | ] -> [player|moveto|moveto|moveto] again
[> player no moveto| | ] -> [player|moveto|moveto] again
[moveto wall] -> [wall]
[player|moveto] -> [> player|] again


[moving player|body] -> [moving player|moving body]
[moving body|body] -> [moving body|moving body]

[> player|crate] -> [> player|> crate]
[> body|crate] -> [> body|> crate]
[moving crate1|crate1] -> [moving crate1|moving crate1]
[moving crate2|crate2] -> [moving crate2|moving crate2]

[> moveable no stop|wall] -> [> moveable stop|wall]
[> crate1|crate2] -> [crate1 stop|crate2]
[> crate2|crate1] -> [crate2 stop|crate1]
[> moveable] [stop] -> [moveable] [stop]
[moveto] [stop] -> [   ] [stop]

late [crate1 target] -> [crate1 target on1]
late [crate2 target] -> [crate2 target on2]

[grid] [no grid] -> [grid] [grid]
==============
WINCONDITIONS
==============

all crate on target
no moveto

=======
LEVELS
=======


1112111121111211112111121
2222222222222222222222222
1211121111211112111121111
2222222222222222222222222
1112111121111211112111121
11121.ppp...........11121
22222,pdp.,....,....22222
12111pppqp...,....,.12111
22222.ppp...........22222
11121.q.p..,....,...11121
11121.....ooooo.....11121
22222,....0...o,....22222
12111...,.o..,o...,.12111
22222.....o...o.....22222
11121.,...o0ooo.,...11121
11121...............11121
22222,....,....,....22222
12111...,....,....,.12111
22222...............22222
11121.,....,....,...11121
11121..........xxxxx11121
22222,....,....x...x22222
12111...,....,.x..,x12111
22222..........x...x22222
11121.,....,...xxxxx11121
1112111121111211112111121
2222222222222222222222222
1211121111211112111121111
2222222222222222222222222
1112111121111211112111121

111211112111121.....11121
222222222222222.....22222
121112111121111.....21111
222222222222222.....22222
111211112111121.....11121
11121..........xxxxx11121
22222,ppp.,....x...x22222
12111.pdq....,.x..,x12111
22222.ppp......x...x22222
11121.,....,...xxxxx11121
11121.....ooooouuuuu11121
22222,....0...on...u22222
12111...,.o..,ou..,u12111
22222.....o...ou...u22222
11121.,...o0ooounuuu11121
11121...............11121
22222,....,....,....22222
12111...,....,....,.12111
22222...............22222
11121.,....,....,...11121
11121xxxxx..........11121
22222x...x,....,....22222
12111x..,x...,....,.12111
22222x...x..........22222
11121xxxxx.,....,...11121
1112111121111211112111121
2222222222222222222222222
1211121111211112111121111
2222222222222222222222222
1112111121111211112111121

1111111111111111111
1........xxx......1
1........x.x......1
1........xxx......1
1........ooo......1
1........o.o......1
1........ooo......1
1........ppp......1
1...xx@oopdpoooxxx1
1........ppp......1
1........ooo......1
1........o.o......1
1........ooo......1
1........xxx......1
1........x.x......1
1........xxx......1
1111111111111111111

1111111111111111111111
1,,,,,xxxxx11111111111
1,...,x...x1...,.....1
1,...,x...x1...,.....1
1,...,x...x1,,,,.....1
1,,,,,xxxxx1....,,,,,1
1.....,,,,,1....,...,1
1.....,...,1....,...,1
1.....,...,1....,...,1
1.....,...,1....,,,,,1
1.....,,,,,11111111111
1@@@@@ppppp,,,,,.....1
1@...@p...p,...,.....1
1@...@p...p,...,.....1
1@...@p.d.p,...,.....1
1@@@@@ppppp,,,,,.....1
1.....,,,,,uuuuu,,,,,1
1.....,...,u...u,...,1
1.....,...,u...u,...,1
1.....,...,u...u,...,1
1.....,,,,,uuuuu,,,,,1
1,,,,,.....11111111111
1,...,.....1.,..,...,1
1,...,.....1.,..,...,1
1,...,.....1.,..,...,1
1,,,,,.....11111111111
1111111111111111111111

1ggggggggg
1gggggg.gg
1.1....oxg
1........g
1...d..o.g
1ggggg.g.g
1gg2gg.g.g
22222g...g
12ggggggxg
2222222g.g
1gg2gggggg