title PUZZLETALE
author Connorses
homepage www.puzzlescript.net
run_rules_on_level_start
(youtube v712NiVK5uY)

========
OBJECTS
========

Background1
purple(
#354974)
(huehuehue)

Background2
#b937ba

Target 
lightgrey grey
00000
01110
01010
01110
00000

Wall_a
#6a136bblack
00000
00000
10110
00000
01011

Wall_b
#202264 darkblue
00000
10101
00000
10101
01010

wall_d
black
.....
.....
.....
.....
00000

wall_u
black
.....
.000.
.000.
.000.
.....

wall_UL
#202264 black
.....
1....
1....
1....
.....

wall_UR
#202264 black
.....
....1
....1
....1
.....

water_U
#30007a
00000
.....
.....
.....
.....

bell
yellow
..0..
.000.
.000.
.000.
.....

ring
yellow
..0..
..00.
..000
...0.
.....

sign1
DARKBLUE BLUE darkbrown
00000
01110
00000
.....
.....

sign2
DARKBLUE BLUE darkbrown
00000
01110
00000
.....
.....

sign3
DARKBLUE BLUE darkbrown
00000
01110
00000
.....
.....

sign4
DARKBLUE BLUE darkbrown
00000
01110
00000
.....
.....

head_u
lightblue lightred #58271e yellow
.....
.222.
2222.
22222
22222

head_d
lightblue lightred #58271e yellow black
.....
.222.
23322
44344
23332

head_l
lightblue lightred #58271e yellow black
.....
.222.
22222
34432
33322

head_r
lightblue lightred #58271e yellow black
.....
.222.
22222
23443
22333

player_u
lightblue lightred #58271e yellow black
.000.
31113
.000.
.2.2.
.....

player_d
lightblue lightred #58271e yellow black
.020.
31113
.000.
.2.2.
.....

player_l
lightblue lightred #58271e yellow black
.000.
.131.
.000.
..2..
.....

player_r
lightblue lightred #58271e yellow black
.000.
.131.
.000.
..2..
.....



Crate 
grey darkgrey
.00..
00110
00001
.011.
.....

spike_floor
grey black
00000
01010
00000
01010
00000

spikes
lightgrey white
.1.1.
.0.0.
.1.1.
.0.0.
.....

bridge_ground
green pink
.....
0.1.0
.010.
0...0
.....

bridge_spawn
transparent

bridge_pickup
green pink
0.1.0
.010.
0...0
.....
.....

bridge_anim1
green pink
..1..
.010.
00100
.....
.....

bridge_anim2
green pink darkgreen
.....
.0.0.
.212.
.020.
.....

bridge_anim3
green pink darkgreen
.....
.020.
.202.
.020.
.....

bridge_anim4
green pink
0.1.0
10101
.111.
00100
.....

bridge_float_rest
green pink
.....
0.1.0
.010.
0...0
.....

bridge_float_u
green pink
.....
0.1.0
.010.
0...0
.....

bridge_float_d
green pink
.....
0.1.0
.010.
0...0
.....

bridge_float_l
green pink
.....
0.1.0
.010.
0...0
.....

bridge_float_r
green pink
.....
0.1.0
.010.
0...0
.....

bridge1
green pink lightblue darkgreen
.010.
03130
11211
03130
.010.

bridge2
grey lightgrey
.....
.00..
.011.
.000.
.....

water
darkblue #1c2e72
00000
00000
00000
00000
00000


goal
blue
.....
..0..
.000.
..0..
.....

sound1
transparent
sound2
transparent
sound3
transparent

fqwk
red
0....
.....
.....
.....
.....
=======
LEGEND
=======
player = player_u or player_d or player_l or player_r
head = head_u or head_d or head_l or head_r
bridge_anim = bridge_anim1 or bridge_anim2 or bridge_anim3 or bridge_anim4
bridge_float = bridge_float_u or bridge_float_d or bridge_float_l or bridge_float_r or bridge_float_rest
wall = wall_a or wall_b


, = background2
. = Background1
% = wall_a
# = Wall_b
P = Player_d
u = player_d and sound3
* = Crate and background2
• = Crate and background1
@ = Crate and Target
O = Target
~ = water
x = bridge_ground and bridge_spawn
" = Goal
; = sound1 and goal
! = spike_floor

f=bell
1=sign1 and wall_b
2=sign2 and wall_b
4=sign4 and wall_b

background=background1 or background2

bridge = bridge1 or bridge2
=======
SOUNDS
=======
sfx0 59854708 (bellflower)
sfx1 76315508 (title drop)
(sfx1 71849908)
sfx2 94528905 (seed pickup)
================
COLLISIONLAYERS
================

Background
fqwk
water, Goal,bridge_spawn, spike_floor
water_U
Target,bridge,bridge_anim, bridge_float_u, bridge_float_d, bridge_float_l, bridge_float_r, bridge_float
Player, Crate, bridge_ground,bell,ring, wall, spikes
wall_d, wall_u
wall_UL
wall_UR
sign1,sign2,sign3,sign4
head
bridge_pickup
sound1,sound2,sound3

======
RULES
======

(Bridge Seed Animation)
[bridge_anim4] -> [bridge1]
[bridge_anim3] -> [bridge_anim4] again
[bridge_anim2] -> [bridge_anim3] again
[bridge_anim1] -> [bridge_anim2] again

(matching bridge seeds that have stopped)
[bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest] -> [bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1] again
[bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest] -> [bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1] again
[bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest] -> [bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1] again
[bridge_float_rest|bridge_float_rest|bridge_float_rest|bridge_float_rest] -> [bridge_anim1|bridge_anim1|bridge_anim1|bridge_anim1] again

(Player facing)
[up player no player_u] -> [up player_u]
[down player no player_d] -> [down player_d]
[left player no player_l] -> [left player_l]
[right  player no player_r] -> [right player_r]

(player can't cross open water)
[> player|water no bridge] -> [player|water  ]

(crate??)
[ >  Player | Crate ] -> [  >  Player | > Crate  ]
late [crate water no bridge] -> [bridge2 water]

(grabbing Seeds)
up [head no bridge_pickup] [action player_u|bridge_ground] -> [head] [player_u bridge_pickup|  ] sfx2
down [head no bridge_pickup] [action player_d|bridge_ground] -> [head] [player_d bridge_pickup|  ] sfx2
left [head no bridge_pickup] [action player_l|bridge_ground] -> [head] [player_l bridge_pickup|  ] sfx2
right [head no bridge_pickup] [action player_r|bridge_ground] -> [head] [player_r bridge_pickup|  ] sfx2

(placing Seeds)
up [action player_u|water no bridge no bridge_float_rest] [bridge_pickup] -> [player_u|water bridge_float_u] [  ] again
down [action player_d|water no bridge no bridge_float_rest] [bridge_pickup] -> [player_d|water bridge_float_d] [  ] again
left [action player_l|water no bridge no bridge_float_rest] [bridge_pickup] -> [player_l|water bridge_float_l] [  ] again
right [action player_r|water no bridge no bridge_float_rest] [bridge_pickup] -> [player_r|water bridge_float_r] [  ] again

(bellflower resets the seeds )
[ring] -> [bell]
up [action player_u|bell] -> [player_u|ring] sfx0 again
down [action player_d|bell] -> [player_d|ring] sfx0 again
left [action player_l|bell] -> [player_l|ring] sfx0 again
right [action player_r|bell] -> [player_r|ring] sfx0 again
[ring] [bridge1] -> [ring] [  ]
[ring] [bridge_float] -> [ring] [  ]
[ring] [bridge_spawn] -> [RING] [BRIDGE_SPAWN BRIDGE_GROUND]

(Seeds moving over water)
[bridge_float_u] -> [up bridge_float_u] again
[bridge_float_d] -> [down bridge_float_d] again
[bridge_float_l] -> [left bridge_float_l] again
[bridge_float_r] -> [right bridge_float_r] again

(Seeds coming to rest when they hit something)
[> bridge_float|bridge_float_rest] -> [bridge_float_rest|bridge_float_rest]

[> bridge_float|no water] -> [bridge_float_rest|  ]

(level-specific sounds and messages)
late [player sound1] -> [player] sfx1 message PUZZLETALE

[action player_u|sign1] -> [player_u|sign1] message LINE UP 4 BRIDGE SEEDS IN A ROW TO MAKE A BRIDGE
[action player_u|sign2] -> [player_u|sign2] message GOOD JOB, YOU FAILED THE PUZZLE.
[action player_u|sign4] -> [player_u|sign4] message RING THE BELLFLOWER TO RESET THE BRIDGE SEEDS.

[player sound2] -> [player] message Inspired by Toby Fox's UNDERTALE
[player sound3] -> [player sound2] sfx1 again message PUZZLETALE

(graphically display that you have a seed so it stays on your head)
late down [bridge_pickup] [no bridge_pickup|player] -> [  ] [bridge_pickup|player]

(detect Rocks on Buttons)
late [spike_floor spikes] -> [spike_floor]

late [spike_floor] [target no crate] -> [spike_floor spikes] [target]

(Graphically display Frisk's stupid face)
[head] -> [ ]
late down [no head|player_u] -> [head_u|player_u]
late down [no head|player_d] -> [head_d|player_d]
late down [no head|player_l] -> [head_l|player_l]
late down [no head|player_r] -> [head_r|player_r]

(extra graphics blocks)
late up [wall|no wall] -> [wall wall_u|  ]
late down [wall|no wall] -> [wall wall_d|  ]
late left [wall_u|wall_u] -> [wall_u wall_UL|wall_u]
late right [wall_u|wall_u] -> [wall_u wall_UR|wall_u]
late left [wall no wall_U|no wall] -> [wall wall_UL|  ]
late right [wall no wall_U|no wall] -> [wall wall_UR|  ]

late up [water|no water] -> [water water_U|  ]

==============
WINCONDITIONS
==============

Some Player on Goal     

=======     
LEVELS
=======
%%%%%%"%%%%%
%%%%%%,%%%%%
%%%%%,,,%%%%
%%%%,,,,,%%%
%%%%,,,,,%%%
%%%%!!!!!%%%
%u,,,,*,,,o%
%%%%%%%%%%%%
%%%%%%%%%%%%
%%%%%%%%%%%%
%%%%%%%%%%%%
%%%%%%%%%%%%

%%%%%%%%%%%%
%%%%%%%%%%%%
%,,,,,,,~~~%
%,,,,,,,~~~%
%,,,,,,,,!,%
%,,*,,o,,!,%
%p,,,,,,,!,"
%,,*,,o,,!,%
%,,,,,,,,!,%
%,,,,,,,~~~%
%,,,,,,,~~~%
%%%%%%%%%%%%

%%%%%%%%%%%%
%%%%%%%%%%%%
%,,,,,,,,,,%
%,,,,,,,,,,%
%%%%%%%%%,,%
%,,o,,,o%,!%
p,,*,%*,!,!;
%,,o,*,*%,!%
%%%%%%%o%,,%
%,,,,,%%%,,%
%,,,,,,,,,,%
%%%%%%%%%%%%

######~~~~##
######~~~~##
######~~~~##
#xx###~~~~##
#xx###~~~~##
#..#1#~~~~##
#.....~~~~."
#.....~~~~."
#.p...~~~~."
#.....~~~~."
######~~~~##
######~~~~##

############
############
..xxx.######
...x..######
p.....f4##.#
...........#
#....~~.~~~#
###~.~~~~~~#
###~~~~~~.~#
#2#~~~#....#
#..~~####.."
############

############
############
####~~.#####
#..#~~.....#
"...~~..~..#
#.~~~~~~~..#
#.~~~~~~..f#
#...~~~...x#
####~~.~.xx#
##~~~~~~.xx#
##........x#
#########p##

#########"##
#########.##
#..........#
#~~~~......#
#~~~~~.....#
#....~~~...#
#.f...~..~~#
#...~~~~~~~#
P...~~~~~~~#
#....~~~...#
#.xxxx.....#
############

############
############
#....~~...~#
#...~~~~#~~#
#..~~..~~~.#
#..~....~..#
#..~..x~~.."
#..~.xx~~..#
p..~~~x~~~##
#xx.~~~~...#
#xx.~~~~...#
############

############
############
############
############
#...####...#
#..•####...#
p...•~~...."
#...####...#
#...####...#
############
############
############

#########"##
#########..#
#....##~~~.#
#..f.#~~~~~#
#....#~~~~~#
#.••.#....~#
#.•.•#~~.~.#
#....~.~~~~#
p....~...###
#xx..~...~~#
#xx..~~.~~~#
############

##"#########
##.#########
#~~~~#######
#~~~~~######
#~~~~~~~####
#~~.~~~~####
#~~.~~######
#~#.~~#...##
#~~....••.##
#~~....•..##
#~xxxx....##
#########p##

message End

message TH TH That's all for now, folks!

