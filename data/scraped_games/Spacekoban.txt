Title Spacekoban
Author Connorses [Loneship Games]
background_color #00001F
again_interval 0.12
========
OBJECTS
========
background
grey lightgrey
10001
00000
00000
00000
10001

player
white darkbrown
.000.
.010.
00000
.000.
.0.0.

wall
black

crate
red pink
.....
.000.
.010.
.000.
.....

target
red
00.00
0...0
.....
0...0
00.00

ladder
darkgrey
.....
00000
.....
00000
.....

beam
blue
.....
..0..
.0.0.
..0..
.....

exit
yellow
00000
0...0
0...0
0...0
00000

cover
black darkgrey
.....
.000.
.010.
.000.
.....

u
transparent
.....
..0..
.....
.....
.....
d
transparent
.....
.....
.....
..0..
.....
l
transparent
.....
.....
.0...
.....
.....
r
transparent
.....
.....
...0.
.....
.....
=======
LEGEND
=======
. = background
p = player and background
# = wall
* = crate
" = ladder
: = player and ladder
o = target
@ = crate and target
% = beam
& = crate and beam
! = beam and target
$ = crate and ladder
] = player and target
e = exit and cover

obj = player or crate
dir = U or D or L or R
solid = wall or cover
=======
SOUNDS
=======

================
COLLISIONLAYERS
================
background
target
ladder, exit
player, wall, crate, cover
beam
u,d,l,r
======
RULES
======
[cover] -> [ ]
[exit] [crate no target] -> [exit cover] [crate]

[< player no ladder|no solid no crate] -> [player| ]

up    [< player|solid] -> [player D|solid] again
down  [< player|solid] -> [player U|solid] again
left  [< player|solid] -> [player R|solid] again
right [< player|solid] -> [player L|solid] again

[up    player ladder] -> [player U ladder]
[down  player ladder] -> [player D ladder]
[left  player ladder] -> [player L ladder]
[right player ladder] -> [player R ladder]

up    [< player|crate] -> [player D|crate U] again
down  [< player|crate] -> [player U|crate D] again
left  [< player|crate] -> [player R|crate L] again
right [< player|crate] -> [player L|crate R] again

up    [obj U|obj] -> [obj U|obj U]
down  [obj D|obj] -> [obj D|obj D]
left  [obj L|obj] -> [obj L|obj L]
right [obj R|obj] -> [obj R|obj R]

[obj U] -> [up    obj up    U] again
[obj D] -> [down  obj down  D] again
[obj L] -> [left  obj left  L] again
[obj R] -> [right obj right R] again

[> dir|solid] -> [     |solid]

late [player dir ladder] -> [player ladder]
late [obj dir beam] -> [obj beam]
==============
WINCONDITIONS
==============
all crate on target
all exit on player
no dir
=======
LEVELS
=======
###########
###########
#######.###
#........##
#p....*..o#
#........##
#######.###
###########
###########

#########
####o####
###...###
##..*..##
#.......#
##.....##
##..p..##
##..*..##
##.....##
##.....##
##.....##
##..o..##
#########

############
###..#.....#
###........#
###........#
#oo@.*p*...#
############
###.......##
############

#########
##.o#####
#...#####
#*#.#####
#o#.*.###
###p..###
#########
#########

########
#o.*p..#
#......#
#......#
#.#o***#
#.....o#
#...o.##
########

#########(*U_2)
#...#####
#..*#####
#..*#...#
#p.....o#
######.o#
#########
#########

###########(*U_2)
#o.......##
#.....o..##
#o.......##
#.....@.###
#....#*..##
#ooo@@***.#
#.....*...#
#.....*...#
#.........#
######...p#
#######..##
###########

##########(*U_1)
###o*..###
##.....###
##...#*###
####*#..##
####]*..##
######oo##
##########
##########

##:############################
##"""##########################
####"##########################
#..............................
#o.**......................."..
#...o..........................
###############################
###############################
###############################

###########
##..o....##
#.........#
#...*.....#
#o.*"$.".o#
#.........#
#...p.....#
#...*.."..#
#......*..#
##o.....o##
###########

##########
#..#o@p..#
#........#
#...*....#
#o*.....*#
#...".".o#
##########

##########(*U_2)
##oo..*..#
##o*...#.#
###.*....#
###...".p#
###."..###
##########

#############
#############
#...........#
#...........#
#.!..*p...%.#
#...........#
#...........#
#############
#############

###########
###########
#.........#
#.........#
#.!.*p*"!.#
#.........#
#.........#
###########
###########

########
#.o..."#
##.#.."#
#..#.#"#
#o.%.$:#
##.#.."#
#....."#
#....$"#
#""""""#
########

#############
#############
#############
#############
#p....*.%%!.#
#############
#############
#############
#############

##########
##.....o##
#.......o#
#..:"""..#
#.."**"..#
#.."**"..#
#..""""..#
#o...%...#
##o.....##
##########

###########
#.*.*.!..o#
#.....%...#
#".:..%...#
#.....%...#
#.*.*.!..o#
###########

############
#####o######
#...""*..o##
#.........##
#....*.#####
#o...%....##
#..#*..:""##
#.%.o..*$".#
##..#o..%#.#
########...#
############

###########
#######"""#
#"""""""""#
#:...*..""#
#"....#...#
#"..*##*..#
#"...#....#
#"..$...oo#
########!!#
########..#
###########