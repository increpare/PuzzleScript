title Surround Yourself With Dogs
author Dan Williams
homepage www.puzzlescript.net

========
OBJECTS
========

Background 
GREEN  #229922
00000
01000
00000
00010
00000

Target 
#ff55dd 
.0.0.
00000
00000
.000.
..0..

Wall 
green

wall2
brown #115511 #229922 #449966 #004400
.....
.213.
.112.
.411.
..0..


Player 
Blue orange brown red
.222.
.111.
00000
.333.
.3.3.

dog1
brown orange black
...0.
0..02
0000.
.000.
.0.0.

=======
LEGEND
=======

. = Background
# = Wall and wall2
P = Player
d = dog1
@ = dog1 and Target
* = Target
dog = dog1

=======
SOUNDS
=======

startgame 72780300

startlevel 80280900

endlevel 48601503

player move 78085304

undo 64001309

================
COLLISIONLAYERS
================

Background
target
player, wall, dog
dog, wall2

======
RULES     
======     

(player can't walk through wall)
[ > player | wall ] -> [ player | wall ]

(player can push dogs)
[ >  Player | dog1 ] -> [ >  Player | > dog1  ]

(dogs can push dogs)
[ > dog | dog ] -> [ > dog | > dog ] 

(player can push target)
[ > Player | target ] -> [ > Player | > target ]

(player can't push through target)
[ > player | Target ] -> [ > player | target ]

(dog won't go through wall)
[ > dog | wall ] -> [ dog | wall ]

(targets move as rigid body)
[ moving target | target ] -> [ moving target | moving target ]

(when target is on dog, dog moves with it)
[ > target dog ] -> [ > target > dog ] 


==============
WINCONDITIONS
==============

All dog on Target   

=======     
LEVELS
=======

Message Be surrounded by dogs on all eight sides. They will follow you if they are next to you!

message Level 1 of 5

############
#.....d....#
#..d.***.d.#
#.d#.*p*#d.#
#.d..***...#
#....d...d.#
############

message Level 2 of 5

############
#d.d....d..#
#***.#d...d#
#*p*.......#
#***....d..#
#..d.#....d#
############

message Level 3 of 5

############
#...d***d..#
#.d#.*p*.#.#
#....***...#
#...#...#.d#
#d.d..d..d.#
############

message Level 4 of 5

############
#d..d.#.d.d#
#..***.....#
#.#*p*#....#
#..***....d#
#.d...#d.d.#
############

message Level 5 of 5

############
#.#d...d..d#
#....#...#.#
#d.***..d..#
#.#*p*.#..d#
#.d***...d.#
############

message You win! You must really love dogs! They're the best!
