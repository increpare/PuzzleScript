title Count Mover
author Jonah Ostroff
run_rules_on_level_start
key_repeat_interval .5
again_interval 1

========
OBJECTS
========
Background
#AAA #EEE
00000
00010
00000
00100
00000

Wall
#842 #420
01000
11111
00010
11111
01000

Player
#222 #57F #111
.111.
.111.
00100
.000.
00.00

Goal
Red Yellow
.000.
01110
01010
01110
.000.

Crate
#630 #951
.....
.000.
.010.
.000.
.....

N1
White
...0.
..00.
...0.
...0.
...0.

N2
White
..00.
.0..0
...0.
..0..
.0000

N3
White
..00.
.0..0
...0.
.0..0
..00.

N4
White
.0..0
.0..0
.0000
....0
....0

N5
White
.0000
.0...
.000.
....0
.000.

N6
White
..00.
.0...
.000.
.0..0
..00.

N7
White
.0000
....0
...0.
..0..
..0..

N8
White
..00.
.0..0
..00.
.0..0
..00.

N9
White
..00.
.0..0
..000
....0
..00.

N0
White
..00.
.0..0
.0..0
.0..0
..00.

Plus
Transparent

Minus
Transparent

Nback
Black

Countstart
Transparent

Ones
Transparent

Step1
White
.....
....0
.....
.....
....0

Step2
White
00.00
....0
0...0
.0..0
0...0

Step3
White
0.000
..0..
..00.
..0..
..000

Step4
White
.00..
.0.0.
.00..
.0...
.0...

Step5
White
.00..
0...0
.0...
..0.0
00...

Min1
White
...00
..0..
..0.0
..0..
...00

Min2
White
...0.
..0.0
0.0.0
0.0.0
...0.

Min3
White
..0..
.0.0.
.000.
.0.0.
.0.0.

Min4
White
0....
0...0
0....
0...0
000..

Done
Transparent

ResultBox
#BBB
.....
.000.
.0.0.
.000.
.....

CheckBox
#22F
....0
....0
0..0.
.0.0.
..0..

XBox
#F00
0...0
.0.0.
..0..
.0.0.
0...0


Ending1
Transparent

Ending2
Transparent

Ending3
Transparent

Ending4
Transparent

=======
LEGEND
=======
Min = Min1 or Min2 or Min3 or Min4
Steps = Step1 or Step2 or Step3 or Step4 or Step5
Number = N0 or N1 or N2 or N3 or N4 or N5 or N6 or N7 or N8 or N9

Ending = Ending1 or Ending2 or Ending3 or Ending4

# = Wall
. = Background
@ = Player
* = Crate
G = Goal
0 = N0 and Nback
! = N0 and Ones and Nback
~ = Nback
5 = Step5 and NBack
R = Resultbox and NBack

=======
SOUNDS
=======
Sfx1 97503900 (Done)
Sfx2 96982503 (See goal)
Sfx3 62353502 (Fail)
Sfx4 25691300 (Success!)
Player CantMove 74490707
Crate Move 76468107
================
COLLISIONLAYERS
================
Background
NBack
Goal ResultBox
Wall Player Crate Number Steps Min XBox Checkbox
Ones Done Ending
Plus Minus
Countstart
======
RULES
======
Right[||||Step5] -> [Step1|Step2|Step3|Step4|Step5]

[Player] -> [Player Countstart]

[> Player|Crate] -> [> Player|> Crate]

late [Player no Countstart][Ones] -> [Player][Ones Plus]

(Are we done?)
late [Player no Ending] -> [Player Done]
late [Done][Goal no Crate] -> [][Goal]
late [Ending3] -> [Ending4]
late [Ending2] -> [Ending3]
late [Ending1] -> [Ending2] Sfx2
late [Done] -> [Ending1] Sfx1

late [Ending4][Checkbox] -> [Ending4][Checkbox] Message Very efficient!

late [Ending4][XBox] -> [Ending4][XBox] Message I solved the puzzle! But I wasn't very efficient.

late RIGHT [Ending3][Step5|N0|N0][ResultBox] -> [Ending3][Step5|N0|N0][Resultbox Checkbox] Sfx4
late RIGHT [Ending3][ResultBox no CheckBox] -> [Ending3][ResultBox XBox] Sfx3


startloop
late [Plus N0] -> [N1]
late [Plus N1] -> [N2]
late [Plus N2] -> [N3]
late [Plus N3] -> [N4]
late [Plus N4] -> [N5]
late [Plus N5] -> [N6]
late [Plus N6] -> [N7]
late [Plus N7] -> [N8]
late [Plus N8] -> [N9]
late LEFT [Plus N9|] -> [N0|Plus]
endloop

late [Plus] -> []

late UP [Ending2][Number||no Number] -> [Ending2][Number||Number]
late UP [Ending2][Ones||][Step5||] -> [Ending2][||Minus][Step5||Min4]
late Right [||||Min4] -> [ResultBox|Min1|Min2|Min3|Min4]

startloop
late [Minus N1] -> [N0]
late [Minus N2] -> [N1]
late [Minus N3] -> [N2]
late [Minus N4] -> [N3]
late [Minus N5] -> [N4]
late [Minus N6] -> [N5]
late [Minus N7] -> [N6]
late [Minus N8] -> [N7]
late [Minus N9] -> [N8]
late LEFT [Minus N0|] -> [N9|Minus]
endloop

late [Minus] -> []

late [Countstart] -> []

late [Ending] -> [Ending] again

==============
WINCONDITIONS
==============
All Crate on Goal
Some Ending4
=======
LEVELS
=======
Message Here at Count Mover's Moving Company, you can count on me to move your boxes as efficiently as possible.

~~~~~~~~~
~~~~~~~~~
~~~~~~50!
#########
#...#...#
#.G.#.@.#
#.*.#...#
#.G*##.##
#.*GGG*.#
#.G***G.#
#.......#
#########

