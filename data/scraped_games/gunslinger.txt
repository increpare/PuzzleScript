(odd computery noise 10272108
BOON 7485308) 
(palletes
wall grey lightgrey darkgrey
bullet yellow grey white
robot1 lightgrey black red
robot2 lightgrey black purple
wood black brown lightgrey
hbar black red white
glass lightblue white lightgrey)
title gunslinger
author Ian Fights
realtime_interval 0.2
noundo
color_palette 6
========
OBJECTS
========
background .
grey
uplayer
red
..0..
.000.
0.0.0
..0..
..0..

dplayer
red
..0..
..0..
0.0.0
.000.
..0..
lplayer
red
..0..
.0...
00000
.0...
..0..

rplayer
red
..0..
...0.
00000
...0.
..0..

ubullet
yellow white lightgrey
.001.
00001
00001
00001
22222

rbullet
yellow white lightgray
2001.
20001
20001
20001
2001.

dbullet
yellow white lightgrey
22222
00001
00001
00001
.001.

lbullet
yellow white lightgray
.0001
00001
00001
00001
.0001
wall w
lightgrey darkgrey
00001
00001
11111
00001
00001
wood =
brown black
...00
00001
10000
00000
...01
nrobot m
lightgrey red black
.....
00000
01110
00000
22.22
hrobot
lightgrey purple black
.....
00000
01110
00000
22.22
hbar p
red black white
11111
22222
00000
00000
11111
redmedkit +
lightgrey red
.000.
.0.0.
00100
01110
00100
stairs s
grey lightgrey darkgrey
10001
10001
12221
12221
11111
gravel g
darkgrey grey
11101
10111
11110
11010
01111
hbar_agent_p
black white
00000
11.00
..1.0
...00
00000 
hbar_overlay
black white
00000
00.11
0.1..
00...
00000
damage_p
transparent
heal_p
transparent
mov
transparent
void v
black
ngun e
lightgrey black red
22000
....0
22000
00000
11.11
hgun
lightgrey black purple
22000
....0
22000
00000
11.11
left_e_bullet
red black
.....
.0000
01111
.0000
.....
right_e_bullet
red black
.....
0000.
11110
0000.
.....
glassv |
lightblue lightgrey white
.002.
.002.
.002.
.002.
.002.
glassh -
lightblue white
.....
11111
00000
00000
.....
enemydoor H
red lightgrey
00000
01111
00111
01111
00000
temp1
transparent
hbar_agent_e
black white
00000
11.00
..1.0
...00
00000
b1_hand_l !
grey red black
.2.2.
.2.2.
22122
21112
22222
b1_hand_r @
grey red black
.2.2.
.2.2.
22122
21112
22222
b1_core c
grey red black
.222.
22022
20102
22022
.222.
down_e_bullet
red black
.010.
.010.
.010.
.010.
..0..
damage_e
transparent
marker #
red black lightgrey
.....
..0..
.020.
..0..
.....
cl ,
black red
11110
10000
10000
10000
11110
ol {
black red
11110
10010
10010
10010
11110
rl /
black red
11110
10010
11100
10010
10010
el '
black red
11110
10000
11000
10000
11110
1l ]
black red
01000
10100
00100
00100
11110
goldenmedkit %
lightgrey lightbrown yellow
.000.
.0.0.
00200
01120
00100
goldendoor :
white lightbrown yellow
00000
00200
01120
00100
00000
=======
LEGEND
=======
(Robot stuff)
robot = nrobot or hrobot or ngun or hgun
gunbot = ngun or hgun
e_bullet = left_e_bullet or right_e_bullet or down_e_bullet
b1_hand = b1_hand_l or b1_hand_r
(player stuff)
u = uplayer
player = uplayer or rplayer or dplayer or lplayer
bullet = ubullet or dbullet or lbullet or rbullet or e_bullet
pbullet = ubullet or dbullet or lbullet or rbullet
(pathfinding stuff)
target = player
follower = robot
obstacle = wall or gravel or enemydoor
block = bullet or wall or robot or enemydoor or wood
(healthbar stuff)
> = hbar and hbar_agent_p
< = hbar and hbar_overlay
(stuff stuff)
glass = glassh or glassv
medkit = redmedkit or goldenmedkit
(gold stuff)
gold = goldenmedkit
(boss 1 stuff)
} = hbar_agent_e and hbar
hand = b1_hand_l or b1_hand_r

(game stuff)
slow = robot or hand
letter = cl or ol or rl or el or 1l
b_core = b1_core
=======
SOUNDS
=======
bullet create 99038304
wood destroy 89794304
hbar_agent_p destroy 95936501
hbar destroy 32270504
enemydoor destroy 41808108
robot destroy 83768302
redmedkit destroy 39948108
goldenmedkit destroy 91118100
goldendoor destroy 30260503
player create 85113107
player move 44793707
(boss defeat)
SFX0 10272108
================
COLLISIONLAYERS
================
background
stairs gravel marker
player wall wood robot glass enemydoor b1_hand_l b1_core goldendoor
b1_hand_r
bullet e_bullet
v medkit letter goldenmedkit
hbar
hbar_agent_p damage_p heal_p hbar_agent_e damage_e
mov hbar_overlay
temp1
======
RULES
======
(movement)
[right uplayer] -> [rplayer]
[right dplayer] -> [rplayer]
[right lplayer] -> [rplayer]

[down uplayer] -> [dplayer]
[down rplayer] -> [dplayer]
[down lplayer] -> [dplayer]

[left uplayer] -> [lplayer]
[left dplayer] -> [lplayer]
[left rplayer] -> [lplayer]

[up dplayer] -> [uplayer]
[up lplayer] -> [uplayer]
[up rplayer] -> [uplayer]

(shooting)
right [action rplayer|no block] -> [action rplayer|rbullet]
down [action dplayer|no block] -> [action dplayer|dbullet]
up [action uplayer|no block] -> [action uplayer|ubullet]
left [action lplayer|no block] -> [action lplayer|lbullet]

(bullet movement)
[rbullet] -> [right rbullet] 
[dbullet] -> [down dbullet]
[lbullet] -> [left lbullet]
[ubullet] -> [up ubullet]
[left_e_bullet] -> [left left_e_bullet]
[Right_e_bullet] -> [right right_e_bullet]
[down_e_bullet] -> [down down_e_bullet]
(bullet interactions)
[> bullet|wall] -> [|wall]
[> bullet|wood] -> [|]
[> bullet|nrobot] -> [|hrobot]
[> bullet|hrobot] -> [|]
[> bullet|e_bullet] -> [e_bullet|> bullet]
[> e_bullet| player] -> [damage_p|player]
[> bullet|ngun] -> [|hgun]
[> bullet|hgun] -> [|]
[> bullet|h] -> [|h]
[> bullet|c] -> [damage_e|c]
[> bullet|hand] -> [|hand]
[> bullet|goldendoor] -> [|goldendoor]
(special robot behaviour)
left [gunbot mov| |...|player] -> [gunbot|left_e_bullet|...|player]
right[gunbot mov||...|player] -> [gunbot|right_e_bullet|...|player]
(Hand-E-Foods pathfinding script)
[ Target Background ] -> [ Target up Background ]
startloop
[ stationary Follower | up Background ] -> [ > Follower | up Background ]
[ up Background ] -> [ down Background ]
[ down Background | no Obstacle stationary Background ] -> [ down Background | up Background ]
endloop
[ down Background ] -> [ stationary Background ]

(player interactions)
[> robot|player] -> [damage_p|player]
[> player|medkit] -> [> player| heal_p]

(robot slowing)
[mov no slow] -> []
[moving slow no mov] -> [slow]
[slow] [stationary player]-> [slow mov] [player]

(health handling)
right [damage_p] [hbar_agent_p hbar] -> [] [left hbar_agent_p void]
right [heal_p] [hbar_agent_p hbar|void] -> [] [right hbar_agent_p hbar|hbar]
late [hbar_agent_p no hbar] [player] -> [] []

(stuff interactions)
[enemydoor temp1] -> [enemydoor]
[enemydoor] [robot] -> [enemydoor temp1] [robot]
[enemydoor no temp1] -> []
[goldendoor temp1] -> [goldendoor]
[goldendoor] [gold] -> [goldendoor temp1] [gold]
[goldendoor no temp1] -> []

(-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-)

(boss)
[damage_e] [hbar_agent_e hbar] -> [] [left hbar_agent_e void]
late [hbar_agent_e no hbar] -> win sfx0

(boss 1)
[b1_hand_l] -> [left b1_hand_l]
[b1_hand_r] -> [right b1_hand_r]
[moving player] [moving hand] -> [moving player] [hand]
[> b1_hand_l|wall] -> [b1_hand_r|wall]
[> b1_hand_r|wall] -> [b1_hand_l|wall]
[hand||...|player] -> [hand|down_e_bullet|...|player]

==============
WINCONDITIONS
==============
some player on stairs
=======
LEVELS
=======

wwwwwwwwwwwww
w...w..=..m.w
w.u.w..w....w
ww=ww..w....w
w......www=ww
w......w...wv
wm.m...w...wv
wwwwwwww...wv
ws.m.m.....wv
wwwwwwwwwwwwv
<pp>vvvvvvvvv

wwwwwwwwwwvvv
ws.m..m..wvvv
w..m..m..wwww
w.m....m....w
wwwwwwwwwwgww
w.....g....mw
w.u...g..m..w
w.....g..m..w
w.....g....mw
wwwwwwwwwwwww
<ppp>vvvvvvvv

wwwww..........
w...wwwwwwwwww.
w.u.h........w.
wwgwwwwwwwwwhw.
w....|.e.....w.
w....|...e...w.
w....|.e.....w.
w....|...e...w.
wwwwwwwwwwwwgww
ws............w
wwwwwwwwwwwwwww
<p>vvvvvvvvvvvv

message BOSS ONE

<pppppp}vvvvvv
vv,{/'v]vvvvvv
vwwwwwwwwwwwww
vw.....c.....w
vw....!.@....w
vw.!..@.!..@.w
vw...........w
vwww-------www
vw+.........+w
vw.....u.....w
vw...........w
vw...........w
vwwwwwwwwwwwww
<p>vvvvvvvvvvv

wwwwwwwwwwww
we|=....=|ew
we|=...%=|ew
we|=.%..=|ew
we|=....=|ew
we|=..%.=|ew
we|=....=|ew
we|=%...=|ew
we|=..%.=|ew
we|=....=|ew
we|=%...=|ew
www......www
wsw......wsw
w.:......:.w
wsw..u...wsw
wwwwwwwwwwww
v<>vvvvvvvvv
