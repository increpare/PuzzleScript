title The Blob
author Guillem G T
noaction

========
OBJECTS
========

Background
White


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

AntiTarget
#faa
.....
.000.
.0.0.
.000.
.....

Wall
Black

GoodPlayer
Green

DyingPlayer
LightGreen

RecentDyingPlayer
LightGreen

BabyPlayer
#8c0

Poison
LightGrey #80c #edf
21212
20202
21212
20202
22222

Life
LightGrey #8c0 #efd
21212
20202
21212
20202
22222

PlayerSeed
Green
.....
.....
..0..
.....
.....


=======
LEGEND
=======

Player = GoodPlayer or DyingPlayer or RecentDyingPlayer or BabyPlayer

. = Background
# = Wall
P = GoodPlayer
O = Target
e = AntiTarget
Q = GoodPlayer and Target
X = Poison
f = Life

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Target, AntiTarget, Poison, Life
PlayerSeed
Player, Wall

======
RULES
======

late [ BabyPlayer ] -> [ GoodPlayer ]
late [ PlayerSeed no Player ] -> [ BabyPlayer ]
late [ PlayerSeed Player ] -> [ Player ]
late [ Life GoodPlayer ] -> [ BabyPlayer ]
late [ BabyPlayer | no Player no Wall ] -> [ BabyPlayer | PlayerSeed ]

late [ DyingPlayer ] -> [ ]
late [ Poison GoodPlayer ] -> [ DyingPlayer ]
late [ RecentDyingPlayer ] -> [ DyingPlayer ]
[ DyingPlayer | GoodPlayer ] -> [ DyingPlayer | RecentDyingPlayer ]


==============
WINCONDITIONS
==============

All Target on Player
No AntiTarget on Player
Some Target

=======
LEVELS
=======

P.....
.P....
..P..o
.#..o.
...o..

PP..
PQ.o
#.#.
.o.o


PP..ooo
PPPPo#o
PP..ooo

PP.ooo
PP.o.o
PP.ooo
P#....
P.....

.......
...#...
..ooo..
..o#o..
..ooo..
..PPPP.
..PPPP.