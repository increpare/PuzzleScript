title Honey Giants
author Jon Reid
homepage www.jonsgames.com

========
OBJECTS
========

Background
#2ECC40

Floor
#2ECC40 #b1ef1d
00000
00010
00000
01000
00000


WallT
#eab000 #eadc00 #a35e13 #d57208 #2ECC40
00000
00000
00000
00000
22322

WallL
#eab000 #eadc00 #a35e13 #d57208 #2ECC40
00002
00002
00003
00002
00002

WallB
#eab000 #eadc00 #a35e13 #d57208 #2ECC40
22322
00000
00000
00000
00000

WallR
#eab000 #eadc00 #a35e13 #d57208 #2ECC40
20000
20000
30000
20000
20000

WallBL
#eab000 #a35e13
00001
00000
00000
00000
00000

WallBR
#eab000 #a35e13
10000
00000
00000
00000
00000

WallTR
#eab000 #a35e13
00000
00000
00000
00000
10000

WallTL
#eab000 #a35e13
00000
00000
00000
00000
00001

WallE
#eab000 #a35e13
00000
00000
00000
00000
00000

Player 
Black blue white yellow
..2..
00000
20002
20.02
3...3



Boulder
#DDDDDD #AAAAAA #c1c1c1 #cfcfcf 
.222.
21123
21233
22330
.330.

Target
#3D9970 #2ECC40
00000
01110
01010
01110
00000

=======
LEGEND
=======

. = Background and Floor
T = WallT
L = WallL
B = WallB
R = WallR
/ = WallTL
\ = WallTR
' = WallBR
` = WallBL
# = WallE

Wall = WallT or WallB or WallR or WallL or WallTL or WallTR or WallBR or WallBL or WallE

P = Player
* = Boulder and Floor
O = Target and Floor
& = Target and Boulder and Floor
@ = Player and WallT


=======
SOUNDS
=======

Boulder Move 92664107
Player Move 85435307
EndLevel 65690303
CloseMessage 36778306
Undo 11360706
Restart 91509302

================
COLLISIONLAYERS
================
Background

Wall
Player,Floor
Target
Boulder
======
RULES
======


startloop


[ Perpendicular Player | Boulder ] -> [Perpendicular  Player |Perpendicular  Boulder  ] 
+ rigid [ moving Boulder | Boulder ] -> [ moving Boulder | moving Boulder ]


endloop

[Action Player | Boulder] -> [ Player | > Boulder]

[> Player | Boulder ] -> [Player | Boulder]
[> Boulder | Wall ] -> [Boulder | Wall ]


==============
WINCONDITIONS
==============

All Boulder on Target

=======
LEVELS
=======

Message "The boulder giants around this way like to keep all the boulders nice and tidy"

/TT@\
L..*R
L.O.R
LO.*R
`BBB'

Message "But they tend to eat lots of honey as well"

/@TT\
L..*R
#LOOR
L*.&R
`BBB'

Message "With their hands so sticky with honey, the boulders easily stick to them"

/TT@TT\
L.....R
L...B*R
L...###
#L..OOR
L*....R
`BBBBB'

/TTT@T\
L.....R
L.B..*R
L*#.R##
###O..R
L.OO.*R
`BBBBB'

/TT#@T\
LOOT*.R
LO...*R
L*...*R
L*....R
LOOB..R
`BB#BB'

/tt#t@tt\
l*.#*..*r
#l.#....r
*l.#....r
.l.t..bor
ol....###
#lo.....r
loo....*r
`bbbbbbb'

/t@#tttt\
l*.#..oor
l*.#*..or
l..#**..r
l..######
l*.o...*r
l.oo...*r
loo....##
`bbbbbb##

/tttt#ttt@\
l...o#*...r
l.#######.r
l.#.....#.r
..#.###.#.r
..#.#*#...r
..#.#o#.#.r
*.#.###.#.r
###.*#o.#.r
lo#######.r
lo...*....r
`bbbbbbbbb'

/ttt@\
lo..or
lob.or
###.r#
l****r
`bbbb'

/@t#t\
l**tor
l*oo.r
l.oo*r
lob**r
`b#bb'

Message "And with the honey giants work for the day complete, they rest easy until tomorrow"