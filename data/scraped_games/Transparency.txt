title Transparency
author pickten
homepage www.puzzlescript.net
run_rules_on_level_start
color_palette 5
( noaction )
( verbose_logging )

(
My favourite set of sokoban levels - here're the first ten of the Microban set.

I tried contacting this guy, but he seems to have vanished from the net. The levels are in lots of places online, so I'm just chancing my arm by including them.  BUT BE WARNED.
)

========
OBJECTS
========

Background
darkgray darkgray
11111
01111
11101
11111
10111


Target
yellow
.....
.000.
.000.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

FullPlayer
white blue
10001
01.10
0.1.0
01.10
10001

TransPlayer
white pink
10001
01.10
0.1.0
01.10
10001

Crate
blue Yellow
00000
0...0
0...0
0...0
00000


InnerCrate
pink Yellow
00000
0...0
0...0
0...0
00000


FallTurn
transparent

MoveTurn
transparent

LastTurn
transparent
=======
LEGEND
=======

. = Background
# = Wall
P = FullPlayer
* = Crate
@ = Crate and Target
8 = InnerCrate
2 = InnerCrate and Target
O = Target

Player = FullPlayer or TransPlayer

Thing = Crate or Wall or InnerCrate
Anything = Thing or Player
FThing = Crate or Wall or FullPlayer
TThing = InnerCrate or Wall or TransPlayer

FPThing = Crate or Wall
TPThing = InnerCrate or Wall


TType = FallTurn or MoveTurn or LastTurn

Cratelike = Crate or InnerCrate

(LLoc = Player or TransPlayer)

=======
SOUNDS
=======

Crate MOVE 36772507
endlevel 83744503
startgame 92244503

================
COLLISIONLAYERS
================

FallTurn
MoveTurn
LastTurn
Background
Target
TransPlayer, InnerCrate
FullPlayer, Wall, Crate


======
RULES
======
(
The design changed significantly over the course of the implementation, 
so the naming scheme is definitely weird and doesn't reflect how things work now.
Originally, action enabled 'transparency' and let objects fall through you. 
You couldn't move while transparent, but then I changed that to avoid certain technical issues,
and then the rest was a natural way to spice things up.
Hopefully some names make more sense with this picture in mind.
)
late [TType] -> []

( generate )
up [ up FullPlayer | Crate ] -> [  up FullPlayer | up Crate ]
up [ horizontal FullPlayer | Crate ] -> [  horizontal FullPlayer | horizontal Crate ]
[ > FullPlayer | Crate ] -> [  > FullPlayer | > Crate ]

up [ up TransPlayer | InnerCrate ] -> [  up TransPlayer | up InnerCrate ]
up [ horizontal TransPlayer | InnerCrate ] -> [  horizontal TransPlayer | horizontal InnerCrate ]
[ > TransPlayer | InnerCrate ] -> [  > TransPlayer | > InnerCrate ]

[orthogonal Crate | stationary Crate] -> [orthogonal Crate | orthogonal Crate]
[orthogonal InnerCrate | stationary InnerCrate] -> [orthogonal InnerCrate | orthogonal InnerCrate]

[Player] -> [Player FallTurn]
[FallTurn] [moving Thing] -> [MoveTurn] [moving Thing]

[FallTurn] [stationary Cratelike] -> [FallTurn] [down Cratelike]


(kill)

[> FThing | stationary FThing] -> [stationary FThing | stationary FThing]
+[> TThing | stationary TThing] -> [stationary TThing | stationary TThing]
+[FallTurn] [> Cratelike Target] -> [FallTurn] [stationary Cratelike Target]
+[> Cratelike | stationary Cratelike] -> [stationary Cratelike | stationary Cratelike]
+[FallTurn] [moving Crate | stationary Crate] -> [FallTurn] [stationary Crate | stationary Crate]
+[FallTurn] [moving InnerCrate | stationary InnerCrate] -> [FallTurn] [stationary InnerCrate | stationary InnerCrate]

(process)
[TType no LastTurn] -> [LastTurn]
[moving AnyThing] [LastTurn] -> [moving AnyThing] [MoveTurn]

[action FullPlayer no TPThing] -> [TransPlayer]
[action TransPlayer no FPThing] -> [FullPlayer]


("late")

[TType no LastTurn] -> [] again
([LastTurn] [TransPlayer] -> [LastTurn] [Player])


==============
WINCONDITIONS
==============

All Target on Cratelike
(All InnerTarget on InnerCrate)
( no TransPlayer )

=======
LEVELS
=======

message level 1 of 10
######
#....#
#.#P.#
#.*@.#
#.O@.#
#....#
######



message level 2 of 10

#####.
#O..##
#P**.#
##...#
.##..#
..##O#
...###



message level 3 of 10

######.#####
#....###...#
#.**.....#P#
#.*.#OOO...#
#...########
#####.......


message level 4 of 10

......#####
......#O..#
......#O#.#
#######O#.#
#.P.*.*.*.#
#.#.#.#.###
#.......#..
#########..


message level 5 of 10


..######
..#.OOP#
..#.**.#
..##.###
...#.#..
...#.#..
####.#..
#....##.
#.#...#.
#...#.#.
###...#.
..#####.



message level 6 of 10

..####...
###..####
#.....*.#
#.#..#*.#
#.O.O#P.#
#########




message level 7 of 10

####..
#.O#..
#..###
#2P*.#
#..*.#
#..###
####..




message level 8 of 10

########
#......#
#.O2@*P#
#......#
#####..#
....####



message level 9 of 10

.#######
.#.....#
.#.O8O.#
##.*P*.#
#..O*O.#
#......#
########



message level 10 of 10

#######
#.....#
#.O8O.#
#.8O*.#
#.O*O.#
#.8O*.#
#..P..#
#######



message congratulations!
