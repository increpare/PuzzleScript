title Loup-Mouton-Chou
author Alain Brobecker
homepage abrobecker.free.fr

again_interval 0.1

========
OBJECTS
========

Background
LightBlue

Ciel
LightBlue

Herbe
Brown LightGreen
01111
11101
11111
10111
11111

Water
LightBlue Blue
00000
00000
11111
11111
11111

Boat
DarkBrown Blue
00000
00000
11111
11111
11111


L1
Black Red
.....
.000.
00.1.
00...
0000.

M1
Grey
.....
..0..
..00.
000..
0.0..

C1
Green LightGreen
.....
.....
.000.
.010.
.000.

B1
White Brown
.....
00.1.
0001.
00.1.
00.1.

L2
Black Red
.....
.000.
.1.00
...00
.0000

M2
Grey
.....
..0..
.00..
..000
..0.0

C2
Green LightGreen
.....
.....
.000.
.010.
.000.

B2
White Brown
.....
.1.00
.1000
.1.00
.1.00

Player
Red
.00..
.....
.....
.....
.....

OnBoard
White
.....
.....
.....
.....
.....

Death
White Black
.....
00000
01010
00000
.010.



=======
LEGEND
=======

. = Background
L = L1
M = M1
C = C1
B = B1 and Player
, = Herbe
# = Ciel
W = Water
_ = Boat
O = OnBoard

Item = L1 or M1 or C1 or L2 or M2 or C2 or Death

=======
SOUNDS
=======

sfx0 5160507 (move)
sfx1 83704102 (jump)
EndGame 10475908

================
COLLISIONLAYERS
================

Background
Herbe,Ciel,Water,Boat
L1,M1,C1,B1,L2,M2,C2,B2,Death
OnBoard
Player

======
RULES
======

(Player movements)
LEFT [ LEFT Player | ] -> [  | Player ]
RIGHT [ RIGHT Player | ] -> [  | Player ]
DOWN [ DOWN Player ] -> [ Player ]
UP [ UP Player ] -> [ Player ]

(Boarding)
[Action Player L1 | | | B1 | OnBoard No Item ] -> [ | | | Player B1 | OnBoard L1 ] sfx0
[Action Player M1 | | B1 | OnBoard No Item ] -> [ | | Player B1 | OnBoard M1 ] sfx0
[Action Player C1 | B1 | OnBoard No Item ] -> [ | Player B1 | OnBoard C1 ] sfx0
[Action Player L2 | | | B2 | OnBoard No Item ] -> [ | | | Player B2 | OnBoard L2 ] sfx0
[Action Player M2 | | B2 | OnBoard No Item ] -> [ | | Player B2 | OnBoard M2 ] sfx0
[Action Player C2 | B2 | OnBoard No Item ] -> [ | Player B2 | OnBoard C2 ] sfx0

(Un-Boarding)
[Action Player OnBoard L1 | B1 | | | No Item ] -> [ OnBoard | B1 | | | Player L1 ] sfx0
[Action Player OnBoard M1 | B1 | | No Item ] -> [ OnBoard | B1 | | Player M1 ] sfx0
[Action Player OnBoard C1 | B1 | No Item ] -> [ OnBoard | B1 | Player C1 ] sfx0
[Action Player OnBoard L2 | B2 | | | No Item ] -> [ OnBoard | B2 | | | Player L2 ] sfx0
[Action Player OnBoard M2 | B2 | | No Item ] -> [ OnBoard | B2 | | Player M2 ] sfx0
[Action Player OnBoard C2 | B2 | No Item ] -> [ OnBoard | B2 | Player C2 ] sfx0

(Boat movement)
[Action Player B1 | OnBoard No Item | No Item | No Item ] -> [ | | OnBoard | Player B2 ]
[Action Player B1 | OnBoard C1 | No Item | No Item ] -> [ | | OnBoard C2 | Player B2 ]
[Action Player B1 | OnBoard M1 | No Item | No Item ] -> [ | | OnBoard M2 | Player B2 ]
[Action Player B1 | OnBoard L1 | No Item | No Item ] -> [ | | OnBoard L2 | Player B2 ]
[Action Player B2 | OnBoard No Item | No Item | No Item ] -> [ | | OnBoard | Player B1 ]
[Action Player B2 | OnBoard C2 | No Item | No Item ] -> [ | | OnBoard C1 | Player B1 ]
[Action Player B2 | OnBoard M2 | No Item | No Item ] -> [ | | OnBoard M1 | Player B1 ]
[Action Player B2 | OnBoard L2 | No Item | No Item ] -> [ | | OnBoard L1 | Player B1 ]

[ Boat ] -> [ Water ]
[ OnBoard | Water ] -> [ OnBoard | Boat ]
[ B1 | Water ] -> [ B1 | Boat ]
[ B2 | Water ] -> [ B2 | Boat ]

( Mouton mange chou? Loup mange Mouton?)
[M1 | C1 | No B1 ] -> [ M1 | Death | ]
[L1 | M1 | | No B1 ] -> [ L1 | Death | | ]
[M2 | C2 | No B2 ] -> [ M2 | Death | ]
[L2 | M2 | | No B2 ] -> [ L2 | Death | | ]

(Win?)
[ L2 | M2 | C2 ] -> Win


==============
WINCONDITIONS
==============


=======
LEVELS
=======

message When shepherd is not around, wolf eats sheep, sheep eats letuce.  You have only one free space on your boat. Bring everybody on right bank!

############
............
.LMCBO......
,,,,__WW,,,,