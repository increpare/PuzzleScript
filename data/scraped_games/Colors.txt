title Colors
author Nada Amin
homepage http://namin.net

run_rules_on_level_start

========
OBJECTS
========

Background
Black

Empty
Black

RedHole
Black

BlueHole
Black

YellowHole
Black

GreenHole
Black

StaleHole
Black

Top
Black

RedPeg 
Red
.000. 
00000
00000
00000
.000.     

BluePeg 
Blue
.000. 
00000
00000
00000
.000.     

YellowPeg 
Yellow
.000. 
00000
00000
00000
.000.     

GreenPeg 
Green
.000. 
00000
00000
00000
.000.     

Player
Orange
.....
.....
..0..
.....
.....


=======
LEGEND
=======

Hole = RedHole or BlueHole or YellowHole or GreenHole
Peg = RedPeg or BluePeg or YellowPeg or GreenPeg

Peg2 = Peg

RedPeg2 = RedPeg
BluePeg2 = BluePeg
YellowPeg2 = YellowPeg
GreenPeg2 = GreenPeg

RedHole2 = RedHole
BlueHole2 = BlueHole
YellowHole2 = YellowHole
GreenHole2 = GreenHole

. = Background
T = Top
O = Empty
P = Player and RedPeg

=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background
Top, Empty, Hole, Peg, StaleHole
Player

======
RULES     
======     

[Empty no Peg] -> [ random Peg ]
[Action Player RedPeg no RedHole | RedPeg2 no RedHole2] -> [ Player RedHole | RedHole2]
[Action Player BluePeg no BlueHole | BluePeg2 no BlueHole2] -> [ Player BlueHole | BlueHole2]
[Action Player YellowPeg no YellowHole | YellowPeg2 no YellowHole2] -> [ Player YellowHole | YellowHole2 ]
[Action Player GreenPeg no GreenHole | GreenPeg2 no GreenHole2] -> [ Player GreenHole | GreenHole2 ]
[RedHole | RedPeg no RedHole2] -> [ RedHole | RedHole2 ]
[BlueHole | BluePeg no BlueHole2] -> [ BlueHole | BlueHole2 ]
[YellowHole | YellowPeg no YellowHole2] -> [ YellowHole | YellowHole2 ]
[GreenHole | GreenPeg no GreenHole2] -> [ GreenHole | GreenHole2 ]
[ Hole no StaleHole] -> [ StaleHole ]
DOWN [Peg | StaleHole] -> [ StaleHole | Peg ]
Horizontal [Action Player StaleHole | Peg ] -> [ Player Peg | StaleHole]

==============
WINCONDITIONS
==============

No Peg

=======     
LEVELS
=======

TTTTTTTTTT
OOOOOOOOOO
OOOOOOOOOO
OOOOOOOOOO
OOOOOOOOOO
OOOOOOOOOO
OOOOOOOOOO
OOOOOOOOOO
OOOOOOOOOO
OOOOOOOOOO
OOOOOOOOOO
POOOOOOOOO