title Fall Leaves 
author Le Slo
homepage www.puzzlescript.net

run_rules_on_level_start
background_color #5d3a2f

========
OBJECTS
========

PlayerL
#ad6545 #c96845 #d97865
0.12.
.0012
.1112
..111
.....


PlayerR
#c96845 #ad6545  #d97865
..222
.0002
.0100
1100.
.....

PlayerN
#c96845 #ad6545  #d97865
.02..
.002.
11102
.000.
.00..

Vent1
#485259 #7a8891 
1..11
01.0.
..0..
.0.01
00..0

Vent2
#485259 #7a8891
..11.
1.0..
00001
..0.0
.00..

Air0
#86A2E4
.0...
...0.
.0.0.
.0...
...0.

Air1
#86A2E4
...0.
.0.0.
.0...
...0.
.0...

Air2
#86A2E4
.0.0.
.0...
...0.
.0...
...0.

Air3
#86A2E4
.0...
...0.
.0...
...0.
.0.0.

Air4
#86A2E4
...0.
.0...
...0.
.0.0.
.0...

Crate
#7b865e #3d743d
.000.
11100
11110
11110
.111.

Target
#5d3a2f
.....
.000.
.0.0.
.000.
.....

Background
#cfb39b #f5bda2 #e6b3a1 #d9b193 #dba886 #f0cbb9
01100
23340
44425
42245
32340

Background2
#9d7a5f #5d3a2f
11111
11111
11111
11111
11111


Wall
#9d7a5f #d07953 #c96845 #906646 #ad6545 #dd906f
01100
23340
44425
42245
32340

auxAnim
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = PlayerN
* = Crate
@ = Crate and Target
O = Target
x = vent1
` = vent2
y = Background2

vent = Vent1 or Vent2
collision = wall or crate or vent
air = Air0 or Air1 or Air2 or Air3 or Air4
pushable = vent or crate
player =  PlayerL or PlayerR or playerN

======
SOUNDS
=======

Crate MOVE 36772507
vent move 19444907
startgame 60152908
undo 31509107
restart 24415508
showmessage 5759908

sfx1 37419308
sfx0 36262108

================
COLLISIONLAYERS
================

Background
Background2
auxAnim
Target
air
Player, Wall, Crate, Vent

======
RULES
======
[right playerN no auxAnim] -> [right playerR auxAnim]
[left playerN no auxAnim] -> [left playerL auxAnim]
[right playerL no auxAnim] -> [right playerN auxAnim]
[left playerR no auxAnim] -> [left playerN auxAnim]
[down player no auxAnim] -> [down playerN auxAnim]

[ up player no air] -> [ player] sfx1

[ >  Player | Crate ] -> [  >  Player | > Crate  ]
[ >  Player | Vent ] -> [  >  Player | > Vent  ]
[ > pushable | pushable] -> [> pushable | > pushable]


up[vent | ... | player air | no collision] -> [vent | ... | up player | no collision]

(animations)
late [vent1] -> [vent2 auxAnim]
late [vent2 no auxAnim] -> [vent1]


late [air crate ] -> [crate]
late down[air | no air no vent] -> [ | ]

late [air0 no auxAnim] -> [ air1 auxAnim]
late [air1 no auxAnim] -> [ air2 auxAnim]
late [air2 no auxAnim] -> [ air3 auxAnim]
late [air3 no auxAnim] -> [ air4 auxAnim]
late [air4 no auxAnim] -> [ air0 auxAnim]

late [ auxAnim ] -> []

startLoop
late up[vent | no air no collision] -> [vent | Air0]
late up[air0 | no air no collision] -> [air0 | air1]
late up[air1 | no air no collision] -> [air1 | air2]
late up[air2 | no air no collision] -> [air2 | air3]
late up[air3 | no air no collision] -> [air3 | air4]
late up[air4 | no air no collision] -> [air4 | air0]
endLoop

late up[vent | ... | player air | no collision] -> again sfx0

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

(##########
#........#
#..#.*...#
#..p.#...#
#.x....#.#
#.....x..#
#...x....#
#..#o#...#
#..###...#
##########)



(SOLVABLE)

message 7 leaves left

########
#.p....#
#...*..#
#.*...`#
#.o...##
#...o.#y
#.....#y
#######y

message 6 leaves left

##########
#........#
#.*..p...#
#.....o..#
#.*o.....#
#.....o..#
#.*......#
#........#
###`##`###
yy######yy

message 5 leaves left

yy#######
###.....#
#.....o.#
#.*..*o.#
#.p.....#
#....*###
##....#yy
y##...#yy
yy#xxx#yy
yy#####yy

message 4 leaves left

yyy#######yyy
####..p..####
#.....#.....#
#..*..#.....#
#..#.....#o.#
#...#.......#
###...x...###
yy####`####yy
yyyyy###yyyyy

message 3 leaves left

y#########y
y#.#.#.#.#y
##.......##
#..p......#
#..*..#.o.#
#..x......#
#..x......#
#..x......#
###########

message 2 leaves left

##########
#.....#o.#
#.#p.....#
#.....#..#
#..*.....#
#....x#..#
#........#
#........#
#.x......#
##########

message 1 leaf left

yyy#####yyy
yyy#.#.#yyy
####.o.####
#.........#
#....p.x..#
#....*....#
#..x......#
#......x..#
#.........#
###########

message no more leaves
message welcome to winter

