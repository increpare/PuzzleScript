title Sokobaiogenesis
author Bagenzo
homepage twitter.com/bagenzo

color_palette 4
require_player_movement

========
OBJECTS
========

Background
darkblue

Void
black white
00000
00001
00000
01000
00010


WhiteTarget
white
.....
.0.0.
.....
.0.0.
.....

CGTarget
pink
.....
.0.0.
.0.0.
.0.0.
.....

TATarget
#DFFF00 (only losers call it yellow-green. it's chartreuse!)
.....
.000.
.....
.000.
.....

GTarget
blue
.....
..0..
.000.
..0..
.....

Wall
white gray lightgray
11111
12021
10201
12021
11111

VoidWall
#545454 darkgray black
11111
12021
10201
12021
11111

PlayerRed
Black brown White gray red
.333.
.414.
13331
13331
.3.3.
PlayerBlue
Black brown White gray blue
.333.
.414.
13331
13331
.3.3.
PlayerYellow
Black brown White gray yellow
.333.
.414.
13331
13331
.3.3.

PlayerGreen
Black brown White gray Green
.333.
.414.
13331
13331
.3.3.

FrankenPlayer
(It's actually FrankenPlayer's MONSTER, but whatever.)
Black green White gray black
.333.
.414.
13331
13331
.3.3.

PlayerSmall
Black brown White gray
33333
10101
33333
33333
33333

FrankenSmall
Black green White gray
33333
10101
33333
33333
33333

CGCrate
pink
00000
0.0.0
0.0.0
0.0.0
00000

TACrate
#DFFF00 (say line 41 again, but louder!)
00000
0...0
00000
0...0
00000

GCrate
blue
00000
00.00
0...0
00.00
00000

DNArb
red blue yellow black darkblue
02221
40213
44033
41203
12220


DNAbr
blue red yellow black darkblue
02221
40213
44033
41203
12220

Shrink
red blue yellow black darkblue gray lightblue
62225
66555
66666
66555
62225

Grow
red blue yellow black darkblue gray lightred
52226
55566
66666
55566
52226

Growdestin
darkblue

CNucleo
red darkred
01110
10000
10000
10000
01110

GNucleo
blue darkblue
01110
10000
10110
10010
01110

TNucleo
yellow orange
11111
00100
00100
00100
00100

ANucleo
green darkgreen
01110
10001
11111
10001
10001

Lab
red lightgray blue yellow green
13331
32103
31413
30123
13331

LabAni
red lightgray blue yellow green lightgreen
13331
34543
35453
34543
13331

LabAni2
red lightgray blue yellow lightgreen green
13331
34543
35453
34543
13331

LabAni4
red lightgray blue yellow purple pink
13331
34543
35453
34543
13331

LabAni5
red lightgray blue yellow pink purple
13331
34543
35453
34543
13331

EyeYellow
yellow white black
2.2.2
.111.
10001
10201
.111.

EyeRed
red white black
2.2.2
.111.
10021
10001
.111.

EyeGreenClosed
darkbrown brown black 
.111.
11111
11111
.000.
2.2.2

EyeGreen
green white black
2.2.2
.111.
10201
10001
.111.

Splitter
red blue yellow black darkblue gray #ca2c92
52225
65156
66666
65156
52225

=======
LEGEND
=======
Player = PlayerRed or PlayerBlue or PlayerYellow or PlayerGreen or PlayerSmall or FrankenPlayer or FrankenSmall
PlayerAlive = PlayerRed or PlayerBlue or PlayerYellow or PlayerGreen
PlayerBig = PlayerRed or PlayerBlue or PlayerYellow or PlayerGreen or FrankenPlayer
TinyVariant = FrankenSmall or PlayerSmall
Targets = WhiteTarget or CGTarget or TATarget or GTarget
Crate = CGCrate or TACrate or Gcrate
Nucleo = CNucleo or GNucleo or TNucleo or ANucleo
Tech = Grow or Shrink
DNAWall = DNArb or DNAbr
NonGuanine = CNucleo or ANucleo or TNucleo
Anis = LabAni or LabAni2 or Labani4 or LabAni5

. = Background
I = Void
# = Wall
@ = VoidWall
P = PlayerRed
$ = PlayerBlue
% = PlayerYellow
O = WhiteTarget
9 = CGTarget
K = TATarget
7 = GTarget
Z = CNucleo and Void
X = GNucleo and Void
C = TNucleo and Void
B = ANucleo and Void
1 = DNArb
2 = DNAbr
L = Lab
- = Shrink
+ = Grow
\ = Growdestin
f = EyeYellow
g = EyeRed
y = EyeGreenClosed
S = Splitter
=======
SOUNDS
=======

Crate MOVE 87129507
endlevel 19985908
sfx0 24677903
sfx1 64721103
sfx2 58275908 (frankenplayer)
sfx3 17479308 (shrink)
sfx4 27058708 (grow)
sfx5 99803708 (nuc push)
sfx6 42119108 (eyes)
sfx7 74328108 (split)
================
COLLISIONLAYERS
================

Background
Void, Growdestin
Lab, Shrink, Grow, Splitter, EyeYellow, EyeRed, EyeGreenClosed, EyeGreen
Targets
Player, Wall, Crate, DNArb, DNAbr, Nucleo, VoidWall
Anis

======
RULES
======


(Players don't bump into each other)
[ > Player | Player] -> [ Player | Player]

(Crates stay where they should)
[> Player|Crate|Tech] -> [Player|Crate| Tech]
[> Player|TACrate|Splitter] -> [Player|TACrate|Splitter]

(Sokoban rules)
[> Player | Crate] -> [> Player | > Crate]

(Sokoban rules, but for nucleotide sized players)
[> Player | Nucleo] -> [> Player | > Nucleo] sfx5


(Crate synthesis)
late [CNucleo|GNucleo] [Lab no Crate] -> [|] [CGCrate LabAni4] sfx0
late [TNucleo|ANucleo] [Lab no Crate] -> [|] [TACrate LabAni] sfx0

(Player synthesis) (It's... ALIVE!!)
late [CNucleo|GNucleo] [Lab TACrate] -> [|] [FrankenPlayer Lab] sfx2
late [TNucleo|ANucleo] [Lab CGCrate] -> [|] [FrankenPlayer Lab] sfx2

(Player / DNA connection)
[moving PlayerRed] [stationary CNucleo] -> [moving PlayerRed] [moving CNucleo]

[moving PlayerBlue] [stationary GNucleo] -> [moving PlayerBlue] [moving GNucleo]

[moving PlayerYellow] [stationary TNucleo] -> [moving PlayerYellow] [moving TNucleo]

[moving PlayerGreen] [stationary ANucleo] -> [moving PlayerGreen] [moving ANucleo]

(Eye change)
late [PlayerAlive EyeYellow] -> [PlayerYellow] sfx6
late [PlayerAlive EyeRed] -> [PlayerRed] sfx6
late [PlayerAlive EyeGreen] -> [PlayerGreen] sfx6

(Frankeneye spawn)
late [FrankenPlayer Lab] [EyeGreenClosed] -> [FrankenPlayer Lab] [EyeGreen]

(shrink workaround for tiny frankenplayers)
late[FrankenSmall Shrink|Void] -> [Shrink|Void FrankenSmall]

(Shrinking)
late [FrankenPlayer Shrink] [FrankenPlayer Shrink|Void] -> [Shrink] [Shrink|Void FrankenSmall] sfx3
late [Player Shrink] [Player Shrink|Void] -> [Shrink] [Shrink|Void Playersmall] sfx3

(Growing)
late [FrankenSmall Grow] [Growdestin|Grow] -> [Grow] [FrankenPlayer Growdestin|Grow] sfx4
late [PlayerSmall Grow] [Growdestin|Grow] -> [Grow] [PlayerBlue Growdestin|Grow] sfx4

(Make sure big players can't get any larger)
late[Growdestin] [PlayerBig Grow] -> [Growdestin PlayerBig] [Grow]

(Nucleotides can't escape tiny zone)
[ > Nucleo | Tech] -> [Nucleo | Tech]

(Splitter Crate -> Nucleo)
late [CGCrate Splitter] [CGcrate Splitter|Void] -> [Splitter] [Splitter|Void GNucleo]
late [Gcrate Splitter] [Gcrate Splitter|Void] -> [Splitter] [Splitter|Void GNucleo] sfx7

(Splitter Nucelo -> Crate)
late [GNucleo Splitter] [GNucleo Splitter| Background no DNAwall] -> [Splitter] [Splitter| Background Gcrate] sfx7

(prevent others from using splitter)
[> NonGuanine |Splitter] -> [NonGuanine |Splitter] 
[> Player |Splitter] -> [Player |Splitter] 

(tiny lab animation)
late [LabAni2] -> [Lab]
late [LabAni] -> [LabAni2] again

late [LabAni5] -> [Lab]
late [LabAni4] -> [LabAni5] again
==============
WINCONDITIONS
==============

All WhiteTarget on Crate
All TATarget on TACrate
All CGTarget on CGCrate
All GTarget on GCrate
Some Player

=======
LEVELS
=======
(test)

(
......2iiiiii
......1iiiiii
......2iiiiii
......1iiiiii
......2iiiiii
......1iiiiii
......2iiiiii

......2iixiii
.9...\+iiiiic
.k.o..2iziixi
..$...1iibiii
....l.2iiiizi
..o...-iciibi
......2iiiiii

#######2i@b@ii
######\+iii@ix
######k2@@i@@b
y......siiiixi
..#.##72@@czcz
###l..#1i@iizi
$...#..-iciiii
#######1@@@@@@

)

(REAL LEVELS)
message Welcome to SOKOBAN LABS! Unfortunately, due to a total lack of crates, warehouse keepers/scientists/warehouse scientists will be required to synthesize their own crates from base pairs.

######2iiiiii
.l..o#1iziiii
.....#2iiiiii
.##..#1iiiiii
..####2iiiiii
#.####1iiiixi
$.####2iiiiii

message Yes, the crates are alive. Don't worry too much about it.

#####.2iiiiii
#$####1izicci
....o#2iibiii
.#.###1bbiiii
..l..#2ixbiii
#....#1iibbic
..#..#2iiiiii


message Lab personnel may be required to shrink down to manually manipulate nucleotides
##....-iiiiii
##.###2i@@i@i
#.l..\+iiii@i
##o###2@z@i@i
######1@@@c@i
......2iiii@i
......-xi@i@i
..$...2ii@iii


######2iiiiii
######1iiz@ii
##k#o#2@@@@xi
..$...-iiiiii
#9l...2iii@c@
##.###1iii@i@
##...\+iiz@bx




message An outbreak of red and yellow eye has been spotted at the facility. Side effects may include itchiness and the ability to manipulate different nucleotides.
#.....2iiiiii
#.f...1ibii@@
####k.2ii@iii
....l.1iii@xx
..##9.2i@i@ci
..#.g.1i@i@ix
$##...2z@x@ii

message Side effects of shrinking may include: dizziness, a blue hue in the iris, and the ability to only manipulate guanine afterwards.
######2iiiiii
%ool.o1iiiizi
.###.#2@ii@ii
..##.#1iixzii
..##.#2iiii@i
#.....-iixiii
#####\+iiz@xi

message Lab memo: Creating a C-G Crate on a A-T Crate may have... unintended consequences.
..####1iicibz
..#...-i@@@@@
.l#...1iiiiii
..###.2ii@i@i
..9.#.1ibxiii
.k..#\+i@xiii
######1@@@c@z
####$.-iiii@@

##9..\+iiiiii
###.##2iibiii
###.##1i@@@@i
###.##2i@ci@i
###l##1i@@iii
###.#.-iiii@@
..%...1iii@xz
......2iii@zx

.o.2iiiiiiii@@@@@
...-iizzixii@@@@@
#l#2ic@@@@ii@iiib
...1iiixii@i@@i@@
.k\+ii@@@bii@@iii
###1@@@ziiiii@c@i
%..-iiiiiiiiiiiii

...#.\+ii@iiib@
o.o#..1ibi@i@@@
...#..2ii@@i@xz
...l..1iicci@zx
...#..2ii@@i@@@
...#..1iii@i@zx
...#.$-iiiii@xz

message Experiment SB-04: By manipulating Frankenstein DNA, we might find a way to control adenine...
$...##2xiii@ib
###.##1@@@i@ii
....#k2ziii@ii
.####.1iiic@ii
..#fl.2@@@i@ii
.y....1ii@i@@i
######2ii@b@ci



message Good news - we can now turn Guanine into Crates and Crates with Guanine into Guanine!
#######2ii@iz@@
###$##f1ii@ii@@
....#9\+ibbi@c@
.####..-ii@x@c@
.####k.2@@x@@i@
.##k.l.siiiiiz@
.##.#..2@@@@@b@
......71iiiiiz@
#####..siiii@b@



message Last experiment of the day!
#....7-iiiizbic@@
##...71i@i@@@@@@@
#####.2i@i@xi@iii
#####\+iii@bi@iii
.y###.2@@i@@iciii
......siibbxi@iii
.##.#.2zzi@@z@cii
#..l..1@@iiii@x@i
$...#.-iiii@c@cii

message THE END
