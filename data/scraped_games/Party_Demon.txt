title Party Demon
author Peter Smyth
homepage www.petersmyths.tumblr.com

run_rules_on_level_start
noaction

========
OBJECTS
========

Background
lightred DarkGray
00000
00000
00000
00000
00000

WallMiddle
Black Black Brown DarkBrown
00110
00110
11001
11001
00110

WallTop
Black Black Brown DarkBrown
22222
30330
13003
31031
00110

WallBottom
Black Black Brown DarkBrown
00110
03113
31030
03303
22222

WallRight
Black Black Brown DarkBrown
03132
00312
11032
13032
00312

WallLeft
Black Black Brown DarkBrown
20310
23130
23001
21301
23130

WallTopLeftCorner
Black Black Brown DarkBrown
22222
23130
23001
21301
23130

WallTopRightCorner
Black Black Brown DarkBrown
22222
30332
13002
31032
00112

WallBottomLeftCorner
Black Black Brown DarkBrown
20110
23113
21031
23303
22222

WallBottomRightCorner
Black Black Brown DarkBrown
03132
00312
11032
13032
22222

WallRightStub
Black Black Brown DarkBrown
22222
00112
11002
11002
22222

WallLeftStub
Black Black Brown DarkBrown
22222
20110
21001
21001
22222

WallTopStub
Black Black Brown DarkBrown
22222
20112
21002
21002
20112

WallBottomStub
Black Black Brown DarkBrown
20112
20112
21002
21002
22222

WallAlone (is all alone)
Black Black Brown
22222
20112
21002
21002
22222

Player
Black Red DarkRed
.0.0.
.111.
22222
.111.
.1.1.

Crate
blue Black DarkBlue lightblue
..101
00000
01000
00111
.000.

HorSadCrate
blue Black DarkBlue lightBlue
..101
00000
01000
00111
.000.

VertSadCrate
blue Black DarkBlue
..101
00000
01000
00111
.000.

(Crate is sad when it's alone)
SadCrate
Green Black 
..101
00000
00111
01000
.000.

StickyCrate
Red Black
..101
00000
01000
00111
.000.

HorSadStickyCrate
Red Black
..101
00000
01000
00111
.000.

VertSadStickyCrate
Red Black
..101
00000
01000
00111
.000.

(Sticky Crate is Sad when it's alone too)
SadStickyCrate
DarkRed Black
..101
00000
00111
01000
.000.

DiscoBall
White Gray DarkGray
.101.
10101
01010
10101
.101.

DiscoLightHorizontal
Yellow
.....
.....
.000.
.....
.....

DiscoLightVertical
Yellow
.....
..0..
..0..
..0..
.....


=======
LEGEND
=======

. = Background
M = WallMiddle
T = WallTop
B = WallBottom
L = WallLeft
R = WallRight
P = Player
* = Crate
$ = StickyCrate
D = DiscoBall

Wall = WallBottom or WallTop or WallLeft or WallRight or WallMiddle or WallTopRightCorner or WallTopLeftCorner or WallBottomRightCorner or WallBottomLeftCorner or WallBottomStub or WallTopStub or WallRightStub or WallLeftStub or WallAlone

Moveable = Crate or VertSadCrate or HorSadCrate or SadCrate or 			StickyCrate or VertSadStickyCrate or HorSadStickyCrate or 				SadStickyCrate or DiscoBall
Happy = Crate or VertSadCrate or HorSadCrate or StickyCrate or 			VertSadStickyCrate or HorSadStickyCrate

Normal = Crate or VertSadCrate or HorSadCrate or SadCrate

Sticky = StickyCrate or VertSadStickyCrate or HorSadStickyCrate or 				SadStickyCrate

HappyNormal = Crate or VertSadCrate or HorSadCrate

HappySticky = StickyCrate or VertSadStickyCrate or HorSadStickyCrate

=======
SOUNDS
=======

(Moveable MOVE 36772507)

================
COLLISIONLAYERS
================

Background
DiscoLightHorizontal, DiscoLightVertical
Player, Wall, Crate, SadCrate, HorSadCrate, VertSadCrate, StickyCrate, SadStickyCrate, HorSadStickyCrate, VertSadStickyCrate, DiscoBall

======
RULES
======

(Change walls on corners and edges)
left [ Wall | no wall ] -> [ WallLeft | no Wall ]
right [ Wall | no wall ] -> [ WallRight | no Wall ]
up [ Wall | no wall ] -> [ WallTop | no Wall ]
down [ Wall | no wall ] -> [ WallBottom | no Wall ]

left [ WallTop | no wall ] -> [ WallTopLeftCorner | no Wall ]
right [ WallTop | no wall ] -> [ WallTopRightCorner | no Wall ]
left [ WallBottom | no wall ] -> [ WallBottomLeftCorner | no Wall ]
right [ WallBottom | no wall ] -> [ WallBottomRightCorner | no Wall ]

up [ WallBottomRightCorner | no Wall ] -> [ WallRightStub | no Wall ]
down [ WallTopRightCorner | no Wall ] -> [ WallRightStub | no Wall ]
up [ WallBottomLeftCorner | no Wall ] -> [ WallLeftStub | no Wall ]
down [ WallTopLeftCorner | no Wall ] -> [ WallLeftStub | no Wall ]
right [ WallBottomLeftCorner | no Wall ] -> [ WallBottomStub | no Wall]
left [ WallBottomRightCorner | no Wall ] -> [ WallBottomStub | no Wall]
right [ WallTopLeftCorner | no Wall ] -> [ WallTopStub | no Wall]
left [ WallTopRightCorner | no Wall ] -> [ WallTopStub | no Wall]

left [ WallRightStub | no Wall ] -> [ WallAlone | no Wall ]
right [ WallLeftStub | no Wall ] -> [ WallAlone | no Wall ]
up [ WallBottomStub | no Wall ] -> [ WallAlone | no Wall ]
down [ WallTopStub | no Wall ] -> [ WallAlone | no Wall ]

(Fuck the old disco lights, i.e. remove them)
[ DiscoLightHorizontal ] -> []
[ DiscoLightVertical ] -> []

(Movement and basic pushery)
[ >  Player | Moveable ] -> [  >  Player | > Moveable ]
[ > Player | Moveable | Moveable] -> [ > Player | > Moveable | > Moveable ]
[ > Player | Moveable | Moveable | Moveable] -> [ > Player | > Moveable | > Moveable | > Moveable ]
[ > Player | Moveable | Moveable | Moveable | Moveable ] -> [ > Player | > Moveable | > Moveable | > Moveable | > Moveable]

(Sticky block collisions)
[ > Player | Moveable | Wall] -> cancel
[ > Player | Moveable | Moveable | Wall ] -> cancel

rigid [ moving Sticky | stationary Sticky | stationary Sticky] -> [ moving sticky 	| moving sticky | stationary Sticky]
[ moving Sticky | stationary Sticky | no Sticky] -> [ moving sticky | moving 	sticky | no Sticky]

(Check normal blocks for solitude)
late [ Normal | Normal] -> [ Crate | Normal ]
late HORIZONTAL [ no Normal | Crate | no Normal] -> [ | HorSadCrate | ]
late VERTICAL [ no Normal | Crate | no Normal] -> [ | VertSadCrate | ]
late HORIZONTAL [ no Normal | VertSadCrate | no Normal ] ->								[ | SadCrate | ]
late Vertical [ no Normal | HorSadCrate | no Normal ] ->								[ | SadCrate | ]

(Check sticky blocks for solitude)
late [ Sticky | Sticky] -> [ StickyCrate | Sticky ]
late HORIZONTAL [ no Sticky | StickyCrate | no Sticky] -> [ | HorSadStickyCrate | ]
late VERTICAL [ no Sticky | StickyCrate | no Sticky] -> [ | VertSadStickyCrate | ]
late HORIZONTAL [ no Sticky | VertSadStickyCrate | no Sticky ] ->								[ | SadStickyCrate | ]
late Vertical [ no Sticky | HorSadStickyCrate | no Sticky ] ->								[ | SadStickyCrate | ]

(Make Disco Light)
late Horizontal [ DiscoBall | no DiscoLightHorizontal no Moveable no Wall ] -> [ DiscoBall | DiscoLightHorizontal ]
late Vertical [ DiscoBall | no DiscoLightVertical no Moveable no Wall ] -> [ DiscoBall | DiscoLightVertical ]
late Horizontal [ DiscoLightHorizontal | no DiscoLightHorizontal no Moveable no Wall ] -> [ DiscoLightHorizontal | DiscoLightHorizontal ]
late Vertical [ DiscoLightVertical | no DiscoLightVertical no Moveable no Wall ] -> [ DiscoLightVertical | DiscoLightVertical ]

(Disco Ball light makes anyone that sees it happy)
late Horizontal [ SadCrate | DiscoLightHorizontal ] -> [ Crate | DiscoLightHorizontal ]
late Vertical [ SadCrate | DiscoLightVertical ] -> [ Crate | DiscoLightVertical ]
late [ SadCrate | DiscoBall ] -> [ Crate | DiscoBall ]
late Horizontal [ SadStickyCrate | DiscoLightHorizontal ] -> [ StickyCrate | DiscoLightHorizontal ]
late Vertical [ SadStickyCrate | DiscoLightVertical ] -> [ StickyCrate | DiscoLightVertical ]
late [ SadStickyCrate | DiscoBall ] -> [ StickyCrate | DiscoBall ] 

==============
WINCONDITIONS
==============

No Happy 

=======
LEVELS
=======

message You are the party demon, it's your job to make everyone at the party sad

message Blue people are happy when they're with their friends

mmmmmmm
mm...mm
mm.p.mm
mm...mm
mmm.mmm
mmm*mmm
mmm.mmm
m.....m
m.**..m
m.....m
m.....m
mmmmmmm

mmmmmmmm
mmmmmmmm
mmp...mm
mm.**.mm
mm.**.mm
mm....mm
mmmmmmmm
mmmmmmmm

mmmmmm
mp...m
m.**.m
m.**.m
mm..mm
m.**.m
m.**.m
m....m
mmmmmm

(
mmmmmmmm
mp.....m
m..**..m
m..mm..m
m**mm**m
m..mm..m
m..mm..m
m**mm**m
m..mm..m
m..**..m
m......m
mmmmmmmm
)

message Red people like to be social too, and they'll try to stick together

mmmmmmm
mp....m
m.....m
mm$$$mm
m.....m
m.....m
m.m...m
m.....m
m.....m
mmmmmmm

mmmmmmmm
mm$....m
m$$....m
m......m
m.pmm$.m
m......m
m....$$m
m....$mm
mmmmmmmm

message Blues and Reds don't make each other happy or sad

mmmmmmmm
mmmp.mmm
mmm..mmm
mmm*$mmm
m......m
m.$$**.m
m......m
mmmmmmmm

mmmmmmmmm
mp......m
m.**.$$.m
m.**.$$.m
m.......m
mmmmmmmmm

message Disco balls make anyone happy!

mmmmmmmmmmm
mmmmmdmdmmm
mmmm*....mm
mp..*....mm
mmmm*....mm
mmmmm....mm
mmmmm....mm
mmmmm....mm
mmmmm.....m
mmmmmmdmdmm
mmmmmmmmmmm

mmmmmmmm
mmmdmmmm
mp....mm
m..**.mm
m.***.dm
m.**..mm
m.....mm
mmmmmmmm

mmmmmmmm
mmmmmmmm
mmp...mm
mm.**dmm
mm.**.mm
mm....mm
mmmmmmmm
mmmmmmmm

mmmmmmmmm
mmmm*mmmm
mmm....mm
mm..**.mm
m*.*d*.*m
mm.**..mm
mmp...mmm
mmmm*mmmm
mmmmmmmmm

mmmmmmmmm
md$.$.$dm
m.......m
m.......m
mm..$..mm
m.......m
m.$...$.m
mp..m...m
mmmmmmmmm

message Congratulations, you made everyone miserable!

