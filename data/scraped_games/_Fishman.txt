Title  Fishman
Author Connorses
Homepage connorspuzzles.tumblr.com
run_rules_on_level_start
========
OBJECTS
========
PlayerG
green yellow darkblue
0.00.
.0020
0.00.
.1.1.
.1.1.

PlayerJ
green yellow darkblue
0.00.
.0020
0.00.
.1.11
1....

background
darkblue

wall1
darkgrey
00000
00000
00000
00000
00000

wall2
darkgrey
00.00
0.0.0
00.00
0.0.0
00.00

rain
blue
...0.
...0.
0..0.
0....
0....

cloud
grey darkgrey lightgrey 
.222.
20022
00000
11111
.....

ladderopen
brown
0...0
00000
0...0
00000
0...0

ladderfloor
brown darkgrey
11111
0...0
00000
0...0
00000

goal
white blue darkblue
0...0
20102
01110
01110
20002

crateo
orange
00000
0..00
0.0.0
00..0
00000

crater
orange
00000
0..0.
0.0..
00...
00000

cratel
orange
00000
...00
..0.0
.0..0
00000

move
transparent
dontmove
transparent

=======
LEGEND
=======
wall = wall1 or wall2
crate = crateo or cratel or crater
wproof = wall1 or crate
player = playerg or playerj
solid=wall or crate or ladderfloor
falling = crate
ladder = ladderfloor or ladderopen
. = background
p= playerg
#= wall1
%=wall2
; = cloud
" = ladderopen
- = ladderfloor
g=goal
o=crateo

=======
SOUNDS
=======
sfx0 66807308
sfx1 68012504
endlevel 99644508
startgame 15349308
player cantmove 68012504

================
COLLISIONLAYERS
================
background
rain,cloud
ladder, ladderfloor
player, wall, crate
goal
======
RULES
======
[> player|no rain no goal no crate] -> cancel
down [playerg no ladder|no solid no ladder] -> [down playerg| ] again
down [falling|no solid] -> [down falling| ]

[> playerj] -> [> playerg] again
down [up playerg no ladder|solid] -> [up playerj|solid] sfx0
down [up playerg no ladder|no solid] -> cancel

horizontal [> player|crate] -> [> player|> crate]
down [horizontal player|solid] -> [horizontal player|solid] sfx0
down [horizontal player|ladder] -> [horizontal player|ladder] sfx0

late [playerj ladder] -> [playerg ladder]
late down [playerg no ladder|no solid no ladder] -> [playerg| ] again
late down [falling|no solid] -> [falling|no solid] again
late [rain] -> [ ]
late down [cloud|no wproof] -> [cloud|rain]
late down [rain|no wproof] -> [rain|rain]
late [player no rain no goal] -> cancel

==============
WINCONDITIONS
==============
some player on goal
=======
LEVELS
=======
message GET TO THE FISHBOWL
;;;;;;;;;;;
...........
...........
.........##
.....##%...
."%%%......
#..........
##.p...g...
###########
###########

;;;;;;;;;
.........
.........
.........
......o.#
##-%%%%%#
..".....g
.."...-##
.."..p"..
#########

;;;;;;;;;;;
...........
...........
.."#.."o...
.."#%%"%%".
.."..."..".
.."...o..".
.."#%%%%%".
.p".g....".
###########

;;;;;;;;;;
..........
....o"....
..o.o"....
####%"..g.
.....".###
........##
.....op###
##########
