Title Sokobot
Author Loneship Games
again_interval 0.3
color_palette pastel
========
OBJECTS
========
b1
darkblue darkgrey
10000
10000
10000
10000
11111

b2
darkblue


rU
lightgrey black white
0...0
00000
12221
12221
12221

rD
lightgrey black white
12221
12221
12221
00000
0...0

rL
lightgrey black white
00111
.0222
.0222
.0222
00111

rR
lightgrey black white
11100
2220.
2220.
2220.
11100

stepF
green darkgreen
.....
..0..
.000.
.111.
.....



stepL
green darkgreen
.....
..01.
.001.
..01.
.....
stepR
green darkgreen
.....
.10..
.100.
.10..
.....
stepB
green darkgreen
.....
.111.
.000.
..0..
.....

empty
lightgrey white
01011
0...1
.....
0...1
00000

step2
blue
..0..
.0.0.
0...0
.0.0.
..0..

step3
red
..0..
.0.0.
0...0
.0.0.
..0..


off
darkgreen black
.00..
.000.
.001.
.11..
.....

on
green
.....
.00..
.000.
.00..
.....


line
darkred
.....
..00.
...00
..00.
.....

line2
red
.....
..00.
...00
..00.
.....

player
yellow
00.00
0...0
.....
0...0
00.00

return
red
.....
.....
..0..
.....
.....

wall
brown

crate
orange
00000
0...0
0...0
0...0
00000
target
lightgrey
.....
.000.
.0.0.
.000.
.....
=======
LEGEND
=======
, = b1
. = b2
u = rU and b1
d = rD and b1
l = rL and b1
r = rR and b1
+ = Player and empty and b2
[ = empty and b2
\ = off and b2
} = line  and b2
2 = step2 and b2
3 = step3 and b2
# = wall
o = crate and b1
* = target  and b1
@ = target and crate and b2
' = step2 and return and b2
" = step3 and return and b2

robot = ru or rd or rl or rr
step = stepF or stepR or stepL or stepB or step2 or step3
background = b1 or b2
=======
SOUNDS
=======
crate move 15668707
robot cantmove 13824702
robot move 23238307
startgame 32845708
endlevel 33550908
================
COLLISIONLAYERS
================
background
empty, target
robot, stepf, stepr, stepl, stepb, off, on, line, line2 step2, step3, crate, wall
return
player

======
RULES
======
[> player on] -> cancel
[action player on] -> cancel

(allow player to set rules)
[action player empty no step] -> [player empty stepF] checkpoint
[action player empty stepF] -> [player empty stepR] checkpoint
[action player empty stepR] -> [player empty stepL] checkpoint
[action player empty stepL] -> [player empty stepB] checkpoint
[action player empty stepB] -> [player empty step2] checkpoint
[action player empty step2 return] -> [player empty step3 return] checkpoint
[action player empty step3 return] -> [player empty] checkpoint
[action player empty step2] -> [player empty step3] checkpoint
[action player empty step3] -> [player empty] checkpoint

up [step2|...|step2 no empty] -> [step2 return|...|step2]
up [step3|...|step3 no empty] -> [step3 return|...|step3]

(start robot)
right [no line|action player off] -> [ |player on] again
(run program)
[line2] -> [line]

[on] [line|empty no step] -> [on] [down line2|empty] again

[on] [line|stepF] [rU] -> [on] [down line2|stepF] [up    rU] again
[on] [line|stepF] [rD] -> [on] [down line2|stepF] [down  rD] again
[on] [line|stepF] [rL] -> [on] [down line2|stepF] [left  rL] again
[on] [line|stepF] [rR] -> [on] [down line2|stepF] [right rR] again

[on] [line|stepR] [rU] -> [on] [down line2|stepR] [rR] again
[on] [line|stepR] [rD] -> [on] [down line2|stepR] [rL] again
[on] [line|stepR] [rL] -> [on] [down line2|stepR] [rU] again
[on] [line|stepR] [rR] -> [on] [down line2|stepR] [rD] again

[on] [line|stepL] [rU] -> [on] [down line2|stepL] [rL] again
[on] [line|stepL] [rD] -> [on] [down line2|stepL] [rR] again
[on] [line|stepL] [rL] -> [on] [down line2|stepL] [rD] again
[on] [line|stepL] [rR] -> [on] [down line2|stepL] [rU] again

[on] [line|stepB] [rU] -> [on] [down line2|stepB] [down  rU] again
[on] [line|stepB] [rD] -> [on] [down line2|stepB] [up    rD] again
[on] [line|stepB] [rL] -> [on] [down line2|stepB] [right rL] again
[on] [line|stepB] [rR] -> [on] [down line2|stepB] [left  rR] again

(Blue Function)
right [on] [line|step2 empty no return] [no line|step2 no empty no return] -> [on] [ |step2 empty return] [down line2|step2] again

right [on] [line|no empty step2 return] [ |empty step2 return] -> [on] [ |step2 return] [down line2|empty step2] again

right [on] [line|empty step2 return] [ |no empty step2] -> [on] [ |empty step2 return] [down line2|step2] again

(Red Function)
right [on] [line|step3 empty no return] [no line|step3 no empty no return] -> [on] [ |step3 empty return] [down line2|step3] again

right [on] [line|no empty step3 return] [ |empty step3 return] -> [on] [ |step3 return] [down line2|empty step3] again

right [on] [line|empty step3 return] [ |no empty step3] -> [on] [ |empty step3 return] [down line2|step3] again

(push crates!)
[> robot|crate] -> [> robot|> crate]


==============
WINCONDITIONS
==============
all crate on target
=======
LEVELS
=======

.........
.#####}+\
.#,,,#.[.
.#,*,#.[.
.#,o,#.[.
.#,,,#.[.
.#,u,#...
.#####...
.........

............
.########}+\
.#,,,,,,#.[.
.#,,,o*,#.[.
.#,,,,,,#.[.
.#,u,####.[.
.#,,,#......
.#####......
............

#######}+...
#,,*,,#.[.\.
#*ooo*#.[...
#,,u,,#.[...
#######.[...

########....2.
#,,,,,,#}+..[.
#,####,#.[..[.
#,#..#,#.[..[.
#,#..#,#.[..[.
#u#..#o#.[..[.
######*#....[.
.........\..[.
............'.

########....2
#,*,,,,#....[
#,o,,o*#}+..[
#,,,,,,#....[
#,u,,,,#.\..[
#*o,,o,#....[
#,,,,*,#....'
########.....

########}+...
#,,**,,#.[..2
#,,oo,,#.[..[
#*o,,,,#.[..[
#*o,,,,#.[..[
#,,u,,,#.[..[
#,,,,,,#.[..'
########.[.\.
.............

..........2.
.#####}+..[.
.#,,,#.[..[.
.#,#,#.[..[.
.#,#,#.[..[.
.#u#,#....'.
.###o#.\....
...#*#......
...###......
............

.............
...........2.
.######}+..[.
.#,*,,#.[..[.
.#,,ol#.[..[.
.#,,###....[.
.####...\..'.
.............

....###.....2.
....#*#.....[.
...##o##}+..[.
.###,,##.[..[.
.#,,o*,#.[..[.
.#,*#,,#.[..[.
.#,o####.[..[.
.##u####....[.
..#####..\..[.
............[.
............[.
............'.

##########.....3.
#,,,,,,o*#.}+..[.
#,,,,,,,,#.....[.
#,,,,,,,,#.....[.
#,,,,,,,,#..\..[.
#,,,,,,,,#.....[.
#,,,,,,,,#.....".
#u,,,,,,,#.......
##########.......

##########.....2..3
#,,,,,,o*#.}+..[..[
#,,,,,,,,#.....[..[
#,,,,,,,,#..\..[..[
#,,,,,,,,#.....'.."
#,,,,,,,,#.........
#,,,,,,,,#.........
#u,,,,,,,#.........
##########.........

######....2.
#**,,#}+..[.
#oo,,#....[.
#uo*,#....[.
#oo,,#..\.[.
#**,,#....[.
######....[.
..........'.

########...2.
#*,,,,,#}+.[.
#,,o,*,#...[.
#,,*,,,#.\.[.
#o,o,o*#...[.
#,,u#,,#...[.
#,,,,,##...[.
########...'.
.............

.................
.,,*,,,,..\..2..3
.,,,,o,*.}+..[..[
.,,o,,,,..[..[..[
.,,,,,,,..[..[..[
.*,,o,o,..[..[..[
.,,u,,*,.....[..[
.,,,,,,,.....'.."
.................

............2.
........}+\.[.
......d..+..[.
.........+..[.
......l..[..[.
.........[..[.
......u..[..[.
.........[..[.
............'.