title the gods await me
homepage www.puzzlescript.net

run_rules_on_level_start
zoomscreen 9x9
noundo
norestart
noaction

(debug)

========
OBJECTS
========

background
white

void
black yellow
10000
00100
00001
01000
00000

outervoid
black yellow
10000
00100
00001
01000
00000

passablevoid
darkgrey yellow
10000
00100
00001
01000
00000

blockablevoid
white

dangerousvoid
white

watervoid
white

grass
brown lightgreen
00000
00000
10100
01010
01010

dirt
brown

water
blue lightblue
00010
00100
00000
00010
01000

stone
grey

stonecrack1
grey darkgrey
10000
01000
00000
00000
00000

stonecrack2
grey darkgrey
10000
01100
00000
00000
00000

stonecrack3
grey darkgrey
10000
01010
00100
00010
00000

stonecrack4
grey darkgrey
10001
01010
00100
00010
00010

searchingplayer
black blue
..0..
00000
.010.
.000.
.0.0.

spottedplayer
black darkgreen
..0..
00000
.010.
.000.
.0.0.

deadplayer
black red
...0.
0000.
.0100
00001
.110.

playertarget
white

corpsetarget
white

spawnenemy
white

enemy
darkred
..0..
.000.
..0..
.000.
.0.0.

stabbedenemy
darkred red
.....
00.0.
.0000
00101
11111

drownedenemy
blue lightblue darkred
00010
22120
02222
22020
01000

lightaltar
brown lightgrey yellow
00000
20002
10001
11111
11111

darkaltar
brown darkred yellow
00000
20002
10001
11111
11111

path1
black
.....
.....
.....
.....
.....

path2
darkgrey
.....
.....
.....
.....
.....

path3
grey
.....
.....
.....
.....
.....

path4
lightgrey
.....
.....
.....
.....
.....

path5
white
.....
.....
.....
.....
.....

movetok
green


=======
LEGEND
=======
aliveplayer = searchingplayer or spottedplayer
entity = aliveplayer or enemy
player = aliveplayer or deadplayer
deadenemy = stabbedenemy or drownedenemy

anystone = stone or stonecrack1 or stonecrack2 or stonecrack3 or stonecrack4

blockableterrain = grass or dirt or stone
dangerousterrain = grass or dirt or stone or spawnenemy
waterterrain =     grass or dirt or water
passableterrain =  grass or dirt

terrain = passableterrain or blockableterrain or dangerousterrain or waterterrain

genterrain = passablevoid or blockablevoid or dangerousvoid or watervoid

obstacle = stone or deadenemy or water or drownedenemy or corpsetarget

path = path1 or path2 or path3 or path4 or path5
shorter5 = path4 or path3 or path2 or path1
shorter4 = path3 or path2 or path1
shorter3 = path2 or path1
shorter2 = path1

. = void
, = outervoid
g = passablevoid
p = searchingplayer and passablevoid
e = enemy and passablevoid
a = lightaltar 

=======
SOUNDS
=======


(wookie: 65197308)
sfx0 13477304 ( kill enemy )
sfx1 42702108 ( sacrifice self )
sfx7 14928708 ( sacrifice self [dark] )
sfx2 88048109 ( sacrifice enemy )
sfx3 64505308 ( player death )
sfx4 85602504 ( crack rock )
sfx5 81751908 ( break rock )
sfx6 69525702 ( drown enemy )


================
COLLISIONLAYERS
================

background

void, passablevoid, blockablevoid, dangerousvoid, watervoid, grass, dirt, outervoid, drownedenemy

path1, path2, path3, path4, path5
movetok, spawnenemy, playertarget, corpsetarget

water, lightaltar, darkaltar

aliveplayer, deadplayer, enemy, stabbedenemy, stone, stonecrack1, stonecrack2, stonecrack3, stonecrack4 

======
RULES
======

((((((WORLDGEN))))))
random [ outervoid ] -> [ passablevoid lightaltar ]
random [ outervoid ] -> [ passablevoid lightaltar ]
random [ outervoid ] -> [ passablevoid lightaltar ]
random [ outervoid ] -> [ passablevoid lightaltar ]
[ outervoid ] -> [ void ]

[ void ] -> [ random genterrain ] 
+ [ dangerousvoid ] -> [ random dangerousterrain ]
+ [ blockablevoid ] -> [ random blockableterrain ]
+ [ passablevoid ] -> [ random passableterrain ]
+ [ watervoid ] -> [ random waterterrain ]
+ [ spawnenemy ] -> [ enemy random passableterrain ]

(((((DIGGING)))))
[ > aliveplayer | stone ] ->       [ aliveplayer | stonecrack1 ] sfx4
+ [ > aliveplayer | stonecrack1 ] -> [ aliveplayer | stonecrack2 ] sfx4
+ [ > aliveplayer | stonecrack2 ] -> [ aliveplayer | stonecrack3 ] sfx4
+ [ > aliveplayer | stonecrack3 ] -> [ aliveplayer | stonecrack4 ] sfx4
+ [ > aliveplayer | stonecrack4 ] -> [ aliveplayer | dirt ] sfx5

((((((PLAYER COMBAT))))))
[ > aliveplayer | enemy ] -> [ aliveplayer playertarget | stabbedenemy ] sfx0

(((((PLAYER MOVEMENT)))))
[ > player | no stone no water no enemy ] -> [ > player | playertarget ]

((((((CORPSE PUSHING))))))
[ > aliveplayer | stabbedenemy | no stone ] -> [ > aliveplayer | > stabbedenemy | corpsetarget ]
[ > stabbedenemy | stabbedenemy | no stone ] -> [ > stabbedenemy | > stabbedenemy | corpsetarget ]
[ > stabbedenemy | enemy ] -> [ > stabbedenemy | > enemy ]
[ > enemy | water no deadenemy ] -> [ | drownedenemy ] sfx6

((((((PATHFINDING))))))
[ playertarget | no obstacle ] -> [ playertarget | path1 ]
[ path1 | no obstacle ] -> [ path1 | path2 ]
[ path2 | no obstacle no shorter2 ] -> [ path2 | path3 ]
[ path3 | no obstacle no shorter3 ] -> [ path3 | path4 ]
[ path4 | no obstacle no shorter4 ] -> [ path4 | path5 ]

[ enemy ] -> [ enemy movetok ]
[ enemy movetok | playertarget ] -> [ enemy | playertarget ]
[ enemy no path movetok | path ] -> [ > enemy | path ]
[ enemy path5 movetok | shorter5 no enemy ] -> [ > enemy path5 | shorter5 ]
[ enemy path4 movetok | shorter4 no enemy ] -> [ > enemy path4 | shorter4 ]
[ enemy path3 movetok | shorter3 no enemy ] -> [ > enemy path3 | shorter3 ]
[ enemy path2 movetok | shorter2 no enemy ] -> [ > enemy path2 | shorter2 ]
[ enemy no path movetok | no path ] -> [ randomdir enemy | ]

((((((ENEMY COMBAT))))))
[ enemy | > aliveplayer ] -> [ stationary enemy | deadplayer ] sfx3
+ [ enemy | v aliveplayer ] -> [ stationary enemy | deadplayer ] sfx3
+ [ enemy | ^ aliveplayer ] -> [ stationary enemy | deadplayer ] sfx3
+ [ enemy | action aliveplayer ] -> [ stationary enemy | deadplayer ] sfx3 
+ [ enemy | stationary aliveplayer ] -> [ stationary enemy | deadplayer ] sfx3

[ corpsetarget ] -> []
[ playertarget ] -> []
[ movetok ] -> []
[ path ] -> []

(((((WATER BLOCKING)))))
[ > entity | water ] -> [ entity | water ]
[ > enemy | water ] -> [ enemy | water ]

late [ stabbedenemy water ] -> [ drownedenemy ]

(((((MOVED TOWARDS ALTAR)))))
late [ spottedplayer ] -> [ searchingplayer ]
late [ searchingplayer | ... | lightaltar ] -> [ spottedplayer | ... | lightaltar ]

((((("VICTORY")))))
late [ aliveplayer lightaltar ] -> [ deadplayer lightaltar ] sfx1 message the gods welcome you
late [ aliveplayer darkaltar ] -> [ deadplayer darkaltar ] sfx7 message your soul is delicious
late [ deadenemy lightaltar ] -> [ darkaltar ] sfx2

==============
WINCONDITIONS
==============

no aliveplayer

=======
LEVELS
=======

,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,..................g....................,,,,,
,,,,,.................ggg...................,,,,,
,,,,,................ggpgg..................,,,,,
,,,,,.................ggg...................,,,,,
,,,,,..................g....................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,.......................................,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,


