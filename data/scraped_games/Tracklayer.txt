title Tracklayer
author Franklin P. Dyer
text_color Orange

========
OBJECTS
========

Background .
Lightgreen
00000
00000
00000
00000
00000

Off <
Gray Red
.1...
.0...
.0...
00000
00000

On >
Gray Red
...1.
...0.
...0.
00000
00000

Track
Darkgray
.0.0.
00000
.0.0.
00000
.0.0.

Head
Orange Yellow
00000
01110
01010
01110
00000

Car
Orange Yellow
00000
01110
01110
01110
00000

Occupied
Orange Yellow Brown
00000
01110
01210
01110
00000

Lose
Darkred Red
00000
01110
01010
01110
00000

Player !
Black
0.0.0
.....
0...0
.....
0.0.0

Platform
Brown

End =
#302007 #402b09 #51360c
00001
01112
01112
01112
12222

Person
Lightbrown Blue
.....
.000.
.101.
.0.0.
.....

Rock a
Gray Darkgray Lightgray
.200.
.200.
20001
20001
20001

Sheep b
White Black
.000.
00011
00011
.000.
.1.1.

Tree c
Darkgreen Brown
..0..
.000.
.000.
00000
..1..

Roadkill
Red Gray Darkred
.120.
20112
12011
12010
.011.

Water d
Blue

=======
LEGEND
=======

# = Platform and Person
@ = Head and track
+ = Car and track

Body = Occupied or Car
Train = Head or Body
Obstacle = Rock or Tree
Solid = Obstacle or On or Off or Train or Platform or End

=======
SOUNDS
=======

Head Move 50398307
On Create 74796902
Lose Create 60643702
Occupied Create 99838700
Person Create 77941701
Track Create 47794102

================
COLLISIONLAYERS
================

Background
Water
Track
Head, Body, Off, On, Lose, Platform, End, Roadkill, Obstacle, Sheep
Person
Player

======
RULES
======

[> Player|Off] -> [|On]

[Head Track No Water|Track No Train] [On] -> [> Head Track|Track] [On] again
[Body|...|Head Track Water] [On] -> [Body|...|> Head Track Water] [On] again
startloop
[Body|> Train] -> [> Body|> Train] again
[Body|^ Train] -> [> Body|^ Train] again
[Body|v Train] -> [> Body|v Train] again
[> Train|Sheep] -> [Lose|Roadkill]
[> Head|Train] -> [Lose|Train]
[Train Water No Track] -> [Lose Water]
endloop

RIGHT[Sheep|No Solid Background] [On] -> [> Sheep|No Solid Background] [On]

[Occupied|End No Person] -> [Car|Person End]
[Person Platform|Car] -> [Platform|Occupied]

[Action Player No Obstacle No On No Off No Train No Platform No End] -> [Player Track Background]

==============
WINCONDITIONS
==============

All person on end
Some Person
No Occupied
No Lose

=======
LEVELS
=======

message Create tracks by pressing x. Then hit the lever to deploy the train on the tracks.
message Pick up all of the passengers, and drop them off at their blank platforms.

..<......
++@......
...##....
!..==....
.........

message You can cross tracks, but then the train may be unreliable.
message NEVER have  two sets of tracks directly next to each other. This may cause a train accident.

..........
..#...=...
..#...=...
..#...=...
..........
@<..!.....
+.........

............
............
............
....=c......
....=c......
a...=#.#....
cacaa#.#....
c...=#.#...@
....=a....<+
....=c!....+
...........+

message Look out for sheep!

......
.=.!..
b.....
......
.#@<..
..+...

message You can run a track over the water, but if you turn it the train will slip off!

+<.a.....c
+....cca.c
@...ac....
...ca!.c..
...#c.aa..
...#ddd..c
..ddddddda
.ddd=.=ddd
ddd.....dd

message It's a busy day... there are two train lines, this time.

.ac.....=ac.c.
+c......==aa.c
+...##b.....ca
+...ab.......a
@...##...ca...
....#b...a.c.a
c...#!...cc.a.
..b.....@<..c.
.=.....a+c.c.a
.==a..ca+.a.ac
.....cac+...a.

message You Win!



