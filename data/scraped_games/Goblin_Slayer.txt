title Goblin Slayer
author Mark Richardson
again_interval 0.0
run_rules_on_level_start
noundo
zoomscreen 9x7

========
OBJECTS
========

Background
#210 #321 #432 #543 #654
21242
22220
22312
02222
23224

Player
Pink #BBB #CCC #DDD #888
.1234
.0004
21232
.123.
12.23

Heart
#000 #F00 #A00
21012
11111
21112
02120
00100

Wall
#222 #333 #444 #555 #666
34040
01001
03214
14303
01101

ClosedDoorH
DarkRed
.....
.....
00000
.....
.....

OpenDoorH
DarkRed
0...0
0...0
0...0
.....
.....

ClosedDoorV
DarkRed
..0..
..0..
..0..
..0..
..0..

OpenDoorV
DarkRed
000..
.....
.....
.....
000..

SpikesDown
#222
.....
.0.0.
.....
0.0.0
.....

SpikesUp
Gray #F00
.1.1.
.0.0.
1.1.1
0.0.0
.....

ArrowTrap
#222 #333 #444 #555 #666
34.40
01001
.321.
14303
01.01

Blood
#F00
.....
.0.0.
..0..
.0.0.
.....

Plate
#210 #321 #432 #543 #654
.....
.33..
.3.1.
..11.
.....

Boulder1
#555 #666 #777 #888 #999
...20
.2322
.4212
42223
22022

Boulder2
#555 #666 #777 #888 #999
21...
2232.
2421.
42223
22022

Boulder3
#555 #666 #777 #888 #999
21220
22322
.4212
.2223
...22

Boulder4
#555 #666 #777 #888 #999
21220
22322
2421.
4222.
22...

FlameReset
#D00 #866
01010
11111
01010
11111
01010

FlameCold1
#D00 #866
01010
11111
01010
11111
01010

FlameCold2
#D00 #866
01010
11111
01010
11111
01010

FlameHot
#F00 #F40 #F80 #FC0 #FF0 #D00
54345
42124
31013
42124
54345

Goblin
Green DarkGray
.....
..01.
.000.
..0..
.0.0.

King5
Green DarkGray #DD0 #FF0 #FF2 #F00
.2341
.2041
.000.
..0..
.0.0.

King4
Green DarkGray #DD0 #FF0 #FF2 #F00
.2341
.2041
.500.
..0..
.0.0.

King3
Green DarkGray #DD0 #FF0 #FF2 #F00
.2341
.2041
.500.
..0..
.0.5.

King2
Green DarkGray #DD0 #FF0 #FF2 #F00
.2341
.2541
.500.
..0..
.0.5.

King1
Green DarkGray #DD0 #FF0 #FF2 #F00
.2341
.2541
.500.
..5..
.0.5.

Dead
#F00
.0...
....0
.000.
0..00
.....

Stab
#F00
.....
.....
.....
.....
.....

Dark
#000

Dim
#000
.0.0.
0.0.0
.0.0.
0.0.0
.0.0.

Dimmer
#000


=======
LEGEND
=======

. = Dark and Background
, = Dark and Background and Dim
; = Dark and Background and Dimmer
@ =          Player
# = Dark and Wall
* = Dark and Wall and Heart
$ = Dark and Wall and Dim
% = Dark and Wall and Dimmer
| =          ClosedDoorV
- =          ClosedDoorH
" = Dark and SpikesDown
' = Dark and SpikesUp and Dead
A = Dark and ArrowTrap
B = Dark and ArrowTrap and Dead
+ = Dark and Plate
1 =          Boulder1
2 =          Boulder2
3 =          Boulder3
4 =          Boulder4
5 = Dark and FlameCold1
6 = Dark and FlameCold2
7 = Dark and FlameHot
G = Dark and Goblin
H = Dark and Goblin and Dead
K = Dark and King5

King = King5 or King4 or King3 or King2 or King1
Monster = Goblin or King
Person = Player or Monster
Boulder = Boulder1 or Boulder2 or Boulder3 or Boulder4
ClosedDoor = ClosedDoorH or ClosedDoorV
FlameTrap = FlameCold1 or FlameCold2 or FlameHot or FlameReset
Blockage = Wall or Boulder or ArrowTrap
Pain1 = SpikesUp or Boulder or Blood or FlameHot
Pain2 = Pain1 or Stab


=======
SOUNDS
=======

SFX0 22109704 ( Player Hurt )
SFX1 26961104 ( Monster Hurt )
SFX2 49269908 ( Spike Trap )
SFX3 49002301 ( Arrow Trap )
SFX4 98661508 ( Boulder )
SFX5 89620908 ( Open Door )

================
COLLISIONLAYERS
================

Background
Plate, FlameTrap, SpikesDown, SpikesUp
Dead
Wall, ArrowTrap, Person
ClosedDoor, OpenDoorH, OpenDoorV, Dark, Boulder, Blood
Heart, Stab, Dim, Dimmer


======
RULES
======

( Initialise random dungeon )
StartLoop
random horizontal [ SpikesDown    |    SpikesDown ] -> [ SpikesDown    |               ]
     + horizontal [ SpikesDown    |    SpikesDown ] -> [               |    SpikesDown ]
	 + vertical   [ SpikesDown    |    SpikesDown ] -> [ SpikesDown    |               ]
	 + vertical   [ SpikesDown    |    SpikesDown ] -> [               |    SpikesDown ]
     + horizontal [ SpikesDown   | |   SpikesDown ] -> [ SpikesDown   | |              ]
     + horizontal [ SpikesDown   | |   SpikesDown ] -> [              | |   SpikesDown ]
	 + vertical   [ SpikesDown   | |   SpikesDown ] -> [ SpikesDown   | |              ]
	 + vertical   [ SpikesDown   | |   SpikesDown ] -> [              | |   SpikesDown ]
random horizontal [ ArrowTrap     |     ArrowTrap ] -> [ ArrowTrap     |          Wall ]
     + horizontal [ ArrowTrap     |     ArrowTrap ] -> [ Wall          |     ArrowTrap ]
	 + vertical   [ ArrowTrap     |     ArrowTrap ] -> [ ArrowTrap     |          Wall ]
	 + vertical   [ ArrowTrap     |     ArrowTrap ] -> [ Wall          |     ArrowTrap ]
     + horizontal [ ArrowTrap    | |    ArrowTrap ] -> [ ArrowTrap    | |         Wall ]
     + horizontal [ ArrowTrap    | |    ArrowTrap ] -> [ Wall         | |    ArrowTrap ]
	 + vertical   [ ArrowTrap    | |    ArrowTrap ] -> [ ArrowTrap    | |         Wall ]
	 + vertical   [ ArrowTrap    | |    ArrowTrap ] -> [ Wall         | |    ArrowTrap ]
     + horizontal [ ArrowTrap   | | |   ArrowTrap ] -> [ ArrowTrap   | | |        Wall ]
     + horizontal [ ArrowTrap   | | |   ArrowTrap ] -> [ Wall        | | |   ArrowTrap ]
	 + vertical   [ ArrowTrap   | | |   ArrowTrap ] -> [ ArrowTrap   | | |        Wall ]
	 + vertical   [ ArrowTrap   | | |   ArrowTrap ] -> [ Wall        | | |   ArrowTrap ]
     + horizontal [ ArrowTrap  | | | |  ArrowTrap ] -> [ ArrowTrap  | | | |       Wall ]
     + horizontal [ ArrowTrap  | | | |  ArrowTrap ] -> [ Wall       | | | |  ArrowTrap ]
	 + vertical   [ ArrowTrap  | | | |  ArrowTrap ] -> [ ArrowTrap  | | | |       Wall ]
	 + vertical   [ ArrowTrap  | | | |  ArrowTrap ] -> [ Wall       | | | |  ArrowTrap ]
     + horizontal [ ArrowTrap | | | | | ArrowTrap ] -> [ ArrowTrap | | | | |      Wall ]
     + horizontal [ ArrowTrap | | | | | ArrowTrap ] -> [ Wall      | | | | | ArrowTrap ]
	 + vertical   [ ArrowTrap | | | | | ArrowTrap ] -> [ ArrowTrap | | | | |      Wall ]
	 + vertical   [ ArrowTrap | | | | | ArrowTrap ] -> [ Wall      | | | | | ArrowTrap ]
EndLoop
	 
( Kill player )
[ Player Dim ] -> [ Dead ]

( Player hits wall )
[ > Player | Blockage ] -> [ Player | Blockage ]

( Open doors )
[ > Player | ClosedDoorV ] -> [ Player | OpenDoorV ] SFX5
[ > Player | ClosedDoorH ] -> [ Player | OpenDoorH ] SFX5

( Stab monsters )
[ > Player | Goblin ] -> [ Player | Dead ]
[ > Player | King   ] -> [ Player | King Stab ]

( Move overlay with player )
[ > Player ] [ Heart  ] -> [ > Player ] [ > Heart  ]
[ > Player ] [ Dim    ] -> [ > Player ] [ > Dim    ]
[ > Player ] [ Dimmer ] -> [ > Player ] [ > Dimmer ]

( Move player )
[ > Player | no Blockage ] -> [ | Player ]

( Clear darkness )
[ no Dark no Blockage no ClosedDoor | Dark SpikesUp     Dead ] -> [ | SpikesUp  Dead ] message As the door opens, the first thing you see is a man laying on the ground, several spikes protruding from his body.
[ no Dark no Blockage no ClosedDoor | Dark Goblin       Dead ] -> [ | Goblin         ] message The door opens, revealing a pair of small green goblins.  This must be the lair!
[ no Dark no Blockage no ClosedDoor | Dark Goblin    no Dead ] -> [ | Goblin         ]
[ no Dark no Blockage no ClosedDoor | Dark ArrowTrap    Dead ] -> [ | ArrowTrap Dead ] message This room is littered with arrows, but goblins are too short to use bows...
[ no Dark no Blockage no ClosedDoor | Dark ArrowTrap no Dead ] -> [ | ArrowTrap      ]
[ no Dark no Blockage no ClosedDoor | Dark King              ] -> [ | King           ] message YAAAAH! Kill the human!
[ no Dark no Blockage no ClosedDoor | Dark no SpikesUp no Goblin no ArrowTrap no King ] -> [ | ]
[ no Dark    Blockage   | Dark Wall      | Dark no Blockage ] -> [ Blockage   | Wall      | Dark ]
[ no Dark    ClosedDoor | Dark Wall      | Dark no Blockage ] -> [ ClosedDoor | Wall      | Dark ]
[ no Dark    Blockage   | Dark ArrowTrap | Dark no Blockage ] -> [ Blockage   | ArrowTrap | Dark ]
[ no Dark    ClosedDoor | Dark ArrowTrap | Dark no Blockage ] -> [ ClosedDoor | ArrowTrap | Dark ]

( Move goblins )
[ Monster no Dark | ... | Player ] -> [ > Monster | ... | Player ]
  [ stationary Monster no Dark ] -> [ randomdir Monster ]
+ [ > Monster | SpikesUp   ] -> [ Monster | SpikesUp ]
+ [ > Monster | Blood      ] -> [ Monster | Blood    ]
+ [ > Monster | Blockage   ] -> [ Monster | Blockage ]
+ [ > Monster | FlameCold2 ] -> [ Monster | FlameCold2 ]
[ > Monster | Player ] -> [ Monster | Player Stab ]
[ > Monster | Player ] -> [ Monster | Player Stab ]

( Roll boulder )
left [ Boulder1 | no Blockage ] -> [ > Boulder1 | ]
left [ > Boulder | ... | Plate] -> [ Boulder | ... | Plate ]
[ > Boulder | Blood ] -> [ > Boulder | ]
[ > Boulder1 ] [ Boulder ] -> [ > Boulder1 ] [ > Boulder ]
[ > Boulder1 ] -> SFX4

( Cycle flame traps )
late [ FlameHot   ] -> [ FlameReset ]
late [ FlameCold2 ] -> [ FlameHot   ]
late [ FlameCold1 ] -> [ FlameCold2 ]
late [ FlameReset ] -> [ FlameCold1 ]

( Trigger spike traps )
late [ SpikesDown Person ] -> [ SpikesUp Person ] SFX2

( Trigger arrow traps )
late [ ArrowTrap               |               Person ] -> [ ArrowTrap  |  Person Blood ] SFX3
late [ ArrowTrap | no Blockage no ClosedDoor | Person ] -> [ ArrowTrap | | Person Blood ] SFX3

( Trigger plate traps )
late [ Player Plate | Plate ] -> [ Player Plate | ]
late [ Player Plate ] -> [ Player ] message Click! Rumble...

( Hurt people )
random late up [ Pain2 Player ] [    Heart | Heart | no Heart ] -> [ Pain2 Player     ] [ Heart | Dimmer | ] SFX0
     + late up [ Stab  Player ] [ no Heart | Heart | no Heart ] -> [       Player Dim ] [       | Dimmer | ] SFX0 again
     + late up [ Pain1 Player ] [ no Heart | Heart | no Heart ] -> [ Pain1 Player Dim ] [       | Dimmer | ] SFX0 again
late [ Pain1 Goblin ] -> [ Pain1 Dead  ] SFX1
late [ Pain2 King1  ] -> [ Pain2 Dead  ] SFX1
late [ Pain2 King2  ] -> [ Pain2 King1 ] SFX1
late [ Pain2 King3  ] -> [ Pain2 King2 ] SFX1
late [ Pain2 King4  ] -> [ Pain2 King3 ] SFX1
late [ Pain2 King5  ] -> [ Pain2 King4 ] SFX1
late [ Stab ] -> [ ]


==============
WINCONDITIONS
==============

no Monster
some Heart


=======
LEVELS
=======

message Goblins have been raiding settlements across the county.

message Someone must put a stop to their attacks.

message You have followed a goblin back to what you hope is his lair.

message It's time to exterminate every last one of them!

(
	=====
	Rooms
	=====
	Entrance corridor
	Simple curving passage with scattered spikes
	Goblin guard rooms
	Fire trap chamber with goblins
	Diagonal arrow hall
	Hall with spikes and boulder
	Fire traps interlaced with spikes
	Goblins
	Hall with fires and boulder
	Grid with spikes and arrows
	Throne room
)

.............................................................................
............##...............................................................
...........####..............................................................
..........##..##............#####################............................
.........#A....A#...........#....G.........G....#............................
........##......##..........#.......#####.......#............................
.......##........##.........#..##..G#.K.#G..##..#............................
......##....##....##........#..##.G.......G.##..#............................
.....##....####....#A##A##A##.G.................##########################A..
....##....A#..#A............#......G.....G.....G#.........................#..
...#A....A#....##...........#...G...........G...|.##.##.##.##.##.##.##.##.A..
..##....##......##..........#.........G........G#.#A.##.A#.AA.##.##.A#.##.#..
..##....##.......######A##-######################.."."................."."#..
...#A....A#......#.....#.....".."..".."..+....12#.##.##.#A.##.##.##.A#.AA.#..
....##....##.....#.G..G#.....".."..".."..+....34#.##.##.##.##.AA.A#.A#.##.#..
.....##....#.....#.....######################-###........."...............#..
......##...#.....#.....#...###".".###..#"6"5...7#.##.##.#A.##.##.##.A#.#A.#..
.......B...B.....#.....#..##."..".".##.#""67""6"#.##.##.##"##.##.##.##.##.#..
.......#...#.....###-###.##".".""."..#.#575"75"5#.........."."............#..
.......##-##.....#.....###.".".###.".###6""#############################-##..
.......#...#.....#...G.#..""."##.##".".#"75"6"6#7656#75#55#66#77#55#66#...#..
.......#...#.....#.....|.."..##...#"...#"5"7""7#...56.65.567..57...+....12#..
.......#555#.....#.H...#...###....##.".#67"6"5.#...7565567.76.67...+....34#..
.......#..G##....#.....#####.......#"".######-####-##75#55#66#77#55#66#####..
.......##G..##...###-##########...##"".#.G..G....G..###################......
........#..5.#####.....#......#...#.'..#.......G...G#........................
........##5.G.5..#.....#..G...#...#...##.G.#....#...#........................
.........##...5..|..G..|......#...##-####..G..G..G.##........................
..........###.5..#.....#...G..#.;;;%,%;;%#........##.........................
............######...G.#......#.;;;$.$;;*###..G.###..........................
.................##############.;;,#.#,;*..######............................
................................;,.#@#.,*....................................
................................;;,###,;*....................................
................................;;;,.,;;*....................................
................................;;;;,;;;;....................................

message The last goblin falls in a pool of blood.

message You sever the head of the goblin king as proof of your victory and carefully make you way back out into the daylight.

message You are showered in glory in every town you pass through.

message

message One night, a young boy runs into the tavern where you are staying.

message Exhausted and bruised, he shouts one word before collapsing.

message "DRAGON!"
