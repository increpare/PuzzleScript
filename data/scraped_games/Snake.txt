title Snake
author Baconaro
homepage www.puzzlescript.net
require_player_movement 
(realtime_interval 0.3)
========
OBJECTS
========

Background
Brown Lightgreen
11111
01111
11101
11111
10111

playerup
Green red

..1..
.000.
.000.
.000.
.000.

playerdown
Green red

.000.
.000.
.000.
.000.
..1..



playerleft
Green red
.....
.0000
10000
.0000
.....


playerright
Green red
.....
0000.
00001
0000.
.....

(THE BODY)
BodyUpLeft
Green
.000.
0000.
0000.
0000.
.....


BodyUpRight
Green
.000.
.0000
.0000
.0000
.....

BodyUpDown
Green
.000.
.000.
.000.
.000.
.000.

BodyLeftUp
Green
.000.
0000.
0000.
0000.
.....

BodyLeftRight
Green
.....
00000
00000
00000
.....

BodyLeftDown
Green
.....
0000.
0000.
0000.
.000.

BodyRightUp
Green
.000.
.0000
.0000
.0000
.....

BodyRightLeft
Green
.....
00000
00000
00000
.....

BodyRightDown
Green
.....
.0000
.0000
.0000
.000.


BodyDownUp
Green
.000.
.000.
.000.
.000.
.000.

BodyDownLeft
Green
.....
0000.
0000.
0000.
.000.


BodyDownRight
Green
.....
.0000
.0000
.0000
.000.
(Tails, (And Sonic :p))
Tailup
Green
.000.
.000.
..0..
.....
.....

Taildown
Green
.....
.....
..0..
.000.
.000.

Tailleft
Green
.....
00...
000..
00...
.....

TailRight
Green
.....
...00
..000
...00
.....


Apple
red Brown
..1..
.010.
00000
.000.
..0..

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

player
Black

(fake tails, used for the snake disapearing)
FakeTailup
Black
FakeTailLeft
Black
FakeTailRight
Black
FakeTailDown
Black

Dead
red

=======
LEGEND
=======
Tail = Tailup or TailLeft or TailRight or TailDown 
Faketail = FakeTailup or FakeTailLeft or FakeTailRight or FakeTailDown (fake tails are bodies)
player = playerup or playerdown or playerleft or playerright
Head = playerup or playerdown or playerleft or playerright
Body = BodyUpLeft or BodyUpRight or BodyUpDown or BodyLeftUp or BodyLeftRight or BodyLeftDown or  BodyRightUp or BodyRightLeft or BodyRightDown or BodyDownUp or BodyDownLeft or BodyDownRight
o = BodyUpLeft
. = Background
# = Wall
A = Apple
P = playerRight
B = BodyLeftRight
T = TailRight
Solid = Head or Body or wall 
Appled = Apple or Solid or Tail (to have a place to spawn the apple)
snake = Tail or Faketail  or body or head
=======
SOUNDS
=======


================
COLLISIONLAYERS
================
Player	(useless :p)
Background
Apple
Tail
Wall, player, Body, Head, Faketail, Dead

======
RULES
======
Up		[ stationary  Playerup] -> [ > Playerup]
Up 		[< Playerup] -> [ > Playerup](you can't turn around!)
Down	[ stationary  PlayerDown] -> [ > PlayerDown]
Down 	[< PlayerDown] -> [ > PlayerDown](you can't turn around!)
Left  	[ stationary  PlayerLeft] -> [ > PlayerLeft]
Left	[< PlayerLeft] -> [ > PlayerLeft](you can't turn around!)
Right	[ stationary  PlayerRight] -> [ > PlayerRight]
Right	[< PlayerRight] -> [ > PlayerRight](you can't turn around!)


[> Head | solid ] -> [ Dead | solid] ( you lose!)
[Dead | snake ] -> [Dead | Dead] again

(testing the player on apple with all the Tails!)
random	[> Head | Apple] [TailUp]  [no Appled]		-> [ > Head  | ] [FakeTailup] [ Apple]
random	[> Head | Apple] [TailDown]  [no Appled]	-> [ > Head |  ] [FakeTailDown] [ Apple]
random	[> Head | Apple] [TailLeft]  [no Appled]	-> [ > Head |  ] [FakeTailLeft] [ Apple]
random	[> Head | Apple] [TailRight]  [no Appled]	-> [ > Head |  ] [FakeTailRight] [ Apple]



Up 		[ > Playerup | no Solid] -> [ BodyDownUp| PlayerUp]
Left	[ > Playerup | no Solid] -> [ BodyDownLeft| PlayerLeft]
Right 	[ > Playerup | no Solid] -> [ BodyDownRight| PlayerRight]

Down 	[ > Playerdown | no Solid] -> [ BodyUpDown| PlayerDown]
Left	[ > Playerdown | no Solid] -> [ BodyUpLeft| PlayerLeft]
Right 	[ > Playerdown | no Solid] -> [ BodyUpRight| PlayerRight]

Down 	[ > PlayerLeft | no Solid] -> [ BodyRightDown| PlayerDown]
Up 		[ > PlayerLeft | no Solid] -> [ BodyRightUp| PlayerUp]
Left	[ > PlayerLeft | no Solid] -> [ BodyRightLeft| PlayerLeft]


Down 	[ > PlayerRight | no Solid] -> [ BodyLeftDown| PlayerDown]
Up 		[ > PlayerRight | no Solid] -> [ BodyLeftUp| PlayerUp]
Right 	[ > PlayerRight | no Solid] -> [ BodyLeftRight| PlayerRight]


Up		[ TailUp | BodyDownUp] 		-> [ | FakeTailup]
Up		[ TailUp | BodyDownLeft] 	-> [ | FakeTailLeft]
Up		[ TailUp | BodyDownRight] 	-> [ | FakeTailRight]

Down 	[ TailDown | BodyUpLeft] 	-> [ | FakeTailLeft]
Down	[ TailDown | BodyUpRight] 	-> [ | FakeTailRight]
Down	[ TailDown | BodyUpDown] 	-> [ | FakeTailDown]

Left 	[ TailLeft | BodyRightUp] 	-> [ | FakeTailUp]
Left	[ TailLeft | BodyRightLeft] -> [ | FakeTailLeft]
Left	[ TailLeft | BodyRightDown] -> [ | FakeTailDown]


Right 	[ TailRight | BodyLeftUp] 	-> [ | FakeTailUp]
Right	[ TailRight | BodyLeftRight]-> [ | FakeTailRight]
Right	[ TailRight | BodyLeftDown] -> [ | FakeTailDown]

late 	[FakeTailup] 	-> [Tailup]
late 	[FakeTailLeft] 	-> [TailLeft]
late 	[FakeTailRight] -> [TailRight]
late 	[FakeTailDown] 	-> [TailDown]

WINCONDITIONS
==============



=======
LEVELS
=======
###########
#.........#
#.........#
#.........#
#.........#
#.........#
#TBBBP..A.#
#.........#
#.........#
###########



