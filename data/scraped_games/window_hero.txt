title window hero
author Giles
require_player_movement
youtube BrRUTCMP8mk
========
OBJECTS
========
background ' 
#888888
player 
#fe8600 #947d27 #514c4e #dcccb5 
01210
10021
30001
13321
01213
(white orange black
11111
11021
10101
12011
11111)

wall 
grey #445599 #6699AA white
02220
21112
20012
20012
12220

snow1 1
#fffffd #cacbd2 #b7b8b4 #ff0000 
02000
0.012
200.0
10000
12.0.
snow2 2
#fffffd #cacbd2 #b7b8b4 #ff0000 
01000
10001
10001
10101
10010

snow3 3
#fffffd #cacbd2 #b7b8b4 #ff0000 
.222.
11112
10011
10222
.222.


windowlit
#b5c8c6 #aac9f2 #fafed2 #b4a0a8 
11011
10111
21101
11211
11111



windowdim
#4d4f68 #5b534d #514c4e #373033 
33333
33213
31333
33333
30333

snowstop
red
.....
.....
.....
.....
.....


=======
LEGEND
=======
P = player 
window = windowlit or windowdim
snow = snow1 or snow2 or snow3
lightsnow = snow1 or snow2
heavysnow = snow2 or snow3
% = windowlit and snow1
$ = windowdim and snow1
# = wall and snowstop
W = windowlit
=======
SOUNDS
=======
sfx1 40378307
sfx2 34640508
================
COLLISIONLAYERS
================
background
window
snow snowstop
player heavysnow wall
======
RULES
======
[> player] -> sfx1
[> player| lightsnow] -> [ > player | > lightsnow]
[ > snow1| snow1] -> [|snow2]
[ > snow1| snow2] -> [|snow3] 
[ > snow2|snow1] -> [|snow3] 
[ > snow1|snow3] -> [snow1|snow3]
==============
WINCONDITIONS
==============
no snow on window
=======
LEVELS
=======
message It's important for citizen morale to have natural sunlight
message So everyday it snows, I head outside
message To clean the windows.
111111
1%%%%1
1%%%%1
1%%%%1
111111
###'##
11''w#
'p'ww#

111111
1%%%%1
1###%1
1%%%%1
111111
p11111
message It's not an easy job
message But it delivers hope to the hopeless

#1111111111
1%%%%1%%%%1
1%11%1%11%1
1%11%1%11%1
1%%%%1%%%%1
p111111111#

message And we need that hope

#11#11#11
1%%%%%$$1
1%%%%%%$1
1%%%%%$%1
1%%%%%%%1
p#11#11#1

1111#111
1%%%$$$1
1%#%%#$1
1%%%$$$1
p1111111

message I need that hope

#11111111#1
#1$$$$$$1#1
#1111111111
#1$$$$$$111
#11111111#1
#1%%%%%%1#1
1p1111111#1

111111#111
1%%1$$#$$1
1%%1$$#$$1
111111#111
1$$1$$1$$1
1$$1$$1$$1
p111111111

message so at the end of the day I can cope

3###11#11
3###1%%%1
3####%%%1
13###%%%1
112211111
p11111111
113111111
13###$$$1
3####$$$1
3###1$$$1
3###11#11

message to smile at my son and say

########
1#####1#
111###1#
11%###%#
11111111
p1$###%#
1111111#
111#####
message (you will never reach this) Dad pushed the darkness away.