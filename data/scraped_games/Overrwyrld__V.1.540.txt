title Overrwyrld  V.1.540
author David Kempe
homepage stayingsquareinma.wordpress.com
flickscreen 11x9

key_repeat_interval 0.12

noundo

========
OBJECTS
========

Background
yellow lightbrown
00000
00100
00000
01000
00000

Ground
yellow lightbrown
00000
00100
00000
01000
00000

GroundE
yellow lightbrown
00000
00100
00000
01000
00000

GroundX
yellow lightbrown
00000
00100
00000
01000
00000

GroundI
yellow lightbrown
00000
00100
00000
01000
00000

GroundT
yellow lightbrown
00000
00100
00000
01000
00000

Arrive
yellow lightbrown
00000
00100
00000
01000
00000

home
yellow lightbrown
00000
00100
00000
01000
00000

Seed
red lightgrey
.1.1.
.....
..0..
.....
.1.1.

Secret
yellow white
01010
10001
00000
10001
01010

Flower
white red  darkred lightgrey
.3.3.
..1..
.202.
..1..
.3.3.

Wall
BROWN darkbrown
00000
10000
00010
01000
00001

Calculator
BROWN darkbrown
00000
10000
00010
01000
00001

Calculator2
BROWN darkbrown
00000
10000
00010
01000
00001

CalculatorTemp
BROWN darkbrown
00000
10000
00010
01000
00001

MagicRelay
BROWN darkbrown
00000
10000
00010
01000
00001

MagicTemp
BROWN darkbrown
00000
10000
00010
01000
00001

MagicWall
BROWN darkbrown
00000
10000
00010
01000
00001

WallAction
BROWN darkbrown
00000
10000
00010
01000
00001

WallAction2
BROWN darkbrown
00000
10000
00010
01000
00001

Water
darkblue blue
00000
00100
00001
10000
00000

Water2
darkblue blue
00000
00100
00001
10000
00000

Water3
darkblue blue
00000
00100
00001
10000
00000

WaterTemp
darkblue blue
00000
00100
00001
10000
00000

Save
purple
00000
00000
00000
00000
00000

Save2
purple darkgrey
00110
01000
00100
00010
01100

Rock
Orange
.....
.000.
.000.
.000.
.....

RockRight
Orange
.....
.000.
.000.
.000.
.....

RockLeft
Orange
.....
.000.
.000.
.000.
.....

RockUp
Orange
.....
.000.
.000.
.000.
.....

RockJump
yellow lightbrown
00000
00100
00000
01000
00000

Tree1
lightgreen
.....
.0000
.0000
.0000
..000

Tree2
lightgreen
.....
..000
.0000
.0000
.0000

Tree3
lightgreen
.....
.000.
.0000
.0000
.0000

Tree4
lightgreen
.....
.0000
.0000
.0000
.000.

TreeDig
lightgreen
.....
.0000
.0000
.0000
..000

Stump
lightgreen darkbrown yellow
.....
.02..
..10.
.21..
.....

StumpF
lightgreen darkbrown yellow
.....
.02..
..10.
.21..
.....

Fire
red orange
.....
.000.
.010.
.000.
.....

Fire2
red orange
00.00
00000
.010.
00000
00.00

Bomb
brown darkbrown lightgrey
..222
.020.
.001.
.111.
.....

BombTemp
brown darkgreen white
.222.
.0222
2001.
22122
2..22

Tunnel
darkgrey
00000
00000
00000
00000
00000

TunnelTemp
darkgrey
.....
.000.
.000.
.000.
.....

TunnelTemp2
darkgrey
.....
.000.
.000.
.000.
.....

Magic
yellow lightbrown
00000
00100
00000
01000
00000

1Push
orange
..0..
.000.
00000
.000.
..0..

2Jump
yellow lightbrown
.101.
10001
00000
10001
.101.

3Swim
blue
..0..
.000.
00000
.000.
..0..

4Burn
lightgreen
..0..
.000.
00000
.000.
..0..

5Dig
darkgrey
..0..
.000.
00000
.000.
..0..

6Climb
brown
..0..
.000.
00000
.000.
..0..

7Spell
Purple
..0..
.000.
00000
.000.
..0..

8Action
red
..0..
.000.
00000
.000.
..0..

9Flower
red darkred
.....
..1..
.101.
..1..
.....

Player0
green
00000
00000
00000
00000
00000

Player1
green orange
00000
01110
01010
01110
00000

Player2
green yellow
00000
00110
00000
01100
00000

Player3
green blue darkblue
11100
00000
01110
00000
00111

SwimU
green blue darkblue
11100
00000
01110
00000
00111

SwimD
green blue darkblue
11100
00000
01110
00000
00111

SwimL
green blue darkblue
11100
00000
01110
00000
00111

SwimR
green blue darkblue
11100
00000
01110
00000
00111

Player4
green lightgreen
11011
11011
00000
11011
11011

Player5
green darkgrey
11011
10001
00100
10001
11011

Player6
green brown
11011
10001
11011
10001
11011

Player7
green purple
01110
01010
00010
00100
00100

PlayerX
red
0...0
.0.0.
..0..
.0.0.
0...0

Winner
red darkred
01100
00001
10101
10000
00110

WinnerU
red darkred
01100
00001
10101
10000
00110

WinnerD
red darkred
01100
00001
10101
10000
00110

WinnerL
red darkred
01100
00001
10101
10000
00110

WinnerR
red darkred
01100
00001
10101
10000
00110

Exit
red darkred lightred
11111
10001
10201
10001
11111

Quest
red darkred lightred
11111
10001
10201
10001
11111

ExitSpawn
red darkred
00110
10000
10101
00001
01100

Finish
red darkred lightred
11111
10001
10201
10001
11111


BombSpawn
brown lightgrey
1...1
..0..
.000.
..0..
1...1

WallA
darkbrown brown
11011
11011
10000
00111
11111

WallB
darkbrown brown
11011
11011
10000
00111
11111

WallC
darkbrown brown
11011
11011
10000
00111
11111

WallD
darkbrown brown
11011
11011
10000
00111
11111

WallZ
darkbrown brown
11011
11011
10000
00111
11111

GateA
yellow lightred red
.21..
.21..
.2111
.2222
.....

GateE
yellow lightred red
.21..
.21..
.2111
.2222
.....

GateL
yellow lightred red
.21..
.21..
.2111
.2222
.....

GateB
yellow lightred red
.....
.2222
.2111
.21..
.21..

GateX
yellow lightred red
.....
.2222
.2111
.21..
.21..

GateC
Yellow lightred red
.....
2222.
1112.
..12.
..12.

GateI
Yellow lightred red
.....
2222.
1112.
..12.
..12.

GateD
yellow lightred red
..12.
..12.
1112.
2222.
.....

GateT
yellow lightred red
..12.
..12.
1112.
2222.
.....

GateR
yellow lightred red
..12.
..12.
1112.
2222.
.....

ExitV
darkred
.....
..0..
.....
..0..
.....


ExitH
darkred
.....
.....
.0.0.
.....
.....

Hint
white darkbrown yellow
..1..
00000
00000
.21..
..1..

Ending
brown black yellow
01110
01010
01020
01010
01110

box01
yellow brown blue green
11111
10100
13222
10302
10032

box02
yellow brown blue green
11111
10030
13001
01111
01100

box03
yellow brown blue green
11111
00101
11110
00000
01010

box04
yellow brown blue green
11111
30010
00000
00011
01013

box05
yellow brown blue green
11111
10201
22201
12221
12221

box06
yellow brown blue green
10102
13102
10002
10100
10111

box07
yellow brown blue green
10002
00222
22002
00000
00001

box08
yellow brown blue green
22000
00030
22030
00003
11003

box09
yellow brown blue green
10100
01010
00013
03313
33310

box10
yellow brown blue green
22001
12221
12221
10201
12221

box11
yellow brown blue green
10101
10111
10000
10101
11111

box12
yellow brown blue green
01100
00101
30000
00010
11111

box13
yellow brown blue green
00033
01033
00000
01001
11111

box14
yellow brown blue green
03033
33331
00000
00101
11111

box15
yellow brown blue green
02201
12111
02001
00101
11111

House
lightblue white
11111
00000
11111
00000
00000

Roof
lightgray darkgray
01010
00100
10001
01010
00100

=======
LEGEND
=======
GateWalls = WallA or WallB or WallC or WallD or WallZ
Walls = Wall or MagicRelay or MagicTemp or MagicWall or calculator or calculator2 or calculatortemp or wallaction or wallaction2
Rocks = rock or rockup or rockright or rockleft
Trees = Tree1 or tree2 or tree3 or tree4 or TreeDig
box = box02 or box03 or box04 or box05 or box06 or box07 or box08 or box09 or box10 or box11 or box12 or box13 or box14 or box15
Waters = Water or Water2 or Water3
Grounds = Ground or GroundE or GroundX or GroundI or GroundT
Gates = ExitV or ExitH or GateE or GateX or GateI or GateT
Tunnels = Tunnel or TunnelTemp or TunnelTemp2
Floor = Grounds or Tunnels or Gates or magic or 9Flower or Seed or Flower or stump or exitspawn or bombspawn or home
Solid = Rocks or Trees or GateL or GateR
Pusher = player1 or winner
Jumper = player2 or winner
Swimmer = SwimU or SwimD or SwimL or SwimR or player3 or winnerU or WinnerD or WinnerL or WinnerR or Winner 
Nonswimmer = Player0 or player1 or player2 or player4 or player5 or player6 or player7 or PlayerX
Burner = player4 or winner
Digger = player5 or winner
Climber = player6 or winner
Wizard = player7 or winner
Player = Player0 or Pusher or Jumper or Swimmer or NonSwimmer or Burner or Digger or Climber or Wizard or Winner or PlayerX 
Pickup = 1push or 2jump or 3swim or 4burn or 5Dig or 6climb or 7spell or 8action or 9Flower or GateA or GateB or GateC or GateD

- = Background
. = Ground
# = Wall
% = MagicWall
& = MagicRelay
~ = Water
; = BombSpawn
$ = Save
* = Rock
, = RockRight
' = RockUp
` = RockLeft
Y = Tree1
J = Tree2
G = Tree3
H = Tree4
" = Calculator
: = Seed
@ = Tunnel
? = Magic
1 = 1Push
2 = RockJump (spawns 2Jump)
3 = 3Swim
4 = 4Burn
5 = TreeDig (spawns 5Dig)
6 = 6Climb
7 = 7Spell
8 = WallAction (spawns 8Action)
9 = 9Flower
0 = Player0
O = ExitSpawn (Spawns Exit and Bomb)
! = Hint
R = GateR
L = GateL
A = WallA
B = WallB
C = WallC
D = WallD
Z = WallZ
E = GroundE
X = GroundX
I = GroundI
T = GroundT
α = box01
β = box02
γ = box03
δ = box04
ε = box05
ζ = box06
η = box07
θ = box08
ι = box09
κ = box10
λ = box11
μ = box12
ν = box13
ξ = box14
ο = box15
q = quest
u = arrive
n = home
f = finish
m = ending
P = House
W = Roof

=======
SOUNDS
=======

StartLevel 14503508 (Start)
SFX1 80999902 (Push)
SFX2 10611705 (Jump/Climb) 
SFX3 20004902 (Swim)
SFX4 64111702 (Burn)
SFX5 39992301 (warp)
SFX6 68202300 (artifact)
SFX7 31384308 (spell)
SFX8 39589508 (Save)
SFX9 495703 (Powerup)
SFX0 81024708 (secret)
ShowMessage 58223503 (Messages)

================
COLLISIONLAYERS
================

Background
Floor, WaterTemp, waters, StumpF, finish, save, save2, box01, hint, secret, exit, ending, quest arrive, rockjump
Player, Walls, Trees, Rocks, box, winner, bomb, House, Roof, GateL, GateR, GateWalls
pickup, fire, fire2, bombtemp

======
RULES     
======     



(powerups)
([ > Player | Powerup ] -> [ | Player# ] SFX0)
(1push or 2jump or 3swim or 4burn or 5Dig or 6climb or 7spell or 8action)

[ > Player | 1push ] -> [ | Ground Player1 ] SFX9 message Push!
[ > Player | 2jump ] -> [ | Ground Player2 ] SFX9 message Jump!
[ > Player | 3swim ] -> [ | Ground Player3 ] SFX9 message Swim!
[ > Player | 4burn ] -> [ | Ground Player4 ] SFX9 message Burn!
[ > Player | 5Dig ] -> [ | Stump Player5 ] SFX9 message Dig!
[ > Player | 6climb ] -> [ | Ground Player6 ] SFX9 message Climb!
[ > Player | 7spell ] -> [ | Ground Player7 ] SFX9 message ???...!
[ > Player | 8action ] -> [ | Ground PlayerX ] SFX9 message Action!

(Flowers)
(Picking up Flowers sends a relay signal to plant them near the exit. This way they can be picked up in any order, but still arrive in a straight line)
late [Player 9Flower | ... | MagicRelay ] -> [Player ground | ... | MagicTemp ] sfx6
late [MagicTemp | ... | Calculator ] -> [ MagicRelay | ... | CalculatorTemp ]
late [ CalculatorTemp | ... | MagicWall ] -> [ Calculator | ... | MagicTemp ]
late [ MagicTemp | ... | Seed ] -> [ MagicWall | ... | Flower ]


(when all 10 Flowers are found...)
[Calculator | ... | Flower | Flower | Flower | Flower | Flower | Flower | Flower | Flower | Flower | Flower ] -> [ Calculator2 | ... |  Flower | Flower | Flower | Flower | Flower | Flower | Flower | Flower | Flower | Flower ]
late [ Calculator2 | ... | MagicWall ] -> [Calculator2 | ...| Calculator2 ]
late [ Calculator2 | ... | Water ] -> [Calculator2 | ... | Water2 ]
late [ Water2 | ... | Player ] -> [Water3 | ... | Winner ] sfx0
late [ Water3 | ... | Calculator2 ] -> [Water | ... | Wall ]
late [Wall | ... | Water2 ] -> [Wall |... |Water ]
late [Wall | ... | Water3 ] -> [Wall |... |Water ]

(Bomb)
[ > Winner | Bomb ] -> [ > Winner | > Bomb ]
[action Winner ] [Bomb] -> [Winner] [BombTemp] sfx4
late [BombTemp| WallA ] -> [ BombTemp | GateA] sfx7
late [BombTemp| WallB ] -> [ BombTemp | GateB] sfx7
late [BombTemp| WallC ] -> [ BombTemp | GateC] sfx7
late [BombTemp| WallD ] -> [ BombTemp | GateD] sfx7
late [BombTemp| WallZ ] -> [ BombTemp | Ground ] sfx7
late [Bombtemp] -> []
late [Bomb water] -> [water] sfx3
[fire bomb] -> [] sfx4 Message "Bombs are flammable!" --Overrwyrld guide to combustibles

late [winner ExitSpawn] [Bomb] -> [winner ExitSpawn] [] sfx4
late [Winner ExitSpawn | ... | BombSpawn] -> [Winner ExitSpawn | ... | BombSpawn Bomb ] sfx0 Message X-plode!

(Gates)
late [Player ExitSpawn] -> [Player ExitSpawn] sfx0
[ Player GateA ] [ GroundE ] -> [ Player Ground ] [GateE] sfx0
[ Player GateB ] [ GroundX ] -> [ Player Ground ] [GateX] sfx0
[ Player GateC ] [ GroundI ] -> [ Player Ground ] [GateI] sfx0
[ Player GateD ] [ GroundT ] -> [ Player Ground ] [GateT] sfx0
[ GateE | Ground | GateX ] -> [ GateE | ExitV | GateX ]
[ GateX | Ground | GateI ] -> [ GateX | ExitH | GateI ]
[ GateI | Ground | GateT ] -> [ GateI | ExitV | GateT ]
[ GateT | Ground | GateE ] -> [ GateT | ExitH | GateE ]
[ExitV | ExitSpawn | ExitV ] -> [ExitV | Exit | ExitV ]

(Push)
Right [ > Pusher | Rockright | ] -> [ | Ground Pusher | Rock] sfx1
Up [ > Pusher | RockUp |  ] -> [  |  Ground Pusher | Rock ] sfx1
Left [ > Pusher | Rockleft | ] -> [ | Ground Pusher | Rock ] sfx1
late [Ground | Rock | Rock | Rock | Wall | Rock] -> [background | Rock | Rock | Rock | Wall | Rock]
late [Ground | Tree2 | Rock ] -> [Background | Tree2 | Rock ]
late [Stump | Tree4 | Rock ] -> [ StumpF | Tree4 | Rock ]
late [Rock |Player | | | RockJump ] -> [Rock| Player | | | 2Jump ]

(Jump)
[ > Jumper Floor | Solid | Floor ] -> [ Floor | Solid | Jumper Floor] sfx2
[ > Jumper Floor | Water | Floor ] -> [ Floor | Water | Jumper Floor ] sfx2

(Swim)
[> NonSwimmer | Water] -> [ NonSwimmer |  Water ]
[ > Player | Water | Solid] -> [Player | Water | Solid]
Up [ Up Player3 | Water | Water | Rock ] -> [ Player3 | Water | Water | Rock ]
Up [ Up Winner | Water | Water | Rock ] -> [ Winner | Water | Water | Rock ]

Up [ Up Player3 | Water] -> [ | SwimU Water] sfx3
Up [ SwimU Water | Floor ] -> [ Water | Player3 Floor ] sfx3
[ SwimU Water| ... | Floor] -> [ Up SwimU Water| ... | Floor ] again

Down [ Down Player3 | Water] -> [ | SwimD Water] sfx3
Down [ SwimD Water | Floor ] -> [ Water | Player3 Floor ] sfx3
[ SwimD Water| ... | Floor] -> [ Down SwimD Water| ... | Floor ] again

Left [ Left Player3 | Water] -> [ | SwimL Water] sfx3
Left [ SwimL Water | Floor ] -> [ Water | Player3 Floor ] sfx3
[ SwimL Water| ... | Floor] -> [ Left SwimL Water| ... | Floor ] again

Right [ Right Player3 | Water] -> [ | SwimR Water] sfx3
Right [ SwimR Water | Floor ] -> [ Water | Player3 Floor ] sfx3
[ SwimR Water| ... | Floor] -> [ Right SwimR Water| ... | Floor ] again

Up [ Up Winner | Water] -> [ | WinnerU Water] sfx3
Up [ WinnerU Water | Floor ] -> [ Water | Winner Floor ] sfx3
[ WinnerU Water| ... | Floor] -> [ Up WinnerU Water| ... | Floor ] again

Down [ Down Winner | Water] -> [ | WinnerD Water] sfx3
Down [ WinnerD Water | Floor ] -> [ Water | Winner Floor ] sfx3
[ WinnerD Water| ... | Floor] -> [ Down WinnerD Water| ... | Floor ] again

Left [ Left Winner | Water] -> [ | WinnerL Water] sfx3
Left [ WinnerL Water | Floor ] -> [ Water | Winner Floor ] sfx3
[ WinnerL Water| ... | Floor] -> [ Left WinnerL Water| ... | Floor ] again

Right [ Right Winner | Water] -> [ | WinnerR Water] sfx3
Right [ WinnerR Water | Floor ] -> [ Water | Winner Floor ] sfx3
[ WinnerR Water| ... | Floor] -> [ Right WinnerR Water| ... | Floor ] again

(Burn)
[ > burner | | Tree1 ] -> [ burner > Fire| | Tree1 ] again sfx4
[ burner | Water | Tree1 ] -> [ burner > Fire | Water | Tree1 ] again sfx4
[> player0 | | Tree1 | MagicRelay] -> [player0 > Fire | | Tree1 | MagicRelay] again sfx4
[ Fire | Tree1 ] -> [ > Fire | Tree1 ] again sfx4
[ Fire Tree1 ] -> [ Stump ] again sfx4
[Water Fire | Fire Stump ] -> [ Water | Stump ]
[ > burner | | TreeDig ] -> [ burner > Fire| | TreeDig ] again sfx4
[ Fire | TreeDig] -> [ > Fire | TreeDig] again sfx4
[ Fire TreeDig ] -> [ 5Dig ] again sfx4
late [ MagicRelay | Stump | Ground] -> [ MagicRelay | Secret | Ground ]

(Dig)
late [ Tunnel Digger | ... | tunnel ] -> [ tunnelTemp | ... | tunnel Digger ] sfx5
late [ tunnelTemp ] -> [ Tunnel ]
[ Bomb Tunnel | ... | Tunnel ] -> [ Tunnel | ... | > Bomb Tunneltemp2 ]
[> Bomb Tunneltemp2 | ] -> [ Tunnel| Bomb ]

(Climb)
up [ > Climber Floor | walls | Floor ] -> [ Floor | walls | Floor Climber] sfx2
down [ > Climber Floor | walls | Floor ] -> [ Floor | walls | Floor Climber] sfx2

(???)
late [ Wizard Magic | ... | MagicRelay ] -> [ Wizard Magic | ... | MagicTemp ] sfx7
late [ Wizard Stump | Ground | Wall | MagicRelay ] -> [ Wizard Stump | Ground | Wall | MagicTemp ] sfx7
late [ Wall | Wizard Magic | Wall | Water | Water] -> [ Wall | Wizard Ground | Wall | WallAction2 | Wallaction2 ] sfx7
late [ Player box01] [Calculator] -> [player box01] [MagicRelay] sfx0
late [WallAction2 | ... | MagicWall ] -> [WallAction2 | ...| Water ]
late [WallAction2 | WallAction2 ] -> [ Water | Water ]
late [MagicTemp | ... | MagicWall ] -> [ MagicTemp | ... | Ground ]
late [MagicTemp | ... | WallAction ] -> [ WallAction2 | ... | 8Action ]

(Action button)
[ action playerx ][WallAction2] -> [player1][WallAction2] sfx5
[ action player1 ][WallAction2] -> [player2][WallAction2] sfx5
[ action player2 ][WallAction2] -> [player3][WallAction2] sfx5
[ action player3 ][WallAction2] -> [player4][WallAction2] sfx5
[ action player4 ][WallAction2] -> [player5][WallAction2] sfx5
[ action player5 ][WallAction2] -> [player6][WallAction2] sfx5
[ action player6 ][WallAction2] -> [player7][WallAction2] sfx5
[ action player7 ][WallAction2] -> [player1][WallAction2] sfx5

(Saving)
late [ Player Save ] -> [Player Save2] CHECKPOINT sfx8 Message Save!
[ Player | Save2 ] -> [Player |Save]

(Hints)
late [ Wall | Ground | Player Hint | Ground | Wall ] -> [ Wall | Ground | Player Hint | Ground | Wall ] Message "Welcome to the Overrwyrld.      YOU'LL NEVER ESCAPE!!!         Please enjoy your time here."                --The Overrlord
late [ Rock | Ground | Player Hint | Ground | Wall ] -> [ Rock | Ground | Player Hint | Ground | Wall ] Message "ROTATIONAL SYMMETRY ordinance: any ORANGE ROCKS found out of place will be corrected by FORCE."                        --mr. Arm
late [ Rock | Player Hint | Ground | Ground | Rock] -> [ Rock | Player Hint | Ground | Ground | Rock] Message "My palindrome of a cousin is wasting his time... why PUSH when you can JUMP!?"                      --mr. Leg
late [ Water | Wall | Player Hint] -> [ Water | Wall | Player Hint ] Message "By now you're probably wondering whether or not you get POWERS back. I don't know, but if you're stuck on this island without SWIM, the answer is definitely not!"       --The Overrlord
late [MagicRelay | Wall | Wall | player Hint] -> [MagicRelay | Wall | Wall | player Hint] Message "SAFETY FIRST! Stand clear when removing shrubbery to avoid accidents. TWO SQUARES AWAY and work another day!"                    --Overrwyrld Association of Fastidious Lumberjacks
late [ player hint | Ground | Tree3 | Water] -> [ player hint | Ground | Tree3 | Water] Message "The trouble with shrubs is there's ALWAYS ONE too many!"   --Citizens for a Greenless Overrwyrld
late [ Calculator | Player Hint] -> [ Calculator | Player Hint] Message "WATCH THE CORNERS! When a shrubbery wilts, it TURNS where healthy shrubs do not. Wilted shrubs are ripe for BURNING!"          --Overrwyrld guide to combustibles
late [ player hint | ground | Tree2] -> [ player hint | ground | Tree2] Message "Morale is in decline after repeat setbacks to our research. Each time the expedition descends, we rapidly find ourselves back at the surface. It's possible that the rumored UNDERRWYRLD may have only been the result of wishful thinking." --Professor Scapegoat
late [Wall | player Hint| Ground |Wall] -> [Wall | player Hint | Ground | Wall] Message "When walls stack on walls, even I can't climb over them!"                         --Tall Steve
late [Ground | Wall | ground player | rock | wall] -> [Ground | Wall | ground player | RockUp | wall] Message "GET OUT of here! This spot is taken!"          --Small Steve
late [ Player Hint | Ground | Ground | Ground | Magic ] -> [ Player Hint | Ground | Ground | Ground | Magic ] Message "They said I was MAD to capture the Overrwyrld, claiming it was too vast to CONTAIN with such limited resources. Fools! They never suspected I could define THIS space as the OUTSIDE!           --The Overrlord
late [Save | PlayerX] -> [Save | PlayerX] Message "What a strange shape you've taken... what it could mean?"                 --?
late [Player Secret] -> [Player Secret] Message "Shhh..."                         --@kempedavid
late [Wall | Wall | player hint | Ground | Tree4 ] -> [Wall | Wall | player hint | Ground | Tree4 ] Message "FOUR are found within the WALL, but what is found I don't recall." --?
late [ Tree2 | Player Hint | Wall] -> [Tree2 | Player Hint | Wall] Message "The GATES were fractured long ago, but where they've gone I do not know." --?
late [ Rock | Player Hint | Rock ] -> [ Rock | Player Hint | Rock ] Message "Between the rocks and shrubs you'll see, a hidden path to move the KEY." --?

(Entrance & Exit)
late [ Player Quest | ... | Arrive ] -> [ Quest | ... | Player0 Arrive ] sfx5 Message !?
late [player exit] [home] -> [exit] [player home] sfx9 Message ESCAPE!

==============
WINCONDITIONS
==============

all Player on ending 

=======     
LEVELS
=======

(No cheating by looking at the map!)

###########################################################################*****##@##@#~~~~~#####~~####WWWWW##
##.......#####......%...~~~~~~~~~~~~.#.####@..#.6...#C##############*...###*...*.##.G%#~~~~#.....#~#.J.PPPPP.#
&..~~~~~..###..~~~~~.##.#######GHJJ#..'###########@##...Y,.H*G*..###..`..2###***..,.YH9#~~~#.###.#~#...PPMPP.#
&.~~...~~.###..~...~.##..*##*.#J9GH.##.#$####@#......###..*!..*.......*.*..##*..###.###~~~~~%#...#~#..G......#
&....4..~~~~~~~..~~~.##..H*.*.#GYGY.##*....@############..J.9.J......~~~~~~~~~~~~~~~~~~~~~~~~~.##~~#.......H.#
#.~~...~~.###....~...##.GJ.`.*#JHJG.....########..@..###..*...*.......*.*..##?G.#~~~~&#*~~~~~~~~~~~#..G......#
#..~~~~~..###GGG~....##!.H.##.@HGH..##.######...####.###..*G*H*..####....*.###.G@*~~~***##~~~#?##~~#....J..G.#
##...Y..........~Y.#&####@#####@######*#......#......###.........#.*###...*###.###~~#*!...#~~~~#.#~#.Y.H.....#
#####.##########~########.#####.########.###############.........#**##########H.~~~~.***#~~~~~~#@#~##"###..###
#####.##########~########.#####.########.######......###.........#############.~~~~~~~~~~~~~~~~~.#~######..###
&#$#....########~########.#####..........#######9..!####..#####..##...#.G..*###~~~#.~~~~~~~~.#~~#~~#....L..R.#
##...#.Y..##...G~G...##@#.###############....###########.#@....#...'J.#H.GYH##~~~~~#~~#~#.#~#~~~~~~#.........#
&#..###.#.......~Y.@.##.#.###...####*####.......#####....#...@.#.##.###.~~~~~~~~~~~~.#9##.#~~~~~~~~#.........#
##...#..####...H~H...##.#.#$#...##..#.*.#.####...#$#..##Y#J@...#J##.#.HG~H.###~~~~~~##....@#~~~~~~~#.αβγδε...#
&##!...#.......J~G...##.........###Y.....`.###.......###.#....@#.##.9HGH~J!###~~~~~~#######~~#.##~~#.ζηθικ...#
#######..###.!.G~J...##.........##..##.##.###@#.....####.#!....#.##JG.H.~~~~~~~.#~~#.~~~~~~~~...!#~#.λμνξο...#
######..####...H~J...#############...#.....#@.##...##@##..#####..##*JG.J.JG*##~#~~~~~~~~~~~~~####~~#.....U...#
######.#########~#.####.........##.###.###.######.###.##.........#####.######~~~~~~~~~~~~~~~~~~~~~~###########
######.#########~#.###############.GG#.###.######.###.@#.........#####.######~~~~~~~~~~~~~~~~~~~~~~RLQRQQQQQRR
#........H###$#.~..................YG............`.#####GJGJHH.JJ##.G..GHJJH#~~~##~#.#~#~#~#~~~~~~~LRQLQRLRQLR
#.....JGG.##...~~..........~~~~~~~~~~~~~~~~~~~~~~....###.........######J..@J#~~#..#...~.~.~.#~~~~~~RRQRRLQQQRL
#..GG..JG.##..~~....~~~~~~~~~~~~~~~X.I~~~~~~~~~~~.....##HJ.JGJ.GH##%#***.JGH#~~#.G..##~#~#~~~~~~~~~LLQLRLQRLRR
"!.J.HY.......~~~~~~~~~~~~~....~~~~.O.;::::::::::....Y...........##.@##*.GY.#~~~#.##~~~~~~#.#~~##~%RLLRRRLRLLR
#....JG...##...~~~~~..........#.~~~E.T~~~~~~~~~~~.....##GHYGJHJJG##.#.,.**###~~~~~~#~#~~~~~~~~#.@#~RLQLRLQRLRL
#..HG.....##...............#....~~~~~~~~~~~~~~~~~....###.........##&#H#.#?#.#~###.~.#.#~~~#.###.#~~RLLLRR.RLRL
#####.#################......#....................*###@#.........##..H.G#...##Y.#~~~~~~~~~#...#.#~~LRRLLR.LRLR
#####.#################........###..#############.####.@GJJH.HGHJ######.######G@#~#.#~~~~~~~#.###~~RLLRRL.LRRL
#####.#############B###........###..##########JHG.GJHGJJHHJH.HJGHG#####.######.#~~#.#~~~~~~~#.#.#~~######Z##$#
###A#.######.$$$$$...##.######..##..*......##.........HHHGHG.JGHJJHJ......#$##.~~~~~~.~~~~~~~~~.#~~#.,.J.....#
&..,..*...##.$.7.$...##.#...9.#.##.**....*.##..H..G.G.GGHJGH.HJHHGG.G.......#####~#.##*~~~~~#.##~~~#LRLRR#Z#@#
#.........##...$$$...##.#..#..#.##....*...*##.J.......HJHGHH.HGGJHH..H......#####.#~~~~~~~~~~~~~~~~#R.#OLZ;Z*#
#..*.1.*..##...$.....%%...###.......*....`.....GH.JJH.GGJ.5G..........,..J........#.###~~~~~#.###~~#H#LRR%Z~G#
&.........##.........##.#..#..#.##*...*....##.Y.......GGJ.HGHJHGJHH....HH..###......#.9#~~#...#$#~~#*.RY.~~~@#
#...*.*.!.##...?...!.&&.#.....#.##.*....**.##..H..J.G.HJG.GHJGHHGHG....Y.#####.`.~..#.#~~~#..3..#~~#.G.J#HG~J#
#####.######.........##..######.##......*..##.........GJH.HHGHJGHHJ..JG.G.#####Y#..#~~~~~~#.....#~~#@#?#N&*@H#
&####.######.........###........######.#######HGH.JGJHJGH.HGJHGHGH9JHHG.HJJ####.#####~##~~~#####~~~###########
#####.##################........######.##########.#######.JHJGJGHG.GJHH.GJ#####.#####~########D###~######RL###
#........*##.........##.....J...###$#..######.........###.......YHY.G.....#$##.......~.##8.......#~#.........#
#......*..##....#....##....HHG..###....######....*....###........JJ...J.....##...~~~~~.###.#####.#~#......0..#
#.........##.........##.....G...##......#####..*.*#*..##......@.GHH.....H...##..~~~~~......#...#.#~#.........#
&..*****.'....#...#..###YHJ.H..................*'9#*..##...*.*.'..........J.....~~.~~~.###...###.#~#.........#
#*........##..............HGJ...##....U..!.##..*.***..###*...G...#####.H.#####....Y.~H.###...#...#~#..Q......#
#..*......####..########........##.........##....*.......~~~~...##########@###.H.......###.......#~#.........#
#*........###############......##############.........###~H..GJ..###@.G.G..###.........##&...?...#~#.........#
########################################################~~~#######################################~###########

message "Welcome back! Did you have fun in the sandbox again? Such an active imagination."   --Mom                                                                                                  THE END              Thank you for Playing!