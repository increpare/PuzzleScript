title Variations
author arrogant.gamer
homepage www.arrogantgamer.com

========
OBJECTS
========

Background
white
00000
00000
00000
00000
00000

A_1
black white
00000
01100
01000
00010
00000

A_2
black white
00000
00110
00010
01000
00000

A_3
black white
00000
01000
00010
00110
00000

A_4
black white
00000
00010
01000
01100
00000

Player
black white
00000
01110
01010
01110
00000

Omega
white black
00000
00000
00100
00000
00000

W
black
00000
00000
00000
00000
00000

=======
LEGEND
=======

. = Background
z = Omega
P = Player
1 = A_1
2 = A_2
3 = A_3
4 = A_4


A = A_1 or A_2 or A_3 or A_4

! = Omega and A_1
@ = Omega and A_2
# = Omega and A_3
$ = Omega and A_4


=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Z
Player, A_1, A_2, A_3, A_4, W

======
RULES
======

[ > player | A ] -> [ > player | > A ]

( mark squares as "wanting to move" )

[ action player | A_1 ] -> [ action player | action A_1 ]
[ action player | A_2 ] -> [ action player | action A_2 ] 
[ action player | A_3 ] -> [ action player | action A_3 ]
[ action player | A_4 ] -> [ action player | action A_4 ]

( rotate squares that want to move )

[ action A_4 ] -> [ A_1 ]
[ action A_1 ] -> [ A_2 ]
[ action A_2 ] -> [ A_3 ]
[ action A_3 ] -> [ A_4 ]

( when pushing a square, push all similar squares )

[ > A_4 ] [ A_4 ] -> [ > A_4 ] [ > A_4 ]
[ > A_1 ] [ A_1 ] -> [ > A_1 ] [ > A_1 ]
[ > A_2 ] [ A_2 ] -> [ > A_2 ] [ > A_2 ]
[ > A_3 ] [ A_3 ] -> [ > A_3 ] [ > A_3 ]

==============
WINCONDITIONS
==============

all A on Z

=======
LEVELS
=======




( sokoban 0 -- this is a traversal game )

wwww..
w.zw..
w..www
w@P..w
w..1.w
w..www
wwww..

( sokoban 1 -- blocks of the same type move together )

wwww..
w.zw..
w..www
w!P..w
w..1.w
w..www
wwww..


( sokoban 2 -- you can push a stationary block to move a neighbour )

wwww..
w.zwww
w..w1w
w!P..w
w....w
w..www
wwww..

( sokoban 3 -- trivial remote contro example )

....wwww..
.$..w.zw..
....w..www
....w!...w
....w..3.w
.p..w..www
....wwww..

( sokoban 4 -- remote control challenge )

....wwww..
.$..w.zw..
....w..www
....w!...w
....w..1.w
.p..w..www
....wwww..

( sokoban 5 -- double reverse sokoba! )

..wwww.....wwww..
..wz.w..$..w.zw..
www..w.....w..www
w...!w.....w!...w
w.3..w.....w..1.w
www..w..p..w..www
..wwww.....wwww..

( sokoban 6 -- remote control with jama )

....wwww..
.$..w.zw..
....w..www
....w!p..w
....w..1.w
.p..w..www
....wwww..


( remote controlling puzzles )
( single ) ( double ) ( quad )

( sokoban puzzles )




( RC quad )

z....w....z
.3...w...1.
.....w.....
...4.w.4...
....zwz....
wwwwwwwwwww
....zwz....
...1.w.2...
..p..w.....
.1...w...3.
z....w....z


( RC double )

z.4.w..z
....w...
....w.2.
....w...
..p.w...
....w...
....w.1.
...4w...
z...w..z

message "fin"

