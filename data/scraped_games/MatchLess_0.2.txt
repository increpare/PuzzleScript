title MatchLess 0.2
author James ah ha

run_rules_on_level_start
flickscreen 15x8

(verbose_logging)

========
OBJECTS
========

background _
#aaaaaa

backBlock .
lightgrey

selectArea +
grey

redBlock
red
.....
..0..
.000.
..0..
.....
greenBlock
green
.....
..0..
.000.
..0..
.....
yellowBlock
yellow
.....
..0..
.000.
..0..
.....
blueBlock
blue
.....
..0..
.000.
..0..
.....
purpleBlock
purple
.....
..0..
.000.
..0..
.....

Player P
brown
..0..
.0.0.
0...0
.0.0.
..0..
TrailU
brown
.0.0.
.0.0.
.....
.0.0.
.....
TrailD
brown
.....
.0.0.
.....
.0.0.
.0.0.
TrailL
brown
.....
00.0.
.....
00.0.
.....
TrailR
brown
.....
.0.00
.....
.0.00
.....

2matches 7
red
.....
00.00
00.00
.....
.....
3matches 8
red
00.00
00.00
.....
00...
00...
4matches 9
red
00.00
00.00
.....
00.00
00.00
matchNum
transparent
match1
transparent
match2
transparent
match3
transparent
match4
transparent

blockNum
transparent
block2 2
red green
...00
...00
.....
11...
11...
block3 3
red green yellow
00...
001..
.111.
..122
...22
block4 4
red green yellow blue
00.11
00.11
.....
22.33
22.33
block5 5
red green yellow blue purple
00.11
0.4.1
.444.
2.4.3
22.33
sizeNum
transparent
sizeSmall x
Green
.....
.....
..0..
.....
.....
sizeMed y
Green
.....
.000.
.000.
.000.
.....
sizeLarge z
Green
00000
00000
00000
00000
00000

Help ?
white
..00.
.0..0
...0.
.....
...0.
Go O
white
.....
....0
...0.
0.0..
.0...
Play
transparent
startPtSmall
transparent
startPtMed
transparent
startPtLarge
transparent

temp
transparent
winId
transparent
loseId
transparent
loseline
(transparent)
brown
.....
00.00
.....
00.00
.....

headSm H
transparent
headMed I
transparent
headLrg J
transparent
body =
transparent
tail t
transparent

=======
LEGEND
=======

2blocks = redBlock or greenBlock
3blocks = redBlock or greenBlock or yellowBlock
4blocks = redBlock or greenBlock or yellowBlock or blueBlock
5blocks = redBlock or greenBlock or yellowBlock or blueBlock or purpleBlock
allBlocks = 5blocks
block25 = block2 or block3 or block4 or block5

matchbits = match1 or match2 or match3 or match4
trailBits = trailU or trailD or trailL or trailR

m = matchNum and selectArea
b = blockNum and selectArea
s = sizeNum and selectArea
match24 = 2matches or 3matches or 4matches
startPtBits = startPtSmall or startPtMed or startPtLarge
U = startPtSmall and backblock
Q = startPtMed and backblock
W = startPtLarge and backblock
sizeSL = sizeSmall or sizeMed or sizeLarge
head = headSm or headMed or headLrg
pastLog = head or body or tail
$ = backBlock and redBlock

=======
SOUNDS
=======

SFX0 36072308
SFX1 77854705
SFX2 57544706
player move 68226507
undo 85445904
endgame 22796708

================
COLLISIONLAYERS
================

background
backBlock, pastLog
5blocks, selectArea
TrailU, sizeSL, loseId
temp, match1, TrailD
match2, TrailR
match3, TrailL
match4, play, startPtBits
winId, block25, match24
loseline, matchNum, blockNum, sizeNum, go, help
player


======
RULES     
======     

(SELECTING)
[Action player sizeSL] [sizeNum] -> [player sizeSL] [sizeSL sizeNum] SFX2
[Action player match24] [matchNum] -> [player match24] [match24 matchNum] SFX2
[Action player block25] [blockNum] -> [player block25] [block25 blockNum] SFX2
(if ACTION tick and not all selections made then display help
OR action help)
[Action player go] [sizeNum no sizeSL] -> [Action player go temp] [sizeNum no sizeSL]
[Action player go] [matchNum no match24] -> [Action player go temp] [matchNum no match24]
[Action player go] [blockNum no block25] -> [Action player go temp] [blockNum no block25]
[Action player help] [go]  -> [Action player help] [go temp]
[go temp] -> [go] message select a size of play area (small, medium or large) AND the sequence length to avoid (2, 3 or 4) AND the number of colours (2, 3, 4 or 5)

(if ACTION tick & all selections made then set to play)
[Action player go] [sizeSL sizeNum] [match24 matchNum] [block25 blockNum] -> [player go play] [sizeSL sizeNum] [match24 matchNum] [block25 blockNum] SFX1

(depending on size select jump to play area, initate head and place first block)
[player go play] [sizeNum sizeSmall] [U] [H]->  [go play] [sizeNum sizeSmall] [U player random 2blocks] [H play]
[player go play] [sizeNum sizeMed] [Q] [I] ->  [go play] [sizeNum sizeMed] [Q player random 2blocks] [I play]
[player go play] [sizeNum sizeLarge] [W] [J] ->  [go play] [sizeNum sizeLarge] [W player random 2blocks] [J play]

(SET UP play area)
(random layout blocks in play area, only place block if it is next to another block)
[blockNum block2][allBlocks | backBlock no 2blocks] -> [blockNum block2][allBlocks | backBlock random 2blocks]
[blockNum block3][allBlocks | backBlock no 3blocks] -> [blockNum block3][allBlocks | backBlock random 3blocks]
[blockNum block4][allBlocks | backBlock no 4blocks] -> [blockNum block4][allBlocks | backBlock random 4blocks]
[blockNum block5][allBlocks | backBlock no 5blocks] -> [blockNum block5][allBlocks | backBlock random 5blocks]

(set up trail all other play areas)
[player allBlocks] [head play no allBlocks] [backBlock no allBlocks] -> [player allBlocks] [head play no allBlocks] [backBlock no allBlocks trailU]
(log block player is on)
late [player allBlocks] [head play no allBlocks] -> [player allBlocks] [head play allBlocks]


(check player move)
(can not go into select area)
[> player | selectArea] -> cancel
(if not playing then do not stray onto playing area])
[> player | backBlock] [go no play] -> cancel
(if playing stay in play area)
[> player | no backBlock] [go play] -> cancel
(if playing no action)
[Action player] [go play] -> cancel
(can not walk over trail)
[> player | trailbits] -> cancel

(PLAYING)

(shuffle past blocks down by one)
(remove tail block)
[tail allBlocks] -> [tail]
(do this just once for last block in log line)
random right[allBlocks pastLog| pastlog no allBlocks] -> [pastLog temp no allBlocks | pastLog allBlocks]
(shuffle other blocks on one)
right [allBlocks | temp] -> [temp no allBlocks | allBlocks ]
[temp] -> []

(move players current block to head)
[> player | allBlocks] [head play] -> [> player | allBlocks] [head play allBlocks]
(mark all blocks in body that are same as head block)
right[head play redBlock| ... | redBlock] -> [head play redBlock | ... | redBlock match1]
right[head play blueBlock| ... | blueBlock] -> [head play blueBlock | ... | blueBlock match1]
right[head play yellowBlock| ... | yellowBlock] -> [head play yellowBlock | ... | yellowBlock match1]
right[head play greenBlock| ... | greenBlock] -> [head play greenBlock | ... | greenBlock match1]
right[head play purpleBlock| ... | purpleBlock] -> [head play purpleBlock | ... | purpleBlock match1]

(check lose condition)
right[match1 | match2] [matchNum 2matches] -> [match1 loseline | match2 loseline] [matchNum 2matches loseId]
right[match1 | match2 | match3] [matchNum 3matches] -> [match1 loseline | match2 loseline | match3 loseline] [matchNum 3matches loseId]
right[match1 | match2 | match3 | match4] [matchNum 4matches] -> [match1 loseline | match2 loseline | match3 loseline | match4 loseline] [matchNum 4matches loseId]
[matchNum loseId] -> message You have repeated block sequence     Press X to see the repeated sequence (you can then press Z to undo your last move and try again) 
[matchNum loseId] -> [matchNum no loseId] SFX0

(put trail in place)
right [right player | allBlocks] -> [trailR | trailL allBlocks player]
left [left player | allBlocks] -> [trailL | trailR allBlocks player]
down [down player | allBlocks] -> [trailD | trailU allBlocks player]
up [up player | allBlocks] -> [trailU | trailD allBlocks player]

(change match markers to next level of match)
[match4] -> []
[matchNum 3matches] [match3] -> [matchNum 3matches] []
right [match3 | ] -> [ | match4]
[matchNum 2matches] [match2] -> [matchNum 2matches] []
right [match2 | ] -> [ | match3]
right [match1 | ] -> [ | match2]
===============
WINCONDITIONS
==============

all backBlock on trailBits
 
=======     
LEVELS
=======
__P__________++
?___x__y__z__+S
_____________++
O___7__8__9__+M
_____________++
_2__3__4__5__+B
_____________++
_____________++
+++++++++++++++
+++++++++++++++
++++.......++++
++++...U...++++
++++.......++++
+++++++++++++++
+++++++++++++++
H=============T
+++++++++++++++
++.....+.....++
++...........++
+++....Q....+++
++...........++
++...........++
+++++++++++++++
I=============T
...............
...............
...............
.......W.......
...............
...............
...............
J=============T

message welldone