title Solitaire
author Daniel Sherlock
(Note: The game itself is traditional.)
homepage https://en.wikipedia.org/wiki/Peg_solitaire
(I [the author] am temorarily lacking in webpage, so you can read all about solitaire instead. It's good fun.)

background_color DarkGreen
run_rules_on_level_start
key_repeat_interval 0.2


========
OBJECTS
========

(As you can probably tell, I've struggled to get reasonable colours and shapes for all the things. Suggestions welcome.)

Background
#6bb745 #45b758 #93b745
10201
00000
20102
00000
10201

Board
#ad914a #937b3e
01000
10000
00001
00010
00100

TargetBoard
#ad914a #937b3e
01000
10000
00001
00010
00100

EdgeBoard_Top
#ad914a #937b3e LightBrown
22222
10000
00001
00010
00100

EdgeBoard_Top_Right
#ad914a #937b3e LightBrown
2....
12...
002..
0002.
00102

EdgeBoard_Right
#ad914a #937b3e LightBrown
01002
10002
00002
00012
00102

EdgeBoard_Bottom_Right
#ad914a #937b3e LightBrown
01002
1002.
002..
02...
2....

EdgeBoard_Bottom
#ad914a #937b3e #8d712a
01000
10000
00001
00010
22222

EdgeBoard_Bottom_Left
#ad914a #937b3e DarkBrown
21000
.2000
..201
...20
....2

EdgeBoard_Left
#ad914a #937b3e DarkBrown
21000
20000
20001
20010
20100

EdgeBoard_Top_Left
#ad914a #937b3e #8d712a
....2
...20
..201
.2010
20100

Hole
Black DarkGrey Grey
.....
..1..
.201.
..2..
.....

TargetHole
DarkRed Red LightRed
.....
..1..
.201.
..2..
.....

Marble
DarkBlue LightBlue
.....
.011.
.001.
.000.
.....

TargetMarble
DarkBlue LightBlue LightRed
..2..
.011.
20012
.000.
..2..

WalkingPlayer
Blue
00.00
0...0
.....
0...0
00.00

LiftingPlayer
Purple
00.00
0...0
.....
0...0
00.00


=======
LEGEND
=======

. = Background
! = Board
o = Hole and Board
+ = TargetHole and TargetBoard
0 = Marble and Board
X = TargetMarble and TargetBoard
@ = WalkingPlayer and Hole and Board
* = WalkingPlayer and TargetHole and TargetBoard

EdgeBoard = EdgeBoard_Top or EdgeBoard_Top_Right or EdgeBoard_Right or EdgeBoard_Bottom_Right or EdgeBoard_Bottom or EdgeBoard_Bottom_Left or EdgeBoard_Left or EdgeBoard_Top_Left

AnyBoard = Board or TargetBoard or EdgeBoard
AnyHole = Hole or TargetHole
AnyMarble = Marble or TargetMarble
Player = WalkingPlayer or LiftingPlayer


=======
SOUNDS
=======

(I considered doing some sounds for various things, but was listening to music at the time, so didn't. As with above, suggestions are welcome.)


================
COLLISIONLAYERS
================

Background
AnyBoard
AnyHole, AnyMarble
Player


======
RULES
======

[> WalkingPlayer | AnyHole] -> [| WalkingPlayer AnyHole]
[> WalkingPlayer | AnyMarble] -> [| WalkingPlayer AnyMarble]

[action WalkingPlayer AnyMarble] -> [LiftingPlayer AnyMarble]
[action LiftingPlayer AnyMarble] -> [WalkingPlayer AnyMarble]

[> LiftingPlayer AnyMarble | AnyMarble | AnyHole] -> [Hole | Hole | WalkingPlayer Marble]

[> Player] -> [Player]

late [Hole TargetBoard] -> [TargetHole TargetBoard]
late [TargetHole Board] -> [Hole Board]

late [Marble TargetBoard] -> [TargetMarble TargetBoard]
late [TargetMarble Board] -> [Marble Board]

late up [Board | no AnyBoard] -> [EdgeBoard_Top |]
late right [EdgeBoard_Top | no AnyBoard] -> [EdgeBoard_Top_Right |]
late right [Board | no AnyBoard] -> [EdgeBoard_Right |]
late down [EdgeBoard_Right | no AnyBoard] -> [EdgeBoard_Bottom_Right |]
late down [Board | no AnyBoard] -> [EdgeBoard_Bottom |]
late left [EdgeBoard_Bottom | no AnyBoard] -> [EdgeBoard_Bottom_Left |]
late left [Board | no AnyBoard] -> [EdgeBoard_Left |]
late left [EdgeBoard_Top | no AnyBoard] -> [EdgeBoard_Top_Left |]



==============
WINCONDITIONS
==============

All Marble on TargetBoard

(
--Testing Win Condition:
--For when you can't be bothered to solve a whole puzzle.
--Note: You must comment out the other win condition for this to have any effect.

Some Player on TargetMarble

--End of testing win condition.
)

=======
LEVELS
=======

(
--Tutorial:
--Comment/Uncomment this as necessary.
)

message Hi.
message "Solitaire" is a traditional solo board game played all over the world.
message In it, you lift marbles over each other to remove them from the board - aiming to be left with one marble in the centre of the board.
message Here's a practice round: "X" or "SPACE" to lift a marble, arrow keys to move, "R" to restart, "Z" to undo.

...........
...!!!!!...
..!!ooo!!..
.!!!ooo!!!.
.!ooooooo!.
.!o00*ooo!.
.!ooooooo!.
.!!!ooo!!!.
..!!ooo!!..
...!!!!!...
...........

message See? That wasn't so bad! Now, the real thing:

(
--End of tutorial.
)

...........
...!!!!!...
..!!000!!..
.!!!000!!!.
.!0000000!.
.!000*000!.
.!0000000!.
.!!!000!!!.
..!!000!!..
...!!!!!...
...........

message That was the "standard" (English-style) board - but history has seen many different shapes of solitaire.
message For example, this is the European-style board, attested as far back as 17th century France:
message [Note here though that the target hole for the final marble is no longer in the centre.]

...........
...!!!!!...
..!!000!!..
.!!00000!!.
.!000@000!.
.!0000000!.
.!000X000!.
.!!00000!!.
..!!000!!..
...!!!!!...
...........

message Well done!
message Here's one from 18th century Germany - similar to the one you started with, but with 12 more holes:

.............
....!!!!!....
...!!000!!...
..!!!000!!!..
.!!!!000!!!!.
.!000000000!.
.!0000*0000!.
.!000000000!.
.!!!!000!!!!.
..!!!000!!!..
...!!000!!...
....!!!!!....
.............

message You're getting good at these!
message Another one on the European-style board now, but with different start and finish positions:

...........
...!!!!!...
..!!000!!..
.!!00000!!.
.!0000000!.
.!0X000@0!.
.!0000000!.
.!!00000!!.
..!!000!!..
...!!!!!...
...........

message ... And this one is diamond-shaped:

.............
....!!!!!....
...!!!0!!!...
..!!!000!!!..
.!!!00000!!!.
.!!0000000!!.
.!0000*0000!.
.!!0000000!!.
.!!!00000!!!.
..!!!000!!!..
...!!!0!!!...
....!!!!!....
.............

message <insert somewhat patronising congratulatory comment here>
message Yeah, I went there.
message Ok, here's your 3rd-and-final puzzle based on the European-style board:

...........
...!!!!!...
..!!00X!!..
.!!00000!!.
.!0000000!.
.!0000000!.
.!0000000!.
.!!00000!!.
..!!@00!!..
...!!!!!...
...........

message Did you think that was the last one? Haha! Sorry, no.
message This one, first described in the 20th century, is all asymmetric. Funky.

............
...!!!!!....
..!!000!!...
.!!!000!!!..
.!!!000!!!!.
.!00000000!.
.!000*0000!.
.!00000000!.
.!!!000!!!!.
..!!000!!!..
...!!!!!!...
............

message Wow, you did it! You solved every version I could throw at you. Metaphorically thrown, obvs.
message Seriously, that's genuinely impressive.

message If you still need more solitaire, that's easy - why not make one yourself...
message 1. Find the source code for all this at https://gist.github.com/DanielSherlock/4471fa6feffa53988779c6bbe4050830
message 2. Copy and paste it into https://puzzlescript.net/editor.html
message 3. Click "Level Editor" and draw to your heart's content. Then click "S" to save.
message 4. Copy and past the output (in blue) into the source on the left, just under where it says "Levels".
message 5. There is no step 5 - you can just run the new code (click "Run").

message Oh, and I should say that...
message ... other than the amazing PuzzleScript by https://www.increpare.com/ ...
message ... the "Peg Solitaire" page on Wikipedia was a massive help in writing this.
