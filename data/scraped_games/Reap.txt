title Reap
text_color darkred

noundo
key_repeat_interval 0.2
noaction
realtime_interval 0.5


========
OBJECTS
========

(Background
darkblue darkgreen
01110
11100
10000
00111
01110)

Background
darkblue

Target
darkgray yellow lightbrown black
.333.
.212.
10001
10001
.0.0.

Ghost
white lightblue lightgray
.111.
.202.
10001
10001
.00..

Wall
gray darkgray
00010
11111
01000
11111
00010

Heaven
black

Player
Black darkgray white
.0000
00222
00022
.0000
00000

Scythe
lightgray darkbrown
1000.
1...0
1....
1....
1....

=======
LEGEND
=======

. = Background
# = Wall
P = Player
O = Scythe
@ = Target
C = Heaven


=======
SOUNDS
=======

Player move 72644907
sfx1 69392502
sfx2 85547108
Ghost move 25346108


================
COLLISIONLAYERS
================

Background
Heaven
Player, Wall, Scythe, Target
Ghost

======
RULES
======

[ Scythe | moving Player] -> [ moving Scythe | moving Player ]
[ > PLayer | Wall ] -> cancel
[ > PLayer | Target ] -> cancel

[Action Player | Scythe ] -> [Player | > scythe] SFX1

[stationary Player][Ghost] -> [Player][up Ghost] sfx3
vertical [up Ghost | Ghost] -> [up Ghost | left Ghost]
horizontal [Ghost | left Ghost] -> [left Ghost | left Ghost]

[> Scythe | Target] -> [ > Scythe | Ghost] sfx2


==============
WINCONDITIONS
==============

all Ghost on Heaven
no Target

=======
LEVELS
=======

message Reap their souls...


CCCCCCCCC
#########
#po....@#
#########


CCCCCCC
#######
#.p...#
#....@#
#.....#
#....##
#....o#
#######


CCCCCCC
#######
#.p...#
#....@#
#.....#
##....#
#o....#
#######

cccccccc
########
#p.#..@#
#......#
#.####.#
#..###.#
#..###.#
##...#.#
###..o.#
########

cccccccc
########
#@#...@#
#.....##
#......#
#..po..#
##.....#
#@...#@#
########

ccccccccCC
##########
#@#.....@#
#.#..#.###
#........#
#.#.##...#
#...##.#.#
#....po..#
###.#..#.#
#@.....#@#
##########



message Now they are in heaven, at peace.
