title Michael Bay's Legend of Zelda
author Soolseem
youtube 4qJ-xEZhGms
again_interval .4
noaction
verbose_logging

========
OBJECTS
========

Background
darkgrey grey
11111
01111
11101
11111
10111

Temp
transparent

Target
White
.....
.....
.....
.....
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black lightBrown White Green
.333.
.111.
22222
.333.
.3.3.

Torch
Brown
.00..
..00.
...00
.....
.....

LitTorch
Brown Yellow Red Orange
.21..
..30.
...00
.....
.....

LitBrazier
Black Yellow Red
0.0.0
01020
01210
.000.
..0..

UnlitBrazier
Black
0.0.0
0.0.0
0.0.0
.000.
..0..

Closed
Darkbrown Yellow
00100
00100
01110
00100
00100

Open
Darkbrown
0...0
0...0
0...0
0...0
0...0


TNT
Blue Darkblue White 
....2
...2.
.000.
00010
.000.

Explosion
Red orange yellow
02122
00121
11102
21021
11220

T2
transparent

T3
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player
T = Torch
L = LitBrazier
* = Target
U = UnlitBrazier
C = Closed
O = Open
@ = Open and Target
N = TNT
E = Explosion

Item = Torch or LitTorch
Fire = LitTorch or LitBrazier


=======
SOUNDS
=======

LitTorch Create 84920302
LitBrazier Create 18757502
Player Move 43555907
Target Create 49001708
EndLevel 66884103
Explosion Create 90347302

================
COLLISIONLAYERS
================

Background
Temp
T2
T3
Target
Open
Player, Wall, Torch, LitTorch, LitBrazier, UnlitBrazier, Closed, TNT, Explosion

======
RULES
======

[Player | Item] -> [ Player T2 | Item T3 ]
late [ Player T2 ] [ Item no T3 ] -> cancel
late [ T2 ] -> []
late [ T3 ] -> []

[ Moving Player | Stationary Item ] -> [ Moving Player | Moving Item ]
[ > player | TNT] -> [> Player | > TNT]
late [Torch | LitBrazier] -> [LitTorch | LitBrazier]
late [LitTorch | UnlitBrazier] -> [LitTorch | LitBrazier] again
late [Closed] -> [Open]
late [UnlitBrazier][Open] -> [UnlitBrazier][Closed]
late [Open] -> [Open Target]
late [Fire | TNT] -> [ Fire | Explosion]
late [Explosion | ] -> [Explosion | Temp] again
late [Temp] -> [Explosion] again

==============
WINCONDITIONS
==============

some player
All player on target

=======
LEVELS
=======

###C###
#.....#
#t..u.#
#.....#
#..u..#
#.....#
#l...p#
#######

#c#############
#..l###########
#...###########
#p..tututututu#
###############

Message You might want to be careful around the bombs. Or not. I'm not your boss.

########
#......#
#...n..#
#u.l.n.#
##n.t#.#
##.n...c
##..p..#
########

Message You win! This is obviously the best Zelda game, and anyone who says otherwise will be shot.

Message Eat your heart out Ocarina of Time.