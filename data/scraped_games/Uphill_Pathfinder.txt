Title Uphill Pathfinder
Author Loneship Games
homepage http://connorses.deviantart.com/
========
OBJECTS
========
background
darkblue

player
blue
.....
.000.
00000
.0.0.
.....
monster1
red white
.000.
.010.
00000
.0.0.
.....

monster2
red white
.000.
.010.
00000
.0.0.
.....

monster3
darkred white
.000.
.010.
00000
.0.0.
.....

wall
brown

1
grey
.....
.....
..0..
.....
.....
2
grey
.....
...0.
.....
.0...
.....
3
grey
.....
.0...
..0..
...0.
.....

target
green lightgreen
.....
00000
01110
01110
00000
=======
LEGEND
=======
. = background
p = player
# = wall
m = monster1
* = target
path = 1 or 2 or 3
monster = monster1 or monster2 or monster3
=======
SOUNDS
=======

================
COLLISIONLAYERS
================
background
path
target
monster
player, wall
======
RULES
======
[path] -> [ ]
[monster2] ->[monster1]

[> player|wall] -> [player|wall]
[> player|monster3] -> [player|monster3]

[action player] [monster1] -> [player] [monster3]
[action player] [monster3] -> [player] [monster1]

startloop
late [player|no path no wall] -> [player|3]
late [3|no wall no player no path] -> [3|2]
late [2|no wall no player no path] -> [2|1]
late [1|no wall no player no path] -> [1|3]
endloop

[monster1|moving player] -> [> monster2|moving player]

late [monster1 no path|3 no wall no monster] -> [ |monster2 3]
late [monster1 no path|2 no wall no monster] -> [ |monster2 2]
late [monster1 no path|1 no wall no monster] -> [ |monster2 1]

[> monster|wall] -> [monster|wall]

late [monster1 1|2 no wall no monster] -> [1|monster2 2]
late [monster1 2|3 no wall no monster] -> [2|monster2 3]
late [monster1 3|1 no wall no monster] -> [3|monster2 1]
==============
WINCONDITIONS
==============
all player on target
=======
LEVELS
=======
.........#.#######.
.#######.#.#.......
.#.....#.#...##..#.
....##.#.##..##...#
..###..#........#..
##m##.##....#...#..
#......#..#.#.###p.
########...........