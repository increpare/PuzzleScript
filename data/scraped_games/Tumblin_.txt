( There are no hints hidden in the source- reading it won't get you to the answer. )

title Tumblin'
author Westward
homepage aka.ms/westward

========
OBJECTS
========

Background
Black

Sand 
Yellow Brown
00000
01000
00000
00010
00000

Empty
transparent

Target 
Blue
0...0
.0.0.
..0..
.0.0.
0...0

Wall 
BROWN

Player 
lightbrown brown
.000.
00000
00000
00000
.000.

RightPlayer
lightbrown brown
.000.
00100
00010
00100
.000.

LeftPlayer
lightbrown brown
.000.
00100
01000
00100
.000.

UpPlayer
lightbrown brown
.000.
00100
01010
00000
.000.

DownPlayer
lightbrown brown
.000.
00000
01010
00100
.000.

ActionFlag
transparent

ActionFlag2
transparent

=======
LEGEND
=======

- = Empty
. = Sand
# = Wall and Sand
P = Player and Sand
O = Target and Sand
Q = Player and Target and Sand

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Empty, Sand
Target
Player, Wall, RightPlayer, LeftPlayer, UpPlayer, DownPlayer
ActionFlag, ActionFlag2

======
RULES     
======     

(Pressing right turns the ball into a RightPlayer, which moves
 until it hits something and turns back)
[ RIGHT Player ] -> [ RightPlayer ]
RIGHT [ RightPlayer ] -> [ > RightPlayer ]
RIGHT [ RightPlayer | Wall ] -> [ Player | Wall ]
RIGHT [ RightPlayer | Player ] -> [ Player | Player ]

[ UP Player ] -> [ UpPlayer ]
UP [ UpPlayer ] -> [ > UpPlayer ]
UP [ UpPlayer | Wall ] -> [ Player | Wall ]
UP [ UpPlayer | Player ] -> [ Player | Player ]

[ DOWN Player ] -> [ DownPlayer ]
DOWN [ DownPlayer ] -> [ > DownPlayer ]
DOWN [ DownPlayer | Wall ] -> [ Player | Wall ]
DOWN [ DownPlayer | Player ] -> [ Player | Player ]

[ LEFT Player ] -> [ LeftPlayer ]
LEFT [ LeftPlayer ] -> [ > LeftPlayer ]
LEFT [ LeftPlayer | Wall ] -> [ Player | Wall ]
LEFT [ LeftPlayer | Player ] -> [ Player | Player ]

(If there are any rolling balls, we need another turn)
late [ RightPlayer ] -> [ RightPlayer ] again
late [ UpPlayer ] -> [ UpPlayer ] again
late [ DownPlayer ] -> [ DownPlayer ] again
late [ LeftPlayer ] -> [ LeftPlayer ] again

(You can only win on a turn when you pushed X)
[Action Player ] -> [ActionFlag Player]
late [ActionFlag ] -> [ActionFlag2]
[ActionFlag2] -> []

==============
WINCONDITIONS
==============

All Target on Player

(No intermediate rolling balls)
No RightPlayer
No LeftPlayer
No UpPlayer
No DownPlayer

Some ActionFlag2

=======     
LEVELS
=======

MESSAGE The wind blows all the tumbleweeds until they hit something.

MESSAGE Press X when all goals are covered to proceed.

########--------
#.#....#-#######
#.O.P..#-#..P..#
#.....##-#....P#
##.....#-##.P.##
#...#..#-#######
########--------

##########--------
#....#...#-#######
#P#..#.P.#-#....P#
###.##.#O#-##..P##
#O...#####-###.###
##########-#######

#########--------
###...P.#--------
###.#.#.#-#######
#......O#-#...P.#
#.#.#.#.#-#..P.##
#......##-#.P..##
#.#.#.###-#.....#
#.....###-#######
#########--------

#########--------
###O....#--------
###.#.#.#-#######
#....P..#-#.....#
#.#.#.#.#-##...##
#O......#-#.PP###
#.#P#.###-#######
#.....###--------
#########--------

#######---------
#O#.PO#-########
#...#P#-###.P.##
##....#-#.P....#
##....#-#.PP..##
##.#..#-########
#######---------

########-########
#P##P.O#-#P##P..#
#.P..OO#-#.P....#
##.P.O##-##.P..##
########-########


MESSAGE Congratulations!
MESSAGE Can you feel your way to the fastest solution of each level?