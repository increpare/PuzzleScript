OBJECTS

Background
#FFFFFF #F8F8F8
00000
01010
00000
01010
00000

Player
#FFBBEE
00000
0...0
0...0
0...0
00000

Wall
Yellow White
00000
00000
00000
00000
00000

Hydrogen
Red Black
.....
.111.
.101.
.111.
.....

Nitrogen
Green Black
.....
.111.
.101.
.111.
.....

Oxygen
Blue Black
.....
.111.
.101.
.111.
.....

Carbon
Yellow Black
.....
.111.
.101.
.111.
.....

Helium
White Black
.....
.111.
.101.
.111.
.....

1Electron
LightGray
.....
.0...
.....
.....
.....

2Electron
LightGray
.....
...0.
.....
.0...
.....

3Electron
LightGray
.....
.0.0.
.....
.0...
.....

4Electron
LightGray
.....
.0.0.
.....
.0.0.
.....

BondU
Black
..0..
.....
.....
.....
.....

BondL
Black
.....
.....
0....
.....
.....

BondR
Black
.....
.....
....0
.....
.....

BondD
Black
.....
.....
.....
.....
..0..

BondUL
Black
..0..
.....
0....
.....
.....

BondUR
Black
..0..
.....
....0
.....
.....

BondUD
Black
..0..
.....
.....
.....
..0..

BondLR
Black
.....
.....
0...0
.....
.....

BondLD
Black
.....
.....
0....
.....
..0..

BondRD
Black
.....
.....
....0
.....
..0..

BondULR
Black
..0..
.....
0...0
.....
.....

BondULD
Black
..0..
.....
0....
.....
..0..

BondURD
Black
..0..
.....
....0
.....
..0..

BondLRD
Black
.....
.....
0...0
.....
..0..

BondULRD
Black
..0..
.....
0...0
.....
..0..

Temp1
Red

Temp2(These are placeholders for various things.)
Green

Temp3
Blue

Temp4
Yellow


LEGEND

Atom = Hydrogen or Oxygen or Nitrogen or Carbon or Helium
Orbital = 1Electron or 2Electron or 3Electron or 4Electron
UBond = BondU or BondUL or BondUR or BondUD or BondULR or BondULD or BondURD or BondULRD
LBond = BondL or BondUL or BondLR or BondLD or BondULR or BondULD or BondLRD or BondULRD
RBond = BondR or BondUR or BondLR or BondRD or BondULR or BondURD or BondLRD or BondULRD
DBond = BondD or BondUD or BondLD or BondRD or BondULD or BondURD or BondLRD or BondULRD
1Bond = BondU or BondL or BondR or BondD
2Bond = BondUL or BondUR or BondUD or BondLR or BondLD or BondRD
3Bond = BondULR or BondULD or BondURD or BondLRD
Bond = 1Bond or 2Bond or 3Bond or BondULRD
Temps = Temp1 or Temp2 or Temp3 or Temp4
AtomStuff = Bond or Orbital
. = Background
# = Wall
H = Hydrogen or 1Electron
O = Oxygen or 2Electron
N = Nitrogen or 3Electron
C = Carbon or 4Electron
E = Helium
0 = Helium or Player
1 = Hydrogen or 1Electron or Player
2 = Oxygen or 2Electron or Player
3 = Nitrogen or 3Electron or Player
4 = Carbon or 4Electron or Player
P = Player

COLLISIONLAYERS

Background
Player
Bond
Wall, Atom
Orbital
Temps

RULES

[ Up Player ] -> [ Up Player Temp1 ] 
[ Left Player ] -> [ Left Player Temp2 ] 
[ Right Player ] -> [ Right Player Temp3 ] 
[ Down Player ] -> [ Down Player Temp4 ] 
Up [ Temps UBond | noTemps ] -> [ Temps Ubond | Temps ] 
+ Left [ Temps LBond | noTemps ] -> [ Temps Lbond | Temps ] 
+ Right [ Temps RBond | noTemps ] -> [ Temps Rbond | Temps ] 
+ DOwn [ Temps DBond | noTemps ] -> [ Temps Dbond | Temps ] 
+ Up [ Temp1 | Atom no Temp1 ] -> [ Temp1 | AtomTemp1 ] 
+ Left [ Temp2 | Atom no Temp2 ] -> [ Temp2 | AtomTemp2 ] 
+ Right [ Temp3 | Atom no Temp3 ] -> [ Temp3 | AtomTemp3 ] 
+ Down [ Temp4 | Atom no Temp4 ] -> [ Temp4 | AtomTemp4 ] 
[ Temp1 Atom ] -> [ Up Atom ] 
[ Temp2 Atom ] -> [ Left Atom ] 
[ Temp3 Atom ] -> [ Right Atom ] 
[ Temp4 Atom ] -> [ Down Atom ] 
[ > Atom | Wall ] -> Cancel 
[ > Atom AtomStuff ] -> [ > Atom> AtomStuff ] 
Late Right [ Orbital no Temps no RBond | Orbital no Temps no LBond ] -> [ Orbital Temp1 | Orbital Temp2 Sfx1 ] (Bond horizontally.) 
+ Late [ Temp1 no Bond ] -> [ Temp3 BondR ] 
+ Late [ Temp1 BondU ] -> [ Temp3 BondUR ] 
+ Late [ Temp1 BondL ] -> [ Temp3 BondLR ] 
+ Late [ Temp1 BondD ] -> [ Temp3 BondRD ] 
+ Late [ Temp1 BondUL ] -> [ Temp3 BondULR ] 
+ Late [ Temp1 BondUD ] -> [ Temp3 BondURD ] 
+ Late [ Temp1 BondLD ] -> [ Temp3 BondLRD ] 
+ Late [ Temp1 BondULD ] -> [ Temp3 BondULRD ] 
+ Late [ Temp2 no Bond ] -> [ Temp3 BondL ] 
+ Late [ Temp2 BondU ] -> [ Temp3 BondUL ] 
+ Late [ Temp2 BondR ] -> [ Temp3 BondLR ] 
+ Late [ Temp2 BondD ] -> [ Temp3 BondLD ] 
+ Late [ Temp2 BondUR ] -> [ Temp3 BondULR ] 
+ Late [ Temp2 BondUD ] -> [ Temp3 BondULD ] 
+ Late [ Temp2 BondRD ] -> [ Temp3 BondLRD ] 
+ Late [ Temp2 BondURD ] -> [ Temp3 BondULRD ] 
+ Late [ Temp3 1Electron ] -> [  ] 
+ Late [ Temp3 2Electron ] -> [ 1Electron ] 
+ Late [ Temp3 3Electron ] -> [ 2Electron ] 
+ Late [ Temp3 4Electron ] -> [ 3Electron ] 
Late Down [ Orbital no Temps no DBond | Orbital no Temps no UBond ] -> [ Orbital Temp1 | Orbital Temp2 Sfx1 ] (Bond vertically.) 
+ Late [ Temp1 no Bond ] -> [ Temp3 BondD ] 
+ Late [ Temp1 BondU ] -> [ Temp3 BondUD ] 
+ Late [ Temp1 BondL ] -> [ Temp3 BondLD ] 
+ Late [ Temp1 BondR ] -> [ Temp3 BondRD ] 
+ Late [ Temp1 BondUL ] -> [ Temp3 BondULD ] 
+ Late [ Temp1 BondUR ] -> [ Temp3 BondURD ] 
+ Late [ Temp1 BondLR ] -> [ Temp3 BondLRD ] 
+ Late [ Temp1 BondULR ] -> [ Temp3 BondULRD ] 
+ Late [ Temp2 no Bond ] -> [ Temp3 BondU ] 
+ Late [ Temp2 BondL ] -> [ Temp3 BondUL ] 
+ Late [ Temp2 BondR ] -> [ Temp3 BondUR ] 
+ Late [ Temp2 BondD ] -> [ Temp3 BondUD ] 
+ Late [ Temp2 BondLR ] -> [ Temp3 BondULR ] 
+ Late [ Temp2 BondLD ] -> [ Temp3 BondULD ] 
+ Late [ Temp2 BondRD ] -> [ Temp3 BondURD ] 
+ Late [ Temp2 BondLRD ] -> [ Temp3 BondULRD ] 
+ Late [ Temp3 1Electron ] -> [  ] 
+ Late [ Temp3 2Electron ] -> [ 1Electron ] 
+ Late [ Temp3 3Electron ] -> [ 2Electron ] 
+ Late [ Temp3 4Electron ] -> [ 3Electron ] 


WINCONDITIONS

No Orbital

LEVELS

#########
####....#
####....#
#....#H.#
#...O#..#
#.1.....#
#....####
#########

######
#H..O#
#....#
#1.###
######

########
###...##
#2.H.H.#
###...##
########

#######
#..O..#
#..#..#
#.1#H.#
#..#..#
#..O..#
#######

#######
#.H.3.#
#.....#
#.H.H.#
#######

#######
#.....#
#.....#
#H#H#H#
#.....#
#..3..#
#######

#########
####4####
###...###
##.H.H.##
#.......#
##.H.H.##
###...###
####.####
#########

#########
##H...H##
#...4...#
##H...H##
#########

########
#......#
#...H..#
#O.H..3#
#...H..#
#......#
########

#########
#.......#
#..H.H..#
#.2...C.#
#..H.H..#
#.......#
#########

#########
#.......#
#.1...E.#
###.H.###
###...###
###.O.###
#########

#########
#.......#
#.E.E.E.#
###H.1###
###.H.###
###...###
###.N.###
#########

#######
###..##
##..H.#
#..N..#
#.H..H#
##..0.#
#######

#######
#.....#
#..N..#
#..0..#
#H.H.H#
#.#.#.#
#.....#
#######

########
#......#
#...H#N#
#......#
##H#H.0#
##.....#
####...#
########
