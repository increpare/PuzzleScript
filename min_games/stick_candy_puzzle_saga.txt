OBJECTS

Background
Black DarkGrey
00000
00000
00000
00000
00000

Wall
Pink #febfc6
10001
00011
00110
01100
11000

Player
Pink #febfc6
.000.
.111.
00100
.000.
.0.0.

Yellow
Yellow White Orange
.000.
01100
01000
00002
.022.

Red
Red Orange DarkRed
.000.
01100
01000
00002
.022.

Blue
LightBlue White #3468f7
.000.
01100
01000
00002
.022.

YellowTarget
Brown Yellow
00000
01110
01010
01110
00000

RedTarget
DarkBrown Red
00000
01110
01010
01110
00000

BlueTarget
Purple LightBlue
00000
01110
01010
01110
00000

Marker
White
.....
.....
.....
.....
.....

WallL
White
0....
0....
0....
0....
0....

WallR
White
....0
....0
....0
....0
....0

WallU
White
00000
.....
.....
.....
.....

WallD
White
.....
.....
.....
.....
00000

WallLR
White
0...0
0...0
0...0
0...0
0...0

WallUD
White
00000
.....
.....
.....
00000

JoinL
#febfc6
0....
.....
.....
.....
0....

JoinR
#febfc6
....0
.....
.....
.....
....0

JoinU
#febfc6
0...0
.....
.....
.....
.....

JoinD
#febfc6
.....
.....
.....
.....
0...0

JoinBoth
#febfc6
0...0
.....
.....
.....
0...0

Zero
Purple
.000.
.0.0.
.0.0.
.0.0.
.000.

One
Purple
.00..
..0..
..0..
..0..
.000.

Two
Purple
.000.
...0.
.000.
.0...
.000.

Three
Purple
.000.
...0.
.000.
...0.
.000.

Four
Purple
.0.0.
.0.0.
.000.
...0.
...0.

Five
Purple
.000.
.0...
.000.
...0.
.000.

Six
Purple
.000.
.0...
.000.
.0.0.
.000.

Seven
Purple
.000.
...0.
...0.
...0.
...0.

Eight
Purple
.000.
.0.0.
.000.
.0.0.
.000.

Nine
Purple
.000.
.0.0.
.000.
...0.
.000.


LEGEND

. = Background
# = Wall
P = Player
A = Red
B = Yellow
C = Blue
X = RedTarget
Y = YellowTarget
Z = BlueTarget
WallCoverLR = WallL or WallR or WallLR
WallCoverUD = WallU or WallD or WallUD
JoinCoverLR = JoinL or JoinR or JoinBoth
JoinCoverUD = JoinU or JoinD
Join = JoinCoverLR or JoinCoverUD
CoverLR = WallCoverLR or JoinCoverLR
CoverUD = WallCoverUD or JoinCoverUD
0 = Zero or Wall
1 = One or Wall
2 = Two or Wall
3 = Three or Wall
4 = Four or Wall
5 = Five or Wall
6 = Six or Wall
7 = Seven or Wall
8 = Eight or Wall
9 = Nine or Wall
Number = Zero or One or Two or Three or Four or Five or Six or Seven or Eight or Nine
Candy = Yellow or Red or Blue
Target = YellowTarget or RedTarget or BlueTarget

COLLISIONLAYERS

Background
Marker
Target
Player
Wall, Candy
CoverUD
CoverLR, Number

RULES

[ > Player | Candy ] -> [ > Player | > Candy ] 
[ moving Candy | stationaryCandy ] -> [ moving Candy | movingCandy ] 
[ > Candy | Wall ] [ > Player | > Candy ] -> [ Candy | Wall ] [ > Player | Candy ] 
[ > Player | stationaryCandy ] [ moving Candy ] -> [ > Player | Candy ] [ Candy ] 
[ > Candy | noCandy ] -> [ > Candy | Marker ] 
[ Marker | stationaryCandy ] -> 
[ Marker ] -> [  ] 
[ > Player | Wall ] -> 
[ > Player | stationaryCandy ] -> 
left [ Wall no WallCoverLR | noWall ] -> [ Wall WallL |  ] 
right [ Wall no WallCoverLR | noWall ] -> [ Wall WallR |  ] 
right [ Wall WallL | noWall ] -> [ Wall WallLR |  ] 
up [ Wall no WallCoverUD | noWall ] -> [ Wall WallU |  ] 
down [ Wall no WallCoverUD | noWall ] -> [ Wall WallD |  ] 
down [ Wall WallU | noWall ] -> [ Wall WallUD |  ] 
late [ Join ] -> [  ] 
late left [ Candy | Candy ] -> [ Candy JoinL | Candy ] 
late right [ Candy no JoinL | Candy ] -> [ Candy JoinR | Candy ] 
late right [ Candy JoinL | Candy ] -> [ Candy JoinBoth | Candy ] 
late up [ Candy no JoinBoth | Candy ] -> [ Candy JoinU | Candy ] 
late down [ Candy no JoinU no JoinBoth | Candy ] -> [ Candy JoinD | Candy ] 
late down [ Candy JoinU | Candy ] -> [ Candy JoinBoth | Candy ] 


WINCONDITIONS

All Yellow on YellowTarget
All Red on RedTarget
All Blue on BlueTarget

LEVELS

#######
##...##
#..b.y#
#pb#.y#
#..b.y#
##...##
1######

#########
##.....##
#..a.#.x#
#pa....x#
#..a.#.x#
##.....##
2########

########
#p...yy#
#.b.ayy#
#..b.a.#
#.a.b..#
#xxa.b.#
#xx....#
3#######

###########
#....p....#
#..b.c.b..#
#...c.c...#
###.zzz.###
####b.b####
####yyy####
#####y#####
4##########

#########
#.a...###
#.a...###
#..c.axx#
#zz.p.xx#
#zzc.a..#
###...c.#
###...c.#
5########

###########
##.......##
#y.#.p.#.z#
#yc.b.c.bz#
##.......##
###..#..###
6##########

#########
#####..z#
#...b.zz#
#.b.bb.z#
#p.###..#
#y.cc.c.#
#yy.c...#
#y..#####
7########

#########
##......#
#x.c.#by#
#.b.p..y#
#z.a.#by#
##......#
8########

#########
#yy.c.x.#
#y.b..xx#
#.b.....#
#...#..b#
#.a...c.#
#.aa.c.z#
#p....zz#
9########

#########
#yy###zz#
#...x...#
#.##a##.#
#..b.b..#
#..#.#..#
#..c.c..#
##..p..##
10#######

###########
#####y#####
####yyy####
###.b.b.###
###a...c###
#z...p...x#
#za.b.b.cx#
###.....###
11#########

###########
####...A###
##......###
#..#A#..xx#
#pA...A.xx#
#..#A#..xx#
##......###
####...A###
12#########

#########
#..xxx..#
#..#a#..#
#..b.b..#
##.#p#.##
##.aya.##
##..y..##
13#######

###########
##..zzz..##
#..a.p.b..#
#yy#ccc#xx#
#..b...a..#
##...#...##
14#########

#############
#...#####...#
#....c.b....#
#yy#..p..#zz#
#.y.cc.bb.z.#
####.....####
15###########

#########
#...#...#
#.a.a.a.#
#...x...#
#z#.x.#y#
#zc.x.by#
#.c.p.b.#
###...###
16#######

###########
##......###
##.#..c...#
#...c#..zz#
#pc..#c#zz#
#...c#..zz#
##.#..c...#
##......###
17#########

###############
#...###z###...#
#....bzzzb....#
#...c.c.c.c...#
#####.yyy.#####
#####.#b#.#####
#####.....#####
######.p.######
18#############

#############
###x#...#z###
#.xxa.c.azz.#
#..c.....c..#
###..#a#..###
####..p..####
19###########

###########
###.....###
##c.a.a.b##
#.cyy.zzb.#
#..yy.zz..#
#a..c.b..a#
#..xc.bx..#
#..xxxxx..#
##a..a..a##
###...p.###
20#########
