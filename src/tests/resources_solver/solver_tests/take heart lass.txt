title Take Heart Lass
author Kevin Zuhn
homepage www.kevinzuhn.com

========
OBJECTS
========

Background
LightGreen Green
11111
11011
10101
11011
11111

BackFlowers
Green Purple Yellow
10001
00100
01210
00100
10001

Target
RED LightRed
.0.0.
01010
01110
.010.
..0..

Wall
Red DarkGreen
00000
00100
01010
00100
00000

DespairWall
DarkBrown DarkBlue
00000
00100
01010
00100
00000

NewDespairWall
DarkRed DarkBlue
00000
00100
01010
00100
00000

Player
Blue LightRed Yellow DarkBlue
.333.
.323.
20002
.111.
.000.

OldHope
White Pink
11111
11011
10101
11011
11111

NewHope
White LightRed
11111
11011
10101
11011
11111

Crate
Pink White
.0.0.
01010
01110
.010.
..0..

OldDespair
Black DarkBlue
01010
10101
01010
10101
01010

AltOldDespair
Black DarkBlue
10101
01010
10101
01010
10101

NewDespair
DarkBlue DarkGray
01010
10101
01010
10101
01010

AltNewDespair
DarkBlue DarkGray
10101
01010
10101
01010
10101

=======
LEGEND
=======

. = Background
f = BackFlowers
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target
X = OldDespair
Z = AltOldDespair
H = OldHope

Despair = OldDespair or NewDespair or AltOldDespair or AltNewDespair
Hope = OldHope or NewHope
Blocker = Despair or Crate or Target or Wall or Player or DespairWall or NewDespairWall or Hope
HopeBlocker = Crate or Target or Wall or DespairWall or NewDespairWall or Hope


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target, BackFlowers, Hope
Player, Wall, Crate, Despair, NewDespair, DespairWall, NewDespairWall

======
RULES
======

[NewDespair] -> [OldDespair]
[AltNewDespair] -> [AltOldDespair]
[NewDespairWall] -> [DespairWall]
[NewHope] -> [OldHope]
late [OldDespair | No Blocker] -> [OldDespair | AltNewDespair]
late [AltOldDespair | No Blocker] -> [AltOldDespair | NewDespair]
late [OldDespair | Wall] -> [OldDespair | NewDespairWall]
late [DespairWall | Wall] -> [DespairWall | NewDespairWall]
late [OldHope | No HopeBlocker] -> [ OldHope | NewHope]

[> Player | Crate] -> [> Player | > Crate]
[> Crate | Blocker] -> [ Crate | Blocker ]

late [Target | Despair] -> [ Target |  ]
late [Despair Target] -> [Target]
late [Crate | Despair] -> [ Crate |  ]
late [Hope Despair] -> [Hope]
late [Hope | DespairWall] -> [Hope | Wall]
late [Hope | NewDespairWall] -> [Hope | Wall]

==============
WINCONDITIONS
==============

Some Player on Target

=======
LEVELS
=======

message You have love in your heart, Lass.

######
#...x#
#.ff.#
#.ff.#
#o..P#
######

message So your heart can weather Despair.

#########
#x.f...f#
#.#.....#
#.p.f#..#
#.#..#f.#
#xzf...o#
#########

message And you've others who love you.

#########
#x......#
#...#...#
#p..*..##
#...#...#
#x.....o#
#########

message The war can't last forever, Lass.

########
#.#zx#p#
#....*.#
#......#
#o#*.#.#
########

message Think of the time we shared.

###########
#.........#
#.*#.o.####
#..*###...#
#.......*.#
#......#..#
####...####
#x.......p#
###########

message Then bottle your grief up tight.

###########
#o........#
####.##.#.#
#.....*.#.#
#.#.###.#.#
#.#....*..#
#.#*.#.##.#
#x#...p#z.#
###########

message I wish I could be there, Lass.

###########
#...o...zx#
#.#.....#z#
#...#.#...#
#.........#
#..**.**..#
#z........#
#xz......p#
###########


message There is always Hope, Lass.

###########
#........H#
#.......p.#
#x........#
#zx.......#
#xzx......#
#zxzx.....#
#ozxzx....#
###########

message  It may waver, but it stays lit.

###########
#....p....#
###.#*#.###
#x.......x#
###.*.*.###
#HH*.....x#
###.#.#.###
#x.......o#
###########

message Temper your hope with wisdom.

############
#......zxzx#
#.##.##.####
#......*...#
#.*p.#.#.#o#
#...*.*.*#.#
####.###...#
#H.........#
############

message For I have hope as well.
message Take Heart, Lass. Goodbye.