title Midas
author wanderlands
homepage www.wanderlands.org

========
OBJECTS
========

Background
WHITE

lovebase
white

Target 
BLACK
.....
..0..
.000.
..0..
.0.0.


GoldTarget 
ORANGE
.....
..0..
.000.
..0..
.0.0.

PlayerGround
BLACK 
.....
..0..
.000.
.000.
.0.0.

PlayerJump1
BLACK 
.....
..0..
00000
.000.
.0.0.

PlayerJump2 
BLACK 
.....
..0..
00000
.000.
.0.0.

Crown
Orange
.000.
.0.0.
.....
.....
.....


GoldBlock
Orange


GoldingBlock
brown

Water
Blue

Wall     
GRAY

love 
pink
.0.0.
00000
00000
.000.
..0..

=======
LEGEND
=======

JumpingPlayer = PlayerJump1 or PlayerJump2
Player = PlayerGround or PlayerJump1 or PlayerJump2
Obstacle = Player or Wall or Water or GoldingBlock or GoldBlock or Target or GoldTarget
CanFall = GoldBlock or Target or GoldTarget
. = Background
# = Wall
P = PlayerGround and Crown
T = Target
W = Water
G = GoldBlock
D = GoldingBlock


=========
SOUNDS
=========
SFX0 28219707 (touch water)
SFX1 45545307 (golding)
SFX2 42491507 (make lady gold :/ )
StartGame 8892900
EndLevel 28067900
EndGame 28067900
CloseMessage 44855507

================
COLLISIONLAYERS
================

Background
lovebase
PlayerGround, PlayerJump1,PlayerJump2, Wall, Water, GoldBlock, Target, GoldTarget, GoldingBlock
Crown
love

======
RULES     
======     

UP [ Up PlayerGround | No Obstacle ] -> [ Up PlayerJump2 | ]
DOWN [ Up JumpingPlayer | no obstacle ] -> [ JumpingPlayer | ]
DOWN [ Up PlayerJump2 | no obstacle ] -> [ PlayerJump2 | ]

[ Stationary CanFall ] -> [ Down CanFall ]

HORIZONTAL [ > PlayerJump1 | NO obstacle ] -> [ | PlayerGround ]
HORIZONTAL [ > PlayerJump2 | NO obstacle ] -> [ | PlayerJump1 ]
LATE DOWN [ PlayerGround | NO obstacle ] -> [ | playerground ]

LATE [ crown no player ] [ player ] -> [ ] [ crown player ]
LATE [ goldingblock ] -> [goldblock]
LATE [ crown | wall ] -> [crown | goldingblock] SFX1
LATE [ crown | water ] -> [ | water ] SFX0
LATE [ crown | target ] -> [ crown | goldtarget ] SFX2
LATE DOWN [ JumpingPlayer | Obstacle ] -> [ PlayerGround | Obstacle ]

LATE [ player | target ] -> [ player lovebase | target ]
LATE UP [ lovebase | ] -> [ | love ]

==============
WINCONDITION
==============
some love (Awwww!)

=======     
LEVELS
=======
##############
#............#
#............#
#.P.......T..#
##G######WWW##
##############

message MIDAS

###############
#.............#
#.............#
#.....#.......#
#.P..##..#.T..#
##G#######WWW##
###############

message She waits for me every day

###############
#.............#
#.............#
#.............#
#.....#.#.....#
#.P..##.#..T..#
##G#######WWW##
###############

message This is the price of gold:

##############
#............#
#............#
#.P..........#
##G###T##WWW##
##############

message A heavy heart

#################
#...............#
#...............#
#......#.#......#
#.....#####.....#
#.......#.......#
#.T.P...#.......#
####G#######WWW##
#################

message I step in the same river 
message again and again

#####################
#...................#
#...................#
#...................#
#.........#.#.......#
#..P........#.......#
#..d##########......#
#.......######......#
#...................#
#.......T...........#
#....#########WWW...#
#...................#
#...................#
#...................#
#####################

message Just so I can touch her hand.

#####################
#...................#
#...................#
#...................#
#...................#
#..P................#
#..d##T########WWW..#
#.....#.............#
#...................#
#...................#
#####################

message I am a prison to Pactolus

#####################
#...................#
#........###........#
#.......#####.......#
#......##...##......#
#.....T##.P.##W.....#
#.....####d####.....#
#.....#########.....#
#...................#
#####################

message But I must have her!

#####################
#...................#
#...................#
#..T..#...###..W....#
#..#####...#####....#
#.....###...#.......#
#........P..........#
#.....###D###.......#
#.....#######.......#
#...................#
#...................#
#####################

message My burden only grows

#####################
#...................#
#........G..........#
#.....W.GGG.T.......#
#.....#######.......#
#.....#.....#.......#
#.....#..P..#.......#
#.....###D###.......#
#.....#######.......#
#...................#
#...................#
#...................#
#####################

message My world collapses.

#####################
#...................#
#...................#
#..G....WW..........#
#..##.####..........#
#.....####..........#
#...................#
#.#...........T.....#
#.##.......P..#.....#
#..############.....#
#...#.....####......#
#...................#
#...................#
#...................#
#...................#
#...................#
#####################

message And she feels so distant

#####################
#...................#
#...................#
#...................#
#...................#
#..P......#.........#
#..GG...........WW..#
#..##T############..#
#....#..............#
#...................#
#...................#
#####################


message I live on borrowed time


#####################
#...................#
#...................#
#..........T........#
#.........##........#
#...................#
#.......P...........#
#.......D##.........#
#...........#.......#
#...................#
#...................#
#.......###W#.......#
#####################


message Is she still waiting?


#####################
#...................#
#....###########....#
#...................#
#....P.........W....#
#....###########....#
#....#.........#....#
#...................#
#.........#.........#
#......#..#..#......#
#....T.#..#..#......#
#....###########....#
#...................#
#...................#
#...................#
#####################

message Is love just fool's gold?


#####################
#...................#
#...................#
#...........####....#
#..T##.##..#...#....#
#..#..#..#.#.P.#....#
#..#.....#...G#.....#
#...#...#....#......#
#....#W#.....#......#
#.....#.............#
#............#......#
#...................#
#...................#
#####################


message Midas