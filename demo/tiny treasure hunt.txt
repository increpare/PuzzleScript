title tiny treasure hunt 1.0
author Weeble
homepage www.twitter.com/weeble
run_rules_on_level_start

========
OBJECTS
========
LowFloorRaw
BLACK

LowFloor1
#111411 #191d19 #202020 #252725
01011
12310
23320
02321
02110

LowFloor2
#111411 #191d19 #202020 #252725
01001
22312
12331
01310
10101

WallFacade
#555555 #666666 #777777
21221
10011
.....
.....
.....

HighFloor
#909090 #999999 #bbbbbb #cccccc
33232
31012
20002
31012
32222

LowPlayer
#686868 #707070 #787878 #808080
.....
.....
.232.
.121.
.0.0.

HighPlayer
#e8e8e8 #f0f0f0 #f8f8f8 #ffffff
.232.
.121.
.0.0.
.....
.....

Crate
#999944 #88883a #777733
10001
00000
10001
22222
22222

HighCrate
#dddd55 #eeee50 #cccc48
10001
00000
10001
22222
22222

BurntCrate
#222222 #111111 #000000
10001
00000
10001
22222
22222

MeltingIce
White LightBlue #9999ff #330000
.33..
3333.
31013
11330
21112


StaticIce
White LightBlue #9999ff
10002
1...0
0...1
10012
21122

IceCubeN
White LightBlue #9999ff
10002
1...0
0...1
10012
21122

IceCubeE
White LightBlue #9999ff
10002
1...0
0...1
10012
21122

IceCubeS
White LightBlue #9999ff
10002
1...0
0...1
10012
21122

IceCubeW
White LightBlue #9999ff
10002
1...0
0...1
10012
21122

FirePit1
#990000 #770000 #550000 #330000
02111
13321
02321
32110
23121

FirePit2
#990000 #770000 #550000 #330000
10311
21233
11021
23213
20122


Pillar
#cccccc #dddddd #eeeeee #ffffff #bbbbbb #aaaaaa
32123
33333
02120
22122
01310

IceCubeShadow
RED
.....
.....
..0..
.....
.....

Coin
WHITE #eeee55 #aaaa33
.000.
01112
01212
01112
.222.

 
StairsNorth
#333333 #444444 #555555 #666666 #777777
44444
43334
32223
21112
10001
=======
LEGEND
=======
. = LowFloorRaw
" = HighFloor
@ = LowPlayer and LowFloorRaw
$ = Coin and HighFloor
£ = Coin
H = HighCrate and HighFloor
: = WallFacade and FirePit1 and LowFloorRaw
~ = WallFacade and LowFloorRaw
# = Crate and LowFloorRaw
W = Crate and LowFloorRaw and WallFacade
! = FirePit1 and LowFloorRaw
I = Pillar and HighFloor
% = StaticIce and LowFloorRaw
& = StaticIce and LowFloorRaw and WallFacade
/ = StairsNorth and LowFloorRaw
FirePit = FirePit1 or FirePit2
LowFloor = LowFloor1 or LowFloor2
Background = LowFloor or HighFloor or LowFloorRaw
Player = LowPlayer or HighPlayer
Obstacle = StairsNorth
BlocksLow = Crate or HighFloor or StaticIce or Pillar or BurntCrate
Pushable = Crate or StaticIce
MovingIce = IceCubeE or IceCubeS or IceCubeW or IceCubeN
IceCube = StaticIce or MovingIce
Grounded = IceCube or LowPlayer or Crate

=======
SOUNDS
=======

Coin DESTROY 77169700
Crate MOVE 24442507
SFX0 98605308 ( Hiss )
SFX1 89033504 ( Snap )
SFX2 70543502 ( Plink )
SFX3 34905302 ( Slam )
SFX4 93355705 ( Squeak )
SFX5 52776702 ( Grind )
SFX6 86238705 ( Bounce )

================
COLLISIONLAYERS
================

Background
StairsNorth, FirePit
WallFacade
Crate, IceCube, MovingIce, LowPlayer, MeltingIce, BurntCrate
Pillar, HighPlayer, HighCrate, Coin
IceCubeShadow

======
RULES     
======     

(Make stuff pretty)
[ LowFloorRaw ] -> [ random LowFloor ]
[ FirePit ] -> [ random FirePit ]

(Pushing stuff)
[ > LowPlayer | Pushable ] -> [ > LowPlayer | > Pushable ]
[ > HighPlayer | HighCrate ] -> [ > HighPlayer | > HighCrate ]

(Don't walk under pillars. Probably redundant.)
[ > Grounded | Pillar ] -> [ Grounded | Pillar ]

(Ice melts in lava)
[ MeltingIce FirePit ] -> [ ]
[ IceCube FirePit ] -> [ MeltingIce FirePit ] sfx0 again

(Ice slides)
RIGHT [ > StaticIce ] -> [ > IceCubeE ] sfx2
DOWN [ > StaticIce ] -> [ > IceCubeS ] sfx2
LEFT [ > StaticIce ] -> [ > IceCubeW ] sfx2
UP [ > StaticIce ] -> [ > IceCubeN ] sfx2
[ MovingIce no IceCubeShadow ] -> [ MovingIce IceCubeShadow ]
RIGHT [ IceCubeE ] -> [ > IceCubeE ] again
DOWN [ IceCubeS ] -> [ > IceCubeS ] again
LEFT [ IceCubeW ] -> [ > IceCubeW ] again
UP [ IceCubeN ] -> [ > IceCubeN ] again

(Stop things from overlapping the stairs)
[ > IceCube | Obstacle ] -> [ StaticIce | Obstacle ]
[ > Crate | Obstacle ] -> [ Crate | Obstacle ]

(Lava kills you)
late [ LowPlayer FirePit ] -> [ FirePit ] sfx5 message Ouch, hot!

(More ice rules)
late [ MovingIce IceCubeShadow ] -> [ StaticIce ] sfx5
late [ IceCubeShadow ] -> [ ]

(Crates get charred in lava)
late [ Crate FirePit ] -> [ BurntCrate FirePit ] sfx5

(Player collects coins)
[ > HighPlayer | Coin HighFloor ] -> [ | HighPlayer HighFloor ]
[ > LowPlayer | Coin LowFloor ] -> [ | LowPlayer LowFloor ]

(Player can climb stairs)
UP [ > LowPlayer StairsNorth | HighFloor ] -> [ StairsNorth | HighPlayer HighFloor ] sfx6

(Stop things from getting pushed up onto ledges)
[ > Grounded | HighFloor ] -> [ Grounded | HighFloor ]

(Players and crates fall)
late [ HighPlayer no BlocksLow ] -> [ LowPlayer ] sfx1 again
late [ HighCrate no BlocksLow ] -> [ Crate ] sfx1 again

==============
WINCONDITIONS
==============

No Coin

=======     
LEVELS
=======

message 1. the outer walls

""""""!!!"""
"h""""..!"$"
""""""#.!"""
""""""..!"""
i~~/i/..!:::
"..."...!!!!
i"""i.......
"/~~"!!...#.
i.@.i!!.....

message 2. cold storage

..........£
.!!!!!!!!!.
.!!!!!!!!!.
.!!.%%%.!!.
.!!%...%!!.
.!!%.@.%!!.
.!!%...%!!.
.!!.%%%.!!.
.!!!!!!!!!.
.!!!!!!!!!.
...........


message 3. bifrost

!!.@.""""!!
!!%.."~/"!!
!!.%.&..~!!
!!%..%...!!
!!.%.%.%.!!
!!%..%..%!!
!!.%.%.%.!!
!!...%..%!!
!!...%.%.!!
!!$.."..%!!
!!"""".%.!!


message 4. clutter

""""""""""$
"""""""""""
~~~~W~W~~::
.%@.#..#.!!
....#.#..!!
!!!!"""""..
!!!!"""""..
....~/~~~..
...........

message 5. arena

!"!!!!!!!!!"
!~.."..""..:
!...~../~$.!
!"......@~.!
!~...".....!
!....~.%...!
!....%.....!
!."....%...!
!.~......."!
!.."......~!
!".~..."...!
!:!!!!!:!!!!


message 6. the gauntlet


..""."."."..!!!
..w~."#".~#.!"!
""...~.~#.."!~!
~&..@......~!£!
""....#"""."!.!
~~...#."/"#~!"!
.......~."..!:!


message 7. throne room

...#."$".#...
..#.."""..#..
!!.#.:::.#.!!
$"!...!...!"$
""!.!!!!!.!""
~~!.!h"h!.!~~
.#!!!"""!!!#.
.....h"h.....
"!!.."""..!!"
""!..~/~..!""
"""...@..."""
:~~.%.%.%.~~:
!...........!

message Well done!
