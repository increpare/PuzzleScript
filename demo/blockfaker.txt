title Block Faker
author Droqen's
homepage www.droken.org

require_player_movement
key_repeat_interval 0.12

color_palette c64
(
A port of first several levels of Droqen's beautiful game:

http://dl.dropboxusercontent.com/u/4452540/locklake/index.html

(his game has secrets that this one does not)

)

========
OBJECTS
========

Background
White

Grille
gray
0...0
.0.0.
..0..
.0.0.
0...0

EndPoint
Green

Player 
Black     
00000
00000
0.0.0
00000
00000

Wall
Gray

WallBlock
Black Grey
01111
01111
01111
01111
00001

BlueBlock
Blue
00000
00.00
0.0.0
00.00
00000

GreenBlock
LightGreen
00000
00000
00.00
00000
00000

PinkBlock
Red
00000
00.00
0...0
00.00
00000

PurpleBlock
Purple
00000
0...0
0.0.0
0...0
00000

OrangeBlock
Orange
00000
0.0.0
00000
0.0.0
00000


=======
LEGEND
=======

Block = PinkBlock or BlueBlock or PurpleBlock or OrangeBlock or GreenBlock
Moveable = Player or Block
PlayerObstacle = Block or Wall or WallBlock
BlockObstacle = Player or Wall or WallBlock or Grille or Block or EndPoint
. = Background
# = Wall
@ = WallBlock
P = Player
B = BlueBlock
G = GreenBLock
K = PinkBlock
R = PurpleBlock
O = OrangeBlock
E = EndPoint
x = Grille
H = Grille and Player


=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Background
EndPoint
Grille
Player, Wall, WallBlock, PinkBlock, BlueBlock, PurpleBlock, OrangeBlock, GreenBlock

======
RULES     
======     

[ >  Moveable | Moveable ] -> [ > Moveable | > Moveable ]

[ > Block | Grille ] -> [ Block | Grille ]

late [ PinkBlock | PinkBlock | PinkBlock ] -> [ | | ]
late [ BlueBlock | BlueBlock | BlueBlock ] -> [ | | ]
late [ PurpleBlock | PurpleBlock | PurpleBlock ] -> [ | | ]
late [ OrangeBlock | OrangeBlock | OrangeBlock ] -> [ | | ]
late [ GreenBlock | GreenBlock | GreenBlock ] -> [ | | ]

==============
WINCONDITIONS
==============
some player on endpoint
=======     
LEVELS
=======

........########
################
########@@@@@###
####@@@@@...@###
###@..OO....@@##
##@..@@@@@....@#
##@.@..@@@..E@@#
#@..@.O...@@@@##
##@...@.P.######
####@.@@@@######
####@@##########
########........

message congrats: level 2!

##########
####R.R###
#x....#x##
#xO#O.OxE#
#xx##.#xx#
#####R####
#####P####
O#########

message congrats: level 3!

......#######...
.....##P.#####..
....###....####.
..#####O...#B..#
..#####.#.##BB.#
##EBB..O.O.....#
..#####.#.#....#
..######..#G.###
....####..#.OOx.
.....###.GG.#x..
......#######...

message congrats: level 4!

######xx######
######GG######
##xx#...#xEx##
##..O...#x..##
##..#...##O###
#..##.......##
xG....##....Gx
xG....##....Gx
##.......GG###
###G##...#xx##
##x..#...O.x##
##.P.#...#xx##
######GG######
######xx######

message congrats: level 5!

......##......
....######....
..##########..
..#.OG..kk.#..
.##OPO..Gkk##.
.##GOG..GGk##.
###...##...###
###...##...###
.##kkG##ROR##.
.##Gkk..OEO##.
..#.Gk..RO.#..
..##########..
....######....
......##......

message now go play the real game