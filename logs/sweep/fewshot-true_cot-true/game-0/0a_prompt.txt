SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title Collapse
author Terry Cavanagh
homepage www.distractionware.com

color_palette pastel
youtube 4XglYW89WLQ

background_color Black
text_color LightRed

noaction

flickscreen 16x10

========
OBJECTS
========

Exit D
Black
.....
.....
.....
.....
.....

OutdoorBackground
lightred

IndoorBackground
white lightgray
11011
11011
11011
11011
11011


Wall W
Yellow Orange
00000
00010
00000
01000
00000

SolidWall Q
#FF0000 #992222
00000
00001
00111
01111
11111

Door X
DarkBlue White Blue
00200
10201
00200
10201
00200

Jettrail1
DarkGray
..0..
.000.
00.00
.000.
..0..


Jettrail2
DarkGray
.....
..0..
.0.0.
..0..
.....

Key
Blue
.000.
.0.0.
.000.
..0..
.00..

Flower F
DarkBlue Green blue
.22..
2002.
.22..
..1..
..1..

Mine M
Red #BB0000
10001
01110
11011
01110
10001

Blood
#FF0000 #AA0000
10101
01110
11011
01110
10101

Blood2
#FF0000 #AA0000
01010
10101
01010
10101
01010


PlayerDead
Red Black
10001
10001
00000
10001
10101

PlayerGround P
Black
.000.
.000.
00000
.000.
.0.0.

PlayerHover
Black
.000.
00000
.000.
.000.
0...0

PlayerHoverLeft
Black
.000.
0000.
.000.
.000.
..0.0

PlayerHoverRight
Black
.000.
.0000
.000.
.000.
0.0..

Temp
black

=======
LEGEND
=======

Background = OutdoorBackground or IndoorBackground
Player = PlayerGround or PlayerHover or PlayerHoverLeft or PlayerHoverRight or PlayerDead
InGamePlayer = PlayerGround or PlayerHover
FlyingPlayer = PlayerHoverLeft or PlayerHoverRight
Obstacle = Wall or Mine or SolidWall or Door
ObstacleExMine = Wall or SolidWall or Door

. = OutdoorBackground
, = IndoorBackground
# = Wall
P = Player
k = Key and IndoorBackground

=======
SOUNDS
=======

sfx0 53720504 (jump up)
sfx1 14370308 (bomb)
sfx2 41983504 (jetpack)
sfx3 86827507 (fall)
sfx4 56160703 (door)
sfx5 98568902 (blockpush)
sfx6 77665108 (collapse)

================
COLLISIONLAYERS
================

Background
Jettrail1, Jettrail2, Exit
Player, Wall, Solidwall, temp, playerhoverleft, playerhoverright, mine, Key, Flower, Door
blood, blood2

======
RULES
======

right [ moving Playerdead] -> [PlayerDead]

[> IngamePlayer | mine ] -> [ > PlayerDead | blood ] sfx1
Vertical [ PlayerGround | mine ] -> [ playerdead | blood2 ] sfx1
Horizontal [flyingplayer | mine ] -> [ playerdead | blood2 ] sfx1

Horizontal [> PlayerGround | Key] [Door] -> [> PlayerGround | Key] []
Horizontal [> PlayerGround | Key]-> [> PlayerGround | ] sfx4

Horizontal [> PlayerGround | Flower] [Door] -> [> PlayerGround | Flower] []
Horizontal [> PlayerGround | Flower]-> [> PlayerGround | ] sfx6

UP [Up PlayerGround | No Obstacle | No Obstacle] -> [ | | PlayerHover ] sfx0
UP [Up PlayerGround | No Obstacle] -> [ | PlayerHover ] sfx0


right [right PlayerHover] -> [PlayerHoverRight]
left [left PlayerHover] -> [PlayerHoverLeft]
up [up PlayerHover] -> [PlayerHover]
down [down PlayerHover] -> [PlayerGround]

down [JetTrail2] -> []
down [JetTrail1] -> [JetTrail2]

(random rule here is a hack workaround)
random right [ PlayerHoverRight | No Obstacle] -> [ JetTrail1 | PlayerHoverRight] again sfx2
horizontal [ PlayerHoverRight | ObstacleExMine] -> [ PlayerGround | ObstacleExMine]
down [ PlayerHoverRight | ObstacleExMine] -> [ PlayerGround | ObstacleExMine]

random left [ PlayerHoverLeft | No Obstacle] -> [JetTrail1 | PlayerHoverLeft] again sfx2
horizontal [ PlayerHoverLeft | ObstacleExMine] -> [ PlayerGround | ObstacleExMine]
down [ PlayerHoverLeft | ObstacleExMine] -> [ PlayerGround | ObstacleExMine]

horizontal [> PlayerGround | Wall] -> [> PlayerGround | > wall] sfx5
horizontal [> Wall | Wall] -> [> Wall | > wall]
horizontal [> Wall | Mine] -> [> Wall | > Mine]

down [stationary wall] -> [ down wall] again
down [stationary mine] -> [ down mine] again

[blood | no blood ] -> [blood | blood2] again
[blood2 | no blood ] -> [blood2 | blood] again

late down [  PlayerGround | No Obstacle ] -> [ JetTrail1 |  temp] again sfx3
late down [temp] -> [playerground]

==============
WINCONDITIONS
==============
No Flower
All Player on Exit

=======
LEVELS
=======

MESSAGE The last flower...
MESSAGE Will I find it on this planet?

qqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqq
q,,,,,,,,,,,,,,qqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqq
q,,,,,,,,,,,,,,,,,,,,,,,q,,,,,,,,,,,,qqqqqqqqqqq
q,,,,,,,,qq,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,x,D
q,,,,,,,,qqqqqqqqqqqqq,,K,,,,,,,,,,,,,,,,,,,,x,D
qqq,,,,,,qqqqqqqqqqqqq,,qqqqqqqqqqq,,,,,,,,,,x,D
qqq,p,,,,qqqqqqqqqqqqq,,,,,,,,,,,,,,,,,,qqq,,x,D
qqqqq,,,,qqqqqqqqqqqqq,,,,,,,,,,,,,,,,,,qqqqqqqq
qqqqqmmmmqqqqqqqqqqqqqmqqmmmmmmmmmmmmmqqqqqqqqqq
qqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqqq

qq................q........q..........w....qmmmmmmq.............q
qq..................w.................w....qmmmmmmq.............q
qq...............q..w.................wm....qmmmmmq.............q
qqq..............q..w.................ww....qmmmmqq...........f.q
qqq..............qwww.......w.........ww.....qmmmq...........wwwq
q.........ww......wqwmmw....w.........ww..w..qwqqq...........wwwq
DP........ww......q.mqwww...w........wwwwwwwwwwwww...........wwwq
qww.......ww........qqqqq...w.wwwww..wwwwwwwwwqwww..........wwwwm
qwwwwm..wwwwwwwwwwwww.qqqqmmw.wwwwww.wwwwwwwwwqqww...q...wwwmwwwq
qwwwwwwqwwwwmwwwwwmww.wqqqwwwwwwwwwwmwwwwwwwwwqqqqwwwwwwwwqwwwwwq
mxxxxxxxxxxxxxxxxxxxxxxqxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxq
m....................q.q................................m.......q
m......................q................................q.......q
m......................q........................................q
m......................q........................................q
m......................q........................w...............q
m...............w......q........w............w..w...............q
mmm.....m..m....w.mmmm.mmm...m..w....mm..m...wmmw..m.mm.....m...q
mwmmmmmmm.mmwmmmmmmmmmmmmwmmmmmmmwmmmmwmmmmmmmmmmmmmmmmm.mmmmmm.q
mmwmmmmwmmmmmmmmmwmmmwmmmmmmmwmmmmmmmmmmwmmmmmmmmmmmmmmmmmmmmmmmq

MESSAGE Collapse
MESSAGE Music [Cryptobiosis by Sync24]
MESSAGE Thanks for playing

title chaos wizard
author mark wonnacott
homepage http://twitter.com/ragzouken

youtube JDIj3zPwEvw

(run_rules_on_level_start)

realtime_interval 0.2

========
OBJECTS
========

Space
black darkblue
00010
00000
00000
01000
00000

Background
darkblue darkgreen
00000
01000
00010
00001
00100

Wall
black darkbrown darkblue
01112
21212
02121
20021
00010

Indestructible
darkgreen grey darkgrey
01112
21212
02121
20021
00010

Red
red lightred darkred
..0..
.010.
22110
.220.
..2..

Blue
blue lightblue purple
..0..
.010.
22110
.220.
..2..

Green
green lightgreen darkgreen
..0..
.010.
22110
.220.
..2..

Stone
grey lightgrey darkgrey
..0..
.010.
22110
.220.
..2..

Charge
red darkred
1.0.1
..0..
00000
..0..
1.0.1

Prism
white lightblue grey
..0..
.000.
00000
.000.
..0..


MoveUp
white

MoveDown
white

MoveLeft
white

MoveRight
white

Expand
white

Inhibit
white

Hold
blue lightblue
.1.1.
1.0.1
.0.0.
1.0.1
.1.1.

Burn
red orange yellow white
.010.
01210
12321
01210
.010.

Player
black darkgrey yellow
..1..
.111.
.212.
00100
00000

Rival
black darkgrey red
..1..
.111.
.212.
00100
00000

Goal
black
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

=======
LEGEND
=======
, = Space
. = Background
+ = Prism
# = Wall
X = Indestructible
@ = Player and Inhibit
R = Red
G = Green
B = Blue
S = Stone
? = Goal
$ = Charge
% = Rival

3 = Space and Blue
£ = Space and Charge

Wizard = Player or Rival

Gem = Red or Blue or Green or Stone

Movement = MoveUp or MoveDown or MoveLeft or MoveRight or Expand
Mediator = Hold or Burn

Moveable = Wizard or Gem
Destructable = Wizard or Gem or Wall

=======
SOUNDS
=======

(sfx0 76693900 (prism))
sfx1 68148303 (cast)
sfx2 24680901 (fall)

Burn destroy 17088108

================
COLLISIONLAYERS
================

MoveUp, MoveDown, MoveLeft, MoveRight, Expand, Inhibit
Background
Space
Charge, Goal
Wizard, Gem, Stone, Wall, Prism, Indestructible
Hold, Burn

======
RULES
======

[ Inhibit ] [ moving Player ] -> [ ] [ Player ]
[ Inhibit ] -> cancel

[ moving Player Hold ] -> [ Player Hold ]

(rival ai)
[ Rival no Goal | Goal ] -> [ > Rival | Goal ]

(objects fall into pits when not held)
late [ Moveable no Hold Space ] -> [ Space ] sfx2

[ parallel Wizard | Gem ] -> [ parallel Wizard | parallel Gem ]

[ > Gem | Charge ] -> [ Gem | Charge ]
[ > Gem | Gem ] -> [ > Gem | > Gem ]

[ action Wizard Charge ] -> [ Wizard ]
[ Charge ] [ action Wizard ] -> [ ] [ Wizard Charge ]

[ action Wizard ] -> [ Wizard Charge ]


(spells recipes)
[ Blue | Charge no Wizard ] -> [ | > Hold ] sfx1
[ Red  | Charge no Wizard ] -> [ | > Burn ] sfx1

[ Blue | Green | Charge no Wizard ] -> [ Blue | Blue | Blue ] sfx1
[ Red  | Green | Charge no Wizard ] -> [ Red  | Red  | Red  ] sfx1

[ Green | Charge no Wizard ] -> [ Red | Blue ] sfx1

[ Stone | Charge no Wizard ] -> [ Prism | ] sfx1
[ Prism | Charge no Wizard ] -> [ Stone | ] sfx1

(prism expands a spell)
[ stationary Player ] [ Mediator Prism ] -> [ Player ] [ Expand Mediator Prism ] sfx0

(shortcut to create directional mediators)
[    up Mediator ] -> [ Mediator MoveUp    ]
[  down Mediator ] -> [ Mediator MoveDown  ]
[  left Mediator ] -> [ Mediator MoveLeft  ]
[ right Mediator ] -> [ Mediator MoveRight ]

(apply movement to mediators)
[ stationary Player ] [ Mediator MoveUp    ] -> [ Player ] [    up Mediator    up MoveUp    ]
[ stationary Player ] [ Mediator MoveDown  ] -> [ Player ] [  down Mediator  down MoveDown  ]
[ stationary Player ] [ Mediator MoveLeft  ] -> [ Player ] [  left Mediator  left MoveLeft  ]
[ stationary Player ] [ Mediator MoveRight ] -> [ Player ] [ right Mediator right MoveRight ]

(expand multiplies the spell in all directions)
     up [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveUp    ]
+  down [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveDown  ]
+  left [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveLeft  ]
+ right [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveRight ]

[ stationary Player ] [ > Mediator > Movement | Prism ] -> [ Player ] [ > Mediator Movement | Prism ]
[ stationary Player ] [ Mediator Expand ] -> [ Player ] [ ]

(teleport fizzles on dirt and wall)
[ Hold Wall ] -> [ Wall ]
[ Hold Indestructible ] -> [ Indestructible ]

(fireball destroys dirt, fizzles on wall)
[ Burn Destructable   ] -> [ ]
[ Burn Indestructible ] -> [ Indestructible ]

(spells cancel out)
[ stationary Player ] [ > Mediator Movement | Mediator Movement ] -> [ Player ] [ | ]

(clean up spell-less move commands)
[ stationary Player ] [ Movement no Mediator ] -> [ Player ] [ ]

(teleport moves objects)
[ > Hold Moveable ] -> [ > Hold > Moveable ]

(spells end on goal)
[ > Mediator Movement | Goal ] -> [ | Goal ]

(rival leaves level)
[ Rival Goal ] -> [ Goal ]

==============
WINCONDITIONS
==============

some Player on Goal
no Rival

=======
LEVELS
=======

message you are a chaos wizard - heir of an ancient sigil - a sigil with the power to free magick from its mineral slumber

message cast spells by placing your sigil next to mana ores. escape this tomb

(rival wizard, introduce teleport, teach pulling)
x##xxxxxxx##x
xxxxxbbbxxxxx
x,,xx...xx,,x
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#.@.#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#$$$#,,,,
x,,xxbbbxx,,x
xxxxx.%.xxxxx
x##xx???xx##x

(move demo, teach teleport)
x,,xxxxxxx,,x
xxxxx...xxxxx
x##xx.@.xx##x
####x...x####
##xxx...xxx##
##xb......x##
##x.x...x.x##
##x...b.b.x##
##x...$...x##
##x.x.%.x.x##
##x,,.....x##
##x,,,,,..x##
##x,x,,,xbx##
##x,,..,,,x##
##xxx...xxx##
####x???x####

(teach that mana can be teleported)
##xxxxxx#####
##x.....x####
##x.@...,x###
##.....,,,x##
##....,,...x#
#.b##x,b...x#
#.####x..b.x#
#..###.$.$.x#
##.....b.%.x#
#######x.,,x#
#######x,,,x#
#######x,,,x#
#######x,..x#
#######x???x#

(introduce fireball, teach fireball)
###xxxxxxx###
###xx...xx###
###xx.@.xx###
###xx...xx###
###xx...xx###
###xx...xx###
#rr#x...xx###
rrrr..$.xx###
rr####R.xx###
r#######xx###
r#######xx###
######..xx###
###xx...xx###
###xx.%.xx###
###xx???xx###

(puzzle: teleport, fireball, teleporting mana)
###############
#####xxxxx#####
#xxxxxbrbxxxxx#
#x,,,,...,,,,x#
#x,....@....,x#
#x,.,,...,,.,x#
#x,.,,,,,,,.,x#
#xr..,x#x,..bx#
#xr..,#.#,..rx#
#xr$.,x#x,.$bx#
#x,,,,,,,,,,,x#
#x,.,,rrr,,.,x#
#x,.........,x#
#x,,,,$$$,,,,x#
#xxx#.bbbxxxxx#
#####x.%.x#####
#####x???x#####

(introduce & teach pre-sigil)
###xxxxxxx###
###xx...xx###
###x..@..x###
##x.....##x##
#x.r....###x#
#x$#r#x..##x#
#x###x.b.$bx#
#x###x$x..,x#
#x.##x%x..,x#
#x..#,...,,x#
#x...,,,,,,x#
##x...,,,,x##
###x....,x###
###xx...xx###
###xx???xx###

(puzzle: fireball, pre-sigil)
#################
#####xxxxxxx#####
#xxxxxx...xxxxxx#
#xr...x.@.xr###x#
#xr.....#...###x#
#xrr..x...x..##x#
#xxxx..,,,.#xxxx#
####x.x,,,x.x####
####x..,,,.#x####
####x.x,,,x#x####
####x..,,,.#x####
####x.x,,,x.x####
####x..,,,.#x####
####xx#$.$.xx####
#####xxr%rxx#####
#####xx???xx#####

(introduce split, teach split)
###xxxxxxx###
###xx...xx###
###xx.@.xx###
###xx...xx###
###xx...xx###
###xx.g.xx###
####x...xx###
#####...xx###
#######.xx###
########xx###
######,,xx###
#####,,,xx###
####x,..xx###
###xx.%.xx###
###xx???xx###

(puzzle: teleport, fireball, split)
#######xxxxx#
#xxxxxxx...x#
#x###b...@.x#
#x####r$...x#
#x##r$...,.##
#x#.gxx.,,,,#
#x...x##,,,,#
#x###x##,,,,#
#xg##x##,,,,#
#x...xxx,,,.#
#x.......,,.#
#x.....###,b#
#x....###.#x#
#xxxxxxx.%.x#
#######x???x#

(introduce multiply, teach multiply)
x###xxxxx###x
xx#xx...xx#xx
,x#x.@...x#x,
xxxxx...xxxxx
xbg$x.$#x$grx
xbg$x.r#x$grx
xxxxx$##xxxxx
,x#x.r...x#x,
xxxxx##.xxxxx
xbg$###...grx
xbg$x..,x.,,x
xxxxx.,,xxxxx
,x#x.,,,.x#x,
xx#xx,,%xx#xx
x###x???x###x

(puzzle: fireball, teleport mana, multiply)
####xxxxxxx####
####xx...xx####
###xxx.@.xxx###
###x.xr.rx.x###
###x...r...x###
xxxx.......xxxx
x..x,.$$$.,x.rx
x.g.,.rrr.,...x
x.#...x#x.....x
x...,#####,...x
xb.x,##.##,x..x
xxxx#...#..xxxx
###x.##..##x###
###x.x.%#x#x###
###xxx???xxx###

(puzzle: fireball, teleport, teleport mana, split, multiply)
####xxxxxxx####
####xx.@.xx####
#####r...g#####
####r.,,,.#####
####r.,,,.#g###
####..,,,..####
###b.....$.#g##
###....,,r..###
###...,,,,..###
####.$,,,,,,###
####$r,,,#...##
####r,,###....#
###,,,####....#
####xx#%#xx.r##
####xx???xx####

(introduce amplification, teach amplification)
###xxxxxxx###
#xxxx...xxxx#
#xrxx.@.xx,x#
#x.........x#
#x,,.xxxxxgx#
#x+,#x###x.x#
#x,,.xxxxx.x#
#x.r..#...rx#
#xgxxxxx.,,x#
#x#x###x#,+x#
#x#xxxxx.,,x#
#x.###....$x#
#x,xx...xxbx#
#xxxx.%.xxxx#
###xx???xx###

(amplified teleport)
,,xxxxxxx,,
,#xx...xx#,
,#xx.@.xx#,
#+.,$b.,.+#
,..#...,..#
,,,#,,###,#
,..#...#..#
#+.,.+r£.+#
#..,##.,..,
,,,,,,,,,,,
,.#,##.,..#
#x.,.x.,.+#
##xx...xx##
#,xx.%.xx,#
,,xx???xx#,

(puzzle: fireball, amplified fireball)
###xxxxxxx###
#xxxx...xxxx#
#xrxx.@.xxrx#
#xr.......rx#
#x..##.#...x#
#x..,,,,,##x#
#x#.,,,,#.#x#
#x###,+,###x#
#x..#,,,##.x#
#x..,,,,,##x#
#x.######..x#
#x.#####...x#
#xrxx#.#xxrx#
#xxxx.%.xxxx#
###xx???xx###

(puzzle: fireball, split, amplified teleport)
###xxxxxxx###
#xxxx...xxxx#
#xbxx.@.xx.x#
#x..x...x...#
#x...,.,...,#
#x...,.,...,#
#x..,...,..,#
#x.#...r.#.##
#x.x,,,,,x.x#
#xgx+,+,+x.x#
#x#xxx,xxx.x#
#x##..$...#x#
#x#xx.r.xx#x#
#xxxx.%.xxxx#
###xx???xx###

(puzzle: fireball, sigil-first, split, multiply, amplified teleport)
###xxxxxxx###
#xxxx...xxxx#
#x#xx.@.xx.x#
#x........gx#
#xgx..x..x.x#
#x.x.....x.x#
#x,,x...x,,x#
#x+,..,..,+x#
#x,,x$.$x,,x#
#x.x.r.r.x.x#
#x.x.#x#.x.x#
#x...###...x#
#x.xx#.#xx.x#
#xxxx.%.xxxx#
###xx???xx###

(introduce mobile amplify, teach mobile amplify)
(puzzle: teleport, amplified teleport)
###xxxxxxxx,,
#xxxx...xxx,,
#x.xx.@.xx,,,
#xbbs...,,,,#
#xx.,,.,,,+##
#xx.,,.,,,,,#
#xx,,x.x,,,,,
#x,,,r$,,,+##
#xx,,x,x,,,##
#xx.,,,,,,,,#
#xx.,##,,,,,,
#x....##,,,,,
#x.xx..#xx,,,
#xxxx.%.xxx,,
###xx???xxx,,

(puzzle: teleport, fireball, mobile amplify)
#################
#####xxxxxxx#####
#xxxxxx.@.xxxxxx#
#x.bbx.+++.x+.+x#
#x...x.....x.xrx#
#x#.....b....x$x#
#x..$x..$..x.x.x#
#x..rx.....x+.+x#
#xx$xx#.%..xxxxx#
#x#r..##.....##x#
#x#...#,,,.###.x#
#x##...,,,.#.##x#
#x##...,,,.##.bx#
#xxxxxx...xxxxxx#
#####xx...xx#####
#####xx???xx#####

message i cannot allow you to proceed

(gauntlet i: fireball dodging)
,,xxxxxxxxx,,
,,xxx...xxx,,
,,,xx.@.xx,,,
x,,,,...,,,,,
+.,,,.b.,,,,x
..x,,.$.,,,.+
+.+.,.%.,,x..
...x,...,.+.+
+.+.,...,x...
$.x,,...,.+.+
rx,,,...,,x.$
x,,,,...,,,xr
,,,,,...,,,,x
,,,xx...xx,,,
,,xxx...xxx,,
,,xxx???xxx,,

message your power waxes and thus mine wanes

(gauntlet ii: impending fireball doom)
####xxxxx####
#xxxxbbbxxxx#
#x.,..@..,.x#
#x.,.....,.x#
#x.,.....,.x#
#x.,#####,.x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,,#####,,x#
#x,,,,,,,,,x#
#x,x+++++x,x#
#x.xxx$xxx.x#
#x....r....x#
#xxxx.%.xxxx#
####x???x####

(message it seems you cannot be stopped

(gauntlet iii: interference pattern)
#######xxxxx#######
###xxxxx...xxxxx###
###x...s.@.s...x###
###x...s...s...x###
#xxx,,,,sbs,,,,xxx#
#x+,,,,,...,,,,,,x#
#x+,,,,,$r.,,,,,,x#
#x+,,,,,...,,,,,,x#
#x+,,,,,$$$,,,,,,x#
#xxx,,,,bbb,,,,xxx#
###x...$...$...x###
###x...r...r...x###
###xxxxx.%.xxxxx###
#######x???x#######)

message there isn't enough mana in the cosmos to stop fate
message ...but you shall not leave whilst i live

(boss)
,,x###xxxxx###x,,
,,xrrrxb.bxrrrx,,
,,,.....@.....,,,
,,,,,,,...,,,,,,,
,,,,,,,,,,,,,,,,,
,+x,,,,,,,,,,,x+,
+x+,,,,,£3,,,,+x+
x+%,,,,,,,,,,,.+x
,,,,,,x,x,x,,,,,,
,,,,,,,???,,,,,,,
,,,,,,x???x,,,,,,
,,,,,,,???,,,,,,,
,,,,,,x,x,x,,,,,,
x+.,,,,3£,,,,,.+x
+x+,,,,,,,,,,,+x+
,+x,,,,,,,,,,,x+,
,,,,,,,,,,,,,,,,,

message all of this has happened before, and all of this will happen again, my son

title IceCrates
author Tyler Glaiel
homepage twitter.com/tylerglaiel
run_rules_on_level_start true
noaction

========
OBJECTS
========

Background
DarkBlue White LightBlue
22112
21122
11222
12221
22211




Wall_0000
DarkBlue Gray DarkGray DarkGray Gray
10001
00000
00000
00000
30003

Wall_0001
DarkBlue Gray DarkGray DarkGray Gray
11111
00000
00000
00000
30003

Wall_0010
DarkBlue Gray DarkGray DarkGray Gray
10002
00002
00002
00002
30002

Wall_0011
DarkBlue Gray DarkGray DarkGray Gray
11111
00002
00002
00002
30002

Wall_0100
DarkBlue Gray DarkGray DarkGray Gray
10001
00000
00000
00000
33333

Wall_0101
DarkBlue Gray DarkGray DarkGray Gray
11111
00000
00000
00000
33333

Wall_0110
DarkBlue Gray DarkGray DarkGray Gray
10002
00002
00002
00002
33333

Wall_0111
DarkBlue Gray DarkGray DarkGray Gray
11111
00002
00002
00002
33333

Wall_1000
DarkBlue Gray DarkGray DarkGray Gray
40001
40000
40000
40000
40003

Wall_1001
DarkBlue Gray DarkGray DarkGray Gray
11111
40000
40000
40000
40003

Wall_1010
DarkBlue Gray DarkGray DarkGray Gray
40002
40002
40002
40002
40002

Wall_1011
DarkBlue Gray DarkGray DarkGray Gray
11111
40002
40002
40002
40002

Wall_1100
DarkBlue Gray DarkGray DarkGray Gray
40001
40000
40000
40000
33333

Wall_1101
DarkBlue Gray DarkGray DarkGray Gray
11111
40000
40000
40000
33333

Wall_1110
DarkBlue Gray DarkGray DarkGray Gray
40002
40002
40002
40002
33333

Wall_1111
DarkBlue Gray DarkGray DarkGray Gray
11111
40002
40002
40002
33333



PlayerStill
Red Orange DarkGrey Gray Black
.000.
01110
04140
01210
.333.

PlayerL
Red Orange DarkGrey Gray Black
.000.
04110
01410
02110
.333.

PlayerR
Red Orange DarkGrey Gray Black
.000.
01140
01410
01120
.333.

PlayerU
Red Orange DarkGrey Gray Black
.000.
04140
01110
01210
.333.

PlayerD
Red Orange DarkGrey Gray Black
.000.
01110
01110
04240
.333.


Crate
Brown Yellow Orange
11111
10002
10002
10002
22222


Water1
Blue LightBlue
00000
01100
10011
00000
01100

Water2
Blue LightBlue
00000
00110
11001
00000
00110

Water3
Blue LightBlue
00000
00011
11100
00000
00011

Water4
Blue LightBlue
00000
10001
01110
00000
10001

Water5
Blue LightBlue
00000
11000
00111
00000
11000

Water6
Blue LightBlue
00000
01100
10011
00000
01100

FilledWater
LightBrown LightBlue White
11111
10002
10002
10002
22222

Goal
Black Green DarkGreen
..12.
.112.
1112.
...2.
...2.



StillCrate
Orange



=======
LEGEND
=======

Wall = Wall_0000 or Wall_0001 or Wall_0010 or Wall_0011 or Wall_0100 or Wall_0101 or Wall_0110 or Wall_0111 or Wall_1000 or Wall_1001 or Wall_1010 or Wall_1011 or Wall_1100 or Wall_1101 or Wall_1110 or Wall_1111

Water = Water1 or Water2 or Water3 or Water4 or Water5 or Water6

. = Background
# = Wall_1111
P = PlayerStill
* = Crate
W = Water1
G = Goal
(@ = Crate and Target
O = Target)

Player = PlayerStill or PlayerL or PlayerR or PlayerU or PlayerD
Obstacle = Wall or StillCrate or Water

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
FilledWater
Goal
(Target)
Player, Wall, Crate, StillCrate, Water

======
RULES
======

(     0001

1000         0010

      0100
)

(make pretty walls)
Right [Wall | Wall_1111 | Wall] -> [Wall | Wall_0101 | Wall]
Right [Wall_1111 | Wall] -> [Wall_1101 | Wall]
Right [Wall | Wall_1111] -> [Wall | Wall_0111]


Down [Wall | Wall_1111 | Wall] -> [Wall | Wall_1010 | Wall]
Down [Wall_1111 | Wall] -> [Wall_1011 | Wall]
Down [Wall | Wall_1111] -> [Wall | Wall_1110]


Down [Wall | Wall_0101 | Wall] -> [Wall | Wall_0000 | Wall]
Down [Wall_0101 | Wall] -> [Wall_0001 | Wall]
Down [Wall | Wall_0101] -> [Wall | Wall_0100]


Down [Wall | Wall_1101 | Wall] -> [Wall | Wall_1000 | Wall]
Down [Wall_1101 | Wall] -> [Wall_1001 | Wall]
Down [Wall | Wall_1101] -> [Wall | Wall_1100]


Down [Wall | Wall_0111 | Wall] -> [Wall | Wall_0010 | Wall]
Down [Wall_0111 | Wall] -> [Wall_0011 | Wall]
Down [Wall | Wall_0111] -> [Wall | Wall_0110]


(animate water)
[Water5]->[Water6]
[Water4]->[Water5]
[Water3]->[Water4]
[Water2]->[Water3]
[Water1]->[Water2]

[Water6]->[Water1]


(game logic)

[ LEFT  PlayerStill ] -> [ PlayerL ]
LEFT [ PlayerL | Crate ] -> [ PlayerL | LEFT Crate ] again
LEFT [ > Crate | Crate ] -> [ > Crate | > Crate ] again
LEFT [ > Crate | Water ] -> [ | FilledWater]
LEFT [ > Crate | Obstacle ] -> [StillCrate | Obstacle]
LEFT [ PlayerL | Obstacle ] -> [ PlayerStill | Obstacle ]
[ PlayerL ] -> [ LEFT PlayerL ] again

[ RIGHT  PlayerStill ] -> [ PlayerR ]
RIGHT [ PlayerR | Crate ] -> [ PlayerR | RIGHT Crate ] again
RIGHT [ > Crate | Crate ] -> [ > Crate | > Crate ] again
RIGHT [ > Crate | Water ] -> [ | FilledWater]
RIGHT[ > Crate | Obstacle ] -> [StillCrate | Obstacle]
RIGHT [ PlayerR | Obstacle ] -> [ PlayerStill | Obstacle ]
[ PlayerR ] -> [ RIGHT PlayerR ] again

[ UP  PlayerStill ] -> [ PlayerU ]
UP [ PlayerU | Crate ] -> [ PlayerU | UP Crate ] again
UP [ > Crate | Crate ] -> [ > Crate | > Crate ] again
UP [ > Crate | Water ] -> [ | FilledWater]
UP [ > Crate | Obstacle ] -> [StillCrate | Obstacle]
UP [ PlayerU | Obstacle ] -> [ PlayerStill | Obstacle ]
[ PlayerU ] -> [ UP PlayerU ] again

[ DOWN  PlayerStill ] -> [ PlayerD ]
DOWN [ PlayerD | Crate ] -> [ PlayerD | DOWN Crate ] again
DOWN [ > Crate | Crate ] -> [ > Crate | > Crate ] again
DOWN [ > Crate | Water ] -> [ | FilledWater]
DOWN [ > Crate | Obstacle ] -> [StillCrate | Obstacle]
DOWN [ PlayerD | Obstacle ] -> [ PlayerStill | Obstacle ]
[ PlayerD ] -> [ DOWN PlayerD ] again

[StillCrate]->[Crate]


==============
WINCONDITIONS
==============

Some PlayerStill on Goal

=======
LEVELS
=======



message Level 1: Get to the Goal

#######
#g.#..#
#.....#
#.....#
#.#.p.#
###...#
#######

message Level 2: You must land ON the goal

#########
#########
#...g...#
#.......#
#....#p.#
#.......#
#....#..#
##....#.#
#########

message Level 3: Push Crates

#########
####.####
#.......#
#..#...g#
##......#
#...*.###
#.....###
###....p#
..##....#
...######


message Level 4: Fill the Water Hole

###########
#.........#
####......#
#g..W..p..#
####...#..#
#..#***.#.#
#..#......#
#........##
##...##..##
###########

message Level 5

#############
#......#....#
#...........#
#........p..#
##....w.....#
#..**wgw....#
#.....w....##
#.....*.....#
#.....*.....#
#...........#
#....#......#
#############

message Level 6

##############.
#...###......#.
#.#...#....####
#.#...w..**...#
#.....#.......#
#####w#......##
.##..........#.
.##..........#.
###.*........#.
.###.g....p..#.
.#.##........#.
##..##.......#.
.#############.

message You are win congratulations


title Multi-word Dictionary Game
author Sarah Northway
homepage www.sarahnorthway.com

(included with permission of Sarah Northway)

========
OBJECTS
========

Background
pink

Wall
grey

Player
Blue

Won
yellow

LetterC
white black
.111.
1....
1....
1....
.111.

LetterA
white black
.111.
1...1
11111
1...1
1...1

LetterT
white black
11111
..1..
..1..
..1..
..1..

=======
LEGEND
=======

Letter = LetterA or LetterC or LetterT
. = Background
# = Wall
P = Player
W = Won
C = LetterC
A = LetterA
T = LetterT

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Won
Player, Wall, Letter

======
RULES
======

[ >  Player | Letter ] -> [  >  Player | > Letter  ]
[ <  Player | Letter ] -> [  <  Player | < Letter  ]

late right [LetterC | LetterA | LetterT ] -> [ Won LetterC | Won LetterA | Won LetterT ] message You spelled CAT!
late right [LetterA | LetterC | LetterT] -> [ Won LetterA | Won LetterC | Won LetterT ] message You spelled ACT!

==============
WINCONDITIONS
==============

Some Won

=======
LEVELS
=======

#########
#.......#
#.A..C..#
#.......#
#...P...#
#.T.....#
#########


title Simple Block Pushing Game
author David Skinner
homepage www.puzzlescript.net
zoomscreen 3x3
========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

[ >  Player | Crate ] -> [  >  Player | > Crate  ]

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======


####..
#.O#..
#..###
#@P..#
#..*.#
#..###
####..


######
#....#
#.#P.#
#.*@.#
#.O@.#
#....#
######


USER PROMPT:
Output the code for a complete PuzzleScript game. First, reason about your task and determine the best plan of action. Then, write your code. Return your code in full, inside a ```plaintext code block.