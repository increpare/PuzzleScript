SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

========
OBJECTS
========

Background .
black

Player
green

=======
LEGEND
=======

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Player

======
RULES
======

==============
WINCONDITIONS
==============

=======
LEVELS
=======
.


title Heroes of Sokoban II: Monsters
author Jonah Ostroff
text_color yellow
key_repeat_interval 0.25

========
OBJECTS
========

Background
#989898 #C8C8C8
00000
00100
00000
00010
00000

Goal
Brown Yellow
.000.
00000
00010
00000
00000

Wall
Darkgrey #222222
01000
11111
00010
00010
11111

Fighter (Warrior sounds better in-game, but I want different abbreviations for the classes in the editor.)
#FF4444 Pink DarkBrown
.000.
.111.
00000
.000.
.2.2.

Wizard
#2266FF Pink
.000.
.010.
00000
.000.
00000

Thief
#66DD66 Pink DarkBrown
.000.
.010.
00000
.0.0.
.0.0.

Priest
#FFFFAA Pink
.000.
.111.
00000
.000.
00000

SFighter
#880000 Pink DarkBrown
.000.
.111.
00000
.000.
.2.2.

SWizard
#000088 Pink
.000.
.010.
00000
.000.
00000

SThief
#006600 Pink DarkBrown
.000.
.010.
00000
.0.0.
.0.0.

SPriest
#B8B800 Pink
.000.
.111.
00000
.000.
00000

Crate
Orange #552200
.111.
10001
10001
10001
.111.

YellowSwitch
Yellow Black
.....
.111.
.101.
.111.
.....

YellowOpen
Yellow
0...0
.....
.....
.....
0...0

YellowDoor
Yellow #AA7722
01010
10001
00000
10001
01010

PurpleSwitch
#6611CC Black
.....
.111.
.101.
.111.
.....

PurpleDoor
#6611CC #220088
01010
10001
00000
10001
01010

PurpleOpen
#6611CC
0...0
.....
.....
.....
0...0

MonsterDoor
DarkGrey #222222 Red
21002
22122
20212
20012
21112

Warpball
Blue

ShadowDoor
Purple

DragonLeft
Green Red Orange Yellow DarkGreen
.....
1100.
3210.
1100.
...4.

DragonRight
Green Red Orange Yellow DarkGreen
.....
.0011
.0123
.0011
.4...

Goblin
Green DarkBrown DarkGreen
0.2.0
00000
..0..
.111.
.1.1.

Dying
White

Saved
#D8D800
..0..
.000.
..0..
..0..
.....

HasSaved
Yellow
.....
.....
.....
.....
.....

Corpse
#CCC
.000.
.000.
00000
.0.0.
.0.0.

LFireball
Orange

RFireball
Orange

DidNotMove
Purple

=======
LEGEND
=======

Dragon = DragonLeft or DragonRight
Player = Fighter or Wizard or Thief or Priest
SPlayer = Swizard or Sthief or SFighter or SPriest
Hero = Player or Splayer
Healer = Priest or SPriest (Unlike the other heroes, priests must function even when inactive. "Healer" is the generic term for an active or inactive priest.)
ClosedDoor = PurpleDoor or YellowDoor or MonsterDoor
OpenDoor = YellowOpen or PurpleOpen
Switch = PurpleSwitch or YellowSwitch
Monster = Dragon or Goblin
Moveable = Crate or SPlayer or Monster
Static = Wall or ClosedDoor
Weighing = Crate or Hero or Monster
Vulnerable = Hero or Monster
Invulnerable = Crate or Static
Fireball = LFireball or RFireball
. = Background
# = Wall
F = Fighter
W = Wizard
T = Thief
P = Priest
1 = SFighter
2 = SThief
3 = SWizard
4 = SPriest
* = Crate
G = Goal
[ = YellowDoor
] = PurpleDoor
{ = YellowSwitch
} = PurpleSwitch
L = DragonLeft
R = DragonRight
O = Goblin
M = MonsterDoor

=======
SOUNDS
=======

Moveable move 82056307
Sfx0 21221308 (teleport)
Endlevel 36315908
Sfx1 66399906 (switch player)
Sfx2 92172900 (close door)
Sfx3 15129100 (open door)
Sfx4 69860105 (saved)
Sfx5 63194902 (kill enemy)
Sfx6 81996702 (dying player)
EndGame 86793500

================
COLLISIONLAYERS
================

Background
Warpball, Dying
Goal, Switch, OpenDoor, ShadowDoor
ClosedDoor, Hero, Crate, Wall, Monster, Corpse
RFireball, Saved, DidNotMove
LFireball, HasSaved

======
RULES
======
[Saved] [Wall] -> [] [Wall HasSaved] (Remembering whether we saved someone last round by attaching a note to some wall.)

[Corpse] -> Restart

(Adds a DidNotMove to where the player currently is, to cancel moves where they bump into a wall.)

[Player] -> [Player DidNotMove]

[ >  Fighter | Moveable ] -> [  >  Fighter | > Moveable  ]
[ > Moveable | Moveable ] [ Fighter] -> [ > Moveable | > Moveable ] [ Fighter] (Fighters push.)

[ < Thief | Moveable ] -> [ < Thief | < Moveable ] (Thieves pull.)

[ > Wizard ] -> [ Wizard > Warpball]
[ > Warpball | no Moveable no Static] -> [ | > Warpball]
[ > Warpball | Static] [Wizard] -> [ | Static ] [> Wizard]
[ > Warpball | Moveable ] [Wizard] -> [ | Wizard Sfx0] [Moveable] (Wizards telexport. What they really do is shoot a "warpball", which causes them to move normally if it hits a wall and warp if it hits something they can warp with.)

(The following mess is for changing characters.)
[ Action Fighter] [SThief] -> [SFighter] [Thief Sfx1]
[ Action Thief] [SWizard] -> [SThief] [Wizard Sfx1]
[ Action Wizard] [SPriest] -> [SWizard] [Priest Sfx1]
[ Action Priest] [SFighter] -> [SPriest] [Fighter Sfx1]

[ Action Fighter] [no SThief] [SWizard] -> [SFighter] [no SThief] [Wizard Sfx1]
[ Action Thief] [no Swizard] [SPriest] -> [SThief] [no SWizard] [Priest Sfx1]
[ Action Wizard] [no SPriest] [SFighter] -> [SWizard] [no SPriest] [Fighter Sfx1]
[ Action Priest] [no SFighter] [SThief] -> [SPriest] [no SFighter] [Thief Sfx1]

[ Action Fighter] [no SThief] [no SWizard] [SPriest] -> [SFighter] [no SThief] [no SWizard] [Priest Sfx1]
[ Action Thief] [no SWizard] [no SPriest] [SFighter] -> [SThief] [no SWizard] [no SPriest] [Fighter Sfx1]
[ Action Wizard] [no SPriest] [no SFighter] [SThief] -> [SWizard] [no SPriest] [no SFighter] [Thief Sfx1]
[ Action Priest] [no SFighter] [no SThief] [SWizard] -> [SPriest] [no SFighter] [no SThief] [Wizard Sfx1]

(Cancels the move if the player is in the same place that they started. Notice that switching characters negates this. Only matters in levels where one dragon has multiple targets.)

Late [Player DidNotMove] -> Cancel
Late [DidNotMove] -> []

(Now enemies attack.)

Late [DragonLeft] -> [LFireball DragonLeft]
Late [DragonRight] -> [RFireball DragonRight]

Late RIGHT [RFireball | No Vulnerable no Invulnerable] -> [ | RFireball]
Late LEFT [LFireball | No Vulnerable no Invulnerable] -> [ | LFireball]
Late RIGHT [RFireball | Vulnerable] -> [ | Dying Vulnerable]
Late Right [RFireball | Invulnerable] -> [ | Invulnerable]
Late Left [LFireball | Vulnerable] -> [ | Dying Vulnerable]
Late LEFT [LFireball | Invulnerable] -> [ | Invulnerable]

Late [Goblin | Vulnerable] -> [Goblin | Dying Vulnerable]

(Vulnerable objects that are attacked are designated as "Dying", but heroes can be saved by the Priest. Then the stuff that didn't get saved dies.)

Late [Dying Healer] -> [Saved Healer]
Late [Healer | Dying Hero] -> [Healer | Saved Hero]
Late [Dying Monster] -> [Sfx5]
Late [Dying Hero] -> [Corpse Sfx6]

(Doors opening.)

late [Weighing  YellowSwitch] [YellowDoor] -> [Weighing  YellowSwitch] [ShadowDoor]
late [YellowSwitch no Weighing] [YellowOpen] -> [YellowSwitch] [YellowDoor sfx2]
late [YellowSwitch no Weighing] [ShadowDoor] -> [YellowSwitch] [YellowDoor]
late [ShadowDoor] -> [YellowOpen sfx3]

late [Weighing PurpleSwitch] [PurpleDoor] -> [Weighing PurpleSwitch] [ShadowDoor]
late [PurpleSwitch no Weighing] [PurpleOpen] -> [PurpleSwitch] [PurpleDoor sfx2]
late [PurpleSwitch no Weighing] [ShadowDoor] -> [PurpleSwitch] [PurpleDoor]
late [ShadowDoor] -> [PurpleOpen sfx3]

late [MonsterDoor] -> [ShadowDoor]
late [ShadowDoor] [Monster] -> [MonsterDoor] [Monster]
late [ShadowDoor] -> [sfx3]

(Looks for a HasSaved on a wall and moves it to the Priest. Then, if someone was saved this turn, plays the sound only if the Priest doesn't have the HasSaved note. Finally, cleans that up.)

late [Wall HasSaved] [Healer] -> [Wall] [Healer HasSaved]
late [Saved] [Healer no HasSaved] -> [Saved sfx4] [Healer]
late [HasSaved] -> []

==============
WINCONDITIONS
==============

All Hero on Goal
All Goal on Hero

=======
LEVELS
=======

Message Welcome back.

Message [You've played the original, yes?]

Message [If not, this might be confusing.]

#############
#.......[...#
#.*.F.{.[.G.#
#############
#.......[...#
#.*.2.{.[.G.#
#############
#.......[...#
#.*.3.{.[.G.#
#############

Message Watch out for goblins.

#############
#F.O.......O#
#...O.......#
#.......O...#
#O..........#
#....O......#
#O.......O..#
#.O.....O.G.#
#############

#############
###O###O#####
#W.*****...O#
#..*****....#
#####O###O..#
#O########..#
#*G...O.O...#
##..O..O.O..#
#############

Message Goblins attack each other too.

#############
#.......#...#
#...*...#.G.#
#.F...O.#...#
#...*...##O##
#...........#
#############

Message Dragons breath fire.

###############
#.............#
#..T..###.....#
#.....L.#.....#
#.......#.....#
#.............#
#.....#.......#
#.....#.R..G..#
#.....###.....#
#.............#
###############

Message Fire hurts enemies.

##############
#...#....O.RG#
#.T.#......R.#
#...#R*L...R.#
#R*.#...#..R.#
#...#...#..R.#
#...O.*.#..R.#
#.......#....#
##############

###########
#3.O.O.O.T#
#.#########
#......G.G#
#.#########
#R.O.O.O.O#
###########

Message Think fast!

#############
#R.O.O.O.O.T#
###########{#
###########]#
#G#3.[.}.G#G#
#]###########
#{###########
#1..O...O..L#
#############

Message Sorry about that one.

#######
##G*G##
##.G.##
#R...##
##...##
##...##
##...##
##...L#
##...##
##F23##
#######

Message Kill monsters to open the M wall.

###########
#L...R#...#
#.....#...#
#.....M.G.#
#.T.*.#...#
#.....#...#
###########

#############
#.......#...#
#..O.O..#...#
#R......M.G.#
#.*.F.*.#...#
#.......#...#
#############

#############
#.......#...#
#.{.*.{.#...#
#..W.O..M.G.#
#.{.*.[.#...#
#.......#...#
#############

Message Yellow priest is protected.

###########
#...#O#O#O#
#.P.......#
#...**O**.#
#********.#
#R..G.....#
###########

###########
#W#######G#
#.#O#O#O#.#
#.........#
#.##O#O##.#
#4#######G#
###########

Message Heroes next to a priest are safe.

############
#...#.O#O###
#.F.#......#
#...#.##O..#
#.4...O#G.G#
#...#.##.O.#
############

############
#.#..T..#4##
#.#.{.*...L#
#.#.....#..#
#G[.*.{...L#
#.#.....#..#
#.#.{.*...L#
#.#.....#G##
############

###########
#W...O#...#
#.....#.G.#
#..4..M...#
#.....#.G.#
#O...O#...#
###########

###########
#.T.##O#O##
#.........#
#.4.#O#O..#
########.##
#G..####.O#
#G#.O###.##
###.###O..#
#O..O#....#
##...*....#
###########

###########
#{*F#.4#.3#
#{*.#..#..#
###[#[###.#
#R[.......#
####O#.##.#
######.##.#
#G.G.G.#O*#
###########

###########
#.T.####O##
#.3......O#
#.4.###O.##
#####O##.##
#.G..*.O.##
#G.G#O##.##
###########

###########
##O#O#O#O##
#T[.[.[.[.#
##{#{#{#{.#
#.........#
#.3G*G*G4.#
#.........#
###########

Message The final challenge.

#############
#...#O.O#...#
#.F.#G.G#.2.#
#...#.O.#...#
##.###*###.##
#R*L.....R*L#
#.#.......#.#
#R*L.....R*L#
##.###M###.##
#...#...#...#
#.3.#G.G#.4.#
#...#...#...#
#############

Message Thanks for playing!

title chaos wizard
author mark wonnacott
homepage http://twitter.com/ragzouken

youtube JDIj3zPwEvw

(run_rules_on_level_start)

realtime_interval 0.2

========
OBJECTS
========

Space
black darkblue
00010
00000
00000
01000
00000

Background
darkblue darkgreen
00000
01000
00010
00001
00100

Wall
black darkbrown darkblue
01112
21212
02121
20021
00010

Indestructible
darkgreen grey darkgrey
01112
21212
02121
20021
00010

Red
red lightred darkred
..0..
.010.
22110
.220.
..2..

Blue
blue lightblue purple
..0..
.010.
22110
.220.
..2..

Green
green lightgreen darkgreen
..0..
.010.
22110
.220.
..2..

Stone
grey lightgrey darkgrey
..0..
.010.
22110
.220.
..2..

Charge
red darkred
1.0.1
..0..
00000
..0..
1.0.1

Prism
white lightblue grey
..0..
.000.
00000
.000.
..0..


MoveUp
white

MoveDown
white

MoveLeft
white

MoveRight
white

Expand
white

Inhibit
white

Hold
blue lightblue
.1.1.
1.0.1
.0.0.
1.0.1
.1.1.

Burn
red orange yellow white
.010.
01210
12321
01210
.010.

Player
black darkgrey yellow
..1..
.111.
.212.
00100
00000

Rival
black darkgrey red
..1..
.111.
.212.
00100
00000

Goal
black
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

=======
LEGEND
=======
, = Space
. = Background
+ = Prism
# = Wall
X = Indestructible
@ = Player and Inhibit
R = Red
G = Green
B = Blue
S = Stone
? = Goal
$ = Charge
% = Rival

3 = Space and Blue
£ = Space and Charge

Wizard = Player or Rival

Gem = Red or Blue or Green or Stone

Movement = MoveUp or MoveDown or MoveLeft or MoveRight or Expand
Mediator = Hold or Burn

Moveable = Wizard or Gem
Destructable = Wizard or Gem or Wall

=======
SOUNDS
=======

(sfx0 76693900 (prism))
sfx1 68148303 (cast)
sfx2 24680901 (fall)

Burn destroy 17088108

================
COLLISIONLAYERS
================

MoveUp, MoveDown, MoveLeft, MoveRight, Expand, Inhibit
Background
Space
Charge, Goal
Wizard, Gem, Stone, Wall, Prism, Indestructible
Hold, Burn

======
RULES
======

[ Inhibit ] [ moving Player ] -> [ ] [ Player ]
[ Inhibit ] -> cancel

[ moving Player Hold ] -> [ Player Hold ]

(rival ai)
[ Rival no Goal | Goal ] -> [ > Rival | Goal ]

(objects fall into pits when not held)
late [ Moveable no Hold Space ] -> [ Space ] sfx2

[ parallel Wizard | Gem ] -> [ parallel Wizard | parallel Gem ]

[ > Gem | Charge ] -> [ Gem | Charge ]
[ > Gem | Gem ] -> [ > Gem | > Gem ]

[ action Wizard Charge ] -> [ Wizard ]
[ Charge ] [ action Wizard ] -> [ ] [ Wizard Charge ]

[ action Wizard ] -> [ Wizard Charge ]


(spells recipes)
[ Blue | Charge no Wizard ] -> [ | > Hold ] sfx1
[ Red  | Charge no Wizard ] -> [ | > Burn ] sfx1

[ Blue | Green | Charge no Wizard ] -> [ Blue | Blue | Blue ] sfx1
[ Red  | Green | Charge no Wizard ] -> [ Red  | Red  | Red  ] sfx1

[ Green | Charge no Wizard ] -> [ Red | Blue ] sfx1

[ Stone | Charge no Wizard ] -> [ Prism | ] sfx1
[ Prism | Charge no Wizard ] -> [ Stone | ] sfx1

(prism expands a spell)
[ stationary Player ] [ Mediator Prism ] -> [ Player ] [ Expand Mediator Prism ] sfx0

(shortcut to create directional mediators)
[    up Mediator ] -> [ Mediator MoveUp    ]
[  down Mediator ] -> [ Mediator MoveDown  ]
[  left Mediator ] -> [ Mediator MoveLeft  ]
[ right Mediator ] -> [ Mediator MoveRight ]

(apply movement to mediators)
[ stationary Player ] [ Mediator MoveUp    ] -> [ Player ] [    up Mediator    up MoveUp    ]
[ stationary Player ] [ Mediator MoveDown  ] -> [ Player ] [  down Mediator  down MoveDown  ]
[ stationary Player ] [ Mediator MoveLeft  ] -> [ Player ] [  left Mediator  left MoveLeft  ]
[ stationary Player ] [ Mediator MoveRight ] -> [ Player ] [ right Mediator right MoveRight ]

(expand multiplies the spell in all directions)
     up [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveUp    ]
+  down [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveDown  ]
+  left [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveLeft  ]
+ right [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveRight ]

[ stationary Player ] [ > Mediator > Movement | Prism ] -> [ Player ] [ > Mediator Movement | Prism ]
[ stationary Player ] [ Mediator Expand ] -> [ Player ] [ ]

(teleport fizzles on dirt and wall)
[ Hold Wall ] -> [ Wall ]
[ Hold Indestructible ] -> [ Indestructible ]

(fireball destroys dirt, fizzles on wall)
[ Burn Destructable   ] -> [ ]
[ Burn Indestructible ] -> [ Indestructible ]

(spells cancel out)
[ stationary Player ] [ > Mediator Movement | Mediator Movement ] -> [ Player ] [ | ]

(clean up spell-less move commands)
[ stationary Player ] [ Movement no Mediator ] -> [ Player ] [ ]

(teleport moves objects)
[ > Hold Moveable ] -> [ > Hold > Moveable ]

(spells end on goal)
[ > Mediator Movement | Goal ] -> [ | Goal ]

(rival leaves level)
[ Rival Goal ] -> [ Goal ]

==============
WINCONDITIONS
==============

some Player on Goal
no Rival

=======
LEVELS
=======

message you are a chaos wizard - heir of an ancient sigil - a sigil with the power to free magick from its mineral slumber

message cast spells by placing your sigil next to mana ores. escape this tomb

(rival wizard, introduce teleport, teach pulling)
x##xxxxxxx##x
xxxxxbbbxxxxx
x,,xx...xx,,x
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#.@.#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#$$$#,,,,
x,,xxbbbxx,,x
xxxxx.%.xxxxx
x##xx???xx##x

(move demo, teach teleport)
x,,xxxxxxx,,x
xxxxx...xxxxx
x##xx.@.xx##x
####x...x####
##xxx...xxx##
##xb......x##
##x.x...x.x##
##x...b.b.x##
##x...$...x##
##x.x.%.x.x##
##x,,.....x##
##x,,,,,..x##
##x,x,,,xbx##
##x,,..,,,x##
##xxx...xxx##
####x???x####

(teach that mana can be teleported)
##xxxxxx#####
##x.....x####
##x.@...,x###
##.....,,,x##
##....,,...x#
#.b##x,b...x#
#.####x..b.x#
#..###.$.$.x#
##.....b.%.x#
#######x.,,x#
#######x,,,x#
#######x,,,x#
#######x,..x#
#######x???x#

(introduce fireball, teach fireball)
###xxxxxxx###
###xx...xx###
###xx.@.xx###
###xx...xx###
###xx...xx###
###xx...xx###
#rr#x...xx###
rrrr..$.xx###
rr####R.xx###
r#######xx###
r#######xx###
######..xx###
###xx...xx###
###xx.%.xx###
###xx???xx###

(puzzle: teleport, fireball, teleporting mana)
###############
#####xxxxx#####
#xxxxxbrbxxxxx#
#x,,,,...,,,,x#
#x,....@....,x#
#x,.,,...,,.,x#
#x,.,,,,,,,.,x#
#xr..,x#x,..bx#
#xr..,#.#,..rx#
#xr$.,x#x,.$bx#
#x,,,,,,,,,,,x#
#x,.,,rrr,,.,x#
#x,.........,x#
#x,,,,$$$,,,,x#
#xxx#.bbbxxxxx#
#####x.%.x#####
#####x???x#####

(introduce & teach pre-sigil)
###xxxxxxx###
###xx...xx###
###x..@..x###
##x.....##x##
#x.r....###x#
#x$#r#x..##x#
#x###x.b.$bx#
#x###x$x..,x#
#x.##x%x..,x#
#x..#,...,,x#
#x...,,,,,,x#
##x...,,,,x##
###x....,x###
###xx...xx###
###xx???xx###

(puzzle: fireball, pre-sigil)
#################
#####xxxxxxx#####
#xxxxxx...xxxxxx#
#xr...x.@.xr###x#
#xr.....#...###x#
#xrr..x...x..##x#
#xxxx..,,,.#xxxx#
####x.x,,,x.x####
####x..,,,.#x####
####x.x,,,x#x####
####x..,,,.#x####
####x.x,,,x.x####
####x..,,,.#x####
####xx#$.$.xx####
#####xxr%rxx#####
#####xx???xx#####

(introduce split, teach split)
###xxxxxxx###
###xx...xx###
###xx.@.xx###
###xx...xx###
###xx...xx###
###xx.g.xx###
####x...xx###
#####...xx###
#######.xx###
########xx###
######,,xx###
#####,,,xx###
####x,..xx###
###xx.%.xx###
###xx???xx###

(puzzle: teleport, fireball, split)
#######xxxxx#
#xxxxxxx...x#
#x###b...@.x#
#x####r$...x#
#x##r$...,.##
#x#.gxx.,,,,#
#x...x##,,,,#
#x###x##,,,,#
#xg##x##,,,,#
#x...xxx,,,.#
#x.......,,.#
#x.....###,b#
#x....###.#x#
#xxxxxxx.%.x#
#######x???x#

(introduce multiply, teach multiply)
x###xxxxx###x
xx#xx...xx#xx
,x#x.@...x#x,
xxxxx...xxxxx
xbg$x.$#x$grx
xbg$x.r#x$grx
xxxxx$##xxxxx
,x#x.r...x#x,
xxxxx##.xxxxx
xbg$###...grx
xbg$x..,x.,,x
xxxxx.,,xxxxx
,x#x.,,,.x#x,
xx#xx,,%xx#xx
x###x???x###x

(puzzle: fireball, teleport mana, multiply)
####xxxxxxx####
####xx...xx####
###xxx.@.xxx###
###x.xr.rx.x###
###x...r...x###
xxxx.......xxxx
x..x,.$$$.,x.rx
x.g.,.rrr.,...x
x.#...x#x.....x
x...,#####,...x
xb.x,##.##,x..x
xxxx#...#..xxxx
###x.##..##x###
###x.x.%#x#x###
###xxx???xxx###

(puzzle: fireball, teleport, teleport mana, split, multiply)
####xxxxxxx####
####xx.@.xx####
#####r...g#####
####r.,,,.#####
####r.,,,.#g###
####..,,,..####
###b.....$.#g##
###....,,r..###
###...,,,,..###
####.$,,,,,,###
####$r,,,#...##
####r,,###....#
###,,,####....#
####xx#%#xx.r##
####xx???xx####

(introduce amplification, teach amplification)
###xxxxxxx###
#xxxx...xxxx#
#xrxx.@.xx,x#
#x.........x#
#x,,.xxxxxgx#
#x+,#x###x.x#
#x,,.xxxxx.x#
#x.r..#...rx#
#xgxxxxx.,,x#
#x#x###x#,+x#
#x#xxxxx.,,x#
#x.###....$x#
#x,xx...xxbx#
#xxxx.%.xxxx#
###xx???xx###

(amplified teleport)
,,xxxxxxx,,
,#xx...xx#,
,#xx.@.xx#,
#+.,$b.,.+#
,..#...,..#
,,,#,,###,#
,..#...#..#
#+.,.+r£.+#
#..,##.,..,
,,,,,,,,,,,
,.#,##.,..#
#x.,.x.,.+#
##xx...xx##
#,xx.%.xx,#
,,xx???xx#,

(puzzle: fireball, amplified fireball)
###xxxxxxx###
#xxxx...xxxx#
#xrxx.@.xxrx#
#xr.......rx#
#x..##.#...x#
#x..,,,,,##x#
#x#.,,,,#.#x#
#x###,+,###x#
#x..#,,,##.x#
#x..,,,,,##x#
#x.######..x#
#x.#####...x#
#xrxx#.#xxrx#
#xxxx.%.xxxx#
###xx???xx###

(puzzle: fireball, split, amplified teleport)
###xxxxxxx###
#xxxx...xxxx#
#xbxx.@.xx.x#
#x..x...x...#
#x...,.,...,#
#x...,.,...,#
#x..,...,..,#
#x.#...r.#.##
#x.x,,,,,x.x#
#xgx+,+,+x.x#
#x#xxx,xxx.x#
#x##..$...#x#
#x#xx.r.xx#x#
#xxxx.%.xxxx#
###xx???xx###

(puzzle: fireball, sigil-first, split, multiply, amplified teleport)
###xxxxxxx###
#xxxx...xxxx#
#x#xx.@.xx.x#
#x........gx#
#xgx..x..x.x#
#x.x.....x.x#
#x,,x...x,,x#
#x+,..,..,+x#
#x,,x$.$x,,x#
#x.x.r.r.x.x#
#x.x.#x#.x.x#
#x...###...x#
#x.xx#.#xx.x#
#xxxx.%.xxxx#
###xx???xx###

(introduce mobile amplify, teach mobile amplify)
(puzzle: teleport, amplified teleport)
###xxxxxxxx,,
#xxxx...xxx,,
#x.xx.@.xx,,,
#xbbs...,,,,#
#xx.,,.,,,+##
#xx.,,.,,,,,#
#xx,,x.x,,,,,
#x,,,r$,,,+##
#xx,,x,x,,,##
#xx.,,,,,,,,#
#xx.,##,,,,,,
#x....##,,,,,
#x.xx..#xx,,,
#xxxx.%.xxx,,
###xx???xxx,,

(puzzle: teleport, fireball, mobile amplify)
#################
#####xxxxxxx#####
#xxxxxx.@.xxxxxx#
#x.bbx.+++.x+.+x#
#x...x.....x.xrx#
#x#.....b....x$x#
#x..$x..$..x.x.x#
#x..rx.....x+.+x#
#xx$xx#.%..xxxxx#
#x#r..##.....##x#
#x#...#,,,.###.x#
#x##...,,,.#.##x#
#x##...,,,.##.bx#
#xxxxxx...xxxxxx#
#####xx...xx#####
#####xx???xx#####

message i cannot allow you to proceed

(gauntlet i: fireball dodging)
,,xxxxxxxxx,,
,,xxx...xxx,,
,,,xx.@.xx,,,
x,,,,...,,,,,
+.,,,.b.,,,,x
..x,,.$.,,,.+
+.+.,.%.,,x..
...x,...,.+.+
+.+.,...,x...
$.x,,...,.+.+
rx,,,...,,x.$
x,,,,...,,,xr
,,,,,...,,,,x
,,,xx...xx,,,
,,xxx...xxx,,
,,xxx???xxx,,

message your power waxes and thus mine wanes

(gauntlet ii: impending fireball doom)
####xxxxx####
#xxxxbbbxxxx#
#x.,..@..,.x#
#x.,.....,.x#
#x.,.....,.x#
#x.,#####,.x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,,#####,,x#
#x,,,,,,,,,x#
#x,x+++++x,x#
#x.xxx$xxx.x#
#x....r....x#
#xxxx.%.xxxx#
####x???x####

(message it seems you cannot be stopped

(gauntlet iii: interference pattern)
#######xxxxx#######
###xxxxx...xxxxx###
###x...s.@.s...x###
###x...s...s...x###
#xxx,,,,sbs,,,,xxx#
#x+,,,,,...,,,,,,x#
#x+,,,,,$r.,,,,,,x#
#x+,,,,,...,,,,,,x#
#x+,,,,,$$$,,,,,,x#
#xxx,,,,bbb,,,,xxx#
###x...$...$...x###
###x...r...r...x###
###xxxxx.%.xxxxx###
#######x???x#######)

message there isn't enough mana in the cosmos to stop fate
message ...but you shall not leave whilst i live

(boss)
,,x###xxxxx###x,,
,,xrrrxb.bxrrrx,,
,,,.....@.....,,,
,,,,,,,...,,,,,,,
,,,,,,,,,,,,,,,,,
,+x,,,,,,,,,,,x+,
+x+,,,,,£3,,,,+x+
x+%,,,,,,,,,,,.+x
,,,,,,x,x,x,,,,,,
,,,,,,,???,,,,,,,
,,,,,,x???x,,,,,,
,,,,,,,???,,,,,,,
,,,,,,x,x,x,,,,,,
x+.,,,,3£,,,,,.+x
+x+,,,,,,,,,,,+x+
,+x,,,,,,,,,,,x+,
,,,,,,,,,,,,,,,,,

message all of this has happened before, and all of this will happen again, my son

USER PROMPT:
Output the code for a complete PuzzleScript game.  First, reason about your task and determine the best plan of action. Then, write your code. Return your code in full, inside a ```plaintext code block.