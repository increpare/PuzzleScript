SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title Memories Of Castlemouse
author Wayne Myers
homepage www.conniptions.org

text_color lightgreen
again_interval 0
youtube CnjtQbU0Gg8

========
OBJECTS
========

Background
Black

Wall
Brown DarkBrown
00010
11111
01000
11111
00010

Floor
#888855 #885588
00100
01110
11011
01110
00100


Hole
Black #222222 Brown
21112
10000
10000
10000
10000

Marker
Red
00.00
0...0
.....
0...0
00.00

MMarker
Purple
00.00
0...0
.....
0...0
00.00

CMarker
Purple
00.00
0...0
.....
0...0
00.00

DMarker
Purple
00.00
0...0
.....
0...0
00.00

EMarker
Purple
00.00
0...0
.....
0...0
00.00

Mouse
LightGrey Black
.....
.1.1.
.000.
.010.
..1..

UpMovingMouse
LightGrey Black
.....
.1.1.
.000.
.010.
..1..

LeftMovingMouse
LightGrey Black
.....
.1.1.
.000.
.010.
..1..

RightMovingMouse
LightGrey Black
.....
.1.1.
.000.
.010.
..1..

DownMovingMouse
LightGrey Black
.....
.1.1.
.000.
.010.
..1..

Cat
White Green Pink Grey
0...0
00000
01010
30203
.000.

RightMovingCat
White Green Pink Grey
0...0
00000
01010
30203
.000.

LeftMovingCat
White Green Pink Grey
0...0
00000
01010
30203
.000.

UpMovingCat
White Green Pink Grey
0...0
00000
01010
30203
.000.

DownMovingCat
White Green Pink Grey
0...0
00000
01010
30203
.000.

Dog
DarkBrown Black
.0.0.
01010
00000
00100
01110

UpMovingDog
DarkBrown Black
.0.0.
01010
00000
00100
01110

DownMovingDog
DarkBrown Black
.0.0.
01010
00000
00100
01110

LeftMovingDog
DarkBrown Black
.0.0.
01010
00000
00100
01110

RightMovingDog
DarkBrown Black
.0.0.
01010
00000
00100
01110

Elephant
LightGrey Black #eeeeee Grey
.000.
31300
32330
30000
.0..0

UpMovingElephant
LightGrey Black #eeeeee Grey
.000.
31300
32330
30000
.0..0

DownMovingElephant
LightGrey Black #eeeeee Grey
.000.
31300
32330
30000
.0..0

LeftMovingElephant
LightGrey Black #eeeeee Grey
.000.
31300
32330
30000
.0..0

RightMovingElephant
LightGrey Black #eeeeee Grey
.000.
31300
32330
30000
.0..0

=======
LEGEND
=======

X = Background
. = Floor
# = Wall
E = Elephant and Floor and Marker
D = Dog and Floor and Marker
C = Cat and Floor and Marker
M = Mouse and Floor and Marker
3 = Elephant and Floor
2 = Dog and Floor
1 = Cat and Floor
0 = Mouse and Floor
H = Hole and Floor

Player = Marker

Obstacle = Wall or Hole

VerticalMovingElephant = UpMovingElephant or DownMovingElephant
HorizontalMovingElephant = LeftMovingElephant or RightMovingElephant
MovingElephant = VerticalMovingElephant or HorizontalMovingElephant

VerticalMovingDog = UpMovingDog or DownMovingDog
HorizontalMovingDog = LeftMovingDog or RightMovingDog
MovingDog = VerticalMovingDog or HorizontalMovingDog

VerticalMovingCat = UpMovingCat or DownMovingCat
HorizontalMovingCat = LeftMovingCat or RightMovingCat
MovingCat = VerticalMovingCat or HorizontalMovingCat

VerticalMovingMouse = UpMovingMouse or DownMovingMouse
HorizontalMovingMouse = LeftMovingMouse or RightMovingMouse
MovingMouse = VerticalMovingMouse or HorizontalMovingMouse

MoveMarker = MMarker or CMarker or DMarker or EMarker

=======
SOUNDS
=======

SFX0 40739509 (Mouse in hole)
SFX1 65087301 (Scared mouse)
SFX2 10901907 (Hit wall)
SFX3 45415107 (Scared dog)
SFX4 80636305 (Scared cat)
SFX5 3673502  (Scared elephant)

================
COLLISIONLAYERS
================

Background
Floor
Hole
Mouse, MovingMouse, Cat, MovingCat, Dog, MovingDog, Elephant, MovingElephant, Wall
Marker, MoveMarker

======
RULES
======

(Marker rules)
[ > Marker Mouse ] -> [ MMarker > Mouse ]
[ > Marker Cat ] -> [ CMarker > Cat ]
[ > Marker Dog ] -> [ DMarker > Dog ]
[ > Marker Elephant ] -> [ EMarker > Elephant ]

startLoop

(Elephant rules)
[ UP Elephant ] -> [ UP UpMovingElephant ]
[ DOWN Elephant ] -> [ DOWN DownMovingElephant ]
[ LEFT Elephant ] -> [ LEFT LeftMovingElephant ]
[ RIGHT Elephant ] -> [ RIGHT RightMovingElephant ]

[ UpMovingElephant ] -> [ Up UpMovingElephant ] again
[ LeftMovingElephant ] -> [ Left LeftMovingElephant ] again
[ RightMovingElephant ] -> [ Right RightMovingElephant ] again
[ DownMovingElephant ] -> [ Down DownMovingElephant ] again

right [ Elephant | Stationary Dog | No Obstacle ] -> [ Elephant | right Dog | SFX3 ]
left [ Elephant | Stationary Dog | No Obstacle ] -> [ Elephant | left Dog | SFX3 ]
up [ Elephant | Stationary Dog | No Obstacle ] -> [ Elephant | up Dog | SFX3 ]
down [ Elephant | Stationary Dog | No Obstacle ] -> [ Elephant | down Dog | SFX3]
right [ VerticalMovingElephant | Stationary Dog | No Obstacle ] -> [ VerticalMovingElephant | right Dog | SFX3 ]
left [ VerticalMovingElephant | Stationary Dog | No Obstacle ] -> [ VerticalMovingElephant | left Dog | SFX3 ]
up [ HorizontalMovingElephant | Stationary Dog | No Obstacle ] -> [ HorizontalMovingElephant | up Dog | SFX3 ]
down [ HorizontalMovingElephant | Stationary Dog | No Obstacle ] -> [ HorizontalMovingElephant | down Dog | SFX3]

[ > MovingElephant | Cat | ] -> [ Elephant | Cat | SFX5 ]
[ > MovingElephant | Mouse ] -> [ Elephant | Mouse ]
[ > MovingElephant | Dog | Obstacle | ] -> [ Elephant | Dog | Obstacle | SFX3 ]
[ > MovingElephant | Dog | No Obstacle ] -> [ Elephant | > Dog | SFX3 ]
[ > MovingElephant | Obstacle | ] -> [ Elephant | Obstacle | SFX2 ]

(Dog rules)
[ UP Dog ] -> [ UP UpMovingDog ]
[ DOWN Dog ] -> [ DOWN DownMovingDog ]
[ LEFT Dog ] -> [ LEFT LeftMovingDog ]
[ RIGHT Dog ] -> [ RIGHT RightMovingDog ]

[ UpMovingDog ] -> [ Up UpMovingDog ] again
[ LeftMovingDog ] -> [ Left LeftMovingDog ] again
[ RightMovingDog ] -> [ Right RightMovingDog ] again
[ DownMovingDog ] -> [ Down DownMovingDog ] again

right [ Dog | Stationary Cat | No Obstacle ] -> [ Dog | right Cat | SFX4 ]
left [ Dog | Stationary Cat | No Obstacle ] -> [ Dog | left Cat | SFX4 ]
up [ Dog | Stationary Cat | No Obstacle ] -> [ Dog | up Cat | SFX4 ]
down [ Dog | Stationary Cat | No Obstacle ] -> [ Dog | down Cat | SFX4]
right [ VerticalMovingDog | Stationary Cat | No Obstacle ] -> [ VerticalMovingDog | right Cat | SFX4 ]
left [ VerticalMovingDog | Stationary Cat | No Obstacle ] -> [ VerticalMovingDog | left Cat | SFX4 ]
up [ HorizontalMovingDog | Stationary Cat | No Obstacle ] -> [ HorizontalMovingDog | up Cat | SFX4 ]
down [ HorizontalMovingDog | Stationary Cat | No Obstacle ] -> [ HorizontalMovingDog | down Cat | SFX4]

[ > MovingDog | Mouse | ] -> [ Dog | Mouse | SFX3 ]
[ > MovingDog | Elephant ] -> [ Dog | Elephant ]
[ > MovingDog | Cat | Obstacle | ] -> [ Dog | Cat | Obstacle | SFX4 ]
[ > MovingDog | Cat | No Obstacle ] -> [ Dog | > Cat | SFX4 ]
[ > MovingDog | Obstacle | ] -> [ Dog | Obstacle | SFX2 ]

(Cat rules)
[ UP Cat ] -> [ UP UpMovingCat ]
[ DOWN Cat ] -> [ DOWN DownMovingCat ]
[ LEFT Cat ] -> [ LEFT LeftMovingCat ]
[ RIGHT Cat ] -> [ RIGHT RightMovingCat ]

[ UpMovingCat ] -> [ Up UpMovingCat ] again
[ LeftMovingCat ] -> [ Left LeftMovingCat ] again
[ RightMovingCat ] -> [ Right RightMovingCat ] again
[ DownMovingCat ] -> [ Down DownMovingCat ] again

right [ Cat | Stationary Mouse | No Wall ] -> [ Cat | right Mouse | SFX1 ]
left [ Cat | Stationary Mouse | No Wall ] -> [ Cat | left Mouse | SFX1 ]
up [ Cat | Stationary Mouse | No Wall ] -> [ Cat | up Mouse | SFX1 ]
down [ Cat | Stationary Mouse | No Wall ] -> [ Cat | down Mouse | SFX1]
right [ VerticalMovingCat | Stationary Mouse | No Wall ] -> [ VerticalMovingCat | right Mouse | SFX1 ]
left [ VerticalMovingCat | Stationary Mouse | No Wall ] -> [ VerticalMovingCat | left Mouse | SFX1 ]
up [ HorizontalMovingCat | Stationary Mouse | No Wall ] -> [ HorizontalMovingCat | up Mouse | SFX1 ]
down [ HorizontalMovingCat | Stationary Mouse | No Wall ] -> [ HorizontalMovingCat | down Mouse | SFX1]

[ > MovingCat | Elephant | ] -> [ Cat | Elephant | SFX4 ]
[ > MovingCat | Dog ] -> [ Cat | Dog ]
[ > MovingCat | Mouse | Wall | ] -> [ Cat | Mouse | Wall | SFX1 ]
[ > MovingCat | Mouse | No Wall ] -> [ Cat | > Mouse | SFX1 ]
[ > MovingCat | Obstacle | ] -> [ Cat | Obstacle | SFX2 ]

(Mouse rules)
[ UP Mouse ] -> [ UP UpMovingMouse ]
[ DOWN Mouse ] -> [ DOWN DownMovingMouse ]
[ LEFT Mouse ] -> [ LEFT LeftMovingMouse ]
[ RIGHT Mouse ] -> [ RIGHT RightMovingMouse ]

[ UpMovingMouse ] -> [ Up UpMovingMouse ] again
[ LeftMovingMouse ] -> [ Left LeftMovingMouse ] again
[ RightMovingMouse ] -> [ Right RightMovingMouse ] again
[ DownMovingMouse ] -> [ Down DownMovingMouse ] again

right [ Mouse | Stationary Elephant | No Obstacle ] -> [ Mouse | right Elephant | SFX5 ]
left [ Mouse | Stationary Elephant | No Obstacle ] -> [ Mouse | left Elephant | SFX5 ]
up [ Mouse | Stationary Elephant | No Obstacle ] -> [ Mouse | up Elephant | SFX5 ]
down [ Mouse | Stationary Elephant | No Obstacle ] -> [ Mouse | down Elephant | SFX5]
right [ VerticalMovingMouse | Stationary Elephant | No Obstacle ] -> [ VerticalMovingMouse | right Elephant | SFX5 ]
left [ VerticalMovingMouse | Stationary Elephant | No Obstacle ] -> [ VerticalMovingMouse | left Elephant | SFX5 ]
up [ HorizontalMovingMouse | Stationary Elephant | No Obstacle ] -> [ HorizontalMovingMouse | up Elephant | SFX5 ]
down [ HorizontalMovingMouse | Stationary Elephant | No Obstacle ] -> [ HorizontalMovingMouse | down Elephant | SFX5]

[ > MovingMouse | Dog | ] -> [ Mouse | Dog | SFX1 ]
[ > MovingMouse | Cat ] -> [ Mouse | Cat ]
[ > MovingMouse | Elephant | Wall | ] -> [ Mouse | Elephant | Wall | SFX5 ]
[ > MovingMouse | Elephant | No Obstacle ] -> [ Mouse | > Elephant | SFX5 ]
[ > MovingMouse | Wall | ] -> [ Mouse | Wall | SFX2 ]
[ > MovingMouse | Hole | ] -> [ No MovingMouse | Hole | SFX0 ]

endloop


(Late marker rules)

late [ Mouse ] [ MMarker ] -> [ Mouse Marker ] []
late [ Cat ] [ CMarker ] -> [ Cat Marker ] []
late [ Dog ] [ DMarker ] -> [ Dog Marker ] []
late [ Elephant ] [ EMarker ] -> [ Elephant Marker ] []

==============
WINCONDITIONS
==============

No MovingMouse
No Mouse

=======
LEVELS
=======

Message In 2001 I was very lost.

XXXXXXXXXXXXXXXXXXXXX
X###################X
X#...............###X
X#..#........#...###X
X#...............###X
X###...M..#........#X
X##H..........#....#X
X###..#.....#......#X
X#.............#...#X
X#..#....#.........#X
X#..............####X
X###################X
XXXXXXXXXXXXXXXXXXXXX

Message I was just trying to hide from the world.

XXXXXXXXXXXXXXXXXXXXXXXXXX
X########################X
X#...##....###.###....###X
X#...##....#H#........###X
X#.M.............#......#X
X#....##..##.....##.....#X
X#.....#......##........#X
X#..##.......###........#X
X#..##....#.......###...#X
X#.......##........##...#X
X#...##..##........##...#X
X#...##.................#X
X###.........###.....####X
X###.........###....#####X
X########################X
XXXXXXXXXXXXXXXXXXXXXXXXXX

Message I spent a lot of time playing a game called Castlemouse.

XXXXXXXXXXXXXXXXXXXXX
X###################X
X#.................#X
X#.........#.......#X
X###...............#X
X##H.....0.....#...#X
X###...............#X
X#.................#X
X#....#..........###X
X#.........0.....H##X
X#C..............###X
X###################X
XXXXXXXXXXXXXXXXXXXXX

Message Everything seemed to be slipping away from me.

XXXXXXXXXXXXXXXXXXXXXXXXXX
X########################X
X#.......##.###.........#X
X#......##..#H#.........#X
X#.................#....#X
X#..........0...0#......#X
X###....#...............#X
X##H..................###X
X###......0...........H##X
X#........#.....#.....###X
X#................#.....#X
X#......0...............#X
X#.C.......#H#..........#X
X#.........###..........#X
X########################X
XXXXXXXXXXXXXXXXXXXXXXXXXX

Message The world seemed full of fear.

XXXXXXXXXXXXXXXXXXX
X#################X
X#...............#X
X#.......1#......#X
X###.............#X
X##H....0........#X
X###.............#X
X#.............###X
X#.D......0....H##X
X#.#...........###X
X#...#...........#X
X#################X
XXXXXXXXXXXXXXXXXXX

Message Everything was falling apart for me at once.

XXXXXXXXXXXXXXXXXXXXXXXXXX
X########################X
X###..................###X
X##H.......0.0........H##X
X###..................###X
X#......................#X
X#..#...................#X
X#.1.........#..........#X
X#.........0............#X
X##.........1...........#X
X#.......D..............#X
X###..................###X
X##H.........0........H##X
X###.......#..........###X
X########################X
XXXXXXXXXXXXXXXXXXXXXXXXXX

Message I don't remember that time very well.

XXXXXXXXXXXXXXXXXXX
X#################X
X#...............#X
X#.......1#......#X
X###.............#X
X##H....0........#X
X###.............#X
X#.............###X
X#.....2..0....H##X
X#...#.......E.###X
X#...#...........#X
X#################X
XXXXXXXXXXXXXXXXXXX

Message I don't even remember exactly how Castlemouse went.

XXXXXXXXXXXXXXXXXXXXXXXXXX
X########################X
X#....................#.#X
X#....1..........#......#X
X#...........0...#...2..#X
X#......................#X
X#..........#H#.........#X
X#..........###.........#X
X#..........###.........#X
X#..........#H#.........#X
X#...........0..........#X
X#...2...........1......#X
X#.E...................##X
X#......................#X
X########################X
XXXXXXXXXXXXXXXXXXXXXXXXXX

Message Many things were set in motion at that time.

XXXXXXXXXXXXXXXXXXX
X#################X
X#......0.......H#X
X##..........3..##X
X#H...0..........#X
X##..............#X
X####..1.........#X
X#............#..#X
X#...#..###......#X
X#..D#..#H#......#X
X#.......0.......#X
X#..........1....#X
X#################X
XXXXXXXXXXXXXXXXXXX

Message Many unexpected chains of events, not all bad.

XXXXXXXXXXXXXXXXXXXXXXXXXX
X########################X
X#.....#..............###X
X#.....0..............H##X
X#.......3............###X
X#......................#X
X###....................#X
X##H.................0..#X
X###....................#X
X#......................#X
X#.....................C#X
X#...2.................##X
X#....#...2............##X
X#.....................##X
X########################X
XXXXXXXXXXXXXXXXXXXXXXXXXX

Message I'm doing a lot better now.

Message Also, Castlemouse is a much better game than this - you should track it down.

Message Thank you for playing.

Message Music: Broken - Fit and the Conniptions (music.conniptions.org)

title chaos wizard
author mark wonnacott
homepage http://twitter.com/ragzouken

youtube JDIj3zPwEvw

(run_rules_on_level_start)

realtime_interval 0.2

========
OBJECTS
========

Space
black darkblue
00010
00000
00000
01000
00000

Background
darkblue darkgreen
00000
01000
00010
00001
00100

Wall
black darkbrown darkblue
01112
21212
02121
20021
00010

Indestructible
darkgreen grey darkgrey
01112
21212
02121
20021
00010

Red
red lightred darkred
..0..
.010.
22110
.220.
..2..

Blue
blue lightblue purple
..0..
.010.
22110
.220.
..2..

Green
green lightgreen darkgreen
..0..
.010.
22110
.220.
..2..

Stone
grey lightgrey darkgrey
..0..
.010.
22110
.220.
..2..

Charge
red darkred
1.0.1
..0..
00000
..0..
1.0.1

Prism
white lightblue grey
..0..
.000.
00000
.000.
..0..


MoveUp
white

MoveDown
white

MoveLeft
white

MoveRight
white

Expand
white

Inhibit
white

Hold
blue lightblue
.1.1.
1.0.1
.0.0.
1.0.1
.1.1.

Burn
red orange yellow white
.010.
01210
12321
01210
.010.

Player
black darkgrey yellow
..1..
.111.
.212.
00100
00000

Rival
black darkgrey red
..1..
.111.
.212.
00100
00000

Goal
black
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

=======
LEGEND
=======
, = Space
. = Background
+ = Prism
# = Wall
X = Indestructible
@ = Player and Inhibit
R = Red
G = Green
B = Blue
S = Stone
? = Goal
$ = Charge
% = Rival

3 = Space and Blue
£ = Space and Charge

Wizard = Player or Rival

Gem = Red or Blue or Green or Stone

Movement = MoveUp or MoveDown or MoveLeft or MoveRight or Expand
Mediator = Hold or Burn

Moveable = Wizard or Gem
Destructable = Wizard or Gem or Wall

=======
SOUNDS
=======

(sfx0 76693900 (prism))
sfx1 68148303 (cast)
sfx2 24680901 (fall)

Burn destroy 17088108

================
COLLISIONLAYERS
================

MoveUp, MoveDown, MoveLeft, MoveRight, Expand, Inhibit
Background
Space
Charge, Goal
Wizard, Gem, Stone, Wall, Prism, Indestructible
Hold, Burn

======
RULES
======

[ Inhibit ] [ moving Player ] -> [ ] [ Player ]
[ Inhibit ] -> cancel

[ moving Player Hold ] -> [ Player Hold ]

(rival ai)
[ Rival no Goal | Goal ] -> [ > Rival | Goal ]

(objects fall into pits when not held)
late [ Moveable no Hold Space ] -> [ Space ] sfx2

[ parallel Wizard | Gem ] -> [ parallel Wizard | parallel Gem ]

[ > Gem | Charge ] -> [ Gem | Charge ]
[ > Gem | Gem ] -> [ > Gem | > Gem ]

[ action Wizard Charge ] -> [ Wizard ]
[ Charge ] [ action Wizard ] -> [ ] [ Wizard Charge ]

[ action Wizard ] -> [ Wizard Charge ]


(spells recipes)
[ Blue | Charge no Wizard ] -> [ | > Hold ] sfx1
[ Red  | Charge no Wizard ] -> [ | > Burn ] sfx1

[ Blue | Green | Charge no Wizard ] -> [ Blue | Blue | Blue ] sfx1
[ Red  | Green | Charge no Wizard ] -> [ Red  | Red  | Red  ] sfx1

[ Green | Charge no Wizard ] -> [ Red | Blue ] sfx1

[ Stone | Charge no Wizard ] -> [ Prism | ] sfx1
[ Prism | Charge no Wizard ] -> [ Stone | ] sfx1

(prism expands a spell)
[ stationary Player ] [ Mediator Prism ] -> [ Player ] [ Expand Mediator Prism ] sfx0

(shortcut to create directional mediators)
[    up Mediator ] -> [ Mediator MoveUp    ]
[  down Mediator ] -> [ Mediator MoveDown  ]
[  left Mediator ] -> [ Mediator MoveLeft  ]
[ right Mediator ] -> [ Mediator MoveRight ]

(apply movement to mediators)
[ stationary Player ] [ Mediator MoveUp    ] -> [ Player ] [    up Mediator    up MoveUp    ]
[ stationary Player ] [ Mediator MoveDown  ] -> [ Player ] [  down Mediator  down MoveDown  ]
[ stationary Player ] [ Mediator MoveLeft  ] -> [ Player ] [  left Mediator  left MoveLeft  ]
[ stationary Player ] [ Mediator MoveRight ] -> [ Player ] [ right Mediator right MoveRight ]

(expand multiplies the spell in all directions)
     up [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveUp    ]
+  down [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveDown  ]
+  left [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveLeft  ]
+ right [ Mediator Expand | no Movement no Wall ] -> [ Mediator Expand | Mediator MoveRight ]

[ stationary Player ] [ > Mediator > Movement | Prism ] -> [ Player ] [ > Mediator Movement | Prism ]
[ stationary Player ] [ Mediator Expand ] -> [ Player ] [ ]

(teleport fizzles on dirt and wall)
[ Hold Wall ] -> [ Wall ]
[ Hold Indestructible ] -> [ Indestructible ]

(fireball destroys dirt, fizzles on wall)
[ Burn Destructable   ] -> [ ]
[ Burn Indestructible ] -> [ Indestructible ]

(spells cancel out)
[ stationary Player ] [ > Mediator Movement | Mediator Movement ] -> [ Player ] [ | ]

(clean up spell-less move commands)
[ stationary Player ] [ Movement no Mediator ] -> [ Player ] [ ]

(teleport moves objects)
[ > Hold Moveable ] -> [ > Hold > Moveable ]

(spells end on goal)
[ > Mediator Movement | Goal ] -> [ | Goal ]

(rival leaves level)
[ Rival Goal ] -> [ Goal ]

==============
WINCONDITIONS
==============

some Player on Goal
no Rival

=======
LEVELS
=======

message you are a chaos wizard - heir of an ancient sigil - a sigil with the power to free magick from its mineral slumber

message cast spells by placing your sigil next to mana ores. escape this tomb

(rival wizard, introduce teleport, teach pulling)
x##xxxxxxx##x
xxxxxbbbxxxxx
x,,xx...xx,,x
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#.@.#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#...#,,,,
,,,,#$$$#,,,,
x,,xxbbbxx,,x
xxxxx.%.xxxxx
x##xx???xx##x

(move demo, teach teleport)
x,,xxxxxxx,,x
xxxxx...xxxxx
x##xx.@.xx##x
####x...x####
##xxx...xxx##
##xb......x##
##x.x...x.x##
##x...b.b.x##
##x...$...x##
##x.x.%.x.x##
##x,,.....x##
##x,,,,,..x##
##x,x,,,xbx##
##x,,..,,,x##
##xxx...xxx##
####x???x####

(teach that mana can be teleported)
##xxxxxx#####
##x.....x####
##x.@...,x###
##.....,,,x##
##....,,...x#
#.b##x,b...x#
#.####x..b.x#
#..###.$.$.x#
##.....b.%.x#
#######x.,,x#
#######x,,,x#
#######x,,,x#
#######x,..x#
#######x???x#

(introduce fireball, teach fireball)
###xxxxxxx###
###xx...xx###
###xx.@.xx###
###xx...xx###
###xx...xx###
###xx...xx###
#rr#x...xx###
rrrr..$.xx###
rr####R.xx###
r#######xx###
r#######xx###
######..xx###
###xx...xx###
###xx.%.xx###
###xx???xx###

(puzzle: teleport, fireball, teleporting mana)
###############
#####xxxxx#####
#xxxxxbrbxxxxx#
#x,,,,...,,,,x#
#x,....@....,x#
#x,.,,...,,.,x#
#x,.,,,,,,,.,x#
#xr..,x#x,..bx#
#xr..,#.#,..rx#
#xr$.,x#x,.$bx#
#x,,,,,,,,,,,x#
#x,.,,rrr,,.,x#
#x,.........,x#
#x,,,,$$$,,,,x#
#xxx#.bbbxxxxx#
#####x.%.x#####
#####x???x#####

(introduce & teach pre-sigil)
###xxxxxxx###
###xx...xx###
###x..@..x###
##x.....##x##
#x.r....###x#
#x$#r#x..##x#
#x###x.b.$bx#
#x###x$x..,x#
#x.##x%x..,x#
#x..#,...,,x#
#x...,,,,,,x#
##x...,,,,x##
###x....,x###
###xx...xx###
###xx???xx###

(puzzle: fireball, pre-sigil)
#################
#####xxxxxxx#####
#xxxxxx...xxxxxx#
#xr...x.@.xr###x#
#xr.....#...###x#
#xrr..x...x..##x#
#xxxx..,,,.#xxxx#
####x.x,,,x.x####
####x..,,,.#x####
####x.x,,,x#x####
####x..,,,.#x####
####x.x,,,x.x####
####x..,,,.#x####
####xx#$.$.xx####
#####xxr%rxx#####
#####xx???xx#####

(introduce split, teach split)
###xxxxxxx###
###xx...xx###
###xx.@.xx###
###xx...xx###
###xx...xx###
###xx.g.xx###
####x...xx###
#####...xx###
#######.xx###
########xx###
######,,xx###
#####,,,xx###
####x,..xx###
###xx.%.xx###
###xx???xx###

(puzzle: teleport, fireball, split)
#######xxxxx#
#xxxxxxx...x#
#x###b...@.x#
#x####r$...x#
#x##r$...,.##
#x#.gxx.,,,,#
#x...x##,,,,#
#x###x##,,,,#
#xg##x##,,,,#
#x...xxx,,,.#
#x.......,,.#
#x.....###,b#
#x....###.#x#
#xxxxxxx.%.x#
#######x???x#

(introduce multiply, teach multiply)
x###xxxxx###x
xx#xx...xx#xx
,x#x.@...x#x,
xxxxx...xxxxx
xbg$x.$#x$grx
xbg$x.r#x$grx
xxxxx$##xxxxx
,x#x.r...x#x,
xxxxx##.xxxxx
xbg$###...grx
xbg$x..,x.,,x
xxxxx.,,xxxxx
,x#x.,,,.x#x,
xx#xx,,%xx#xx
x###x???x###x

(puzzle: fireball, teleport mana, multiply)
####xxxxxxx####
####xx...xx####
###xxx.@.xxx###
###x.xr.rx.x###
###x...r...x###
xxxx.......xxxx
x..x,.$$$.,x.rx
x.g.,.rrr.,...x
x.#...x#x.....x
x...,#####,...x
xb.x,##.##,x..x
xxxx#...#..xxxx
###x.##..##x###
###x.x.%#x#x###
###xxx???xxx###

(puzzle: fireball, teleport, teleport mana, split, multiply)
####xxxxxxx####
####xx.@.xx####
#####r...g#####
####r.,,,.#####
####r.,,,.#g###
####..,,,..####
###b.....$.#g##
###....,,r..###
###...,,,,..###
####.$,,,,,,###
####$r,,,#...##
####r,,###....#
###,,,####....#
####xx#%#xx.r##
####xx???xx####

(introduce amplification, teach amplification)
###xxxxxxx###
#xxxx...xxxx#
#xrxx.@.xx,x#
#x.........x#
#x,,.xxxxxgx#
#x+,#x###x.x#
#x,,.xxxxx.x#
#x.r..#...rx#
#xgxxxxx.,,x#
#x#x###x#,+x#
#x#xxxxx.,,x#
#x.###....$x#
#x,xx...xxbx#
#xxxx.%.xxxx#
###xx???xx###

(amplified teleport)
,,xxxxxxx,,
,#xx...xx#,
,#xx.@.xx#,
#+.,$b.,.+#
,..#...,..#
,,,#,,###,#
,..#...#..#
#+.,.+r£.+#
#..,##.,..,
,,,,,,,,,,,
,.#,##.,..#
#x.,.x.,.+#
##xx...xx##
#,xx.%.xx,#
,,xx???xx#,

(puzzle: fireball, amplified fireball)
###xxxxxxx###
#xxxx...xxxx#
#xrxx.@.xxrx#
#xr.......rx#
#x..##.#...x#
#x..,,,,,##x#
#x#.,,,,#.#x#
#x###,+,###x#
#x..#,,,##.x#
#x..,,,,,##x#
#x.######..x#
#x.#####...x#
#xrxx#.#xxrx#
#xxxx.%.xxxx#
###xx???xx###

(puzzle: fireball, split, amplified teleport)
###xxxxxxx###
#xxxx...xxxx#
#xbxx.@.xx.x#
#x..x...x...#
#x...,.,...,#
#x...,.,...,#
#x..,...,..,#
#x.#...r.#.##
#x.x,,,,,x.x#
#xgx+,+,+x.x#
#x#xxx,xxx.x#
#x##..$...#x#
#x#xx.r.xx#x#
#xxxx.%.xxxx#
###xx???xx###

(puzzle: fireball, sigil-first, split, multiply, amplified teleport)
###xxxxxxx###
#xxxx...xxxx#
#x#xx.@.xx.x#
#x........gx#
#xgx..x..x.x#
#x.x.....x.x#
#x,,x...x,,x#
#x+,..,..,+x#
#x,,x$.$x,,x#
#x.x.r.r.x.x#
#x.x.#x#.x.x#
#x...###...x#
#x.xx#.#xx.x#
#xxxx.%.xxxx#
###xx???xx###

(introduce mobile amplify, teach mobile amplify)
(puzzle: teleport, amplified teleport)
###xxxxxxxx,,
#xxxx...xxx,,
#x.xx.@.xx,,,
#xbbs...,,,,#
#xx.,,.,,,+##
#xx.,,.,,,,,#
#xx,,x.x,,,,,
#x,,,r$,,,+##
#xx,,x,x,,,##
#xx.,,,,,,,,#
#xx.,##,,,,,,
#x....##,,,,,
#x.xx..#xx,,,
#xxxx.%.xxx,,
###xx???xxx,,

(puzzle: teleport, fireball, mobile amplify)
#################
#####xxxxxxx#####
#xxxxxx.@.xxxxxx#
#x.bbx.+++.x+.+x#
#x...x.....x.xrx#
#x#.....b....x$x#
#x..$x..$..x.x.x#
#x..rx.....x+.+x#
#xx$xx#.%..xxxxx#
#x#r..##.....##x#
#x#...#,,,.###.x#
#x##...,,,.#.##x#
#x##...,,,.##.bx#
#xxxxxx...xxxxxx#
#####xx...xx#####
#####xx???xx#####

message i cannot allow you to proceed

(gauntlet i: fireball dodging)
,,xxxxxxxxx,,
,,xxx...xxx,,
,,,xx.@.xx,,,
x,,,,...,,,,,
+.,,,.b.,,,,x
..x,,.$.,,,.+
+.+.,.%.,,x..
...x,...,.+.+
+.+.,...,x...
$.x,,...,.+.+
rx,,,...,,x.$
x,,,,...,,,xr
,,,,,...,,,,x
,,,xx...xx,,,
,,xxx...xxx,,
,,xxx???xxx,,

message your power waxes and thus mine wanes

(gauntlet ii: impending fireball doom)
####xxxxx####
#xxxxbbbxxxx#
#x.,..@..,.x#
#x.,.....,.x#
#x.,.....,.x#
#x.,#####,.x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,#######,x#
#x,,#####,,x#
#x,,,,,,,,,x#
#x,x+++++x,x#
#x.xxx$xxx.x#
#x....r....x#
#xxxx.%.xxxx#
####x???x####

(message it seems you cannot be stopped

(gauntlet iii: interference pattern)
#######xxxxx#######
###xxxxx...xxxxx###
###x...s.@.s...x###
###x...s...s...x###
#xxx,,,,sbs,,,,xxx#
#x+,,,,,...,,,,,,x#
#x+,,,,,$r.,,,,,,x#
#x+,,,,,...,,,,,,x#
#x+,,,,,$$$,,,,,,x#
#xxx,,,,bbb,,,,xxx#
###x...$...$...x###
###x...r...r...x###
###xxxxx.%.xxxxx###
#######x???x#######)

message there isn't enough mana in the cosmos to stop fate
message ...but you shall not leave whilst i live

(boss)
,,x###xxxxx###x,,
,,xrrrxb.bxrrrx,,
,,,.....@.....,,,
,,,,,,,...,,,,,,,
,,,,,,,,,,,,,,,,,
,+x,,,,,,,,,,,x+,
+x+,,,,,£3,,,,+x+
x+%,,,,,,,,,,,.+x
,,,,,,x,x,x,,,,,,
,,,,,,,???,,,,,,,
,,,,,,x???x,,,,,,
,,,,,,,???,,,,,,,
,,,,,,x,x,x,,,,,,
x+.,,,,3£,,,,,.+x
+x+,,,,,,,,,,,+x+
,+x,,,,,,,,,,,x+,
,,,,,,,,,,,,,,,,,

message all of this has happened before, and all of this will happen again, my son

title horizontal Block Pushing Game
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Background
lightgreen green
11111
01111
11101
11111
10111

Target
darkblue
.....
.000.
.0.0.
.000.
.....

Wall
brown darkbrown
00010
11111
01000
11111
00010

Player
black orange white blue
.000.
.111.
22222
.333.
.3.3.

Crate
blue white
00000
00000
01110
00000
00000

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target

=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

horizontal [ > Player | Crate ] -> [ > Player | > Crate ] 

==============
WINCONDITIONS
==============

all Target on Crate

=======
LEVELS
=======

#########
#.......#
#.....@.#
#.P.*.O.#
#.......#
#.......#
#########

.#######.
.#.....#.
.#O##..##
##.##.*.#
#.*..**.#
#.OOO.#.#
#.P.....#
#########


title Block Faker
author Droqen
homepage www.droqen.com

require_player_movement
key_repeat_interval 0.12

background_color white
text_color black

color_palette c64

(

A port of first several levels of Droqen's beautiful game:

https://droqen.itch.io/block-faker

(His game has secrets that this one does not.)

)

========
OBJECTS
========

Background
white

Grille
gray
0...0
.0.0.
..0..
.0.0.
0...0

EndPoint
green

Player
black
00000
00000
0.0.0
00000
00000

Wall
gray

WallBlock
black grey
01111
01111
01111
01111
00001

BlueBlock
blue
00000
00.00
0.0.0
00.00
00000

GreenBlock
lightgreen
00000
00000
00.00
00000
00000

PinkBlock
red
00000
00.00
0...0
00.00
00000

PurpleBlock
purple
00000
0...0
0.0.0
0...0
00000

OrangeBlock
orange
00000
0.0.0
00000
0.0.0
00000

=======
LEGEND
=======

Block = PinkBlock or BlueBlock or PurpleBlock or OrangeBlock or GreenBlock
Moveable = Player or Block
PlayerObstacle = Block or Wall or WallBlock
BlockObstacle = Player or Wall or WallBlock or Grille or Block or EndPoint
. = Background
# = Wall
@ = WallBlock
P = Player
B = BlueBlock
G = GreenBLock
K = PinkBlock
R = PurpleBlock
O = OrangeBlock
E = EndPoint
x = Grille
H = Grille and Player

=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Background
EndPoint
Grille
Player, Wall, WallBlock, PinkBlock, BlueBlock, PurpleBlock, OrangeBlock, GreenBlock

======
RULES
======

[ > Moveable | Moveable ] -> [ > Moveable | > Moveable ] 

[ > Block | Grille ] -> [ Block | Grille ] 

late [ PinkBlock | PinkBlock | PinkBlock ] -> [ | | ] 
late [ BlueBlock | BlueBlock | BlueBlock ] -> [ | | ] 
late [ PurpleBlock | PurpleBlock | PurpleBlock ] -> [ | | ] 
late [ OrangeBlock | OrangeBlock | OrangeBlock ] -> [ | | ] 
late [ GreenBlock | GreenBlock | GreenBlock ] -> [ | | ] 

==============
WINCONDITIONS
==============

some Player on Endpoint

=======
LEVELS
=======

........########
################
########@@@@@###
####@@@@@...@###
###@..OO....@@##
##@..@@@@@....@#
##@.@..@@@..E@@#
#@..@.O...@@@@##
##@...@.P.######
####@.@@@@######
####@@##########
########........

message congrats: level 2!

##########
####R.R###
#x....#x##
#xO#O.OxE#
#xx##.#xx#
#####R####
#####P####
O#########

message congrats: level 3!

......#######...
.....##P.#####..
....###....####.
..#####O...#B..#
..#####.#.##BB.#
##EBB..O.O.....#
..#####.#.#....#
..######..#G.###
....####..#.OOx.
.....###.GG.#x..
......#######...

message congrats: level 4!

######xx######
######GG######
##xx#...#xEx##
##..O...#x..##
##..#...##O###
#..##.......##
xG....##....Gx
xG....##....Gx
##.......GG###
###G##...#xx##
##x..#...O.x##
##.P.#...#xx##
######GG######
######xx######

message congrats: level 5!

......##......
....######....
..##########..
..#.OG..kk.#..
.##OPO..Gkk##.
.##GOG..GGk##.
###...##...###
###...##...###
.##kkG##ROR##.
.##Gkk..OEO##.
..#.Gk..RO.#..
..##########..
....######....
......##......

message now go play the real game



USER PROMPT:
Output the code for a complete PuzzleScript game. First, reason about your task and determine the best plan of action. Then, write your code. Return your code in full, inside a ```plaintext code block.