SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title Kettle
author increpare
homepage www.increpare.com

color_palette gameboycolour

(

A port of a flash game of mine, the original of which can be played here:
http://ded.increpare.com/~locus/kettle

)

========
OBJECTS
========

Background
darkgray

Target
yellow
00000
0...0
0...0
0...0
00000

UPolice
black
.000.
.000.
00000
.000.
.0.0.

DPolice
black
.000.
.000.
00000
.000.
.0.0.

LPolice
black
.000.
.000.
0000.
.000.
.0.0.

RPolice
black
.000.
.000.
.0000
.000.
.0.0.

Crate
blue
..0..
00000
..0..
.000.
.0.0.

Wall
black

=======
LEGEND
=======

. = Background
player = UPolice or DPolice or LPolice or RPolice
i = UPolice
k = DPolice
j = LPolice
l = RPolice
c = Crate
@ = Crate and Target
t = Target
# = Wall

=========
SOUNDS
=========

Target move 59810707
Player move 86872107
startlevel 63638302
endlevel 86642102
endgame 63929902
showmessage 42200307
closemessage 42200307
startgame 94059902

================
COLLISIONLAYERS
================

Background
Target
Player, Crate, Wall

======
RULES
======

(The idea is simple: lots of Players, each of which only responds to a particular input direction.)

[ up RPolice ] -> [ RPolice ] 
[ down RPolice ] -> [ RPolice ] 
[ left RPolice ] -> [ RPolice ] 

[ down UPolice ] -> [ UPolice ] 
[ left UPolice ] -> [ UPolice ] 
[ right UPolice ] -> [ UPolice ] 

[ up LPolice ] -> [ LPolice ] 
[ down LPolice ] -> [ LPolice ] 
[ right LPolice ] -> [ LPolice ] 

[ up DPolice ] -> [ DPolice ] 
[ left DPolice ] -> [ DPolice ] 
[ right DPolice ] -> [ DPolice ] 

[ > Player | Crate ] -> [ > Player | > Crate ] 
[ > Crate | Crate ] -> [ > Crate | > Crate ] 

==============
WINCONDITIONS
==============

all target on Crate

=======
LEVELS
=======

message Let's see your ID

###############
#.............#
#.............#
#.....kkkk....#
#....l...cj...#
#....l.tt.j...#
#....lctt.j...#
#....lc..cj...#
#.....iiii....#
#.............#
#.............#
#.............#
###############

message Area Secure
message Gonna lock you up, sonny!

###############
#.............#
#.............#
#.....kkkk....#
#....l@@t.j...#
#....ltttcj...#
#....l@t@cj...#
#....lc.ccj...#
#.....iiii....#
#.............#
#.............#
#.............#
###############

message Area Secure
message We didn't see nothing!

###############
#.............#
#.............#
#....kkkkk....#
#...l@t@@cj...#
#...l@@ttcj...#
#...ltt@tcj...#
#...lt@@@.j...#
#...l.ccccj...#
#....iiiii....#
#.............#
#.............#
###############

message Area Secure
message You'll be late for class, eh!

###############
#.............#
#.............#
#....kkkkk....#
#...lc.c.cj...#
#...lc@tt.j...#
#...l.tt@cj...#
#...l.ttt.j...#
#...lc...cj...#
#....iiiii....#
#.............#
#.............#
###############

message Area Secure
message Ha! You shat yourself!

###############
#.............#
#.............#
#....kkkkkk...#
#...lc.c..cj..#
#...lcttt..j..#
#...l.t@@..j..#
#...l.ttt..j..#
#...lc...ccj..#
#....iiiiii...#
#.............#
#.............#
###############

message Area Secure
message Have to confiscate your camera!

###############
#.............#
#.............#
#....kkkkkk...#
#...lcc..ccj..#
#...l.tttt.j..#
#...l.t@@t.j..#
#...lcttt@cj..#
#...lct@ttcj..#
#...lcc..ccj..#
#....iiiiii...#
#.............#
###############

message Area Secure
message Keeping the peace is hard work

###############
#.............#
#...kkkkkkk...#
#..l..cc..cj..#
#..l.tttt..j..#
#..lc@t@t..j..#
#..lctttt.cj..#
#..l.@@tt..j..#
#..lc.....cj..#
#..lcc.c..cj..#
#...iiiiiii...#
#.............#
###############

message Area Secure
message You should've packed lunch!

###############
#.............#
#..kkkkkkkk...#
#.lccccc...j..#
#.lct@@tt.cj..#
#.l.t@t@t.cj..#
#.lc@@ttt.cj..#
#.lct@tttccj..#
#.l.tt@@t..j..#
#.l........j..#
#.lc..cc...j..#
#..iiiiiiii...#
###############

message Area Secure
message I want to hit you

###############
#.............#
#..kkkkkkkkk..#
#.lc.cccc...j.#
#.l.........j.#
#.lccttttt..j.#
#.lc.tt@tt.cj.#
#.lc.tt@@t.cj.#
#.l..@@tt@.cj.#
#.l..t@t@t..j.#
#.l.........j.#
#.lc.c.c.c.cj.#
#..iiiiiiiii..#
###############

message Area Secure
message Don't be naughty!

###############
#.kkkkkkkkkk..#
#lccc..c...cj.#
#lc.........j.#
#lc.@t@tt...j.#
#lc.t@@@t...j.#
#l..@ttt@..cj.#
#lc.tt@@t..cj.#
#l..ttttt...j.#
#l..........j.#
#l.........cj.#
#l....c.c.ccj.#
#.iiiiiiiiii..#
###############

message Area Secure
message I'm upholding human rights!

###############
#.kkkkkkkkkkk.#
#lccc.c.c...cj#
#l....c......j#
#lc.@ttttt...j#
#l..@ttt@t...j#
#l..t@@ttt..cj#
#lc.@tt@@t..cj#
#l..@@t@@t...j#
#l..t@t@tt..cj#
#lc..c.......j#
#lc....c.....j#
#lcc.c.c.c..cj#
#.iiiiiiiiiii.#
###############

message Area Secure
message I'm all out of lines, matey:
message It's game over for you


title Multi-word Dictionary Game
author Sarah Northway
homepage www.sarahnorthway.com

(included with permission of Sarah Northway)

========
OBJECTS
========

Background
pink

Wall
grey

Player
Blue

Won
yellow

LetterC
white black
.111.
1....
1....
1....
.111.

LetterA
white black
.111.
1...1
11111
1...1
1...1

LetterT
white black
11111
..1..
..1..
..1..
..1..

=======
LEGEND
=======

Letter = LetterA or LetterC or LetterT
. = Background
# = Wall
P = Player
W = Won
C = LetterC
A = LetterA
T = LetterT

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Won
Player, Wall, Letter

======
RULES
======

[ >  Player | Letter ] -> [  >  Player | > Letter  ]
[ <  Player | Letter ] -> [  <  Player | < Letter  ]

late right [LetterC | LetterA | LetterT ] -> [ Won LetterC | Won LetterA | Won LetterT ] message You spelled CAT!
late right [LetterA | LetterC | LetterT] -> [ Won LetterA | Won LetterC | Won LetterT ] message You spelled ACT!

==============
WINCONDITIONS
==============

Some Won

=======
LEVELS
=======

#########
#.......#
#.A..C..#
#.......#
#...P...#
#.T.....#
#########


title Many parallel players, unlimited rigidbodies
author increpare
homepage www.increpare.com

verbose_logging

(

This is a sample of 

- Unlimited rigid bodies
- Multiple players moving in parallel

It's made as an example for the extended rigid body tutorial here:

https://www.puzzlescript.net/Documentation/rigidbodies.html

)

========
OBJECTS
========

Background
green

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

 Crate
Yellow orange
00000
01110
01110
01110
00000


(The directional overlays bellow establish connections between crates)

 Crate_U
orange
.000.
.....
.....
.....
.....

 Crate_D
orange
.....
.....
.....
.....
.000.

 Crate_L
orange
.....
0....
0....
0....
.....

 Crate_R
orange
.....
....0
....0
....0
.....

CanMove
red
.....
.....
..0..
.....
.....

=======
LEGEND
=======

. = Background
# = Wall
P = Player

w = Crate and Crate_U
x = Crate and Crate_D
a = Crate and Crate_L
d = Crate and Crate_R

e = Crate and Crate_U and Crate_L
z = Crate and Crate_D and Crate_L
q = Crate and Crate_U and Crate_R
c = Crate and Crate_D and Crate_R

r = Crate and Crate_L and Crate_R
t = Crate and Crate_D and Crate_U

1 = Crate and Crate_D and Crate_L and Crate_R
2 = Crate and Crate_U and Crate_L and Crate_R
3 = Crate and Crate_U and Crate_D and Crate_L
4 = Crate and Crate_U and Crate_D and Crate_R

0 = Crate and Crate_U and Crate_D and Crate_L and Crate_R

5 = Crate

 Crate_Connection = Crate_U or Crate_D or Crate_L or Crate_R

Pusher = Player or Crate
Pushable = Crate 
Solid = Crate or Player

=======
SOUNDS
=======

 Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Player, Wall, Crate
 Crate_U
 Crate_D
 Crate_L
 Crate_R
CanMove

======
RULES
======

(For making maps easily: uncomment out the next few lines + press action to discard all boundaries and recreate them)

( [ action player ] [ Crate_connection ] -> [ action player ] [ ] )
( up [ action player ] [ Crate | Crate ] -> [ action player ] [ Crate Crate_u | Crate Crate_d ] )
( right [ action player ] [ Crate | Crate ] -> [ action player ] [ Crate Crate_r | Crate Crate_l ] )


(Step 1: propagate motions forward)
[ > Pusher | Pushable ] -> [ > Pusher | > Pushable ]
+ up [ moving Crate Crate_U | Crate ] -> [ moving Crate Crate_U | moving Crate ]
+ down [ moving Crate Crate_D | Crate ] -> [ moving Crate Crate_D | moving Crate ]
+ left [ moving Crate Crate_L | Crate ] -> [ moving Crate Crate_L | moving Crate ]
+ right [ moving Crate Crate_R | Crate ] -> [ moving Crate Crate_R | moving Crate ]


(Step 1: If a crate can't move, we have to back-propagate the can't-move-ness, including possibly through players)
[ > Solid | wall ] -> [ Solid | wall ]

[ > Solid | stationary Solid ] -> [ Solid | Solid ]
+ up [ moving Crate Crate_U | stationary Crate ] -> [ Crate Crate_U | Crate ]
+ down [ moving Crate Crate_D | stationary Crate ] -> [ Crate Crate_D | Crate ]
+ left [ moving Crate Crate_L | stationary Crate ] -> [ Crate Crate_L | Crate ]
+ right [ moving Crate Crate_R | stationary Crate ] -> [ Crate Crate_R | Crate ]


(Step 3: Mark all crates that are still moving as possibly being able to move)
[ moving Pushable ] -> [ Pushable CanMove ]

(Step 4: redo Step 1, but only through things that are marked as being possibly able to move)
[ > Pusher | CanMove Pushable ] -> [ > Pusher | CanMove > Pushable ]
+ up [ moving Crate Crate_U | CanMove Crate ] -> [ moving Crate Crate_U | CanMove moving Crate ]
+ down [ moving Crate Crate_D | CanMove Crate ] -> [ moving Crate Crate_D | CanMove moving Crate ]
+ left [ moving Crate Crate_L | CanMove Crate ] -> [ moving Crate Crate_L | CanMove moving Crate ]
+ right [ moving Crate Crate_R | CanMove Crate ] -> [ moving Crate Crate_R | CanMove moving Crate ]

(Step 5: remove all the CanMove tags)
[ CanMove ] -> [ ]

(Step 6: move connections along with the crates themselves)
[ moving Crate stationary Crate_connection ] -> [ moving Crate moving Crate_connection ]


==============
WINCONDITIONS
==============

=======
LEVELS
=======

###############
#..ca.........#
#.ce....ca....#
#.t...d1e.....#
#.w....w......#
#....d1ap5.dz.#
#....pw...5.w5#
#......dz.....#
#.......t..dz.#
#..x..cre...w.#
#..w..w...5...#
#.............#
###############


title Midas
author wanderlands
homepage https://web.archive.org/web/20160706204929/http://wanderlands.org/

(

A port of the luxuriant Wanderlands game, Midas, with permission of the authors.  It's a bit messy, if you can find a way to tidy it UP, let me know : ] 

the original is here
https://web.archive.org/web/20120219201941/http://wanderlands.org/main/midas

update: uh, it's flash, so not a playable link any more...also the original developer site is gone.

)

========
OBJECTS
========

Background
white

Lovebase
white

Target
black
.....
..0..
.000.
..0..
.0.0.

GoldTarget
orange
.....
..0..
.000.
..0..
.0.0.

PlayerGround
black
.....
..0..
.000.
.000.
.0.0.

PlayerJump1
black
.....
..0..
00000
.000.
.0.0.

PlayerJump2
black
.....
..0..
00000
.000.
.0.0.

Crown
orange
.000.
.0.0.
.....
.....
.....

GoldBlock
orange

GoldingBlock
brown

Water
blue

Wall
gray

Love
pink
.0.0.
00000
00000
.000.
..0..

=======
LEGEND
=======

JumpingPlayer = PlayerJump1 or PlayerJump2
Player = PlayerGround or PlayerJump1 or PlayerJump2
Obstacle = Player or Wall or Water or GoldingBlock or GoldBlock or Target or GoldTarget
CanFall = GoldBlock or Target or GoldTarget
. = Background
# = Wall
P = PlayerGround and Crown
T = Target
W = Water
G = GoldBlock
D = GoldingBlock

=========
SOUNDS
=========

sfx0 28219707 (touch Water)
sfx1 45545307 (golding)
sfx2 42491507 (make lady gold :/ )
StartGame 8892900
EndLevel 28067900
EndGame 28067900
CloseMessage 44855507

================
COLLISIONLAYERS
================

Background
Lovebase
PlayerGround, PlayerJump1,PlayerJump2, Wall, Water, GoldBlock, Target, GoldTarget, GoldingBlock
Crown
Love

======
RULES
======

UP [ UP PlayerGround | NO Obstacle ] -> [ UP PlayerJump2 | ] 
DOWN [ UP JumpingPlayer | NO Obstacle ] -> [ JumpingPlayer | ] 
DOWN [ UP PlayerJump2 | NO Obstacle ] -> [ PlayerJump2 | ] 

[ STATIONARY CanFall ] -> [ Down CanFall ] 

HORIZONTAL [ > PlayerJump1 | NO Obstacle ] -> [ | PlayerGround ] 
HORIZONTAL [ > PlayerJump2 | NO Obstacle ] -> [ | PlayerJump1 ] 
LATE DOWN [ PlayerGround | NO Obstacle ] -> [ | PlayerGround ] 

LATE [ Crown NO Player ] [ Player ] -> [ ] [ Crown Player ] 
LATE [ GoldingBlock ] -> [ GoldBlock ] 
LATE [ Crown | Wall ] -> [ Crown | GoldingBlock ] sfx1
LATE [ Crown | Water ] -> [ | Water ] sfx0
LATE [ Crown | Target ] -> [ Crown | GoldTarget ] sfx2
LATE DOWN [ JumpingPlayer | Obstacle ] -> [ PlayerGround | Obstacle ] 

LATE [ Player | Target ] -> [ Player Lovebase | Target ] 
LATE UP [ Lovebase | ] -> [ | Love ] 

==============
WINCONDITIONS
==============

some Love (Awwww!)

=======
LEVELS
=======
##############
#............#
#............#
#.P.......T..#
##G######WWW##
##############

message MIDAS

###############
#.............#
#.............#
#.....#.......#
#.P..##..#.T..#
##G#######WWW##
###############

message She waits for me every day

###############
#.............#
#.............#
#.............#
#.....#.#.....#
#.P..##.#..T..#
##G#######WWW##
###############

message This is the price of gold:

##############
#............#
#............#
#.P..........#
##G###T##WWW##
##############

message A heavy heart

#################
#...............#
#...............#
#......#.#......#
#.....#####.....#
#.......#.......#
#.T.P...#.......#
####G#######WWW##
#################

message I step in the same river
message again and again

#####################
#...................#
#...................#
#...................#
#.........#.#.......#
#..P........#.......#
#..d##########......#
#.......######......#
#...................#
#.......T...........#
#....#########WWW...#
#...................#
#...................#
#...................#
#####################

message Just so I can touch her hand.

#####################
#...................#
#...................#
#...................#
#...................#
#..P................#
#..d##T########WWW..#
#.....#.............#
#...................#
#...................#
#####################

message I am a prison to Pactolus

#####################
#...................#
#........###........#
#.......#####.......#
#......##...##......#
#.....T##.P.##W.....#
#.....####d####.....#
#.....#########.....#
#...................#
#####################

message But I must have her!

#####################
#...................#
#...................#
#..T..#...###..W....#
#..#####...#####....#
#.....###...#.......#
#........P..........#
#.....###D###.......#
#.....#######.......#
#...................#
#...................#
#####################

message My burden only grows

#####################
#...................#
#........G..........#
#.....W.GGG.T.......#
#.....#######.......#
#.....#.....#.......#
#.....#..P..#.......#
#.....###D###.......#
#.....#######.......#
#...................#
#...................#
#...................#
#####################

message My world collapses.

#####################
#...................#
#...................#
#..G....WW..........#
#..##.####..........#
#.....####..........#
#...................#
#.#...........T.....#
#.##.......P..#.....#
#..############.....#
#...#.....####......#
#...................#
#...................#
#...................#
#...................#
#...................#
#####################

message And she feels so distant

#####################
#...................#
#...................#
#...................#
#...................#
#..P......#.........#
#..GG...........WW..#
#..##T############..#
#....#..............#
#...................#
#...................#
#####################

message I live on borrowed time

#####################
#...................#
#...................#
#..........T........#
#.........##........#
#...................#
#.......P...........#
#.......D##.........#
#...........#.......#
#...................#
#...................#
#.......###W#.......#
#####################

message Is she still waiting?

#####################
#...................#
#....###########....#
#...................#
#....P.........W....#
#....###########....#
#....#.........#....#
#...................#
#.........#.........#
#......#..#..#......#
#....T.#..#..#......#
#....###########....#
#...................#
#...................#
#...................#
#####################

message Is Love just fool's gold?

#####################
#...................#
#...................#
#...........####....#
#..T##.##..#...#....#
#..#..#..#.#.P.#....#
#..#.....#...G#.....#
#...#...#....#......#
#....#W#.....#......#
#.....#.............#
#............#......#
#...................#
#...................#
#####################

message Midas


title Sticky Blocks
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Background
lightgreen green
11111
01111
11101
11111
10111

Target
darkblue
.....
.000.
.0.0.
.000.
.....

Wall
brown darkbrown
00010
11111
01000
11111
00010

Player
black orange white blue
.000.
.111.
22222
.333.
.3.3.

Crate
orange
00000
0...0
0...0
0...0
00000

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target

=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

[ < Player | Crate ] -> [ < Player | < Crate ] 

==============
WINCONDITIONS
==============

all Target on Crate

=======
LEVELS
=======

#########
#.......#
#.....@.#
#.P.*.O.#
#.......#
#.......#
#########

.#######.
.#.....#.
.#O##..##
##.##.*.#
#.*..**.#
#.OOO.#.#
#.P.....#
#########


title Extended Body Example: 1 Player, 1 Extended Object
author increpare
homepage www.puzzlescript.net

(
    Example for
    https://www.puzzlescript.net/Documentation/rigidbodies.html#one_one
)

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange
00000
0...0
0...0
0...0
00000

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target

=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

[ >  Player | Crate ] -> [  >  Player | > Crate  ]
[ > Crate ] [ stationary Crate ] -> [ > Crate ] [ > Crate ]
[ > Crate | Wall ] -> cancel

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

######
#....#
#.#..#
#.*@.#
#.O@.#
#.p..#
######


title Sokobond: The Demake
author Jonah Ostroff
homepage www.sokobond.com
text_color #202020
background_color #FDFDFD
key_repeat_interval 0.25
run_rules_on_level_start
noaction
youtube 2R0sTlruOZc

========
OBJECTS
========

Background
#FFFFFF #F8F8F8
00000
01010
00000
01010
00000

Player
#FFBBEE
00000
0...0
0...0
0...0
00000

Wall
Yellow White
00000
00000
00000
00000
00000

Hydrogen
Red Black
.....
.111.
.101.
.111.
.....

Nitrogen
Green Black
.....
.111.
.101.
.111.
.....

Oxygen
Blue Black
.....
.111.
.101.
.111.
.....

Carbon
Yellow Black
.....
.111.
.101.
.111.
.....

Helium
White Black
.....
.111.
.101.
.111.
.....

1Electron
LightGray
.....
.0...
.....
.....
.....

2Electron
LightGray
.....
...0.
.....
.0...
.....

3Electron
LightGray
.....
.0.0.
.....
.0...
.....

4Electron
LightGray
.....
.0.0.
.....
.0.0.
.....

BondU
Black
..0..
.....
.....
.....
.....

BondL
Black
.....
.....
0....
.....
.....

BondR
Black
.....
.....
....0
.....
.....

BondD
Black
.....
.....
.....
.....
..0..


BondUL
Black
..0..
.....
0....
.....
.....

BondUR
Black
..0..
.....
....0
.....
.....

BondUD
Black
..0..
.....
.....
.....
..0..

BondLR
Black
.....
.....
0...0
.....
.....

BondLD
Black
.....
.....
0....
.....
..0..

BondRD
Black
.....
.....
....0
.....
..0..

BondULR
Black
..0..
.....
0...0
.....
.....

BondULD
Black
..0..
.....
0....
.....
..0..

BondURD
Black
..0..
.....
....0
.....
..0..

BondLRD
Black
.....
.....
0...0
.....
..0..

BondULRD
Black
..0..
.....
0...0
.....
..0..

Temp1
Red

Temp2 (These are placeholders for various things.)
Green

Temp3
Blue

Temp4
Yellow

=======
LEGEND
=======

Atom = Hydrogen or Oxygen or Nitrogen or Carbon or Helium
Orbital = 1Electron or 2Electron or 3Electron or 4Electron
UBond = BondU or BondUL or BondUR or BondUD or BondULR or BondULD or BondURD or BondULRD
LBond = BondL or BondUL or BondLR or BondLD or BondULR or BondULD or BondLRD or BondULRD
RBond = BondR or BondUR or BondLR or BondRD or BondULR or BondURD or BondLRD or BondULRD
DBond = BondD or BondUD or BondLD or BondRD or BondULD or BondURD or BondLRD or BondULRD
1Bond = BondU or BondL or BondR or BondD
2Bond = BondUL or BondUR or BondUD or BondLR or BondLD or BondRD
3Bond = BondULR or BondULD or BondURD or BondLRD
Bond = 1Bond or 2Bond or 3Bond or BondULRD
Temps = Temp1 or Temp2 or Temp3 or Temp4
AtomStuff = Bond or Orbital

. = Background
# = Wall
H = Hydrogen and 1Electron
O = Oxygen and 2Electron
N = Nitrogen and 3Electron
C = Carbon and 4Electron
E = Helium
0 = Helium  and Player
1 = Hydrogen and 1Electron and Player
2 = Oxygen and 2Electron and Player
3 = Nitrogen and 3Electron and Player
4 = Carbon and 4Electron and Player
P = Player

=======
SOUNDS
=======

Sfx1 64050702 (Bonding sound)
EndLevel 96545300 (End level)

================
COLLISIONLAYERS
================

Background
Player
Bond
Wall, Atom
Orbital
Temps

======
RULES
======

(This is the old way I did movement, and it was super inefficient.)
([> Player Atom] -> [> Player > Atom]

Up [ Moving Atom UBond | Atom] -> [ Moving Atom UBond | Moving Atom]
+ Left [ Moving Atom LBond | Atom] -> [ Moving Atom LBond | Moving Atom]
+ Right [ Moving Atom RBond | Atom] -> [ Moving Atom RBond | Moving Atom]
+ Down [ Moving Atom DBond | Atom] -> [ Moving Atom DBond | Moving Atom]
+ [> Atom | Atom] -> [> Atom | > Atom])

[Up Player] -> [Up Player Temp1]
[Left Player] -> [Left Player Temp2]
[Right Player] -> [Right Player Temp3]
[Down Player] -> [Down Player Temp4]

Up [Temps UBond | no Temps] -> [Temps Ubond | Temps]
+ Left [Temps LBond | no Temps] -> [Temps Lbond | Temps]
+ Right [Temps RBond | no Temps] -> [Temps Rbond | Temps]
+ DOwn [Temps DBond | no Temps] -> [Temps Dbond | Temps]
+ Up [Temp1 | Atom no Temp1] -> [Temp1 | Atom Temp1]
+ Left [Temp2 | Atom no Temp2] -> [Temp2 | Atom Temp2]
+ Right [Temp3 | Atom no Temp3] -> [Temp3 | Atom Temp3]
+ Down [Temp4 | Atom no Temp4] -> [Temp4 | Atom Temp4]

[Temp1 Atom] -> [Up Atom]
[Temp2 Atom] -> [Left Atom]
[Temp3 Atom] -> [Right Atom]
[Temp4 Atom] -> [Down Atom]

[ > Atom | Wall] -> Cancel

[ > Atom AtomStuff] -> [ > Atom > AtomStuff]

Late Right [Orbital no Temps no RBond|Orbital no Temps no LBond] -> [Orbital Temp1|Orbital Temp2 Sfx1] (Bond horizontally.)
+ Late [Temp1 no Bond] -> [Temp3 BondR]
+ Late [Temp1 BondU] -> [Temp3 BondUR]
+ Late [Temp1 BondL] -> [Temp3 BondLR]
+ Late [Temp1 BondD] -> [Temp3 BondRD]
+ Late [Temp1 BondUL] -> [Temp3 BondULR]
+ Late [Temp1 BondUD] -> [Temp3 BondURD]
+ Late [Temp1 BondLD] -> [Temp3 BondLRD]
+ Late [Temp1 BondULD] -> [Temp3 BondULRD]
+ Late [Temp2 no Bond] -> [Temp3 BondL]
+ Late [Temp2 BondU] -> [Temp3 BondUL]
+ Late [Temp2 BondR] -> [Temp3 BondLR]
+ Late [Temp2 BondD] -> [Temp3 BondLD]
+ Late [Temp2 BondUR] -> [Temp3 BondULR]
+ Late [Temp2 BondUD] -> [Temp3 BondULD]
+ Late [Temp2 BondRD] -> [Temp3 BondLRD]
+ Late [Temp2 BondURD] -> [Temp3 BondULRD]
+ Late [Temp3 1Electron] -> []
+ Late [Temp3 2Electron] -> [1Electron]
+ Late [Temp3 3Electron] -> [2Electron]
+ Late [Temp3 4Electron] -> [3Electron]

Late Down [Orbital no Temps no DBond|Orbital no Temps no UBond] -> [Orbital Temp1|Orbital Temp2 Sfx1] (Bond vertically.)
+ Late [Temp1 no Bond] -> [Temp3 BondD]
+ Late [Temp1 BondU] -> [Temp3 BondUD]
+ Late [Temp1 BondL] -> [Temp3 BondLD]
+ Late [Temp1 BondR] -> [Temp3 BondRD]
+ Late [Temp1 BondUL] -> [Temp3 BondULD]
+ Late [Temp1 BondUR] -> [Temp3 BondURD]
+ Late [Temp1 BondLR] -> [Temp3 BondLRD]
+ Late [Temp1 BondULR] -> [Temp3 BondULRD]
+ Late [Temp2 no Bond] -> [Temp3 BondU]
+ Late [Temp2 BondL] -> [Temp3 BondUL]
+ Late [Temp2 BondR] -> [Temp3 BondUR]
+ Late [Temp2 BondD] -> [Temp3 BondUD]
+ Late [Temp2 BondLR] -> [Temp3 BondULR]
+ Late [Temp2 BondLD] -> [Temp3 BondULD]
+ Late [Temp2 BondRD] -> [Temp3 BondURD]
+ Late [Temp2 BondLRD] -> [Temp3 BondULRD]
+ Late [Temp3 1Electron] -> []
+ Late [Temp3 2Electron] -> [1Electron]
+ Late [Temp3 3Electron] -> [2Electron]
+ Late [Temp3 4Electron] -> [3Electron]

==============
WINCONDITIONS
==============

No Orbital

=======
LEVELS
=======

Message This is a demake of Sokobond by Alan Hazelden and Harry Lee.

Message Get the full version at www.sokobond.com!

#########
####....#
####....#
#....#H.#
#...O#..#
#.1.....#
#....####
#########

Message Water atoms are icosahedral.

######
#H..O#
#....#
#1.###
######

Message The blue tint seen in large bodies of water comes from the pigment in the oxygen atoms.

########
###...##
#2.H.H.#
###...##
########

Message The Earth's surface and the human body are both 70% water. That CAN'T be a coincidence, right?

#######
#..O..#
#..#..#
#.1#H.#
#..#..#
#..O..#
#######

Message "One hydrogen per oxide" is defined as the speed at which atoms move.

#######
#.H.3.#
#.....#
#.H.H.#
#######

Message Ammonia is an inflammatory condition of the lungs.

#######
#.....#
#.....#
#H#H#H#
#.....#
#..3..#
#######

Message Hang on, I was thinking of "pneumonia".

#########
####4####
###...###
##.H.H.##
#.......#
##.H.H.##
###...###
####.####
#########

Message 90% of all the methane in the world is produced in Arizona.

#########
##H...H##
#...4...#
##H...H##
#########

Message Methane contains the name "Ethan". Do you think it was discovered by a guy named Ethan?

########
#......#
#...H..#
#O.H..3#
#...H..#
#......#
########

Message Hydroxylamine contains almost every vowel of the alphabet almost exactly once.

#########
#.......#
#..H.H..#
#.2...C.#
#..H.H..#
#.......#
#########

Message Methanol occurs naturally in peppermint oils.

#########
#.......#
#.1...E.#
###.H.###
###...###
###.O.###
#########

Message Helium is named after the Greek god Helios, who first discovered its existence.

#########
#.......#
#.E.E.E.#
###H.1###
###.H.###
###...###
###.N.###
#########

Message Inhaling helium contracts the ear drum, causing pitches to sound higher. Your voice is perceived normally by everyone else.

#######
###..##
##..H.#
#..N..#
#.H..H#
##..0.#
#######

Message The first helium balloons were used at the wedding of Queen Victoria and Prince Albert, popularizing the term "noble gas".

#######
#.....#
#..N..#
#..0..#
#H.H.H#
#.#.#.#
#.....#
#######

Message Helium does not form chemical bonds with other elements, but it's the bonds of friendship that really matter.

########
#......#
#...H#N#
#......#
##H#H.0#
##.....#
####...#
########

Message Scientists have been searching for centuries to discover the missing elements between hydrogen and helium on the periodic table.

Message Thanks for playing! Go get the full version, which has lots of cool stuff that I couldn't write in PuzzleScript: www.sokobond.com

USER PROMPT:
Output the code for a complete PuzzleScript game. Return your code in full, inside a ```plaintext code block.