SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title Smother
author Team Borse

background_color #9CBD0F
text_color #0F380F

run_rules_on_level_start

========
OBJECTS
========

Background
#9CBD0F

LaserGV
#9CBD0F #8CAD0F #306230 #0F380F
22222
.222.
..3..
..3..
.....

LaserGH
#9CBD0F #8CAD0F #306230 #0F380F
2....
22...
2233.
22...
2....

LaserV
#9CBD0F #8CAD0F #306230 #0F380F
.....
..3..
..3..
..3..
.....

LaserH
#9CBD0F #8CAD0F #306230 #0F380F
.....
.....
.333.
.....
.....

LaserB
#9CBD0F #8CAD0F #306230 #0F380F
.....
..3..
.333.
..3..
.....

Exit
#9CBD0F #8CAD0F #306230 #0F380F
2...2
2...2
2...2
2...2
2.2.2

Wall
#9CBD0F #8CAD0F #306230 #0F380F
33333
31223
32123
32223
33333

Heart
#9CBD0F #8CAD0F #306230 #0F380F
.2.2.
22222
.222.
..2..
.....


PlayerFull
#9CBD0F #8CAD0F #306230 #0F380F
.222.
.323.
.222.
.222.
.2.2.

Protector
#9CBD0F #8CAD0F #306230 #0F380F
.333.
.232.
.333.
.333.
.3.3.

ProtectorSolo
#9CBD0F #8CAD0F #306230 #0F380F
.333.
.232.
.333.
.333.
.3.3.

victimSolo
#9CBD0F #8CAD0F #306230 #0F380F
.222.
.323.
.222.
.222.
.2.2.


PlayerDead
#9CBD0F #8CAD0F #306230 #0F380F
.222.
22222
2.2.2
.222.
.222.

pushBlock
#9CBD0F #8CAD0F #306230 #0F380F
33333
31113
31113
31113
33333

=======
LEGEND
=======

Player = PlayerFull or Protector or ProtectorSolo or victimSolo
PlayerWin = PlayerFull or PlayerDead or Protector or victimSolo
Laser = LaserV or LaserH or LaserB
LaserBlock = Wall or Player or PlayerDead or Exit
. = Background
# = Wall
g = LaserGV
h = LaserGH
P = PlayerFull
n = Protector
q = ProtectorSolo
k = victimSolo
O = Exit
l = Heart
b = pushBlock

=======
SOUNDS
=======

startgame 83279102 (sound at start of game)
CloseMessage 123434 (close message noise)
ShowMessage 123434 (show message noise)
sfx0 8545502 (death)
sfx1 26 (heart)


================
COLLISIONLAYERS
================
Background
Laser
Exit
ProtectorSolo, Heart, Wall
Player, Wall, PlayerDead, LaserGV, LaserGH, Protector, victimSolo, pushBlock

======
RULES
======

(clear lasers)
late [Laser] -> []

(zip the lasers out of the guns and through the levels)
late down [LaserGV | no Laser] -> [LaserGV | LaserV]
late right [LaserGH | no Laser] -> [LaserGH | LaserH]

late down [LaserV no LaserBlock | ] -> [LaserV | LaserV]
late right [LaserH no LaserBlock | ] -> [LaserH | LaserH]

(generate overlapping lasers)
late down [LaserV no LaserBlock | LaserH] -> [LaserV | LaserB]
late down [LaserGV no LaserBlock | LaserH] -> [LaserGV | LaserB]
late right [LaserH no LaserBlock | LaserV] -> [LaserH | LaserB]
late right [LaserGH no LaserBlock | LaserV] -> [LaserGH | LaserB]

(deal with overlapping lasers)
late right [LaserB no LaserBlock | no Laser] -> [LaserB | LaserH]
late right [LaserH no LaserBlock | no Laser] -> [LaserH | LaserH]
late down [LaserB no LaserBlock | no Laser] -> [LaserB | LaserV]
late down [LaserV no LaserBlock | no Laser] -> [LaserV | LaserV]

(solo Victim can push a block)
 [ > victimSolo | pushBlock ] -> [ > victimSolo| > pushBlock]

(pushing block to switch breaks laser)
[pushBlock  laser] -> [ wall]


(if a player is over a laser, then cause them harm)
late [ PlayerFull Laser ] -> [PlayerDead Laser] sfx0

(victim solo dies in the laser)
late [ victimSolo Laser ] -> [PlayerDead Laser] sfx0

(protector levels)
[ > ProtectorSolo | ... | Heart ] -> [ | ... | ProtectorSolo ] sfx1
[ > ProtectorSolo ] -> [ ProtectorSolo]


==============
WINCONDITIONS
==============
All PlayerWin on Exit
No Heart

=======
LEVELS
=======

message then...

message I PROTECT YOU

###g###g###
#.........#
#n.......o#
#........o#
#p...#....#
#.........#
###########

message YOU SMOTHER ME

###########
#n........#
#......#..#
h..#h..#..#
#p.....#..#
#......#oo#
###########

message I DEFEND YOU

#####g#####
#.P.....n.#
#.........#
#..#...#..#
#.........#
#......oo.#
###########

message YOU SPEAK FOR ME

#####g#####
#n.......o#
#....#h...#
h....g....#
#.....#..o#
#p......#.#
###########

message WE ARE CARE FREE

#P####g##g###
#.#..#.#....#
#..........o#
h...........#
#.#h..#..#.o#
#.....#....n#
#############

message I CARRY OUR BURDENS

message before...

message I AM ALWAYS SEARCHING

###########
#l...l....#
#....l....#
#.........#
#q..l.....#
#l..l.....#
###########


message I NEVER SEE A FUTURE

###########
#.........#
#..l..q.l.#
#.l....l..#
#.ll...l.l#
#l.l...l..#
###########

message I NEVER FEEL COMFORTABLE

###########
#....l....#
#lq..l..l.#
#.l..l....#
#.l.....l.#
#....l....#
###########


message NO ONE GETS ME

###########l##l#####
#l.l...............#
#.....ll...........#
#l..l..............#
#..q..l.......l....#
#..ll..l......l....#
###########........#
###########l....l..#
###########........#
###########.l.l.l..#
####################

message I NEED CONTACT

###################
#.l...l############
#......############
#l.l..l##########l#
#l.......l.....l#.#
#....l............#
#ll..q.........l.l#
#..l.....l.......l#
#.l..l............#
###################

message I NEED VALIDATION

message before...

message I AM INDEPENDENT

###########
#.k.g.....#
#.......b.#
#.b...###.#
#.#.......#
#...h.....#
#...#...O.#
###########


message I AM STRONG


###g##g##
#.......#
#kb.b...#
#.......#
h.....#.#
#..#b#h.#
#....#..#
h....#o.#
#.......#
#########

message I NEED NO ONE

###g#######
#.o.......#
#...#.##..#
#.#..bkb.##
h...#.#.b.#
#.......#.#
#.....#...#
###########

message I ENJOY MY COMPANY

#####g#####g#####g######
#.k.....b...........o..#
#..b....#.#.......##...#
#.........b...#b..#g...#
h..........#......#....#
##.###.#######.####.#.##
h..............#h......#
####.#####.###.##.#.#.##
###.b.#....#........#..#
###.#....#b#.........b.#
##.........#.......#...#
########################

message I AM FEARLESS

##g##g#g####
#o..#.#....#
#...#.#.#..#
#..........#
#.....#..b.#
#........b.#
#..#.##.#b.#
#..#......k#
############

message I AM ALONE


message now...

###########
#...o.o...#
#.........#
#.........#
#.........#
#...n.p...#
###########

message WE DON'T SEE THINGS AS THEY ARE, WE SEE THEM AS WE ARE
message Credits
message Concept/Design/Programming/Graphics - Steven McStravog
message Design/Programming/Graphics - Darragh McLernon
message Porting/Design/Programming - Rachel Gawley
message Artwork - Ricky Woodside


title neko puzzle
author lexaloffle
homepage www.lexaloffle.com

(

An inadequate but illustrative demake of one of my favourite puzzle games ever, by the extremely amicable Lexaloffle, and with his permission.

www.lexaloffle.com/neko.htm

The game proper has a lot of magic and love in it.

)

========
OBJECTS
========

Background .
darkblue

Fruit F
red green lightred
..1..
.000.
00020
00000
.000.

Player P
gray white
.1.1.
10101
11111
.111.
00.00

=======
LEGEND
=======

=========
SOUNDS
=========

endlevel 97301508
startgame 97301508
endgame 97301508

================
COLLISIONLAYERS
================

Background
Player, Fruit

======
RULES
======

[ > Player | ... | Fruit ] -> [ | ... | Player ] 

(if you're not sure what this line does, try removing it, and see what happens)
[ > Player ] -> [ Player ] 

==============
WINCONDITIONS
==============

no Fruit

=======
LEVELS
=======

message level 1 of 10

......F.
........
F......F
......FF
........
........
P.......

message level 2 of 10

........
....P...
...F.F..
........
........
..F.FF..
........

message level 3 of 10

........
.FF....F
........
........
F...P...
........
........
F.F.....

message level 4 of 10

....F..F
........
.......F
F.......
P.......
........
F...F...

message level 5 of 10

........
....F.P.
F.F.....
........
F...F...
..F.F...
........

message level 6 of 10

F.....F.
........
........
FF..F...
F.....F.
........
.P......

message level 7 of 10

...FP..F
F.F.....
F......F
........
........
..F.....
........

message level 8 of 10

..F..P..
........
........
.FFF....
F..F..F.
........
........

message level 9 of 10

....F..F
........
.F..F...
........
.F.P..F.
.F......
........

message level 10 of 10

....F.F.
........
.F..F..F
...F..F.
.......F
...FP...
........

message congratulations!


title Octat
author increpare
homepage www.increpare.com

noundo
require_player_movement

(
    
A port of a flash game I made.  The original's here:

http://ded.increpare.com/~locus/octat/

)

========
OBJECTS
========

Background
purple
.....
.000.
.000.
.000.
.....

Activetile
pink red
00000
0...0
0...0
0...0
00000

Target
red
0...0
.0.0.
..0..
.0.0.
0...0

Player
white
.....
.000.
.000.
.000.
.....

Void 0
black

Temp
black

=======
LEGEND
=======

. = Background
P = Player and Activetile
a = Activetile
x = Target
y = target and Activetile

=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Background
Void
Activetile
Player
Temp
Target

======
RULES
======

[ > Player | no Activetile ] -> cancel

late [ Activetile | Player ] -> [ Temp | Player ] 
late [ no Temp | Player ] -> [ Activetile | Player ] 
late [ Temp ] -> [ ] 

late [ Activetile Void ] -> [ Void ] 

==============
WINCONDITIONS
==============

some Player on target

=======
LEVELS
=======

message level 1 of 8

..........
..........
..........
..........
..........
.x...ap...
..........
..........
..........
..........

message level 2 of 8

...........
...........
...........
...........
...........
....xpa....
...........
...........
...........
...........

message level 3 of 8

...........
...........
...........
...........
..a........
.axaaap....
..a........
...........
...........
...........

message level 4 of 8

...........
...........
...a.......
..a.a......
.a.a.a.....
a.aya.a.ap.
.a.a.a.....
..a.a......
...a.......
...........

message level 5 of 8

...........
...........
...........
...........
aaaaaaaaaaa
...y..ap...
aaaaaaaaaaa
...........
...........
...........

message level 6 of 8

000000....0
000000..0.0
000000..0..
000000000..
....A...0.0
.AY.A...AAP
....A...0.0
000000000..
000000..0..
000000..0.0
000000....0

message level 7 of 8

..AAA...000
.AY.A...AP.
..AAA...000

message level 8 of 8

0AAAA..00
0AAAA..00
YAAAA..AP
0AAAA..00
0AAAA..00

message the end


title cute train
author Mark Wonnacott
homepage http://twitter.com/ragzouken

flickscreen 16x10
color_palette pastel
youtube pTgEXwzk7XU

noundo
norestart

========
OBJECTS
========

Background
lightgreen green
01000
00110
10010
10100
01001

Water
blue white lightblue
20010
02002
12020
00220
02002

Water2
blue white lightblue
02000
20210
00202
01022
20200

Gerald
blue black
.000.
01010
10001
01110
.000.

Mountain
darkbrown darkbrown
..0..
.10..
.000.
0000.
11001

Forest
darkgreen brown
.000.
00000
00000
00100
.111.

Beach
yellow white
00010
01000
00001
00100
10000

Player
yellow black
.000.
01010
10001
01110
.000.

GhostVTrack
yellow
GhostHTrack
yellow
GhostURTrack
yellow
GhostULTrack
yellow
GhostDLTrack
yellow
GhostDRTrack
yellow

VTrack
grey darkbrown
0...0
0...0
01110
0...0
0...0

HTrack
grey darkbrown
00000
..1..
..1..
..1..
00000

Structs
black darkbrown
11111
10101
11111
10101
11111

URTrack
grey darkbrown
...00
..0..
.01..
0..1.
0...1

ULTrack
grey darkbrown
00...
..0..
..10.
.1..0
1...0

DLTrack
grey darkbrown
1...0
.1..0
..10.
..0..
00...

DRTrack
grey darkbrown
0...1
0..1.
.01..
..0..
...00

UTrain
red black darkred
.000.
01110
00000
00100
00000

RTrain
red black darkred
0000.
00010
01010
00010
0000.

LTrain
red black darkred
.0000
01000
01010
01000
.0000

DTrain
red black darkred
00000
00100
00000
01110
.000.

Cart
red black darkred
22222
21112
21112
21112
22222

Smoke1
white
..0..
.0.0.
0.0.0
.0.0.
..0..

Smoke2
white
.....
.0.0.
..0..
.0.0.
.....

Smoke3
white
.....
..0..
.0.0.
..0..
.....

Switch
red black
...0.
..1..
.1...
.111.
.....

=======
LEGEND
=======

. = Background
^ = Mountain
% = Forest
~ = Water
, = Beach

| = VTrack
- = HTrack
r = URTrack
¬ = ULTrack
l = DRTrack
j = DLTrack
X = VTrack and Structs
Z = HTrack and Structs
Q = ULTrack and Structs

P = Player

T = VTrack and UTrain
C = VTrack and Cart

[ = HTrack and RTrain
] = HTrack and Cart

/ = Switch

1 = GhostHTrack and URTrack
! = HTrack and GhostURTrack
2 = HTrack and GhostDRTrack
3 = HTrack and GhostULTrack
4 = GhostHTrack and DLTrack
5 = GhostVTrack and URTrack
6 = GhostVTrack and DRTrack
7 = GhostVTrack and ULTrack
8 = GhostVTrack and DLTrack

_ = Beach and HTrack

WaterR = Water or Water2

Ghost = GhostHTrack or GhostVTrack or GhostULTrack or GhostURTrack or GhostDLTrack or GhostDRTrack
Track = HTrack or VTrack or ULTrack or URTrack or DLTrack or DRTrack

Train = UTrain or DTrain or LTrain or RTrain
Vehicle = Train or Cart
Mover = Vehicle or Player

G = Gerald and Structs

=======
SOUNDS
=======

Train move 91301709
SFX1 53409305
SFX2 38557307
SFX3 99431502

================
COLLISIONLAYERS
================

Background
Beach GhostVTrack GhostHTrack GhostURTrack GhostULTrack GhostDLTrack GhostDRTrack
Structs
VTrack HTrack URTrack ULTrack DLTrack DRTrack Switch
Water Water2 Mountain Forest Player UTrain RTrain LTrain DTrain Cart Gerald
Smoke1 Smoke2 Smoke3

======
RULES
======

(animate water)
[        WaterR ] -> [ action WaterR ]
[ action WaterR ] -> [ random WaterR ]

[ action Player no Structs | Cart ] [ Train ] -> [ Player | Cart ] [ action Train ]

(move the train if we're on the impassable structs)
[ Structs Mover ] [ Train ] -> [ Structs Mover ] [ action Train ]

[ action Train ] -> [ action Train Smoke1 ]

[ action UTrain ] -> [    up UTrain ]
[ action DTrain ] -> [  down DTrain ]
[ action RTrain ] -> [ right RTrain ]
[ action LTrain ] -> [  left LTrain ]

late [ Mover Structs ] -> [ Mover Structs ] again (repeat if the train is still on structs)

(cart follows train, player follows cart)
[ Cart   | moving Train ] -> [ > Cart   | moving Train ]
[ Player | moving Cart  ] -> [ > Player | moving Cart  ]

[ > Player | Structs no Gerald no Cart ] -> [ Player | Structs ] message It's dangerous to walk along railway bridges!

(endgame message)
[ >      Player | Gerald ] -> [ Player | Gerald ] message you made it! thanks for playing.
[ action Player | Gerald ] -> [ Player | Gerald ] message you made it! thanks for playing.

(train turns when entering corners)
up    [ > UTrain | URTrack ] -> [ > RTrain | URTrack ]
up    [ > UTrain | ULTrack ] -> [ > LTrain | ULTrack ]

right [ > RTrain | ULTrack ] -> [ > DTrain | ULTrack ]
right [ > RTrain | DLTrack ] -> [ > UTrain | DLTrack ]

down  [ > DTrain | DRTrack ] -> [ > RTrain | DRTrack ]
down  [ > DTrain | DLTrack ] -> [ > LTrain | DLTrack ]

left  [ > LTrain | URTrack ] -> [ > DTrain | URTrack ]
left  [ > LTrain | DRTrack ] -> [ > UTrain | DRTrack ]

(animate smoke)
[ Smoke3 ] -> [ ]
[ Smoke2 ] -> [ Smoke3 ]
[ Smoke1 ] -> [ Smoke2 ]

[ action Player Switch ] -> [ Player action Switch ]

(switch all uncovered tracks at once)
[ action Switch ] [ Ghost Vehicle ] -> [ action Switch ] [ Ghost Vehicle ] SFX3 (play blocking sound if switch tracks are covered while switching)
[ action Switch ] [ Ghost no Vehicle ] -> [ action Switch ] [ action Ghost ] SFX2

[ action GhostHTrack URTrack no Vehicle ] -> [ HTrack GhostURTrack ]
[ HTrack action GhostURTrack no Vehicle ] -> [ GhostHTrack URTrack ]
[ action GhostHTrack DRTrack no Vehicle ] -> [ HTrack GhostDRTrack ]
[ HTrack action GhostDRTrack no Vehicle ] -> [ GhostHTrack DRTrack ]
[ action GhostHTrack ULTrack no Vehicle ] -> [ HTrack GhostULTrack ]
[ HTrack action GhostULTrack no Vehicle ] -> [ GhostHTrack ULTrack ]
[ action GhostHTrack DLTrack no Vehicle ] -> [ HTrack GhostDLTrack ]
[ HTrack action GhostDLTrack no Vehicle ] -> [ GhostHTrack DLTrack ]

[ action GhostVTrack URTrack no Vehicle ] -> [ VTrack GhostURTrack ]
[ VTrack action GhostURTrack no Vehicle ] -> [ GhostVTrack URTrack ]
[ action GhostVTrack DRTrack no Vehicle ] -> [ VTrack GhostDRTrack ]
[ VTrack action GhostDRTrack no Vehicle ] -> [ GhostVTrack DRTrack ]
[ action GhostVTrack ULTrack no Vehicle ] -> [ VTrack GhostULTrack ]
[ VTrack action GhostULTrack no Vehicle ] -> [ GhostVTrack ULTrack ]
[ action GhostVTrack DLTrack no Vehicle ] -> [ VTrack GhostDLTrack ]
[ VTrack action GhostDLTrack no Vehicle ] -> [ GhostVTrack DLTrack ]

==============
WINCONDITIONS
==============

=======
LEVELS
=======

(feel free to try making more puzzles!

editing tip: copy out a few rows of screens to edit in isolation

* use bridges (structs) to force the train over junctions before there's a chance to switch them
* use bridges to make switches unreachable until junctions have been crossed
* use the train itself and narrow passages to make switches unreachable until junctions have been crossed)

message welcome to the breezy isles!

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~,,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~,,,,,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~,,..,,,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~,,,p.,,,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~,,....,,,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~,,,.....,,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~,,,......,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~,,.......,,,,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~,,,........,,,,,,,~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~,,,..........,,,,,,,,~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~,,...............,,,,,,~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~,~~,...%.........%....,,,,~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~,,,,,~..%.................,,,,~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~,,,,,,,,..~~%%%%%.r-------¬.....,,,,~~~~~~~~~~~~~~~~~~~~
,,,,,,,,,,,,,....%%~%%%%%%t.......l-¬.....,,,~~~~~~~~~~~~~~~~~~~
,,,,,,..........%%%~~%%%%%c%%.......l-¬....,,~~~~~~~~~~~~~~~~~~~
........%%%..%%%%%%%~%%%%%|%%%%%....|.l--¬..,,~~~~~~~~~~~~~~~~~~
.%%%%.%%%%%%%%%%%%%%~%%%%%|%%%%%%%..|...%l¬.,,~~~~~~~~~~~~~~~~~~
%%%%%%%%%%%%%%%%%%%%~%%%%%|%%%%%%%%.l¬..%%|.,,~~~~~~~~~~~~~~~~~~
%%%%%%%%%%%%%%%%%%%%~%%%%%|%%%%%%%%..|.%%%|.,,~~~~~~~~~~~~~~~~~~
%%%%r---------------z-¬%%%|%%%%%%....|%%%%|.,,~~~~~~~~~~~~~~~~~~
%%.rj........%%%%%%%~%l¬%.|..........|%%%rj,,,~~~~~~~~~~~~~~~~~~
%%rj.~~~~G~~~~%%%%%%~^%l--¬..........l---j.,,,~~~~~~~~~~~~~~~~~~
%.|.~~~~~x~~~~~%%%%^~^....|.....^^^........,,~~~~~~~~~~~~~~~~~~~
%.|.~~~~~~~~~~~%%^^^~^....l¬...^^^^^^......,,~~~~~~~~~~~~~~~~~~~
%.|.~~~~~~~~~~%%%^^^~^^^..^l¬^^^^^^^^^^^^^.,^^~~~~~~~~~~~~~~~~~~
%%l¬.~~~~~~~%%%^^^^^~^^^^^^^l¬^^^^^^^^^^^^^^^^~~~~~~~~~~~~~~~~~~
%%.|..~~~~%%%%^^^^^~~^^^^^^^^x~^^^^^^^^^^^^^^^^~~~~~~~~~~~~~~~~~
%%%|.%%%%%%%%^^^^^^~^^^^^^^^~x~^^^^^^^^^^^^^^^^~~~~~~~~~~~~~~~~~
%%%x%%%%%%%%%^^^^^~~^^^^^^^^~x~^^^^^^^^^^^^^^^^^^^^^^~~~~~~~~~~~
%%rj%%%%%%%%%^^^^^^~^^^^^^^~~x~%...%%^^%%^^^^^^^^^^^^^~~^~~~~~~~
%%5z---¬%%%%%^^^^^^~^^^^^^^~^l¬..%r------¬%%^^^^^^^^^^^~~~~~~~~~
%%|%%%%|%%%%^^^^^^^~~^^^^^~~^^l¬..|.%%%%%|%%%.^^^^^^^^^^^~~~~~~~
%r4z---¬%%%%^^^^^^^^~~^^^^~^^^.l--l---3--j%%..^^^^^^^^^^^^~~~~~~
%x./%r-¬%%%%^^^^^^^^^~~^^~~^^^........|%%%%%..^^^^^^^^^^^^^~~~~~
%l--zj¬j%%%%^^^^^^^^^^~~~~^^^^%%..../.|.%%...^^^^^^^^^^^^^~~~~~~
%%%%%%5z¬z¬%^^^^^^^^^^^^~~^^^^^%%%....|......^^^^^^^^^^^^^^~~~~~
%%r--¬x%lzj%^^^^^^^^^^^^~^^^^^^%%%%..~x~~~~~~~^^^^^^^^^^^^^^~^~~
%%|%%|x%%%%%%^^^^^^^^^^^~^^^^^^%%%%%~~x~~~~~~~~^^^^^^^^^^^^^~~~~
%%|%%|x%%%%%%%%^^^^^^^^^~^^^^^^%%%%%~~x~~~~~~~~^^^^^^^^^^^^^~~~~
%%r--47---¬¬---zzzzz1zzzzz¬^^^^^%%%..~x~~~~....%^^^^^^^^^^^~~~~~
%%|%%%x%.%lj%%^^^^^^x^^^~^lz¬^^^^%%...|..%...%.%%%^^^^^^,,~~~~~~
%%|%/.l¬.%%%^^^^^^^^x^^^~~^^lz¬^^%%...|.....%...........,,~~~^^~
%%|%%%lj%%%^^^^^^^^^x^^^^~^~^^x^^^%%.rl-----------r-----__zzz¬^~
%%l¬%%%%%^^^^^^^^^^^x^^^^~~^^^x^^^%%.|........%...|.....,,~~^x^~
%%lj%%%%%^^^^^^^^^^r-¬^^^~~~~^x^^^^^%|...%^^^.....l¬../.,,~~~x~~
%%%%%%%%^^^^^^^^^^^|l¬%^^~~~~~x~^^^^^|.^^^^^^^^^^..|....^^^~~x~~
%%%%%%%^^^^^^^^^^%rj%|%^^^~~^^x^^^^^rj%%^^^^^^^%%.%l¬.^^^^^^~x~~
%%%%%%^^^^^^^^^^^%|%%|%^^~^^~^x^^^%.|...%%..%.......|....,,~~x~~
%%%%^^^^^^^^^^^^^%|/.|%^~^^^^^x^^^%.|.%.............|...,,,~^x^~
%%^^^^^^^^^^^^^^^%l--8^^^^^^^rj%^^..|..r----!-------2--1___zzj^~
^^^^^^^^^^^^^^^^^%|%^x^^^^^^^x^^^^%.|.%|%%..|....%..|%.|,,,~~^~~
^^^^^^^^^^^^^^^^^^|.rlzz--3z-4¬^^^%.|.%l¬...|.......|..l¬,,~~~~~
^^^^^^^^^^^^^^^^^^x^|^^^..|^..|%^^..l¬%%l-----------j...|,,,~~~~
^^^^^^^^^^^^^^^^^^l-|---¬/|^.%|%^^%..|.%.%%.l¬%....%....|,,,,~~~
^^^^^^^^^^^^^^^^^^^%|.%%l-j---j%^^^%.l¬.....%l¬.%.....%.|,,,,~~~
^^^^^^^^^^^^^^^^^^^%|%%%%%%%%%%^^^^%%.|.%%%...l--¬......l¬,,,~~~
^^^^^^^^^^^^^^^^^^^^x^^^^%%%^^^^^^^%%.|..%..%...%l-¬%....|,,,~~~
^^^^^^^^^^^^^^^^^^^^x^^^^^^^^^^^^^^%%%|%%%%..%%....l¬....|,,,,~~
^^^^^^^^^^^^^^^^^^^^x^^^^^^^^^^^^^^^%%|%^^%%......%.l¬...|.,,,~~
^^^^^^^^^^^^^^^^^^^^x^%%%%%^^^^^^^^^^^|%%^^^^^^.%%...|...|^^^,~~
^^^^^^^^^^^^^^^^^^^%|%%....%%^^^^^^^^%|%%%^^^^^^^^^%.|.^^|^^,,~~
^^^^^^^^^^^^^^^^^^^%|%..%^..%%^^^^^%%rj%%^^^^^^^%....|...|^,,,~~
^^^^^^^^^^^^^^^^^^^%|./.^%%..%%^^^%%.|.%..%......../.6---¬^,,,~~
^^^^^^^^^^^^^^^^^^^%|..%^%....%^^^%..|%..%...%......rj...|.,,,~~
^^^^^^^^^^^^^^^^^^^%l--!z1--¬--zzz----------------!-j..%.|,,,,~~
^^^^^^^^^^^^^^^^^^^^%%%|^|..|.%^^^%..|.%.%..%..%..|...%..|,,,,~~
^^^^^^^^^^^^^^^^^^^^^^^x^|%%|%^^^^%%.|%%%.........|......|,,,,~~
^^^^^^^^^^^^^^^^^^^^^^^x^x^^x^^^^^^%%|%^^^^^^.....|...^^rj^,,~~~
^^^^^^^^^^^^^^^^^^^^^^^x^x^^x^^^^^^^^|%.^^^^^^^^%.|.^^^^|^^^^~~~
^^^^^^^^^^^^^^^^^^^^^^^x^x^^x^^^^^^^%|^^^^^^^^.%..|.....|,,^^~~~
^^^^^^^^^^^^^^^^^^^^^^^x^x^^l¬^^^^^%%|%%^^^.%.....|...%.|,,,~~~~
^^^^^^^^^^^^^^^^^^^^^^^x^lzzjx^^^^%%.|..%%........|.%...|,,,~~~~
^^^^^^^^^^^^^^^^^^^^^^^l¬^^^^x^^^^%%.l¬%......%..rj..%..|,,,~~~~
^^^^^^^^^^^^^^^^^^^^^^^^x^^^^x^^^^%%%.l¬.%......rj.....rj,,,~~~~
^^^^^^^^^^^^^^^^^^^^^^^^x^^^^x^^^^^%%%%l--------j------j,,,,~~~~
^^^^^^^^^^^^^^^^^^^^^^^^lzzzzj^^^^^^^%%%%%.%%%%...%%%%^^^,,,~~~~
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^%%%%%^^^^^^^^^^^^^^^^^~~
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^~~



title Bouncers
author Tyler Glaiel
homepage twitter.com/tylerglaiel

========
OBJECTS
========

Bg
#111111 #222222
00010
11111
01000
01000
11111

Selection
white gray
..1..
..1..
11011
..1..
..1..

Wall
DarkBlue DarkGreen #333333
20002
22022
22122
21112
11111

Hole
DarkGray Gray Black
.111.
12221
10001
10001
.111.


Arrow
Red Red
..1..
..11.
11111
..11.
..1..


HudDivider
white black
00000
11111
11111
11111
11111

IconGO
green white
10111
10011
10001
10011
10111

Going
green white
10111
10011
10001
10011
10111

LeftBouncer
White White Blue
.221.
2.1.2
21..2
2.1.2
.221.

RightBouncer
White White Blue
.122.
2.1.2
2..12
2.1.2
.122.

UpBouncer
White White Blue
.222.
2.1.2
21.12
1...1
.222.

DownBouncer
White White Blue
.222.
1...1
21.12
2.1.2
.222.

HudBG
black

Empty
black

PlayEmpty
black
.....
.....
.....
.....
.....


Ball
#003300 Green
.000.
01110
01110
01110
.000.

WinBall
LightBlue LightGreen
.000.
01110
01110
01110
.000.



BallUp
#003300 Green Black
.000.
01210
01110
01110
.000.

BallDown
#003300 Green Black
.000.
01110
01110
01210
.000.

BallLeft
#003300 Green Black
.000.
01110
02110
01110
.000.

BallRight
#003300 Green Black
.000.
01110
01120
01110
.000.




=======
LEGEND
=======
Background = Bg or HudBG
. = bg and PlayEmpty
S = Selection and PlayEmpty
- = HudDivider and Wall
H = HudBG and PlayEmpty
A = Arrow and HudBG
G = IconGO
W = Wall
O = Hole

L = LeftBouncer and HudBG
R = RightBouncer and HudBG
U = UpBouncer and HudBG
D = DownBouncer and HudBG

B = BallLeft
N = BallRight
M = BallUp
K = BallDown

Player = Selection


PlacedLeftbouncer = LeftBouncer and Selection
PlacedRightbouncer = RightBouncer and Selection
PlacedUpbouncer = UpBouncer and Selection
PlacedDownbouncer = DownBouncer and Selection

Bouncer = LeftBouncer or RightBouncer or UpBouncer or DownBouncer

emptyPlayer = Selection and PlayEmpty


ballSwitchLeft = Ball and LeftBouncer
ballSwitchRight = Ball and RightBouncer
ballSwitchUp = Ball and UpBouncer
ballSwitchDown = Ball and DownBouncer

ballSwitchedLeft = BallLeft and LeftBouncer
ballSwitchedRight = BallRight and RightBouncer
ballSwitchedUp = BallUp and UpBouncer
ballSwitchedDown = BallDown and DownBouncer

gameGo = Selection and IconGO
gameGoing =  Going



WinL = BallLeft and Hole
WinR = BallRight and Hole
WinU = BallUp and Hole
WinD = BallDown and Hole

Win = WinBall

MovingBall = Ball or BallLeft or BallRight or BallUp or BallDown

=======
SOUNDS
=======
SFX0 41242705 (place bouncer)
SFX1 73889302 (press go)
MovingBall Move 68254307 (ball move)
SFX2 68257500 (pocket ball)
SFX3 82752503 (ball switch direction)

================
COLLISIONLAYERS
================

Background

Arrow, Empty, LeftBouncer, RightBouncer, UpBouncer, DownBouncer, PlayEmpty, IconGO, Going, Hole
WinBall
Wall, Ball, BallUp, BallDown, BallLeft, BallRight
Selection, HudDivider


======
RULES
======

(action button: take icon to the right of the arrow, place under selector)
[action selection PlayEmpty][Arrow|LeftBouncer] -> [PlacedLeftbouncer][Arrow|Empty] SFX0
[action selection PlayEmpty][Arrow|RightBouncer] -> [PlacedRightbouncer][Arrow|Empty] SFX0
[action selection PlayEmpty][Arrow|UpBouncer] -> [PlacedUpbouncer][Arrow|Empty] SFX0
[action selection PlayEmpty][Arrow|DownBouncer] -> [PlacedDownbouncer][Arrow|Empty] SFX0

(win conditions)
[WinL] -> [Win] again SFX2
[WinR] -> [Win] again SFX2
[WinU] -> [Win] again SFX2
[WinD] -> [Win] again SFX2

(slide unused icons over to the left if there's space between them and the arrow)
right [Empty | Bouncer] -> [Bouncer | Empty]

(selector on top of go button -> signals game to run)
[action selection iconGO] -> [gameGoing] again SFX1

(make genericball match the direction of the bouncer)
[ballSwitchLeft]->[ballSwitchedLeft] again
[ballSwitchRight]->[ballSwitchedRight] again
[ballSwitchUp]->[ballSwitchedUp] again
[ballSwitchDown]->[ballSwitchedDown] again

(if ball is going to move on top of a bouncer, switch it to generic ball first)
right [Going][Bouncer | BallLeft] -> [Going][Bouncer | Left Ball] again SFX3
left  [Going][Bouncer | BallRight] -> [Going][Bouncer | Right Ball] again SFX3
up    [Going][Bouncer | BallDown] -> [Going][Bouncer | Down Ball] again SFX3
down  [Going][Bouncer | BallUp] -> [Going][Bouncer | Up Ball] again SFX3



(move the ball if Going)
[Going][BallLeft] -> [Going][LEFT BallLeft] again
[Going][BallRight] -> [Going][RIGHT BallRight] again
[Going][BallUp] -> [Going][UP BallUp] again
[Going][BallDown] -> [Going][DOWN BallDown] again



[Going] -> [Going] again

==============
WINCONDITIONS
==============

no MovingBall

=======
LEVELS
=======

message place bouncers, hit play

......w.....
..o...w.....
......w.....
......w.....
......w..b..
......s.....
............
g-----------
-hhadluhhhhh

message Level 2

............
......o.....
..s.........
......w.....
...o.....b..
......m.....
............
g-----------
-hhalurhhhhh

message Level 3

............
............
..o.wwww....
..s.n..b....
....wwww.o..
............
............
g-----------
-hhaudrlhhhh

message Level 4

............
............
..o......o..
............
............
.s..bbbb....
............
............
..o......o..
............
g-----------
-hhalrudu...

message Level 5

.....n......w...
..wwww.k....o.w.
......b..m......
...o............
.......w........
.....b.w........
......wwo.......
w.ww.www........
.o..............
....s...........
..........wwwo..
g---------------
-hhaddddddrrr...

message I didn't feel like making more levels

USER PROMPT:
Output the code for a complete PuzzleScript game. Return your code in full, inside a ```plaintext code block.