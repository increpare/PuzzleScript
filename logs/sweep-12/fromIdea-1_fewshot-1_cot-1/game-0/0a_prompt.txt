SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title Notsnake
author Terry Cavanagh
homepage www.distractionware.com

color_palette c64
require_player_movement

========
OBJECTS
========

Background
blue

Player
lightblue

Trail
purple

=======
LEGEND
=======

. = Background
P = Player
O = Trail

=======
SOUNDS
=======

startgame 50287705
sfx0 660507
sfx1 41058704
endgame 74955903

================
COLLISIONLAYERS
================

Background
Trail
Player

======
RULES
======

[ > Player | No Trail ] -> [ Trail | Player ] sfx0
[ > Player | Trail ] -> [ | Player ] sfx1

==============
WINCONDITIONS
==============

no Trail

=======
LEVELS
=======

OOOOOOOO
OOOOOOOO
O..POO.O
OOOOOOOO
OOOOOOOO


title Take Heart Lass
author Kevin Zuhn
homepage www.kevinzuhn.com

========
OBJECTS
========

Background
LightGreen Green
11111
11011
10101
11011
11111

BackFlowers
Green Purple Yellow
10001
00100
01210
00100
10001

Target
RED LightRed
.0.0.
01010
01110
.010.
..0..

Wall
Red DarkGreen
00000
00100
01010
00100
00000

DespairWall
DarkBrown DarkBlue
00000
00100
01010
00100
00000

NewDespairWall
DarkRed DarkBlue
00000
00100
01010
00100
00000

Player
Blue LightRed Yellow DarkBlue
.333.
.323.
20002
.111.
.000.

OldHope
White Pink
11111
11011
10101
11011
11111

NewHope
White LightRed
11111
11011
10101
11011
11111

Crate
Pink White
.0.0.
01010
01110
.010.
..0..

OldDespair
Black DarkBlue
01010
10101
01010
10101
01010

AltOldDespair
Black DarkBlue
10101
01010
10101
01010
10101

NewDespair
DarkBlue DarkGray
01010
10101
01010
10101
01010

AltNewDespair
DarkBlue DarkGray
10101
01010
10101
01010
10101

=======
LEGEND
=======

. = Background
f = BackFlowers
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target
X = OldDespair
Z = AltOldDespair
H = OldHope

Despair = OldDespair or NewDespair or AltOldDespair or AltNewDespair
Hope = OldHope or NewHope
Blocker = Despair or Crate or Target or Wall or Player or DespairWall or NewDespairWall or Hope
HopeBlocker = Crate or Target or Wall or DespairWall or NewDespairWall or Hope


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target, BackFlowers, Hope
Player, Wall, Crate, Despair, NewDespair, DespairWall, NewDespairWall

======
RULES
======

[NewDespair] -> [OldDespair]
[AltNewDespair] -> [AltOldDespair]
[NewDespairWall] -> [DespairWall]
[NewHope] -> [OldHope]
late [OldDespair | No Blocker] -> [OldDespair | AltNewDespair]
late [AltOldDespair | No Blocker] -> [AltOldDespair | NewDespair]
late [OldDespair | Wall] -> [OldDespair | NewDespairWall]
late [DespairWall | Wall] -> [DespairWall | NewDespairWall]
late [OldHope | No HopeBlocker] -> [ OldHope | NewHope]

[> Player | Crate] -> [> Player | > Crate]
[> Crate | Blocker] -> [ Crate | Blocker ]

late [Target | Despair] -> [ Target |  ]
late [Despair Target] -> [Target]
late [Crate | Despair] -> [ Crate |  ]
late [Hope Despair] -> [Hope]
late [Hope | DespairWall] -> [Hope | Wall]
late [Hope | NewDespairWall] -> [Hope | Wall]

==============
WINCONDITIONS
==============

Some Player on Target

=======
LEVELS
=======

message You have love in your heart, Lass.

######
#...x#
#.ff.#
#.ff.#
#o..P#
######

message So your heart can weather Despair.

#########
#x.f...f#
#.#.....#
#.p.f#..#
#.#..#f.#
#xzf...o#
#########

message And you've others who love you.

#########
#x......#
#...#...#
#p..*..##
#...#...#
#x.....o#
#########

message The war can't last forever, Lass.

########
#.#zx#p#
#....*.#
#......#
#o#*.#.#
########

message Think of the time we shared.

###########
#.........#
#.*#.o.####
#..*###...#
#.......*.#
#......#..#
####...####
#x.......p#
###########

message Then bottle your grief up tight.

###########
#o........#
####.##.#.#
#.....*.#.#
#.#.###.#.#
#.#....*..#
#.#*.#.##.#
#x#...p#z.#
###########

message I wish I could be there, Lass.

###########
#...o...zx#
#.#.....#z#
#...#.#...#
#.........#
#..**.**..#
#z........#
#xz......p#
###########

message You're loved. You'll find a way.

###########
#....o....#
#..#####..#
#.#.....#.#
#...*p*...#
#....*....#
#...###...#
#x.......x#
###########

message There is always Hope, Lass.

###########
#........H#
#.......p.#
#x........#
#zx.......#
#xzx......#
#zxzx.....#
#ozxzx....#
###########

message  It may waver, but it stays lit.

###########
#....p....#
###.#*#.###
#x.......x#
###.*.*.###
#HH*.....x#
###.#.#.###
#x.......o#
###########

message Temper your hope with wisdom.

############
#......zxzx#
#.##.##.####
#......*...#
#.*p.#.#.#o#
#...*.*.*#.#
####.###...#
#H.........#
############

message Save no Despair for me, Lass.

##################
#..*.#H#o........#
#...*..#######.#.#
#.##.#########.#*#
#.#.....*....#.#.#
#H#...#.p..#z.*H*#
###...#.#*##...#.#
#...##z.....#*.xz#
#.*.......*...xzx#
#x...#.*....#xzxz#
##################

message For I have hope as well.
message Take Heart, Lass. Goodbye.

title maze generator
run_rules_on_level_start
again_interval 0

========
OBJECTS
========

Background .
black white
00000
01110
01110
01110
00000

Player P
transparent

visited
transparent

filler
transparent

w_up
white
.000.
.000.
.000.
.000.
.....

w_down
white
.....
.000.
.000.
.000.
.000.

w_left
white
.....
0000.
0000.
0000.
.....

w_right
white
.....
.0000
.0000
.0000
.....

to_delete
transparent

temp
transparent

tempA
transparent

=======
LEGEND
=======


=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Player
filler
visited
w_up
w_down
w_left
w_right
to_delete
temp
tempA

======
RULES
======

(place player randomly on the first turn only)
random [ no filler ] -> [player visited]
[no filler] -> [filler]

(propagate path until you can't move any more)
random [ player |  no visited ] ->  [  visited > to_delete |  < to_delete player ] again

(if you can't extend the current path, continue from some random other neighbouring point)
[player | no visited ] -> [ player tempA | ]
random [player no tempA] [ visited | no visited ] -> [ visited ] [ visited player |  ] again
[tempA]->[]

[ up to_delete ] -> [ w_up ]
[ down to_delete ] -> [ w_down ]
[ left to_delete ] -> [ w_left ]
[ right to_delete ] -> [ w_right ]

==============
WINCONDITIONS
==============


=======
LEVELS
=======

........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................
........................................




title The Legend of Zokoban
author Joshua Minor
homepage pixelverse.org
flickscreen 9x7

========
OBJECTS
========

Background
yellow orange
00000
01000
00001
00100
10000

Water
blue lightblue
00000
01000
00001
00100
10000

Cave
DarkBlue
00000
00000
00000
00000
00000

Goal
DarkBlue Red
00000
00000
00100
00000
00000

RockWall
BROWN Orange
01110
10000
10010
10000
00100

StoneWall
darkgrey grey
01110
10000
10010
10000
00100

PlayerWithoutSword
GREEN lightgreen
.00..
00000
.000.
.111.
00.00

PlayerWithSword
GREEN lightgreen white grey
.002.
00020
.0222
.110.
00.00

Sword
grey white
..1..
..1..
.111.
..0..
.....

Block
BROWN Orange darkgrey
.111.
10001
10002
10002
.222.

EasyBaddie
white red
.111.
10101
11111
11111
11.11

Money
white lightblue
..1..
.001.
10111
.111.
..1..

=======
LEGEND
=======

Wall = StoneWall or RockWall
Solid = Wall or Block or Water
Player = PlayerWithoutSword or PlayerWithSword
Obstacle = Wall or Block or Player or Water
Baddie = EasyBaddie

. = Background
# = RockWall
S = StoneWall
P = PlayerWithoutSword
* = Block
@ = Block and Cave
O = Cave
w = Water
1 = EasyBaddie
+ = Sword
G = Goal

=======
SOUNDS
=======

StartLevel 40430108 (music-ish)
SFX0 16358103 (power up)
SFX1 84057304 (push)
SFX2 74654508 (stairs)
SFX3 21293301 (fight)
SFX4 49521903 (hurt)
SFX5 47598700 (money)
ShowMessage 82693708 (you win!)

================
COLLISIONLAYERS
================

Background
Cave, Goal
Player, Wall, Block, Water, EasyBaddie
Sword, Money

======
RULES
======

(baddies move at random)
[ Stationary Baddie ] -> [ RandomDir Baddie ]

(player can push blocks)
[ > Player | Block ] -> [ > Player | > Block ] SFX1

(blocks pushed into water become land)
[ > Block | Water ] -> [ | ]

(baddies can attack the player w/o sword)
[ > Baddie | Player No Sword ] -> SFX4

(player can pick up sword)
([ > Player | Sword ] -> [ | Player Sword ] SFX0)
[ PlayerWithoutSword Sword ] -> [ PlayerWithSword ] SFX0

(player with sword can kill baddies & leave money)
[ > PlayerWithSword | Baddie ] -> [ PlayerWithSword | Money ] SFX3
(sword moves with player)
([ > Player Sword ] -> [ > Player > Sword ])
(sword doesn't move into obstacles)
([ > Sword | Stationary Obstacle ] -> [ Sword | Stationary Obstacle ])

(player can pick up money)
[ > Player | Money ] -> [ | Player ] SFX5

(blocks pushed into caves, teleport to other end of cave)
random [ Moving Block Cave | ... | Cave ] -> [ Cave | ... | Block Cave ]
(blocks sitting on cave exits will roll aside to let player follow them)
late random [ Block Cave | No Obstacle ] -> [ Cave | Block ]
(players can enter caves - teleporting to orthogonally matching cave)
late random [ Player Cave | ... | Cave ] -> [ Cave | ... | Player Cave ] SFX2
(sword follows player through the cave)
(late random [ Sword Cave | ... | Cave ] -> [ Cave | ... | Sword Cave ])


==============
WINCONDITIONS
==============

All Player on Goal

=======
LEVELS
=======

sssssssss#################wwwwwwwwww
ss.s1s..s#1#####.##..1..1.wwwwww..ww
s.......s#.#####.##.#.#.#.wwwww..*.w
s..*....o#.##o##.............ww....w
s.s....ss#..........#.*.#....ww..1.w
sss.s.sss##...............wwwwww..ww
sssssssss#####..#####.....wwwwwwwwww
sssssssss#####..#####.....wwwwwwwwww
ss1...sss#####..####.....wwww.....ww
s......os##o#....##...#.1www..s.s..w
s..+...ss#.......##*......ww...g...w
ss.....ss#...p..........#.ww..s.s.1w
ssss..sss##.....###..#.1..www..1..ww
sssssssss#################wwwwwwwwww

message Congratulations!



title Midas
author wanderlands
homepage https://web.archive.org/web/20160706204929/http://wanderlands.org/

(

A port of the luxuriant Wanderlands game, Midas, with permission of the authors.  It's a bit messy, if you can find a way to tidy it UP, let me know : ] 

the original is here
https://web.archive.org/web/20120219201941/http://wanderlands.org/main/midas

update: uh, it's flash, so not a playable link any more...also the original developer site is gone.

)

========
OBJECTS
========

Background
white

Lovebase
white

Target
black
.....
..0..
.000.
..0..
.0.0.

GoldTarget
orange
.....
..0..
.000.
..0..
.0.0.

PlayerGround
black
.....
..0..
.000.
.000.
.0.0.

PlayerJump1
black
.....
..0..
00000
.000.
.0.0.

PlayerJump2
black
.....
..0..
00000
.000.
.0.0.

Crown
orange
.000.
.0.0.
.....
.....
.....

GoldBlock
orange

GoldingBlock
brown

Water
blue

Wall
gray

Love
pink
.0.0.
00000
00000
.000.
..0..

=======
LEGEND
=======

JumpingPlayer = PlayerJump1 or PlayerJump2
Player = PlayerGround or PlayerJump1 or PlayerJump2
Obstacle = Player or Wall or Water or GoldingBlock or GoldBlock or Target or GoldTarget
CanFall = GoldBlock or Target or GoldTarget
. = Background
# = Wall
P = PlayerGround and Crown
T = Target
W = Water
G = GoldBlock
D = GoldingBlock

=========
SOUNDS
=========

sfx0 28219707 (touch Water)
sfx1 45545307 (golding)
sfx2 42491507 (make lady gold :/ )
StartGame 8892900
EndLevel 28067900
EndGame 28067900
CloseMessage 44855507

================
COLLISIONLAYERS
================

Background
Lovebase
PlayerGround, PlayerJump1,PlayerJump2, Wall, Water, GoldBlock, Target, GoldTarget, GoldingBlock
Crown
Love

======
RULES
======

UP [ UP PlayerGround | NO Obstacle ] -> [ UP PlayerJump2 | ] 
DOWN [ UP JumpingPlayer | NO Obstacle ] -> [ JumpingPlayer | ] 
DOWN [ UP PlayerJump2 | NO Obstacle ] -> [ PlayerJump2 | ] 

[ STATIONARY CanFall ] -> [ Down CanFall ] 

HORIZONTAL [ > PlayerJump1 | NO Obstacle ] -> [ | PlayerGround ] 
HORIZONTAL [ > PlayerJump2 | NO Obstacle ] -> [ | PlayerJump1 ] 
LATE DOWN [ PlayerGround | NO Obstacle ] -> [ | PlayerGround ] 

LATE [ Crown NO Player ] [ Player ] -> [ ] [ Crown Player ] 
LATE [ GoldingBlock ] -> [ GoldBlock ] 
LATE [ Crown | Wall ] -> [ Crown | GoldingBlock ] sfx1
LATE [ Crown | Water ] -> [ | Water ] sfx0
LATE [ Crown | Target ] -> [ Crown | GoldTarget ] sfx2
LATE DOWN [ JumpingPlayer | Obstacle ] -> [ PlayerGround | Obstacle ] 

LATE [ Player | Target ] -> [ Player Lovebase | Target ] 
LATE UP [ Lovebase | ] -> [ | Love ] 

==============
WINCONDITIONS
==============

some Love (Awwww!)

=======
LEVELS
=======
##############
#............#
#............#
#.P.......T..#
##G######WWW##
##############

message MIDAS

###############
#.............#
#.............#
#.....#.......#
#.P..##..#.T..#
##G#######WWW##
###############

message She waits for me every day

###############
#.............#
#.............#
#.............#
#.....#.#.....#
#.P..##.#..T..#
##G#######WWW##
###############

message This is the price of gold:

##############
#............#
#............#
#.P..........#
##G###T##WWW##
##############

message A heavy heart

#################
#...............#
#...............#
#......#.#......#
#.....#####.....#
#.......#.......#
#.T.P...#.......#
####G#######WWW##
#################

message I step in the same river
message again and again

#####################
#...................#
#...................#
#...................#
#.........#.#.......#
#..P........#.......#
#..d##########......#
#.......######......#
#...................#
#.......T...........#
#....#########WWW...#
#...................#
#...................#
#...................#
#####################

message Just so I can touch her hand.

#####################
#...................#
#...................#
#...................#
#...................#
#..P................#
#..d##T########WWW..#
#.....#.............#
#...................#
#...................#
#####################

message I am a prison to Pactolus

#####################
#...................#
#........###........#
#.......#####.......#
#......##...##......#
#.....T##.P.##W.....#
#.....####d####.....#
#.....#########.....#
#...................#
#####################

message But I must have her!

#####################
#...................#
#...................#
#..T..#...###..W....#
#..#####...#####....#
#.....###...#.......#
#........P..........#
#.....###D###.......#
#.....#######.......#
#...................#
#...................#
#####################

message My burden only grows

#####################
#...................#
#........G..........#
#.....W.GGG.T.......#
#.....#######.......#
#.....#.....#.......#
#.....#..P..#.......#
#.....###D###.......#
#.....#######.......#
#...................#
#...................#
#...................#
#####################

message My world collapses.

#####################
#...................#
#...................#
#..G....WW..........#
#..##.####..........#
#.....####..........#
#...................#
#.#...........T.....#
#.##.......P..#.....#
#..############.....#
#...#.....####......#
#...................#
#...................#
#...................#
#...................#
#...................#
#####################

message And she feels so distant

#####################
#...................#
#...................#
#...................#
#...................#
#..P......#.........#
#..GG...........WW..#
#..##T############..#
#....#..............#
#...................#
#...................#
#####################

message I live on borrowed time

#####################
#...................#
#...................#
#..........T........#
#.........##........#
#...................#
#.......P...........#
#.......D##.........#
#...........#.......#
#...................#
#...................#
#.......###W#.......#
#####################

message Is she still waiting?

#####################
#...................#
#....###########....#
#...................#
#....P.........W....#
#....###########....#
#....#.........#....#
#...................#
#.........#.........#
#......#..#..#......#
#....T.#..#..#......#
#....###########....#
#...................#
#...................#
#...................#
#####################

message Is Love just fool's gold?

#####################
#...................#
#...................#
#...........####....#
#..T##.##..#...#....#
#..#..#..#.#.P.#....#
#..#.....#...G#.....#
#...#...#....#......#
#....#W#.....#......#
#.....#.............#
#............#......#
#...................#
#...................#
#####################

message Midas


(
level ideas
push battery horizontally
have to assemble and then disassemble a circuit, in order to complete it
add a piece to a circut, then break the circuit from another point
level that starts with a closed circuit
level with very dangerous + block
)
title LED Challenge
author increpare
homepage www.increpare.com

run_rules_on_level_start
color_palette famicom


========
OBJECTS
========

Background .
darkgreen green
00000
00000
00100
00000
00000


Player
Black brown grey Blue
.111.
.111.
22222
13331
.3.3.


Player_Dead
darkgrey darkgrey darkgrey darkgrey
.111.
.111.
22222
.333.
.3.3.

Wall #
black

Red_Off R
black DarkRed
.....
..1..
.101.
.101.
.101.

Red_On T
pink red
.....
..1..
.101.
.101.
.101.


Green_Off G
black Green
.....
..1..
.101.
.101.
.101.

Green_On H
Green LightGreen
.....
..1..
.101.
.101.
.101.


Blue_Off B
black DarkBlue
.....
..1..
.101.
.101.
.101.

Blue_On N
Blue LightBlue
.....
..1..
.101.
.101.
.101.

Battery_H C
Grey lightgrey
.000.
.010.
01110
.010.
.000.

Battery_V O
Grey lightgrey
..0..
00100
01110
00100
..0..


cable_h q
grey
.....
.....
00000
.....
.....

cable_v e
grey
..0..
..0..
..0..
..0..
..0..

cable_ul w
grey
..0..
..0..
000..
.....
.....

cable_ur d
grey
..0..
..0..
..000
.....
.....

cable_dr s
grey
.....
.....
..000
..0..
..0..

cable_dl a
grey
.....
.....
000..
..0..
..0..


cable_omni z
grey
..0..
..0..
00000
..0..
..0..

cable_dlr i
grey
.....
.....
00000
..0..
..0..

cable_ulr k
grey
..0..
..0..
00000
.....
.....

cable_udr j
grey
..0..
..0..
..000
..0..
..0..

cable_udl l
grey
..0..
..0..
000..
..0..
..0..


light
black

light_up
yellow
.000.
.000.
.000.
.....
.....

light_down
yellow
.....
.....
.000.
.000.
.000.

light_Left
yellow
.....
000..
000..
000..
.....

light_right
yellow
.....
..000
..000
..000
.....

=======
LEGEND
=======

P = Player
Unlit = Blue_Off or Green_Off or Red_Off
Lit = Blue_On or Green_On or Red_On
Cable = Cable_h or Cable_v or Cable_dr or Cable_dl or Cable_ul or Cable_ur or Cable_UL or Cable_UR or Cable_UDR or Cable_ULR or Cable_UDL or Cable_DLR or Cable_Omni
Battery = Battery_H or Battery_V
Item = Unlit or Lit or Battery or Cable
Cable_Up = Cable_V or Cable_UL or Cable_UR or Battery_V or Cable_UDR or Cable_ULR or Cable_UDL or Cable_Omni or  Player
Cable_Down = Cable_V or Cable_DL or Cable_DR or Battery_V or Cable_UDR or Cable_UDL or Cable_DLR or Cable_Omni or Player
Cable_Left = Cable_H or Cable_UL or Cable_DL or Battery_H  or Cable_ULR or Cable_UDL or Cable_DLR or Cable_Omni or Player
Cable_Right = Cable_H or Cable_UR or Cable_DR or Battery_H or Cable_UDR or Cable_ULR or Cable_DLR or Cable_Omni or Player
LightEffect = Light_Up or Light_Down or Light_Left or Light_Right
Object = Player or Item
Player_Dying = Player and Light

=======
SOUNDS
=======

EndLevel 39731502
EndGame 20540505
Player Move 6330907
Player_Dead Create 44233902

================
COLLISIONLAYERS
================

Background
Light
Light_Up
Light_Down
Light_Left
Light_Right
Player, Player_Dead, Wall, Item

======
RULES
======

(init stage)
[ Light ] -> [ ]
[ LightEffect no Player] -> [ ]
[ Red_On ] -> [ Red_Off ]
[ Green_On ] -> [ Green_Off ]
[ Blue_On ] -> [ Blue_Off ]

(movement)
[ >  Object | Object ] -> [  > Object | > Object ]

(do circuits)
startLoop
late [ Battery ] -> [ Light Battery ]

late left [ Player | Light_Right ] -> [ Light Player | Light_Right ]
late right [ Player | Light_Left ] -> [ Light Player | Light_Left ]
late up [ Player | Light_Down ] -> [ Light Player | Light_Down ]
late down [ Player | Light_Up ] -> [ Light Player | Light_Up ]

late up [ Player_Dead | Light_Down ] -> [ Light Player_Dead | Light_Down  ]
late down [ Player_Dead | Light_up ] -> [ Light Player_Dead | Light_up  ]

late left [ Cable_Left | Light_Right ] -> [ Light Cable_Left | Light_Right ]
late right [ Cable_Right | Light_Left ] -> [ Light Cable_Right | Light_Left ]
late up [ Cable_Up | Light_Down ] -> [ Light Cable_Up | Light_Down ]
late down [ Cable_Down | Light_Up ] -> [ Light Cable_Down | Light_Up ]

late down [ Red_Off | Light_Up ] -> [ Red_On | Light_Up ]
late down [ Green_Off | Light_Up ] -> [ Green_On | Light_Up ]
late down [ Blue_Off | Light_Up ] -> [ Blue_On | Light_Up ]

late [ Light Cable_Up ] -> [ Light Light_Up Cable_Up ]
late [ Light Cable_Down ] -> [ Light Light_Down Cable_Down ]
late [ Light Cable_Left ] -> [ Light Light_Left Cable_Left ]
late [ Light Cable_Right ] -> [ Light Light_Right Cable_Right ]
endLoop

late [ Player Light ] -> again
late [ Player_Dead Light ] -> again
late [ Player LightEffect ] -> [ Player_Dead LightEffect ]   again
late [ Player_Dead Light ] -> [ Player_Dead ]

==============
WINCONDITIONS
==============

No Unlit
No Player_Dead

=======
LEVELS
=======

message Level 1 of 4

......
.r.g..
..o.p.
.w.d..
......

message Level 2 of 4

.....##.....
.....##.w.d.
..c.........
...p....r.g.
.....##.....

message Level 3 of 4

.......
..rgb..
..wkd..
...o...
.....p.
.......

message Level 4 of 4

...........
...ccccc...
.d.crcgc.w.
...ccccc...
..........p

(
message Level 5 of 3

.......
.bgrgb.
rdzczwr
dqw.dqw
.......
)

message Congratulations

title Ponies Jumping Synchronously
author vytah

background_color #990044
text_color white

========
OBJECTS
========


Background  (special, automatically placed in its own layer)
lightblue

JumpVariableA
lightblue
0...0
.000.
.0.0.
.000.
0...0

JumpVariableA1
lightblue
0...0
..0..
..0..
..0..
0...0

JumpVariableA2
lightblue
0...0
.0.0.
.0.0.
.0.0.
0...0

JumpVariableT
lightblue
..0..
.000.
.0.0.
.000.
.....

JumpVariableT1
lightblue
..0..
..0..
..0..
..0..
.....

JumpVariableT2
lightblue
..0..
.0.0.
.0.0.
.0.0.
.....

JumpVariableB
lightblue
.....
.000.
.0.0.
.000.
..0..

JumpVariableB1
lightblue
.....
..0..
..0..
..0..
..0..

JumpVariableB2
lightblue
.....
.0.0.
.0.0.
.0.0.
..0..

Dirt
brown darkbrown
00000
01000
00000
00010
00000

Tunnel
brown darkbrown
00000
01000
.....
.....
.....


Grass
brown lightgreen green darkBrown
11211
21020
00000
00030
00000

Exit
pink red
.1.1.
10101
10001
.101.
..1..

WoodenWall
Brown orange
.111.
.001.
.111.
.100.
.111.

Teleport
Brown darkBrown pink white
23332
02220
00000
00010
00000

Fire
red orange yellow
..0..
.00..
0110.
01210
.020.

TwiLeft
purple
.0...
00...
.0000
.0.0.
.0.0.

TwiRight
purple
...0.
...00
0000.
.0.0.
.0.0.

TeleportingTwilight
purple
...0.
...00
0000.
.0.0.
.0.0.

RdLeft
#00dddd
.0...
00...
.0000
.0.0.
.0.0.

RdRight
#00dddd
...0.
...00
0000.
.0.0.
.0.0.

AbLeft
yellow red
..1..
.01..
00...
.0000
.0.0.

AbRight
yellow red
..1..
..10.
...00
0000.
.0.0.

AjLeft
orange
.0...
00...
.0000
.0.0.
.0.0.

AjRight
orange
...0.
...00
0000.
.0.0.
.0.0.

DeadPony
lightGray darkGray
.111.
10001
10101
10001
10101

Boulder
lightGray darkGray
.....
.111.
10001
10001
.111.

=======
LEGEND
=======

Applejack=AjLeft or AjRight
RainbowDash=RdLeft or RdRight
Twilight=TwiLeft or TwiRight
AppleBloom=AbLeft or AbRight
(NotPegasus=Twilight or Applejack or AppleBloom or TeleportingTwilight)
Player=Twilight or Applejack or AppleBloom or RainbowDash
Obstacle=Grass or Dirt or DeadPony or WoodenWall or Teleport or Boulder
JumpObstacle=Obstacle or Player or Tunnel
JumpObstacleForFillies=Obstacle or Player
JumpVariablePositiveA=JumpVariableA1 or JumpVariableA2
JumpVariablePositiveT=JumpVariableT1 or JumpVariableT2
JumpVariablePositiveB=JumpVariableB1 or JumpVariableB2

. = Background
# = Dirt
~ = Grass
| = WoodenWall
T = TwiRight
A = AjRight
D = RdRight
B = AbRight
J = JumpVariableA
H = JumpVariableT
L = JumpVariableB
E = Exit
^ = Teleport
F = Fire
O = Boulder
- = Tunnel

=========
SOUNDS
=========

sfx0 47251907 (aj push)
sfx1 13930501 (twi zap)
sfx2 99083104 (death)
sfx3 12667901 (twi teleport)
sfx4 60310105 (jump)
sfx5 58764102 (rd smash)
sfx7 74120302 (aj buck)
endlevel 64815700



================
COLLISIONLAYERS
================

Background
JumpVariableA, JumpVariablePositiveA, JumpVariableT,JumpVariablePositiveT, JumpVariableB, JumpVariablePositiveB
Exit, Fire, Tunnel
Player, Obstacle, TeleportingTwilight

======
RULES
======

[> Player |Stationary Player] -> cancel
[> Player |< Player] -> cancel

[> Applejack| Tunnel] -> [Applejack|Tunnel]
[> RainbowDash| Tunnel] -> [RainbowDash|Tunnel]
[> Twilight| Tunnel] -> [Twilight|Tunnel]
Vertical [> AppleBloom|Tunnel] -> [AppleBloom|Tunnel]

[Left TwiRight] -> [Left TwiLeft]
[Right TwiLeft] -> [Right TwiRight]
[Left RdRight] -> [Left RdLeft]
[Right RdLeft] -> [Right RdRight]
[Left AjRight] -> [Left AjLeft]
[Right AjLeft] -> [Right AjRight]
[Left AbRight] -> [Left AbLeft]
[Right AbLeft] -> [Right AbRight]

Right[Action Applejack|WoodenWall] -> [AjLeft|]sfx7
Left[Action Applejack|WoodenWall] -> [AjRight|]sfx7

Down[Action Twilight|Teleport][no Player|Teleport]-> [|Teleport][TeleportingTwilight|Teleport] sfx3
late[TeleportingTwilight]->[TwiRight]


UP [ Up Applejack | No Obstacle No Player no tunnel] [JumpVariableA] -> [ Up Applejack | ] [JumpVariableA2] sfx4
DOWN [ Up Applejack | no obstacle No Player no tunnel ][JumpVariablePositiveA] -> [ Applejack | ][JumpVariablePositiveA]
DOWN [ Up Applejack | no obstacle No Player no tunnel][JumpVariableA2] -> [ Applejack | ][JumpVariableA2]
HORIZONTAL [ > Applejack | NO obstacle No Player no tunnel][JumpVariableA1] -> [ | Applejack ][JumpVariableA]
HORIZONTAL [ > Applejack | NO obstacle No Player no tunnel][JumpVariableA2] -> [ | Applejack ][JumpVariableA1]
HORIZONTAL [ > Applejack | JumpObstacle][JumpVariableA1] -> [ Applejack|JumpObstacle ][JumpVariableA]
HORIZONTAL [ > Applejack | JumpObstacle][JumpVariableA2] -> [ Applejack|JumpObstacle ][JumpVariableA1]
LATE DOWN [ Applejack | NO obstacle No Player no tunnel][JumpVariableA] -> [ | Applejack ][JumpVariableA]
LATE DOWN [ Applejack | Obstacle ][JumpVariablePositiveA] -> [ Applejack | Obstacle ][JumpVariableA]


UP [ Up Twilight | No Obstacle No Player no tunnel] [JumpVariableT] -> [ Up Twilight | ] [JumpVariableT2] sfx4
DOWN [ Up Twilight | no obstacle No Player no tunnel ][JumpVariablePositiveT] -> [ Twilight | ][JumpVariablePositiveT]
DOWN [ Up Twilight | no obstacle No Player no tunnel][JumpVariableT2] -> [ Twilight | ][JumpVariableT2]
HORIZONTAL [ > Twilight | NO obstacle No Player no tunnel][JumpVariableT1] -> [ | Twilight ][JumpVariableT]
HORIZONTAL [ > Twilight | NO obstacle No Player no tunnel][JumpVariableT2] -> [ | Twilight ][JumpVariableT1]
HORIZONTAL [ > Twilight | JumpObstacle][JumpVariableT1] -> [ Twilight|JumpObstacle ][JumpVariableT]
HORIZONTAL [ > Twilight | JumpObstacle][JumpVariableT2] -> [ Twilight|JumpObstacle ][JumpVariableT1]
LATE DOWN [ Twilight | NO obstacle No Player no tunnel][JumpVariableT] -> [ | Twilight ][JumpVariableT]
LATE DOWN [ Twilight | Obstacle ][JumpVariablePositiveT] -> [ Twilight | Obstacle ][JumpVariableT]


UP [ Up AppleBloom | No Obstacle No Player no tunnel] [JumpVariableB] -> [ Up AppleBloom | ] [JumpVariableB2] sfx4
DOWN [ Up AppleBloom | no obstacle No Player no tunnel ][JumpVariablePositiveB] -> [ AppleBloom | ][JumpVariablePositiveB]
DOWN [ Up AppleBloom | no obstacle No Player no tunnel][JumpVariableB2] -> [ AppleBloom | ][JumpVariableB2]
HORIZONTAL [ > AppleBloom | NO obstacle No Player][JumpVariableB1] -> [ | AppleBloom ][JumpVariableB]
HORIZONTAL [ > AppleBloom | NO obstacle No Player][JumpVariableB2] -> [ | AppleBloom ][JumpVariableB1]
HORIZONTAL [ > AppleBloom | JumpObstacleForFillies][JumpVariableB1] -> [ AppleBloom|JumpObstacleForFillies ][JumpVariableB]
HORIZONTAL [ > AppleBloom | JumpObstacleForFillies][JumpVariableB2] -> [ AppleBloom|JumpObstacleForFillies ][JumpVariableB1]
LATE DOWN [ AppleBloom | NO obstacle No Player no tunnel][JumpVariableB] -> [ | AppleBloom ][JumpVariableB]
LATE DOWN [ AppleBloom | Obstacle ][JumpVariablePositiveB] -> [ AppleBloom | Obstacle ][JumpVariableB]
)

late [Player Fire] -> [DeadPony Fire] sfx2

[> Applejack|Boulder|no Obstacle No Player] -> [> Applejack| > Boulder |] sfx0
[> RainbowDash|Boulder|no Obstacle No Player] -> [> RainbowDash||] sfx5
Right [Action TwiRight|Boulder] -> [TwiRight|] sfx1
Left [Action TwiLeft|Boulder] -> [TwiLeft|] sfx1

[Boulder Fire] -> [Boulder]

late down [Boulder|Player] -> [|DeadPony] sfx2
late down [Boulder| no Obstacle no Tunnel] -> [|Boulder no Fire]
late [Boulder Fire] -> [Boulder]

==============
WINCONDITIONS
==============

some Player
all Exit on Player
no DeadPony


=======
LEVELS
=======

(message #1. Grab the Crystal Heart.)

..........H
...........
.........E.
..T....~~~~
~~~~~~#####
###########


message #2. Applejack bucks and pushes.

..........J
..~~~~~~~~~
...########
.A.|.o...E#
######.####
###########

message #3. Twilight teleports and zaps.

...........H
............
............
.T....E.O...
~^~FFF~~~~^~
############

message #4. Rainbow Dash flies and smashes.

#########
....#..E#
....O..##
....#..##
.D..#..##
#########

message #5. Rocks fall, everyone dies.

.........J
..........
...~~~~~.~
~~O..O...#
##|A.O..E#
##########

message #6. Cooperate with friends.

J.....###
......###
...D..#.E
....#..O#
.A..O..|#
~~~######
#########

message #7. Cooperate with family.

##.......JL#####
---..O.A.O.#...#
###~.~~~~~.#..E#
###.F..B..F-.###
####.#####.#####
################


message #8. Help a filly get a cutie mark!
~~~~~..........L
#####.........D.
###E-....O......
######...O......
#######.###...B.
############~~~~

message Aww... Still no cutie mark...

message #9. Explore dangerous caves!

.........##############
.......###O############
JHL..#####O############
...#######O############
..#####..-O------O-E###
######..##O######O#####
#####..###|...A..O.....
####..####O###^.#######
####.-----O-------...B.
##########O############
..........O..........T.
############^##########

message #10. Friendship is magic!

JH.........###
.......O.#.#OE
DAT....|.#..O#
~~~.~~~.~###O#
.......F.#..O#
########^#^###

message #11. Evil enchantress.
~~~~~...............JHL
#####~~~~~~~~~~........
################.......
###......###...........
###..O.O.F.....#.#..D..
#E-..#B#.|O........A.T.
#############.~~~~~~~~~
#######################


message #12. Equestrian Games.

###############################
#########O#####################
#########O#####################
###......O..................JHL
###..O.#.O..............D......
#E-..#B#.O.....................
#########O#####################
#........|.....................
#########O#####..|.|.|.|.|.....
#........O....##.#.#.#.#.#.A...
############...################
#############..................
###############...F........T...
###############################

message Congratulations!

message The Mushroom Kingdom is now safe!

message I mean, the Crystal Empire.

USER PROMPT:
Output the code for a complete PuzzleScript game. The game should be a simplified `demake` of the following game idea: **Crops & Castles:** Harvest enchanted produce to gain spells and power up your medieval kingdom in this strategic farming and tower defense hybrid..  First, reason about your task and determine the best plan of action. Then, write your code. Return your code in full, inside a ```plaintext code block.