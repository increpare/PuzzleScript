SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title By Your Side
author Alan Hazelden
homepage www.draknek.org

require_player_movement
key_repeat_interval 0.12

(

A port of alan hazelden's game, By Your Side, in 6 lines of code, made with the permission of the author -

the original is here
http://www.draknek.org/games/by-your-side/

)

========
OBJECTS
========

(Objects in all scenes)

Grass .
darkgreen green
00000
01000
00000
00010
00000

Player_Casual P
black pink gray
..0..
..1..
.222.
..2..
.2.2.

TargetMain T
yellow
00000
0...0
0...0
0...0
00000

(Initial Scenes)

GirlFriend G
pink red
..0..
.010.
..1..
.111.
.0.0.

Tree #
darkbrown lightgreen
.111.
11111
.111.
..0..
..0..

(Wedding Scene)

Player_Formal Q
black pink white
..0..
..1..
.020.
..0..
.0.0.

WoodenFloor ,
brown
00000
00000
00000
00000
00000

ChurchWall H
darkbrown brown
11101
00000
10111
00000
11101

Chair C
darkbrown brown
11111
11101
11101
00001
01101

Altar A
gray
.....
00000
.000.
.000.
.000.

Spectator1 B
black pink white
..0..
..1..
.020.
..0..
.0.0.

Spectator2 D
purple pink white
..0..
..1..
10001
.000.
00000

Wife_Wedding W
white pink
..1..
.000.
.101.
.000.
00000

(Post Wedding Scene)

house @
black grey red
..0..
.000.
00000
.111.
.121.

pavement `
darkblue

Road '
gray white
00000
00000
01110
00000
00000

Wife_Repulsed X
white pink
..1..
.000.
.101.
.000.
00000

(grave scene)

gravedirt ~
green brown
01001
10000
00100
00001
01000

grave R
lightgray darkgray grey
.111.
10201
12221
10201
10001

coffin F
darkbrown brown black
.000.
01210
02220
01210
02220

(final scene)

Wife_Dying Y
white pink
..1..
.000.
.101.
.000.
00000

Wife_Coffin
darkbrown brown black
.000.
01210
02220
01210
02220

ColdFeet I
black

=======
LEGEND
=======

Target = TargetMain or Road
Player = Player_Casual or Player_Formal
Background = Grass or WoodenFloor or Pavement or GraveDirt
Spectator = Spectator1 or Spectator2
Obstacle = House or Spectator or Chair or Tree or Altar or Grave or Coffin or Wife_Coffin or ChurchWall
Wife_Attracted = Girlfriend or Wife_Wedding or Wife_Dying
Wife = Wife_Attracted or Wife_Repulsed or Wife_Coffin
Person = Player or Wife
GrassyTarget = Grass and TargetMain
E = GrassyTarget

=========
SOUNDS
=========

Player cantmove 82269707
endlevel 54461500
startgame 10582309
endgame 66004102
Wife_Repulsed move 44043907
sfx0 16417502

================
COLLISIONLAYERS
================

ColdFeet
Background
Target
Obstacle, Person

======
RULES
======

(game logic)

[ > Player ] [ Wife_Attracted ] -> [ > Player ] [ > Wife_Attracted ] 
[ > Player ] [ Wife_Repulsed ] -> [ > Player ] [ < Wife_Repulsed ] 

late [ Wife_Dying ] -> [ Wife_Coffin ] 
late [ Player | Wife_Coffin ] -> [ Player | Wife_Dying ] 

(level-specific events)
late [ ColdFeet Player ] -> [ Player ] message ...
late [ Wife Road ] -> sfx0

==============
WINCONDITIONS
==============

all person on target

=======
LEVELS
=======

....#################
...###...#....#######
..##.........##.....#
.##...#######.......#
##....#....###......#
#...###.............#
#...#...#...#..######
#...#...#..##.P.....#
#....####.T##..######
##......#...#.......#
###..##..#...#...##.#
#.##..#..##..#...#..#
#..#..#....###...#..#
#..##........#......#
#...##...##...##....#
#.......##.....######
.##############......

....#################
...###...#....#######
..##.........##.....#
.##...#######.......#
##....#....###......#
#...###.............#
#...#...#...#..######
#...#...#..##..T...T#
#....####.P##G.######
##......#...#.......#
###..##..#...#...##.#
#.##..#..##..#...#..#
#..#..#....###...#..#
#..##........#......#
#...##...##...##....#
#.......##.....######
.##############......

....#################
...###...#....#######
..##.........##.....#
.##...#######.......#
##....#....###......#
#...###.............#
#...#T..#...#..######
#...#T..#..##..P...G#
#....####..##..######
##......#...#.......#
###..##..#...#...##.#
#.##..#..##..#...#..#
#..#..#....###...#..#
#..##........#......#
#...##...##...##....#
#.......##.....######
.##############......

HHHHHHHHHHHHHHHHHHHHH
,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,
,CCCCCCCCCCCCCCCCCCCC
,CCCCCCCCCCCCCCCCCCCC
A,B,,D,B,D,,B,D,,,B,C
A,T,,I,,,I,,,,I,Q,,,C
A,T,,,,,,,,,,,,,W,,,C
,CCCCCCCCCCCCCCCCCCCC
,CCCCCCCCCCCCCCCCCCCC
,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,
HHHHHHHHHHHHHHHHHHHHH

`````````@@@@````````
`````````@TT@````````
``@@@@@@`@`@@````````
``@````@`@`@@@@``````
``@`@@`@`@````@``````
``@`@@`@`@`@@@@``````
``@`@@`@@@P@`````````
``@```````W@@@@@@````
``@`@@@@@@``````@````
``@`@````@`@@@@`@````
``@`@````@`@``@`@````
@@@@@@@@@@@@@@@@@@@@@
'''''''''''''''''''''
@@@@@@@@@@@@@@@@@@@@@
`````````````````````

`````````@@@@````````
`````````@PX@````````
``@@@@@@`@`@@````````
``@````@`@`@@@@``````
``@`@@`@`@````@``````
``@`@@`@`@`@@@@``````
``@`@@`@@@T@`````````
``@```````T@@@@@@````
``@`@@@@@@``````@````
``@`@````@`@@@@`@````
``@`@````@`@``@`@````
@@@@@@@@@@@@@@@@@@@@@
'''''''''''''''''''''
@@@@@@@@@@@@@@@@@@@@@
`````````````````````

`````````@@@@````````
`````````@TT@````````
``@@@@@@`@`@@````````
``@````@`@`@@@@``````
``@`@@`@`@````@``````
``@`@@`@`@`@@@@``````
``@`@@`@@@P@`````````
``@```````X@@@@@@````
``@`@@@@@@``````@````
``@`@````@`@@@@`@````
``@`@````@`@``@`@````
@@@`@@@@@@`@@@@`@@@@@
'''''''''''''''''''''
@@@@@@@@@@@@@@@@@@@@@
`````````````````````

RRRRRRR~RRRRRR~RRRRR
R~~~~~R~R~~~~R~R~~~~
R~~~~~~~R~~~~R~R~~~~
R~~~~~R~R~~~~R~R~~~~
RRRRRRR~RR~~RR~RRRRR
R~~~~BDBDBDBDBD~~~~R
RQ~~~~~i~i~~i~~~~EFR
RRRRRRRRRRRRRRRRRRRR
R~~R~~~~~~~~~~~R~~~~
R~~R~~~~~~~~~~~R~~~~
R~~RRR~~~RRRR~~R~~~~
R~~~~~~~~R~~~~~R~~~R
R~~R~~~~~R~~R~~~~~~R
R~~R~~~~~R~~R~~~~~~R
RRRRRRRRRR~~RRRRRRRR

RRRRRR~~~~~~~~~~~~~~
R~~~~R~~~~~RRRRRR~~~
R~QY~R~~~~~R~~~~R~~~
R~~~~R~~RRRRR~R~R~~~
R~~~~RRRR~~R~~R~R~~~
R~~~~~~~~~~R~~R~RRR~
RRRRRRRRR~~R~~~~~~R~
~~R~~~~~R~~R~RRR~~R~
~~R~~R~~R~~RRR~R~~R~
~~R~~R~~~~~~~~~~~~R~
~~R~~R~~RR~~~~~RRRR~
~~R~~R~~~RRRRRTTR~~~
~~R~~R~~~R~~~~~~R~~~
~~R~~R~~~~~RRR~~R~~~
~~RRRRRRRRRR~RRRR~~~

RRRRRR~~~~~~~~~~~~~~
R~~~~R~~~~~RRRRRR~~~
R~~~~R~~~~~R~~~~R~~~
R~~~~R~~RRRRR~R~R~~~
R~~~~RRRR~~R~~R~R~~~
R~~~~~~~~~~RT~R~RRR~
RRRRRRRRR~~RT~~~~~R~
~~R~~~~~R~~R@RRR~~R~
~~R~~R~~R~~RRR~R~~R~
~~R~~R~~~~~~~~~~~~R~
~~R~~R~~RR~~~~~RRRR~
~~R~~R~~~RRRRRYQR~~~
~~R~~R~~~R~~~~~~R~~~
~~R~~R~~~~~RRR~~R~~~
~~RRRRRRRRRR~RRRR~~~

message Commiserations

message It is over

title Heroes of Sokoban III: The Bard and The Druid
author Jonah Ostroff
text_color yellow
key_repeat_interval 0.25
run_rules_on_level_start

========
OBJECTS
========

Background
#989898 #C8C8C8
00000
00100
00000
00010
00000

Goal
Brown Yellow
.000.
00000
00010
00000
00000

Wall
Darkgrey #222222
01000
11111
00010
00010
11111

Fighter (Warrior sounds better in-game, but I want different abbreviations for the classes in the editor.)
#FF4444 Pink DarkBrown
.000.
.111.
00000
.000.
.2.2.

Wizard
#2266FF Pink
.000.
.010.
00000
.000.
00000

Thief
#66DD66 Pink DarkBrown
.000.
.010.
00000
.0.0.
.0.0.

Priest
#FFFFAA Pink
.000.
.111.
00000
.000.
00000

Bard
#8800FF #884400 #444444
.222.
.111.
00000
.000.
.0.0.

Druid
#FF8800 Pink DarkBrown
.212.
.111.
00000
.000.
00000

SFighter
#880000 Pink DarkBrown
.000.
.111.
00000
.000.
.2.2.

SWizard
#000088 Pink
.000.
.010.
00000
.000.
00000

SThief
#006600 Pink DarkBrown
.000.
.010.
00000
.0.0.
.0.0.

SPriest
#B8B800 Pink
.000.
.111.
00000
.000.
00000

SBard
#440088 #884400 #444444
.222.
.111.
00000
.000.
.0.0.

SDruid
#884400 Pink DarkBrown
.212.
.111.
00000
.000.
00000

Crate
Orange #552200
.111.
10001
10001
10001
.111.

YellowSwitch
Yellow Black
.....
.111.
.101.
.111.
.....

YellowOpen
Yellow
0...0
.....
.....
.....
0...0

YellowDoor
Yellow #AA7722
01010
10001
00000
10001
01010

PurpleSwitch
#6611CC Black
.....
.111.
.101.
.111.
.....

PurpleDoor
#6611CC #220088
01010
10001
00000
10001
01010

PurpleOpen
#6611CC
0...0
.....
.....
.....
0...0

MonsterDoor
DarkGrey #222222 Red
21002
22122
20212
20012
21112

Warpball
Blue

ShadowDoor
Purple

Goblin
Green DarkBrown DarkGreen
0.2.0
00000
..0..
.111.
.1.1.

Dying
White

Saved
#D8D800
..0..
.000.
..0..
..0..
.....

HasSavedDoor
Yellow
.....
.....
.....
.....
.....

Corpse
#CCC
.000.
.000.
00000
.0.0.
.0.0.

Note
#8800FF
.....
..00.
.00..
.00..
.....

=======
LEGEND
=======

Player = Fighter or Wizard or Thief or Priest or Bard or Druid
SPlayer = Swizard or Sthief or SFighter or SPriest or SBard or SDruid
Hero = Player or Splayer
Healer = Priest or SPriest (Unlike the other heroes, priests must function even when inactive. "Healer" is the generic term for an active or inactive priest.)
ClosedDoor = PurpleDoor or YellowDoor or MonsterDoor
OpenDoor = YellowOpen or PurpleOpen
Switch = PurpleSwitch or YellowSwitch
Monster =  Goblin
Moveable = Crate or SPlayer or Monster
Static = Wall or ClosedDoor
Weighing = Crate or Hero or Monster or Wall
Vulnerable = Hero or Monster
Invulnerable = Crate or Static
. = Background
# = Wall
F = Fighter
W = Wizard
T = Thief
P = Priest
B = Bard
D = Druid
1 = SFighter
2 = SThief
3 = SWizard
4 = SPriest
5 = SBard
6 = SDruid
* = Crate
G = Goal
[ = YellowDoor
] = PurpleDoor
{ = YellowSwitch
} = PurpleSwitch
O = Goblin
M = MonsterDoor

=======
SOUNDS
=======

Moveable move 82056307
Sfx0 21221308 (teleport)
Endlevel 36315908
Sfx1 66399906 (switch player)
Sfx2 92172900 (close door)
Sfx3 15129100 (open door)
Sfx4 69860105 (saved)
Sfx5 63194902 (kill enemy)
Sfx6 81996702 (dying player)
Sfx7 20187909 (Bard leading)
Sfx8 93615108 (Transmutation)
EndGame 86793500

================
COLLISIONLAYERS
================

Background
Warpball, Dying
Goal, Switch, OpenDoor, ShadowDoor, HasSavedDoor
ClosedDoor, Hero, Crate, Wall, Monster, Corpse
Saved
Note

======
RULES
======
[Saved] [Goal] -> [] [HasSavedDoor] (Remembering whether we saved someone last round by temporarily changing some door to a HasSavedDoor. This is purely for sound effects purposes.)

[Corpse] -> Restart

[ >  Fighter | Moveable ] -> [  >  Fighter | > Moveable  ]
[ > Moveable | Moveable ] [ Fighter] -> [ > Moveable | > Moveable ] [ Fighter] (Fighters push.)

[ < Thief | Moveable ] -> [ < Thief | < Moveable ] (Thieves pull.)

[ > Wizard ] -> [ Wizard > Warpball]
[ > Warpball | no Moveable no Static] -> [ | > Warpball]
[ > Warpball | Static] [Wizard] -> [ | Static ] [> Wizard]
[ > Warpball | Moveable ] [Wizard] -> [ | Wizard Sfx0] [Moveable]
[ > Warpball] [Wizard] -> [] [> Wizard]
(Wizards telexport. What they really do is shoot a "warpball", which causes them to move normally if it hits a wall or the edge of the screen and warp if it hits something they can warp with.)

(Druid does his thing. Warpball gets reused as a placeholder here. That's gonna happen a lot.)
[> Druid | Crate] -> [Druid | Wall Sfx8]
[> Druid | Monster] -> [Druid | Wall Sfx8]
[> Druid | Wall] -> [Druid | Crate Sfx8]
[> Druid | Healer] -> [Druid | Healer Saved]
[> Druid | Hero] -> [Druid | Hero Warpball]
[Warpball | Healer] -> [Saved | Healer]
[Warpball Hero] -> [Wall Sfx8]
(What strategic purpose could it serve for healers to protect against petrification? None, but it's fun.)

(I mean, theoretically you could write some horrid dragon breath timing puzzle where the ability to delay an extra turn without moving or switching characters is necessary in order to let you move someone else at the correct moment, but even if dragons still existed, that sounds like a really gross level.)

(Bard moves stuff. Reusing "WarpBall" for totally different purpose, so we can cancel the move if he doesn't move.)
[> Bard no Note] -> [> Note > Bard WarpBall]
[> Note] [Note] -> [> Note] [> Note]
[> Note Moveable | no Static no Moveable no Bard] -> [| Moveable Sfx7]
+[> Note Bard WarpBall| no Static no Moveable no Bard] -> [| Bard]

(Why are we doing this pseudo-movement instead of using the built-in version? Because using movement directions makes the note-adding stuff way easier below, and you can't use movement directions in the LATE phase, so the actual moving has to be done before then.)

[WarpBall] -> Cancel
[Note] -> []

(The following mess is for changing characters. But at least it's less messy than before. Thanks to Stephen for spotting my silly redundancies.)
[ Action Fighter] [SThief] -> [SFighter] [Thief Sfx1]
[ Action Thief] [SWizard] -> [SThief] [Wizard Sfx1]
[ Action Wizard] [SPriest] -> [SWizard] [Priest Sfx1]
[ Action Priest] [SBard] -> [SPriest] [Bard Sfx1]
[ Action Bard] [SDruid] -> [SBard] [Druid Sfx1]
[ Action Druid] [SFighter] -> [SDruid] [Fighter Sfx1]

[ Action Fighter] [SWizard] -> [SFighter] [Wizard Sfx1]
[ Action Thief] [SPriest] -> [SThief] [Priest Sfx1]
[ Action Wizard] [SBard] -> [SWizard] [Bard Sfx1]
[ Action Priest] [SDruid] -> [SPriest] [Druid Sfx1]
[ Action Bard] [SFighter] -> [SBard] [Fighter Sfx1]
[ Action Druid] [SThief] -> [SDruid] [Thief Sfx1]

[ Action Fighter] [SPriest] -> [SFighter] [Priest Sfx1]
[ Action Thief] [SBard] -> [SThief] [Bard Sfx1]
[ Action Wizard] [SDruid] -> [SWizard] [Druid Sfx1]
[ Action Priest] [SFighter] -> [SPriest] [Fighter Sfx1]
[ Action Bard] [SThief] -> [SBard] [Thief Sfx1]
[ Action Druid] [SWizard] -> [SDruid] [Wizard Sfx1]

[ Action Fighter] [SBard] -> [SFighter] [Bard Sfx1]
[ Action Thief] [SDruid] -> [SThief] [Druid Sfx1]
[ Action Wizard] [SFighter] -> [SWizard] [Fighter Sfx1]
[ Action Priest] [SThief] -> [SPriest] [Thief Sfx1]
[ Action Bard] [SWizard] -> [SBard] [Wizard Sfx1]
[ Action Druid] [SPriest] -> [SDruid] [Priest Sfx1]

[ Action Fighter] [SDruid] -> [SFighter] [Druid Sfx1]
[ Action Thief] [SFighter] -> [SThief] [Fighter Sfx1]
[ Action Wizard] [SThief] -> [SWizard] [Thief Sfx1]
[ Action Priest] [SWizard] -> [SPriest] [Wizard Sfx1]
[ Action Bard] [SPriest] -> [SBard] [Priest Sfx1]
[ Action Druid] [SBard] -> [SDruid] [Bard Sfx1]

(Bard plays music. This is my favorite part of the code, btw. Yes, you can make square area effects with just one object!)
[no Note | no Note | Bard] -> [^ Note | ^ Note | Bard]
[no Note | Bard] -> [^ Note | Bard]
[> Note | no Note | no Note] -> [Note | Note | Note]
[> Note | no Note] -> [Note | Note]
[> Note] -> [Note]

(Now enemies attack. Yeah, there's only one kind of enemy now.)

Late [Goblin | Vulnerable] -> [Goblin | Dying Vulnerable]

(Vulnerable objects that are attacked are designated as "Dying", but heroes can be saved by the Priest. Then the stuff that didn't get saved dies.)

Late [Dying Healer] -> [Saved Healer]
Late [Healer | Dying Hero] -> [Healer | Saved Hero]
Late [Dying Monster] -> [Sfx5]
Late [Dying Hero] -> [Corpse Sfx6]

(Doors opening.)

late [Weighing  YellowSwitch] [YellowDoor] -> [Weighing  YellowSwitch] [ShadowDoor]
late [YellowSwitch no Weighing] [YellowOpen] -> [YellowSwitch] [YellowDoor sfx2]
late [YellowSwitch no Weighing] [ShadowDoor] -> [YellowSwitch] [YellowDoor]
late [ShadowDoor] -> [YellowOpen sfx3]

late [Weighing PurpleSwitch] [PurpleDoor] -> [Weighing PurpleSwitch] [ShadowDoor]
late [PurpleSwitch no Weighing] [PurpleOpen] -> [PurpleSwitch] [PurpleDoor sfx2]
late [PurpleSwitch no Weighing] [ShadowDoor] -> [PurpleSwitch] [PurpleDoor]
late [ShadowDoor] -> [PurpleOpen sfx3]

late [MonsterDoor] -> [ShadowDoor]
late [ShadowDoor] [Monster] -> [MonsterDoor] [Monster]
late [ShadowDoor] -> [sfx3]

(Looks for a HasSavedDoor and, if there is one, puts a warpball on the Priest. Then, if someone was saved this turn, plays the sound only if the Priest doesn't have the warpball. Finally, cleans that up.)

late [HasSavedDoor] [Healer] -> [Goal] [Healer WarpBall]
late [Saved] [Healer no WarpBall] -> [Saved sfx4] [Healer]
late [HasSavedDoor] -> [Goal]
late [WarpBall] -> []

late [Note no Moveable] -> []

==============
WINCONDITIONS
==============

All Hero on Goal
All Goal on Hero

=======
LEVELS
=======

Message Many years have passed since the events of Heroes of Sokoban II.

Message Dragons have vanished from the realm, never to be seen again.

Message And new heroes have cropped up, as if there were only room for so many beings in one world.

###################
#.F.#.........#**G#
#***#...*****.#..*#
#.G.#...*...*.#2..#
#####...*...*.#####
#.......*...*.....#
#.......*...*.....#
#####..**..**.#####
#*..#.***.***.#..G#
#*G*#..*...*..#.O.#
#3.*#.........#4..#
###################

Message Purple bard charms nearby crates.

#############
#{#....{#...#
#.#.*#B.[.G.#
#*#.....#...#
#############

###############
#..############
#.B..****.....#
#..######.#####
#########G#####
###############

Message Monsters may also be charmed.

#############
#.......#...#
#...#...#...#
#O..B..OM.G.#
#...#...#...#
#.......#...#
#############

Message No one can resist the bard's music.

############
#G#.F.{..O.#
#.[........#
#G#.5.{..O.#
############

############
##[[[[[[[###
##[#####[###
##[##[[[[{*#
#...#[######
#.T.#......#
#...#.5.G.G#
############

########
#*G{####
###.####
#...[W##
###.#.5#
#*G*####
########

#########
#....G#G#
#.#*..#[#
#.**....#
#.......#
#.{4{B{.#
#...{...#
#########

############
#*.{..{.**{#
############
#B21.......#
##########[#
#.G..G..G..#
############

Message Orange druid turns stone into wood, wood into stone.

#############
#.[.{.2.#.D.#
#.#########.#
#....G.G..#.#
#############

#############
#...#####{{{#
#.D.##...{{{#
#...##.##{{{#
#############
#...#..######
#.1.#..##G.G#
#...#..#[...#
#############

#############
#O*....{...3#
##.####.#####
##.###O.O##G#
##.####.###.#
##D[....###G#
#############

Message Living things may also be turned into stone.

Message Try not to think about the moral implications, and don't do it any more than necessary.

#############
####.4..##G.#
#O..F..OM#.G#
####.6..##G.#
#############

###############
#.............#
#.5.}}}.]]]]].#
#...}}}.]G.G].#
#.D.}}}.]]]]].#
#.............#
###############

#########
#G#####W#
#########
#G#####6#
#########

#############
#......O.G.G#
#.###########
#.###########
#...#########
#B.6#########
#############

################
#...#...########
#.G.#.}.....{[O#
#...#.F.########
#...#...########
#.G.].4.########
#...#...########
#...#.5.########
#.G.#.}.....{[O#
#...#...########
################

#####M########
#G.GM#F.2###O#
#...#M.......#
#G.GM#3.6###O#
#####M########

############
#6######B###
#........###
############
#.#######.4#
#.......##1#
#G.G.G.G##O#
############

###############
#....#O.O#...G#
#..##W...1##..#
#..#O..O..O#.G#
#4.#..OGO..#..#
#..#O..O..O#.G#
#..##2...6##..#
#....#O.O#...G#
###############

Message Get ready.

#######################
#.F.*....#G.G#....*.4.#
#...#....#...#....#...#
#######.........#######
#####.#####O#####.#####
#..##O#..#...#..#O##..#
#.2.*....#G.G#....*.5.#
#..##O#..*...#..#O##..#
#####.####...####.#####
#######..#..*O.*#######
#...###.#######...#...#
#.3.###.##G.G##...#.6.#
#######################

################
#..............#
#.1.4......G.G.#
#..............#
#.2.B......G.G.#
#..............#
#.3.6......G.G.#
#..............#
################

Message Thanks for playing!

title It Dies In The Light
author Christopher Wells
homepage www.tophwells.com

noaction
run_rules_on_level_start
again_interval 0.05

========
OBJECTS
========

BackgroundNeutral
LIGHTGray Gray
11011
10101
01110
10101
11011

BackgroundDark
Black

Wall
Lightblue Blue
00010
11111
01000
11111
00010

LivePlayer
Black LightRed White Blue
.000.
.111.
12321
.232.
.222.

Corpse
LightGrey
.000.
.0.0.
0.0.0
..0..
.0.0.

Crate
Orange LightBrown Brown
.222.
21012
20102
21012
.222.

GoldCrate
White Yellow Orange
.222.
21012
20002
21012
.222.

GlassCrate
White
.000.
0...0
0...0
0...0
.000.

Prism
LightBlue White
.000.
00110
00010
00000
.000.

ActivePrism
LightBlue White Yellow
.000.
02120
01110
02120
.000.

Glass
White
00000
0...0
0...0
0...0
00000

BeamVert
White Yellow
.101.
.101.
.101.
.101.
.101.

BeamHor
White Yellow
.....
11111
00000
11111
.....

BeamCross
White Yellow
.101.
11011
00000
11011
.101.

Shadow
Purple
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

TempShadow
Purple
0.0.0
.0.0.
0.0.0
.0.0.
0.0.0

=======
LEGEND
=======

. = BackgroundNeutral
# = Wall
P = LivePlayer
C = Crate
G = GoldCrate
S = Shadow
X = Prism
O = Glass

Background = BackgroundNeutral or BackgroundDark
Player = LivePlayer or Corpse
Light = BeamVert or BeamHor or BeamCross
Hlight = BeamHor or BeamCross
Vlight = BeamVert or BeamCross
Opaque = Player or Crate or Wall or GoldCrate
Pushable = Crate or GoldCrate or Prism or GlassCrate


=======
SOUNDS
=======

SFX0 93002904

================
COLLISIONLAYERS
================

Background
BeamVert, BeamHor, BeamCross, Shadow, TempShadow
Player, Corpse, Wall, Crate, GoldCrate, GlassCrate, Prism, ActivePrism, Glass

======
RULES
======

[> Corpse] -> RESTART
[Light] -> []
[ActivePrism] -> [Prism]
[ >  Player | Pushable ] -> [  >  Player | > Pushable  ]
late HORIZONTAL [GoldCrate|no Opaque no Light] -> [GoldCrate | BeamHor]
late VERTICAL [GoldCrate|no Opaque no Light] -> [GoldCrate | BeamVert]
late VERTICAL [GoldCrate|no Opaque BeamHor] -> [GoldCrate | BeamCross]
late HORIZONTAL [Hlight|no Opaque no Light] -> [Hlight|BeamHor]
+ late HORIZONTAL [Hlight|no Opaque BeamVert] -> [Hlight|BeamCross]
+ late VERTICAL [Vlight|no Opaque no Light] -> [Vlight|BeamVert]
+ late VERTICAL [Vlight|no Opaque BeamHor] -> [Vlight|BeamCross]
+ late [Light Prism] ->[BeamCross ActivePrism]
late [Shadow Pushable] -> [Pushable]
late [TempShadow] -> [Shadow]
late [Shadow | no Light no Wall no Pushable no Glass no Shadow] -> [shadow | Tempshadow] again
late [Shadow] -> [Shadow BackgroundDark]
late [TempShadow] -> [TempShadow BackgroundDark]
late [no Shadow no TempShadow] -> [no Shadow BackgroundNeutral]
late [Shadow Player] -> [Shadow Corpse]

==============
WINCONDITIONS
==============

no Shadow

=======
LEVELS
=======

......####.
..#####ss#.
###ssssss#.
#..ssscss##
#..#s#s#ss#
#.g#s#ssss#
#p.#s#ssss#
#..css#sss#
###########

........########.
........#...#.C#.
...######...GC.##
...#..CsssssO.C.#
...#P.Csss#sO.CC#
...#..Csss#sO#..#
########ss#s....#
#ssssssOss#s#####
#sssssssss###....
#########s#......
........###......

##########
###G#G#G##
##P......#
#G.......#
##..C....#
#G.......#
##..C....#
#G.......#
##......S#
##########

........########
........#......#
#########..#o#.#
#........gooxo.#
#g#####c#.sso#s#
#.ssssss#.ss.ss#
##soooss#.ss##s#
##c.p.c###ss#ss#
##.....#ssss#ss#
#...g.......#ss#
#########.xx#ss#
........##sssss#
.........#######

MESSAGE Earth is safe once again.

title ScriptCross
author Dennis Au
homepage www.puzzlescript.net

========
OBJECTS
========

Background
White

Target
Black

Fill
Black

Cross
LightGrey
0...0
.0.0.
..0..
.0.0.
0...0


Player
Green
00000
0...0
0...0
0...0
00000

Border #
LightGrey

(Clues 0-9)
Clue0 0
LightGrey

Clue1 1
LightGrey Black
00000
0...0
0.1.0
0...0
00000

Clue2 2
LightGrey Black
00000
0..10
0...0
01..0
00000

Clue3 3
LightGrey Black
00000
0..10
0.1.0
01..0
00000

Clue4 4
LightGrey Black
00000
01.10
0...0
01.10
00000

Clue5 5
LightGrey Black
00000
01.10
0.1.0
01.10
00000

Clue6 6
LightGrey Black
00000
01.10
01.10
01.10
00000

Clue7 7
LightGrey Black
00000
01110
0.1.0
01110
00000

Clue8 8
LightGrey Black
00000
01110
0.110
01110
00000

Clue9 9
LightGrey Black
00000
01110
01110
01110
00000


=======
LEGEND
=======

. = Background
P = Player and Border
b = Target
Clue = Clue0 or Clue1 or Clue2 or Clue3 or Clue4 or Clue5 or Clue6 or Clue7 or Clue8 or Clue9
NoEmpty = Fill or Cross or Clue or Border

=======
SOUNDS
=======
EndLevel 37348103
EndGame 64173909

================
COLLISIONLAYERS
================

(switch order of target and background to see the solutions)
Target
Background
Border, Clue, Fill, Cross
Player


======
RULES
======

[Action Player no NoEmpty] -> [Player Fill]
[Action Player Fill] -> [Player Cross]
[Action Player Cross] -> [Player]



==============
WINCONDITIONS
==============

All Target on Fill
All Fill on Target

=======
LEVELS
=======

(P123456789)

message level 1 - oh so stable

P12
1.b
2bb

message level 2 - it is what it is
P#1###
##113#
##311#
3.bbb.
1...b.
3.bbb.
1.b...
3.bbb.

message level 3 - i see you!
P##1#1#
###111#
##.....
11.b.b.
##.....
#3.bbb.
##.....

message level 4 - that's ace
P#######1####
#######111###
######14141##
#####8111118#
###8.bbbbbbb.
##11.b.....b.
#131.b.bbb.b.
1111.b.b.b.b.
#131.b.bbb.b.
1111.b.b.b.b.
##11.b.....b.
###8.bbbbbbb.
####.........

message congratulations!


title Match 3 Block Push
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Background
lightgreen green
11111
01111
11101
11111
10111


Target
darkblue
.....
.000.
.0.0.
.000.
.....

Wall
brown darkbrown
00010
11111
01000
11111
00010

Player
black orange white blue
.000.
.111.
22222
.333.
.3.3.

Crate
orange yellow
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

[ >  Player | Crate ] -> [  >  Player | > Crate  ]

late [ Crate | Crate | Crate ] -> [ | | ]

==============
WINCONDITIONS
==============

all Crate on Target

=======
LEVELS
=======

#########
#.......#
#.*.*.@.#
#.P...O.#
#...*.*.#
#.......#
#########

.#######.
.#.....#.
.#O##..##
##.##.*.#
#.*..**.#
#.OOO.#.#
#.P.....#
#########


title Robot Spawner
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Background (special, automatically placed in its own layer)
green darkgreen
11111
01111
11101
11111
10111

Spawner (Name)
yellow 	(Color)
..... 	(Sprite)
..0..
.000.
..0..
.....

Player (Required by game)
pink
..0..
00000
..0..
.000.
.0.0.

RedRobot
gray black red
.000.
.200.
...0.
00000
01010

GreenRobot
gray black lightgreen
.000.
.200.
...0.
00000
01010

BlueRobot
gray black lightblue
.000.
.200.
...0.
00000
01010

=======
LEGEND
=======

Robot = RedRobot or GreenRobot or BlueRobot
Obstacle = Player or Robot
. = Background
P = Player
* = Robot
O = Spawner

=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Background
Spawner
Player, Robot

======
RULES
======

[ stationary Robot ] -> [ randomDir Robot ] 
[ Spawner no Obstacle ] -> [ Spawner random Robot ] 

==============
WINCONDITIONS
==============

=======
LEVELS
=======

.......
.......
.......
...O...
.......
.P.....
.......


title neko puzzle
author lexaloffle
homepage www.lexaloffle.com

(

An inadequate but illustrative demake of one of my favourite puzzle games ever, by the extremely amicable Lexaloffle, and with his permission.

www.lexaloffle.com/neko.htm

The game proper has a lot of magic and love in it.

)

========
OBJECTS
========

Background .
darkblue

Fruit F
red green lightred
..1..
.000.
00020
00000
.000.

Player P
gray white
.1.1.
10101
11111
.111.
00.00

=======
LEGEND
=======

=========
SOUNDS
=========

endlevel 97301508
startgame 97301508
endgame 97301508

================
COLLISIONLAYERS
================

Background
Player, Fruit

======
RULES
======

[ > Player | ... | Fruit ] -> [ | ... | Player ] 

(if you're not sure what this line does, try removing it, and see what happens)
[ > Player ] -> [ Player ] 

==============
WINCONDITIONS
==============

no Fruit

=======
LEVELS
=======

message level 1 of 10

......F.
........
F......F
......FF
........
........
P.......

message level 2 of 10

........
....P...
...F.F..
........
........
..F.FF..
........

message level 3 of 10

........
.FF....F
........
........
F...P...
........
........
F.F.....

message level 4 of 10

....F..F
........
.......F
F.......
P.......
........
F...F...

message level 5 of 10

........
....F.P.
F.F.....
........
F...F...
..F.F...
........

message level 6 of 10

F.....F.
........
........
FF..F...
F.....F.
........
.P......

message level 7 of 10

...FP..F
F.F.....
F......F
........
........
..F.....
........

message level 8 of 10

..F..P..
........
........
.FFF....
F..F..F.
........
........

message level 9 of 10

....F..F
........
.F..F...
........
.F.P..F.
.F......
........

message level 10 of 10

....F.F.
........
.F..F..F
...F..F.
.......F
...FP...
........

message congratulations!


USER PROMPT:
Output the code for a complete PuzzleScript game. The game should be a simplified `demake` of the following game idea: **Olive Odyssey:** Uncover ancient secrets and complex recipes by harvesting ingredients and cooking them into magical, story-altering dishes..  First, reason about your task and determine the best plan of action. Then, write your code. Return your code in full, inside a ```plaintext code block.