SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title neko puzzle
author lexaloffle
homepage www.lexaloffle.com

(

An inadequate but illustrative demake of one of my favourite puzzle games ever, by the extremely amicable Lexaloffle, and with his permission.

www.lexaloffle.com/neko.htm

The game proper has a lot of magic and love in it.

)

========
OBJECTS
========

Background .
darkblue

Fruit F
red green lightred
..1..
.000.
00020
00000
.000.

Player P
gray white
.1.1.
10101
11111
.111.
00.00

=======
LEGEND
=======

=========
SOUNDS
=========

endlevel 97301508
startgame 97301508
endgame 97301508

================
COLLISIONLAYERS
================

Background
Player, Fruit

======
RULES
======

[ > Player | ... | Fruit ] -> [ | ... | Player ] 

(if you're not sure what this line does, try removing it, and see what happens)
[ > Player ] -> [ Player ] 

==============
WINCONDITIONS
==============

no Fruit

=======
LEVELS
=======

message level 1 of 10

......F.
........
F......F
......FF
........
........
P.......

message level 2 of 10

........
....P...
...F.F..
........
........
..F.FF..
........

message level 3 of 10

........
.FF....F
........
........
F...P...
........
........
F.F.....

message level 4 of 10

....F..F
........
.......F
F.......
P.......
........
F...F...

message level 5 of 10

........
....F.P.
F.F.....
........
F...F...
..F.F...
........

message level 6 of 10

F.....F.
........
........
FF..F...
F.....F.
........
.P......

message level 7 of 10

...FP..F
F.F.....
F......F
........
........
..F.....
........

message level 8 of 10

..F..P..
........
........
.FFF....
F..F..F.
........
........

message level 9 of 10

....F..F
........
.F..F...
........
.F.P..F.
.F......
........

message level 10 of 10

....F.F.
........
.F..F..F
...F..F.
.......F
...FP...
........

message congratulations!


title Microban
author David Skinner
homepage www.sneezingtiger.com/sokoban/levels/microbanText.html

(My favourite set of sokoban levels - here're the first ten of the Microban set.

I tried contacting this guy, but he seems to have vanished from the net. The levels are in lots of places online, so I'm just chancing my arm by including them.  BUT BE WARNED.)

========
OBJECTS
========

Background
lightgreen green
11111
01111
11101
11111
10111

Target
darkblue
.....
.000.
.0.0.
.000.
.....

Wall
brown darkbrown
00010
11111
01000
11111
00010

Player
black orange white blue
.000.
.111.
22222
.333.
.3.3.

Crate
orange
00000
0...0
0...0
0...0
00000

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target

=======
SOUNDS
=======

Crate MOVE 36772507
endlevel 83744503
startgame 92244503

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

[ > Player | Crate ] -> [ > Player | > Crate ] 

==============
WINCONDITIONS
==============

all Target on Crate

=======
LEVELS
=======

message level 1 of 10

####..
#.O#..
#..###
#@P..#
#..*.#
#..###
####..

message level 2 of 10

######
#....#
#.#P.#
#.*@.#
#.O@.#
#....#
######

message level 3 of 10

..####...
###..####
#.....*.#
#.#..#*.#
#.O.O#P.#
#########

message level 4 of 10

########
#......#
#.O@@*P#
#......#
#####..#
....####

message level 5 of 10

.#######
.#.....#
.#.O*O.#
##.*P*.#
#..O*O.#
#......#
########

message level 6 of 10

######.#####
#....###...#
#.**.....#P#
#.*.#OOO...#
#...########
#####.......

message level 7 of 10

#######
#.....#
#.O*O.#
#.*O*.#
#.O*O.#
#.*O*.#
#..P..#
#######

message level 8 of 10

..######
..#.OOP#
..#.**.#
..##.###
...#.#..
...#.#..
####.#..
#....##.
#.#...#.
#...#.#.
###...#.
..#####.

message level 9 of 10

#####.
#O..##
#P**.#
##...#
.##..#
..##O#
...###

message level 10 of 10

......#####
......#O..#
......#O#.#
#######O#.#
#.P.*.*.*.#
#.#.#.#.###
#.......#..
#########..

message congratulations!


title Manic Ammo
author David Eastman
homepage @eastmad

========
OBJECTS
========

Background
gray

Target
darkgray gray lightgray white
33333
31112
31002
31002
32222

Wall
BROWN

Boom
Red YELLOW orange
.101.
10001
00200
10001
.101.


Player
PINK YELLOW BLACK
.222.
.000.
22122
.222.
.2.2.


CrateW
Brown DarkBrown lightbrown
11111
12001
11111
12001
11111

CrateN
Brown DarkBrown lightbrown
11111
12121
10101
10101
11111

CrateMineN
Brown DarkBrown lightbrown darkblue
11111
12121
10101
10301
11111

CrateGunN
Brown DarkBrown lightbrown darkred
11111
12121
10101
10301
11111

CrateGunS
Brown DarkBrown lightbrown darkred
11111
12321
10101
10101
11111


CrateS
Brown DarkBrown lightbrown
11111
10101
10101
12121
11111

CrateE
Brown DarkBrown lightbrown
11111
10021
11111
10021
11111


Shell
yellow orange
..0..
.000.
.000.
.000.
.101.


Bang
Yellow LightRed
.101.
10001
00000
10001
.101.

GunN
red lightred
..0..
..0..
.101.
00000
.000.

GunW
red lightred
...0.
..100
00000
..100
...0.

GunS
red lightred
.000.
00000
.101.
..0..
..0..

GunE
red lightred
.0...
001..
00000
001..
.0...

InertMine
darkblue blue lightblue
.....
.101.
.020.
.101.
.....

ActiveMine
darkred red lightred
.....
.101.
.020.
.101.
.....

Squawk
lightblue blue
....0
.0000
.0000
.11.0
1111.

SquawkBang
orange lightblue
....0
.0000
.0000
.11.0
1111.

Droid
black green
..0..
.000.
00100
.000.
..0..


=======
LEGEND
=======

. = Background
# = Wall
P = Player
N = CrateN
E = CrateE
W = CrateW
S = CrateS
O = Target
+ = Shell
^ = GunN
< = GunW
> = GunE
Ë… = GunS
M = InertMine
A = ActiveMine
B = Boom
% = CrateGunN
& = CrateMineN
@ = CrateGunS
X = Squawk
D = Droid

=======
SOUNDS
=======

SFX1 55891909
SFX2 5736502
SFX4 6922102

Shell move 61960307
InertMine move 61960307
Droid move 81713505
EndLevel 8341708
Shell create 91688700
GunN create 91688700
GunS create 91688700
GunW create 91688700
InertMine create 91688700
ActiveMine create 93182703

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, CrateN, CrateE, CrateW, CrateS, Shell, GunN, GunW, GunE, GunS, Bang, InertMine, ActiveMine, Boom, CrateMineN, CrateGunN, CrateGunS, Squawk, Droid, SquawkBang

======
RULES
======


[Bang] -> [background]
[boom] -> restart
[> player][Droid] -> [> player][> Droid]
[ >  Player | Shell ] -> [  >  Player | > Shell  ]
[ >  Droid | Shell ] -> [  >  Droid | > Shell  ]
[ >  Player | InertMine ] -> [  >  Player | > InertMine  ]
[Action Player|InertMine] -> [Action Player|ActiveMine]
[> Droid|InertMine] -> [Droid|ActiveMine]
[> Player | ActiveMine] -> [Player | boom ]
[> Droid | ActiveMine] -> [Droid | boom ]
[> shell | ActiveMine] -> [boom | boom ]

[Action Player|Squawk] -> [Action Player|SquawkBang]
[> Droid|Squawk] -> [Droid |SquawkBang]

[SquawkBang][CrateMineN] -> [SquawkBang][ActiveMine]
[SquawkBang][CrateGunN] -> [SquawkBang][GunN]
[SquawkBang][CrateGunS] -> [SquawkBang][GunS]
[SquawkBang][CrateN] -> [SquawkBang][Shell]
[SquawkBang][CrateS] -> [SquawkBang][Shell]
[SquawkBang][CrateE] -> [SquawkBang][Shell]
[SquawkBang][CrateW] -> [SquawkBang][Shell]
[SquawkBang] -> [] SFX1

DOWN [ Action Player |CrateN] -> [Action Player | Shell]
UP [Action Player | CrateS] -> [Action Player | Shell]
LEFT [Action Player | CrateE] -> [Action Player | Shell]
RIGHT [Action Player | CrateW] -> [Action Player | Shell]
DOWN [Action Player | CrateGunN] -> [Action Player | GunN]
UP [Action Player | CrateGunS] -> [Action Player | GunS]
DOWN [Action Player | CrateMineN] -> [Action Player | ActiveMine]

UP [Action Player | GunN| No Bang ] -> [Player | GunN | Bang ] SFX4
LEFT [Action Player | GunW | No Bang] -> [Player | GunW | Bang ] SFX4
DOWN [Action Player | GunS| No Bang ] -> [Player | GunS | Bang ] SFX4
RIGHT [Action Player | GunE | No Bang] -> [Player | GunE | Bang ] SFX4

UP [ > Droid | GunN| No Bang ] -> [Droid | GunN | Bang ] SFX4
LEFT [ > Droid | GunW | No Bang] -> [Droid | GunW | Bang ] SFX4
DOWN [> Droid | GunS| No Bang ] -> [Droid | GunS | Bang ] SFX4
RIGHT [> Droid | GunE | No Bang] -> [Droid | GunE | Bang ] SFX4
[bang|shell] -> [bang|bang ]
[boom|no player] -> [boom|boom] SFX2


==============
WINCONDITIONS
==============

All Player on Target

=======
LEVELS
=======



MESSAGE Due to a clerical error, You are trapped in a bunker

########
#.....P#
#......#
#......#
#......#
#O.....#
########

MESSAGE A bunker with several levels, full of crates and munitions

########
#.....O#
#..+...#
#......#
#..N...#
#P.....#
########

MESSAGE Crates open from one side

########
#...+.P#
#....+.#
#..++..#
#++..++#
#.ON...#
########

MESSAGE But shells are volatile
MESSAGE Try firing the gun

#########
#..+.+.O#
#..N+...#
#....S..#
#...^...#
#.P.....#
#########

MESSAGE Chain reaction

#########
#+.+O+.P#
#+.++...#
#.+.+.N.#
#+......#
#E+++.<.#
#########

MESSAGE Inert mines, active mines

#########
#+.+P+..#
#.......#
#.......#
#SSSSM..#
#.O.E...#
#########

Message Crates can have any munitions in them

#########
#.......#
#.&+..+.#
#.+O&.+.#
#.+++%..#
#.P.S...#
#########


MESSAGE There seems to be another way to open crates

#########
#.X.....#
#.......#
#..P....#
#....NNN#
#.>..SON#
#########


MESSAGE Hey, it's a friendly worker droid!

#########
#.D...PN#
#..+...N#
#A.+....#
#.NN.O..#
#..+....#
#########


MESSAGE Now this is where the main bunker levels begin.

###########
#O+N..+.W.#
#SN&..+.NX#
#.DN.+.+..#
#%.NWP.NS+#
#...+N....#
###########


MESSAGE This doesn't look safe

###########
#P..&&&&&&#
#DX.N....&#
#...&&&&&&#
#.N.N...O.#
#&&&&&&&&&#
###########


MESSAGE Carefully packed

###########
#PESW.WDW.#
#NW..&N@.N#
#.N%NWN.X.#
#N...NNNNN#
#N.NNNNNON#
###########

MESSAGE Symbiosis

###########
#P.NNN.DN.#
#MM>.+++N.#
#..N.NN+..#
#++N++....#
#++...<.O.#
###########

MESSAGE Inert Mine field

###########
#.P..M.M..#
#.MMM.M.MM#
#..M..MNM.#
#MM.MM.MOS#
#.M..MD...#
###########

MESSAGE Last level

###########
#..+..P.+.#
#.N..@.N..#
#+.&..E.&.#
#++..S.SSS#
#.X+&..NO.#
###########

MESSAGE Congratulations! Now you can return to fighting on the front line.

title Sokobond: The Demake
author Jonah Ostroff
homepage www.sokobond.com
text_color #202020
background_color #FDFDFD
key_repeat_interval 0.25
run_rules_on_level_start
noaction
youtube 2R0sTlruOZc

========
OBJECTS
========

Background
#FFFFFF #F8F8F8
00000
01010
00000
01010
00000

Player
#FFBBEE
00000
0...0
0...0
0...0
00000

Wall
Yellow White
00000
00000
00000
00000
00000

Hydrogen
Red Black
.....
.111.
.101.
.111.
.....

Nitrogen
Green Black
.....
.111.
.101.
.111.
.....

Oxygen
Blue Black
.....
.111.
.101.
.111.
.....

Carbon
Yellow Black
.....
.111.
.101.
.111.
.....

Helium
White Black
.....
.111.
.101.
.111.
.....

1Electron
LightGray
.....
.0...
.....
.....
.....

2Electron
LightGray
.....
...0.
.....
.0...
.....

3Electron
LightGray
.....
.0.0.
.....
.0...
.....

4Electron
LightGray
.....
.0.0.
.....
.0.0.
.....

BondU
Black
..0..
.....
.....
.....
.....

BondL
Black
.....
.....
0....
.....
.....

BondR
Black
.....
.....
....0
.....
.....

BondD
Black
.....
.....
.....
.....
..0..


BondUL
Black
..0..
.....
0....
.....
.....

BondUR
Black
..0..
.....
....0
.....
.....

BondUD
Black
..0..
.....
.....
.....
..0..

BondLR
Black
.....
.....
0...0
.....
.....

BondLD
Black
.....
.....
0....
.....
..0..

BondRD
Black
.....
.....
....0
.....
..0..

BondULR
Black
..0..
.....
0...0
.....
.....

BondULD
Black
..0..
.....
0....
.....
..0..

BondURD
Black
..0..
.....
....0
.....
..0..

BondLRD
Black
.....
.....
0...0
.....
..0..

BondULRD
Black
..0..
.....
0...0
.....
..0..

Temp1
Red

Temp2 (These are placeholders for various things.)
Green

Temp3
Blue

Temp4
Yellow

=======
LEGEND
=======

Atom = Hydrogen or Oxygen or Nitrogen or Carbon or Helium
Orbital = 1Electron or 2Electron or 3Electron or 4Electron
UBond = BondU or BondUL or BondUR or BondUD or BondULR or BondULD or BondURD or BondULRD
LBond = BondL or BondUL or BondLR or BondLD or BondULR or BondULD or BondLRD or BondULRD
RBond = BondR or BondUR or BondLR or BondRD or BondULR or BondURD or BondLRD or BondULRD
DBond = BondD or BondUD or BondLD or BondRD or BondULD or BondURD or BondLRD or BondULRD
1Bond = BondU or BondL or BondR or BondD
2Bond = BondUL or BondUR or BondUD or BondLR or BondLD or BondRD
3Bond = BondULR or BondULD or BondURD or BondLRD
Bond = 1Bond or 2Bond or 3Bond or BondULRD
Temps = Temp1 or Temp2 or Temp3 or Temp4
AtomStuff = Bond or Orbital

. = Background
# = Wall
H = Hydrogen and 1Electron
O = Oxygen and 2Electron
N = Nitrogen and 3Electron
C = Carbon and 4Electron
E = Helium
0 = Helium  and Player
1 = Hydrogen and 1Electron and Player
2 = Oxygen and 2Electron and Player
3 = Nitrogen and 3Electron and Player
4 = Carbon and 4Electron and Player
P = Player

=======
SOUNDS
=======

Sfx1 64050702 (Bonding sound)
EndLevel 96545300 (End level)

================
COLLISIONLAYERS
================

Background
Player
Bond
Wall, Atom
Orbital
Temps

======
RULES
======

(This is the old way I did movement, and it was super inefficient.)
([> Player Atom] -> [> Player > Atom]

Up [ Moving Atom UBond | Atom] -> [ Moving Atom UBond | Moving Atom]
+ Left [ Moving Atom LBond | Atom] -> [ Moving Atom LBond | Moving Atom]
+ Right [ Moving Atom RBond | Atom] -> [ Moving Atom RBond | Moving Atom]
+ Down [ Moving Atom DBond | Atom] -> [ Moving Atom DBond | Moving Atom]
+ [> Atom | Atom] -> [> Atom | > Atom])

[Up Player] -> [Up Player Temp1]
[Left Player] -> [Left Player Temp2]
[Right Player] -> [Right Player Temp3]
[Down Player] -> [Down Player Temp4]

Up [Temps UBond | no Temps] -> [Temps Ubond | Temps]
+ Left [Temps LBond | no Temps] -> [Temps Lbond | Temps]
+ Right [Temps RBond | no Temps] -> [Temps Rbond | Temps]
+ DOwn [Temps DBond | no Temps] -> [Temps Dbond | Temps]
+ Up [Temp1 | Atom no Temp1] -> [Temp1 | Atom Temp1]
+ Left [Temp2 | Atom no Temp2] -> [Temp2 | Atom Temp2]
+ Right [Temp3 | Atom no Temp3] -> [Temp3 | Atom Temp3]
+ Down [Temp4 | Atom no Temp4] -> [Temp4 | Atom Temp4]

[Temp1 Atom] -> [Up Atom]
[Temp2 Atom] -> [Left Atom]
[Temp3 Atom] -> [Right Atom]
[Temp4 Atom] -> [Down Atom]

[ > Atom | Wall] -> Cancel

[ > Atom AtomStuff] -> [ > Atom > AtomStuff]

Late Right [Orbital no Temps no RBond|Orbital no Temps no LBond] -> [Orbital Temp1|Orbital Temp2 Sfx1] (Bond horizontally.)
+ Late [Temp1 no Bond] -> [Temp3 BondR]
+ Late [Temp1 BondU] -> [Temp3 BondUR]
+ Late [Temp1 BondL] -> [Temp3 BondLR]
+ Late [Temp1 BondD] -> [Temp3 BondRD]
+ Late [Temp1 BondUL] -> [Temp3 BondULR]
+ Late [Temp1 BondUD] -> [Temp3 BondURD]
+ Late [Temp1 BondLD] -> [Temp3 BondLRD]
+ Late [Temp1 BondULD] -> [Temp3 BondULRD]
+ Late [Temp2 no Bond] -> [Temp3 BondL]
+ Late [Temp2 BondU] -> [Temp3 BondUL]
+ Late [Temp2 BondR] -> [Temp3 BondLR]
+ Late [Temp2 BondD] -> [Temp3 BondLD]
+ Late [Temp2 BondUR] -> [Temp3 BondULR]
+ Late [Temp2 BondUD] -> [Temp3 BondULD]
+ Late [Temp2 BondRD] -> [Temp3 BondLRD]
+ Late [Temp2 BondURD] -> [Temp3 BondULRD]
+ Late [Temp3 1Electron] -> []
+ Late [Temp3 2Electron] -> [1Electron]
+ Late [Temp3 3Electron] -> [2Electron]
+ Late [Temp3 4Electron] -> [3Electron]

Late Down [Orbital no Temps no DBond|Orbital no Temps no UBond] -> [Orbital Temp1|Orbital Temp2 Sfx1] (Bond vertically.)
+ Late [Temp1 no Bond] -> [Temp3 BondD]
+ Late [Temp1 BondU] -> [Temp3 BondUD]
+ Late [Temp1 BondL] -> [Temp3 BondLD]
+ Late [Temp1 BondR] -> [Temp3 BondRD]
+ Late [Temp1 BondUL] -> [Temp3 BondULD]
+ Late [Temp1 BondUR] -> [Temp3 BondURD]
+ Late [Temp1 BondLR] -> [Temp3 BondLRD]
+ Late [Temp1 BondULR] -> [Temp3 BondULRD]
+ Late [Temp2 no Bond] -> [Temp3 BondU]
+ Late [Temp2 BondL] -> [Temp3 BondUL]
+ Late [Temp2 BondR] -> [Temp3 BondUR]
+ Late [Temp2 BondD] -> [Temp3 BondUD]
+ Late [Temp2 BondLR] -> [Temp3 BondULR]
+ Late [Temp2 BondLD] -> [Temp3 BondULD]
+ Late [Temp2 BondRD] -> [Temp3 BondURD]
+ Late [Temp2 BondLRD] -> [Temp3 BondULRD]
+ Late [Temp3 1Electron] -> []
+ Late [Temp3 2Electron] -> [1Electron]
+ Late [Temp3 3Electron] -> [2Electron]
+ Late [Temp3 4Electron] -> [3Electron]

==============
WINCONDITIONS
==============

No Orbital

=======
LEVELS
=======

Message This is a demake of Sokobond by Alan Hazelden and Harry Lee.

Message Get the full version at www.sokobond.com!

#########
####....#
####....#
#....#H.#
#...O#..#
#.1.....#
#....####
#########

Message Water atoms are icosahedral.

######
#H..O#
#....#
#1.###
######

Message The blue tint seen in large bodies of water comes from the pigment in the oxygen atoms.

########
###...##
#2.H.H.#
###...##
########

Message The Earth's surface and the human body are both 70% water. That CAN'T be a coincidence, right?

#######
#..O..#
#..#..#
#.1#H.#
#..#..#
#..O..#
#######

Message "One hydrogen per oxide" is defined as the speed at which atoms move.

#######
#.H.3.#
#.....#
#.H.H.#
#######

Message Ammonia is an inflammatory condition of the lungs.

#######
#.....#
#.....#
#H#H#H#
#.....#
#..3..#
#######

Message Hang on, I was thinking of "pneumonia".

#########
####4####
###...###
##.H.H.##
#.......#
##.H.H.##
###...###
####.####
#########

Message 90% of all the methane in the world is produced in Arizona.

#########
##H...H##
#...4...#
##H...H##
#########

Message Methane contains the name "Ethan". Do you think it was discovered by a guy named Ethan?

########
#......#
#...H..#
#O.H..3#
#...H..#
#......#
########

Message Hydroxylamine contains almost every vowel of the alphabet almost exactly once.

#########
#.......#
#..H.H..#
#.2...C.#
#..H.H..#
#.......#
#########

Message Methanol occurs naturally in peppermint oils.

#########
#.......#
#.1...E.#
###.H.###
###...###
###.O.###
#########

Message Helium is named after the Greek god Helios, who first discovered its existence.

#########
#.......#
#.E.E.E.#
###H.1###
###.H.###
###...###
###.N.###
#########

Message Inhaling helium contracts the ear drum, causing pitches to sound higher. Your voice is perceived normally by everyone else.

#######
###..##
##..H.#
#..N..#
#.H..H#
##..0.#
#######

Message The first helium balloons were used at the wedding of Queen Victoria and Prince Albert, popularizing the term "noble gas".

#######
#.....#
#..N..#
#..0..#
#H.H.H#
#.#.#.#
#.....#
#######

Message Helium does not form chemical bonds with other elements, but it's the bonds of friendship that really matter.

########
#......#
#...H#N#
#......#
##H#H.0#
##.....#
####...#
########

Message Scientists have been searching for centuries to discover the missing elements between hydrogen and helium on the periodic table.

Message Thanks for playing! Go get the full version, which has lots of cool stuff that I couldn't write in PuzzleScript: www.sokobond.com

title Dungeon Janitor
author Farbs
homepage www.farbs.org
noundo
noaction
key_repeat_interval 0.1

========
OBJECTS
========

Background
DARKBLUE BLACK
11111
10000
10000
10000
10000

Entry
LIGHTBROWN BROWN DARKBROWN GRAY
30103
10101
10121
10101
10101

ExitClosed
LIGHTBROWN BROWN DARKBROWN GRAY
30103
10101
10121
10101
10101

ExitOpen
GRAY BLACK DARKBLUE
01110
11111
11111
11111
12221

Slime
LIGHTGREEN GREEN DARKGREEN
.....
.....
.....
.....
.....

Head
LIGHTGREEN GREEN DARKGREEN BLACK
.000.
03130
11111
23332
.222.

SlimeU
LIGHTGREEN GREEN DARKGREEN
11111
21112
.222.
.....
.....

SlimeD
LIGHTGREEN GREEN DARKGREEN
.....
.....
.000.
01110
11111

SlimeUD
LIGHTGREEN GREEN DARKGREEN
11111
11111
11111
11111
11111

SlimeL
LIGHTGREEN GREEN DARKGREEN
00...
110..
111..
112..
22...

SlimeR
LIGHTGREEN GREEN DARKGREEN
...00
..011
..111
..211
...22

SlimeLR
LIGHTGREEN GREEN DARKGREEN
00000
11111
11111
11111
22222

SlimeUL
LIGHTGREEN GREEN DARKGREEN
11111
11112
1112.
112..
22...

SlimeUR
LIGHTGREEN GREEN DARKGREEN
11111
21111
.2111
..211
...22

SlimeULR
LIGHTGREEN GREEN DARKGREEN
11111
11111
11111
11111
22222

SlimeDL
LIGHTGREEN GREEN DARKGREEN
00...
110..
1110.
11110
11111

SlimeDR
LIGHTGREEN GREEN DARKGREEN
...00
..011
.0111
01111
11111

SlimeDLR
LIGHTGREEN GREEN DARKGREEN
00000
11111
11111
11111
11111

SlimeUDLR
LIGHTGREEN GREEN DARKGREEN
11111
11111
11111
11111
11111

SlimeSolo
LIGHTGREEN GREEN DARKGREEN
.000.
01110
11111
21112
.222.

Wall
LIGHTGREY GREY
11111
01000
01000
11111
00010

PlayerA
Black Orange White Blue Brown
.....
.11..
2224.
333.4
3.322

PlayerB
Black Orange White Blue Brown
.....
.11..
.4222
4.333
223.3

Temp
PINK
00000
00000
00000
00000
00000

=======
LEGEND
=======

. = Background
# = Wall
E = Wall AND Entry
P = PlayerA
O = Slime AND Head
X = Wall AND ExitClosed

SlimeDecal = SlimeU OR SlimeD OR SlimeUD OR SlimeL OR SlimeR OR SlimeLR OR SlimeUL OR SlimeUR OR SlimeULR OR SlimeDL OR SlimeDR OR SlimeDLR OR SlimeUDLR OR SlimeSolo
Player = PlayerA OR PlayerB

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Slime
SlimeU, SlimeD, SlimeUD, SlimeL, SlimeR, SlimeLR, SlimeUL, SlimeUR, SlimeULR, SlimeDL, SlimeDR, SlimeDLR, SlimeUDLR, SlimeSolo, ExitOpen
Player, Wall, PlayerA, PlayerB, Temp
Head, Entry, ExitClosed

======
RULES
======

(Stomp Heads)
[ > Player | Head ] -> [ > Player | NO Head ]
[ > Player | Slime ] -> [ > Player | NO Slime ]

(New Head)
[ Head ] -> []
RANDOM [ Slime NO Player ] -> [ Slime Head ]

(Grow Slime)
[ Head | NO Slime NO Wall NO Player ] -> [ Head | Slime ]

(Clear Slime from under player)
[ > Player | Slime ] -> [ > Player | NO Slime ]
LATE [ Player Slime ] -> [ Player NO Head ]

(Make slime pretty)
[ SlimeDecal ] ->[ ]
VERTICAL [ Slime | Slime | Slime ] -> [ Slime | Slime SlimeUD | Slime ]
UP [ Slime NO SlimeUD | Slime ] -> [ Slime SlimeU | Slime ]
DOWN [ Slime NO SlimeUD | Slime ] -> [ Slime SlimeD | Slime ]
HORIZONTAL [ Slime | Slime NO SlimeU NO SlimeD NO SlimeUD | Slime ] -> [ Slime | Slime SlimeLR | Slime ]
LEFT [ Slime NO SlimeU NO SlimeD NO SlimeUD NO SlimeLR | Slime ] -> [ Slime SlimeL | Slime ]
RIGHT [ Slime NO SlimeU NO SlimeD NO SlimeUD NO SlimeLR | Slime ] -> [ Slime SlimeR | Slime ]
HORIZONTAL [ Slime | Slime SlimeU | Slime ] -> [ Slime | Slime SlimeULR | Slime ]
LEFT [ Slime SlimeU | Slime ] -> [ Slime SlimeUL | Slime ]
RIGHT [ Slime SlimeU | Slime ] -> [ Slime SlimeUR | Slime ]
HORIZONTAL [ Slime | Slime SlimeD | Slime ] -> [ Slime | Slime SlimeDLR | Slime ]
LEFT [ Slime SlimeD | Slime ] -> [ Slime SlimeDL | Slime ]
RIGHT [ Slime SlimeD | Slime ] -> [ Slime SlimeDR | Slime ]
[ Slime NO SlimeDecal ] -> [ Slime SlimeSolo ]

( Shut the door if slime still around)
( Have to open it first unfortunately )
[ ExitClosed Wall ] -> [ ExitOpen ]
[ ExitOpen ][ Slime ] -> [ ExitClosed Wall ][ Slime ]

(Player Anim)
[ LEFT PlayerB ] -> [ LEFT PlayerA ]
[ RIGHT PlayerA ] -> [ RIGHT PlayerB ]
LATE [ PlayerA ] -> [ Temp ]
LATE [ PlayerB ] -> [ PlayerA ]
LATE [ Temp ] -> [ PlayerB ]
==============
WINCONDITIONS
==============

ALL Player ON ExitOpen

=======
LEVELS
=======

MESSAGE It's Thursday.
MESSAGE Today I mop the slime pit.


##X##
##.##
##.##
##O##
##.##
##P##
##E##

MESSAGE Damn slime sticking to my shoes.

..####..
..#O.#..
###..###
#......#
#P#..#.#
#E#..#X#
..####..

MESSAGE Goes SHLUCK SHCLUK when I walk.

######X#############
###......#...#...###
###.##.#...#...#...#
##.....###########.#
#..#.#.....#.......#
#.##.###.#.##.######
#......#.#.#.O.....#
#.####.#.#.#.#####.#
#P.....#.#.........#
#E##################

MESSAGE Last room before home.
MESSAGE This one can take a while.

##########X#########
#..................#
#..................#
#..#######.######..#
#..#.#...#.#.#..#..#
#..#...#.#.#.#..#..#
#..#.....#.#.#..#..#
#..##....#.#...##..#
#..#............#..#
#..#......O.....#..#
#..#............#..#
#..#............#..#
#..##############..#
#.........P........#
##########E#########

MESSAGE Done for the day. Back tomorrow.
MESSAGE Fridays is rat scrubbing day.

title Midas
author wanderlands
homepage https://web.archive.org/web/20160706204929/http://wanderlands.org/

(

A port of the luxuriant Wanderlands game, Midas, with permission of the authors.  It's a bit messy, if you can find a way to tidy it UP, let me know : ] 

the original is here
https://web.archive.org/web/20120219201941/http://wanderlands.org/main/midas

update: uh, it's flash, so not a playable link any more...also the original developer site is gone.

)

========
OBJECTS
========

Background
white

Lovebase
white

Target
black
.....
..0..
.000.
..0..
.0.0.

GoldTarget
orange
.....
..0..
.000.
..0..
.0.0.

PlayerGround
black
.....
..0..
.000.
.000.
.0.0.

PlayerJump1
black
.....
..0..
00000
.000.
.0.0.

PlayerJump2
black
.....
..0..
00000
.000.
.0.0.

Crown
orange
.000.
.0.0.
.....
.....
.....

GoldBlock
orange

GoldingBlock
brown

Water
blue

Wall
gray

Love
pink
.0.0.
00000
00000
.000.
..0..

=======
LEGEND
=======

JumpingPlayer = PlayerJump1 or PlayerJump2
Player = PlayerGround or PlayerJump1 or PlayerJump2
Obstacle = Player or Wall or Water or GoldingBlock or GoldBlock or Target or GoldTarget
CanFall = GoldBlock or Target or GoldTarget
. = Background
# = Wall
P = PlayerGround and Crown
T = Target
W = Water
G = GoldBlock
D = GoldingBlock

=========
SOUNDS
=========

sfx0 28219707 (touch Water)
sfx1 45545307 (golding)
sfx2 42491507 (make lady gold :/ )
StartGame 8892900
EndLevel 28067900
EndGame 28067900
CloseMessage 44855507

================
COLLISIONLAYERS
================

Background
Lovebase
PlayerGround, PlayerJump1,PlayerJump2, Wall, Water, GoldBlock, Target, GoldTarget, GoldingBlock
Crown
Love

======
RULES
======

UP [ UP PlayerGround | NO Obstacle ] -> [ UP PlayerJump2 | ] 
DOWN [ UP JumpingPlayer | NO Obstacle ] -> [ JumpingPlayer | ] 
DOWN [ UP PlayerJump2 | NO Obstacle ] -> [ PlayerJump2 | ] 

[ STATIONARY CanFall ] -> [ Down CanFall ] 

HORIZONTAL [ > PlayerJump1 | NO Obstacle ] -> [ | PlayerGround ] 
HORIZONTAL [ > PlayerJump2 | NO Obstacle ] -> [ | PlayerJump1 ] 
LATE DOWN [ PlayerGround | NO Obstacle ] -> [ | PlayerGround ] 

LATE [ Crown NO Player ] [ Player ] -> [ ] [ Crown Player ] 
LATE [ GoldingBlock ] -> [ GoldBlock ] 
LATE [ Crown | Wall ] -> [ Crown | GoldingBlock ] sfx1
LATE [ Crown | Water ] -> [ | Water ] sfx0
LATE [ Crown | Target ] -> [ Crown | GoldTarget ] sfx2
LATE DOWN [ JumpingPlayer | Obstacle ] -> [ PlayerGround | Obstacle ] 

LATE [ Player | Target ] -> [ Player Lovebase | Target ] 
LATE UP [ Lovebase | ] -> [ | Love ] 

==============
WINCONDITIONS
==============

some Love (Awwww!)

=======
LEVELS
=======
##############
#............#
#............#
#.P.......T..#
##G######WWW##
##############

message MIDAS

###############
#.............#
#.............#
#.....#.......#
#.P..##..#.T..#
##G#######WWW##
###############

message She waits for me every day

###############
#.............#
#.............#
#.............#
#.....#.#.....#
#.P..##.#..T..#
##G#######WWW##
###############

message This is the price of gold:

##############
#............#
#............#
#.P..........#
##G###T##WWW##
##############

message A heavy heart

#################
#...............#
#...............#
#......#.#......#
#.....#####.....#
#.......#.......#
#.T.P...#.......#
####G#######WWW##
#################

message I step in the same river
message again and again

#####################
#...................#
#...................#
#...................#
#.........#.#.......#
#..P........#.......#
#..d##########......#
#.......######......#
#...................#
#.......T...........#
#....#########WWW...#
#...................#
#...................#
#...................#
#####################

message Just so I can touch her hand.

#####################
#...................#
#...................#
#...................#
#...................#
#..P................#
#..d##T########WWW..#
#.....#.............#
#...................#
#...................#
#####################

message I am a prison to Pactolus

#####################
#...................#
#........###........#
#.......#####.......#
#......##...##......#
#.....T##.P.##W.....#
#.....####d####.....#
#.....#########.....#
#...................#
#####################

message But I must have her!

#####################
#...................#
#...................#
#..T..#...###..W....#
#..#####...#####....#
#.....###...#.......#
#........P..........#
#.....###D###.......#
#.....#######.......#
#...................#
#...................#
#####################

message My burden only grows

#####################
#...................#
#........G..........#
#.....W.GGG.T.......#
#.....#######.......#
#.....#.....#.......#
#.....#..P..#.......#
#.....###D###.......#
#.....#######.......#
#...................#
#...................#
#...................#
#####################

message My world collapses.

#####################
#...................#
#...................#
#..G....WW..........#
#..##.####..........#
#.....####..........#
#...................#
#.#...........T.....#
#.##.......P..#.....#
#..############.....#
#...#.....####......#
#...................#
#...................#
#...................#
#...................#
#...................#
#####################

message And she feels so distant

#####################
#...................#
#...................#
#...................#
#...................#
#..P......#.........#
#..GG...........WW..#
#..##T############..#
#....#..............#
#...................#
#...................#
#####################

message I live on borrowed time

#####################
#...................#
#...................#
#..........T........#
#.........##........#
#...................#
#.......P...........#
#.......D##.........#
#...........#.......#
#...................#
#...................#
#.......###W#.......#
#####################

message Is she still waiting?

#####################
#...................#
#....###########....#
#...................#
#....P.........W....#
#....###########....#
#....#.........#....#
#...................#
#.........#.........#
#......#..#..#......#
#....T.#..#..#......#
#....###########....#
#...................#
#...................#
#...................#
#####################

message Is Love just fool's gold?

#####################
#...................#
#...................#
#...........####....#
#..T##.##..#...#....#
#..#..#..#.#.P.#....#
#..#.....#...G#.....#
#...#...#....#......#
#....#W#.....#......#
#.....#.............#
#............#......#
#...................#
#...................#
#####################

message Midas


USER PROMPT:
Output the code for a complete PuzzleScript game. The game should be a simplified `demake` of the following game idea: **Pirate Plunge:** Navigate shifting seas and evade rival buccaneers to collect hidden treasure on a cursed archipelago..  First, reason about your task and determine the best plan of action. Then, write your code. Return your code in full, inside a ```plaintext code block.