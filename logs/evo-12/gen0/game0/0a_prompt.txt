SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title By Your Side
author Alan Hazelden
homepage www.draknek.org

require_player_movement
key_repeat_interval 0.12

(

A port of alan hazelden's game, By Your Side, in 6 lines of code, made with the permission of the author -

the original is here
http://www.draknek.org/games/by-your-side/

)

========
OBJECTS
========

(Objects in all scenes)

Grass .
darkgreen green
00000
01000
00000
00010
00000

Player_Casual P
black pink gray
..0..
..1..
.222.
..2..
.2.2.

TargetMain T
yellow
00000
0...0
0...0
0...0
00000

(Initial Scenes)

GirlFriend G
pink red
..0..
.010.
..1..
.111.
.0.0.

Tree #
darkbrown lightgreen
.111.
11111
.111.
..0..
..0..

(Wedding Scene)

Player_Formal Q
black pink white
..0..
..1..
.020.
..0..
.0.0.

WoodenFloor ,
brown
00000
00000
00000
00000
00000

ChurchWall H
darkbrown brown
11101
00000
10111
00000
11101

Chair C
darkbrown brown
11111
11101
11101
00001
01101

Altar A
gray
.....
00000
.000.
.000.
.000.

Spectator1 B
black pink white
..0..
..1..
.020.
..0..
.0.0.

Spectator2 D
purple pink white
..0..
..1..
10001
.000.
00000

Wife_Wedding W
white pink
..1..
.000.
.101.
.000.
00000

(Post Wedding Scene)

house @
black grey red
..0..
.000.
00000
.111.
.121.

pavement `
darkblue

Road '
gray white
00000
00000
01110
00000
00000

Wife_Repulsed X
white pink
..1..
.000.
.101.
.000.
00000

(grave scene)

gravedirt ~
green brown
01001
10000
00100
00001
01000

grave R
lightgray darkgray grey
.111.
10201
12221
10201
10001

coffin F
darkbrown brown black
.000.
01210
02220
01210
02220

(final scene)

Wife_Dying Y
white pink
..1..
.000.
.101.
.000.
00000

Wife_Coffin
darkbrown brown black
.000.
01210
02220
01210
02220

ColdFeet I
black

=======
LEGEND
=======

Target = TargetMain or Road
Player = Player_Casual or Player_Formal
Background = Grass or WoodenFloor or Pavement or GraveDirt
Spectator = Spectator1 or Spectator2
Obstacle = House or Spectator or Chair or Tree or Altar or Grave or Coffin or Wife_Coffin or ChurchWall
Wife_Attracted = Girlfriend or Wife_Wedding or Wife_Dying
Wife = Wife_Attracted or Wife_Repulsed or Wife_Coffin
Person = Player or Wife
GrassyTarget = Grass and TargetMain
E = GrassyTarget

=========
SOUNDS
=========

Player cantmove 82269707
endlevel 54461500
startgame 10582309
endgame 66004102
Wife_Repulsed move 44043907
sfx0 16417502

================
COLLISIONLAYERS
================

ColdFeet
Background
Target
Obstacle, Person

======
RULES
======

(game logic)

[ > Player ] [ Wife_Attracted ] -> [ > Player ] [ > Wife_Attracted ] 
[ > Player ] [ Wife_Repulsed ] -> [ > Player ] [ < Wife_Repulsed ] 

late [ Wife_Dying ] -> [ Wife_Coffin ] 
late [ Player | Wife_Coffin ] -> [ Player | Wife_Dying ] 

(level-specific events)
late [ ColdFeet Player ] -> [ Player ] message ...
late [ Wife Road ] -> sfx0

==============
WINCONDITIONS
==============

all person on target

=======
LEVELS
=======

....#################
...###...#....#######
..##.........##.....#
.##...#######.......#
##....#....###......#
#...###.............#
#...#...#...#..######
#...#...#..##.P.....#
#....####.T##..######
##......#...#.......#
###..##..#...#...##.#
#.##..#..##..#...#..#
#..#..#....###...#..#
#..##........#......#
#...##...##...##....#
#.......##.....######
.##############......

....#################
...###...#....#######
..##.........##.....#
.##...#######.......#
##....#....###......#
#...###.............#
#...#...#...#..######
#...#...#..##..T...T#
#....####.P##G.######
##......#...#.......#
###..##..#...#...##.#
#.##..#..##..#...#..#
#..#..#....###...#..#
#..##........#......#
#...##...##...##....#
#.......##.....######
.##############......

....#################
...###...#....#######
..##.........##.....#
.##...#######.......#
##....#....###......#
#...###.............#
#...#T..#...#..######
#...#T..#..##..P...G#
#....####..##..######
##......#...#.......#
###..##..#...#...##.#
#.##..#..##..#...#..#
#..#..#....###...#..#
#..##........#......#
#...##...##...##....#
#.......##.....######
.##############......

HHHHHHHHHHHHHHHHHHHHH
,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,
,CCCCCCCCCCCCCCCCCCCC
,CCCCCCCCCCCCCCCCCCCC
A,B,,D,B,D,,B,D,,,B,C
A,T,,I,,,I,,,,I,Q,,,C
A,T,,,,,,,,,,,,,W,,,C
,CCCCCCCCCCCCCCCCCCCC
,CCCCCCCCCCCCCCCCCCCC
,,,,,,,,,,,,,,,,,,,,,
,,,,,,,,,,,,,,,,,,,,,
HHHHHHHHHHHHHHHHHHHHH

`````````@@@@````````
`````````@TT@````````
``@@@@@@`@`@@````````
``@````@`@`@@@@``````
``@`@@`@`@````@``````
``@`@@`@`@`@@@@``````
``@`@@`@@@P@`````````
``@```````W@@@@@@````
``@`@@@@@@``````@````
``@`@````@`@@@@`@````
``@`@````@`@``@`@````
@@@@@@@@@@@@@@@@@@@@@
'''''''''''''''''''''
@@@@@@@@@@@@@@@@@@@@@
`````````````````````

`````````@@@@````````
`````````@PX@````````
``@@@@@@`@`@@````````
``@````@`@`@@@@``````
``@`@@`@`@````@``````
``@`@@`@`@`@@@@``````
``@`@@`@@@T@`````````
``@```````T@@@@@@````
``@`@@@@@@``````@````
``@`@````@`@@@@`@````
``@`@````@`@``@`@````
@@@@@@@@@@@@@@@@@@@@@
'''''''''''''''''''''
@@@@@@@@@@@@@@@@@@@@@
`````````````````````

`````````@@@@````````
`````````@TT@````````
``@@@@@@`@`@@````````
``@````@`@`@@@@``````
``@`@@`@`@````@``````
``@`@@`@`@`@@@@``````
``@`@@`@@@P@`````````
``@```````X@@@@@@````
``@`@@@@@@``````@````
``@`@````@`@@@@`@````
``@`@````@`@``@`@````
@@@`@@@@@@`@@@@`@@@@@
'''''''''''''''''''''
@@@@@@@@@@@@@@@@@@@@@
`````````````````````

RRRRRRR~RRRRRR~RRRRR
R~~~~~R~R~~~~R~R~~~~
R~~~~~~~R~~~~R~R~~~~
R~~~~~R~R~~~~R~R~~~~
RRRRRRR~RR~~RR~RRRRR
R~~~~BDBDBDBDBD~~~~R
RQ~~~~~i~i~~i~~~~EFR
RRRRRRRRRRRRRRRRRRRR
R~~R~~~~~~~~~~~R~~~~
R~~R~~~~~~~~~~~R~~~~
R~~RRR~~~RRRR~~R~~~~
R~~~~~~~~R~~~~~R~~~R
R~~R~~~~~R~~R~~~~~~R
R~~R~~~~~R~~R~~~~~~R
RRRRRRRRRR~~RRRRRRRR

RRRRRR~~~~~~~~~~~~~~
R~~~~R~~~~~RRRRRR~~~
R~QY~R~~~~~R~~~~R~~~
R~~~~R~~RRRRR~R~R~~~
R~~~~RRRR~~R~~R~R~~~
R~~~~~~~~~~R~~R~RRR~
RRRRRRRRR~~R~~~~~~R~
~~R~~~~~R~~R~RRR~~R~
~~R~~R~~R~~RRR~R~~R~
~~R~~R~~~~~~~~~~~~R~
~~R~~R~~RR~~~~~RRRR~
~~R~~R~~~RRRRRTTR~~~
~~R~~R~~~R~~~~~~R~~~
~~R~~R~~~~~RRR~~R~~~
~~RRRRRRRRRR~RRRR~~~

RRRRRR~~~~~~~~~~~~~~
R~~~~R~~~~~RRRRRR~~~
R~~~~R~~~~~R~~~~R~~~
R~~~~R~~RRRRR~R~R~~~
R~~~~RRRR~~R~~R~R~~~
R~~~~~~~~~~RT~R~RRR~
RRRRRRRRR~~RT~~~~~R~
~~R~~~~~R~~R@RRR~~R~
~~R~~R~~R~~RRR~R~~R~
~~R~~R~~~~~~~~~~~~R~
~~R~~R~~RR~~~~~RRRR~
~~R~~R~~~RRRRRYQR~~~
~~R~~R~~~R~~~~~~R~~~
~~R~~R~~~~~RRR~~R~~~
~~RRRRRRRRRR~RRRR~~~

message Commiserations

message It is over

title ScriptCross
author Dennis Au
homepage www.puzzlescript.net

========
OBJECTS
========

Background
White

Target
Black

Fill
Black

Cross
LightGrey
0...0
.0.0.
..0..
.0.0.
0...0


Player
Green
00000
0...0
0...0
0...0
00000

Border #
LightGrey

(Clues 0-9)
Clue0 0
LightGrey

Clue1 1
LightGrey Black
00000
0...0
0.1.0
0...0
00000

Clue2 2
LightGrey Black
00000
0..10
0...0
01..0
00000

Clue3 3
LightGrey Black
00000
0..10
0.1.0
01..0
00000

Clue4 4
LightGrey Black
00000
01.10
0...0
01.10
00000

Clue5 5
LightGrey Black
00000
01.10
0.1.0
01.10
00000

Clue6 6
LightGrey Black
00000
01.10
01.10
01.10
00000

Clue7 7
LightGrey Black
00000
01110
0.1.0
01110
00000

Clue8 8
LightGrey Black
00000
01110
0.110
01110
00000

Clue9 9
LightGrey Black
00000
01110
01110
01110
00000


=======
LEGEND
=======

. = Background
P = Player and Border
b = Target
Clue = Clue0 or Clue1 or Clue2 or Clue3 or Clue4 or Clue5 or Clue6 or Clue7 or Clue8 or Clue9
NoEmpty = Fill or Cross or Clue or Border

=======
SOUNDS
=======
EndLevel 37348103
EndGame 64173909

================
COLLISIONLAYERS
================

(switch order of target and background to see the solutions)
Target
Background
Border, Clue, Fill, Cross
Player


======
RULES
======

[Action Player no NoEmpty] -> [Player Fill]
[Action Player Fill] -> [Player Cross]
[Action Player Cross] -> [Player]



==============
WINCONDITIONS
==============

All Target on Fill
All Fill on Target

=======
LEVELS
=======

(P123456789)

message level 1 - oh so stable

P12
1.b
2bb

message level 2 - it is what it is
P#1###
##113#
##311#
3.bbb.
1...b.
3.bbb.
1.b...
3.bbb.

message level 3 - i see you!
P##1#1#
###111#
##.....
11.b.b.
##.....
#3.bbb.
##.....

message level 4 - that's ace
P#######1####
#######111###
######14141##
#####8111118#
###8.bbbbbbb.
##11.b.....b.
#131.b.bbb.b.
1111.b.b.b.b.
#131.b.bbb.b.
1111.b.b.b.b.
##11.b.....b.
###8.bbbbbbb.
####.........

message congratulations!


title Block Faker
author Droqen
homepage www.droqen.com

require_player_movement
key_repeat_interval 0.12

background_color white
text_color black

color_palette c64

(

A port of first several levels of Droqen's beautiful game:

https://droqen.itch.io/block-faker

(His game has secrets that this one does not.)

)

========
OBJECTS
========

Background
white

Grille
gray
0...0
.0.0.
..0..
.0.0.
0...0

EndPoint
green

Player
black
00000
00000
0.0.0
00000
00000

Wall
gray

WallBlock
black grey
01111
01111
01111
01111
00001

BlueBlock
blue
00000
00.00
0.0.0
00.00
00000

GreenBlock
lightgreen
00000
00000
00.00
00000
00000

PinkBlock
red
00000
00.00
0...0
00.00
00000

PurpleBlock
purple
00000
0...0
0.0.0
0...0
00000

OrangeBlock
orange
00000
0.0.0
00000
0.0.0
00000

=======
LEGEND
=======

Block = PinkBlock or BlueBlock or PurpleBlock or OrangeBlock or GreenBlock
Moveable = Player or Block
PlayerObstacle = Block or Wall or WallBlock
BlockObstacle = Player or Wall or WallBlock or Grille or Block or EndPoint
. = Background
# = Wall
@ = WallBlock
P = Player
B = BlueBlock
G = GreenBLock
K = PinkBlock
R = PurpleBlock
O = OrangeBlock
E = EndPoint
x = Grille
H = Grille and Player

=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Background
EndPoint
Grille
Player, Wall, WallBlock, PinkBlock, BlueBlock, PurpleBlock, OrangeBlock, GreenBlock

======
RULES
======

[ > Moveable | Moveable ] -> [ > Moveable | > Moveable ] 

[ > Block | Grille ] -> [ Block | Grille ] 

late [ PinkBlock | PinkBlock | PinkBlock ] -> [ | | ] 
late [ BlueBlock | BlueBlock | BlueBlock ] -> [ | | ] 
late [ PurpleBlock | PurpleBlock | PurpleBlock ] -> [ | | ] 
late [ OrangeBlock | OrangeBlock | OrangeBlock ] -> [ | | ] 
late [ GreenBlock | GreenBlock | GreenBlock ] -> [ | | ] 

==============
WINCONDITIONS
==============

some Player on Endpoint

=======
LEVELS
=======

........########
################
########@@@@@###
####@@@@@...@###
###@..OO....@@##
##@..@@@@@....@#
##@.@..@@@..E@@#
#@..@.O...@@@@##
##@...@.P.######
####@.@@@@######
####@@##########
########........

message congrats: level 2!

##########
####R.R###
#x....#x##
#xO#O.OxE#
#xx##.#xx#
#####R####
#####P####
O#########

message congrats: level 3!

......#######...
.....##P.#####..
....###....####.
..#####O...#B..#
..#####.#.##BB.#
##EBB..O.O.....#
..#####.#.#....#
..######..#G.###
....####..#.OOx.
.....###.GG.#x..
......#######...

message congrats: level 4!

######xx######
######GG######
##xx#...#xEx##
##..O...#x..##
##..#...##O###
#..##.......##
xG....##....Gx
xG....##....Gx
##.......GG###
###G##...#xx##
##x..#...O.x##
##.P.#...#xx##
######GG######
######xx######

message congrats: level 5!

......##......
....######....
..##########..
..#.OG..kk.#..
.##OPO..Gkk##.
.##GOG..GGk##.
###...##...###
###...##...###
.##kkG##ROR##.
.##Gkk..OEO##.
..#.Gk..RO.#..
..##########..
....######....
......##......

message now go play the real game



title A single rigid body
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Background
brown

Target
YELLOW

Player
PINK

BigBlock
darkblue

Wall
darkbrown

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = BigBlock
@ = BigBlock and Target
O = Target

=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, BigBlock

======
RULES
======

rigid [ >  Player | BigBlock ] -> [  >  Player | > BigBlock  ]
+ rigid [ moving BigBlock | BigBlock ] -> [ moving BigBlock | moving BigBlock ]

==============
WINCONDITIONS
==============

=======
LEVELS
=======

...............
..........#....
...............
..#............
.....***.......
.....*.....#...
..P..**#.......
.....*.........
...............
..#............
.........#.....
...............
...............



title Kettle
author increpare
homepage www.increpare.com

color_palette gameboycolour

(

A port of a flash game of mine, the original of which can be played here:
http://ded.increpare.com/~locus/kettle

)

========
OBJECTS
========

Background
darkgray

Target
yellow
00000
0...0
0...0
0...0
00000

UPolice
black
.000.
.000.
00000
.000.
.0.0.

DPolice
black
.000.
.000.
00000
.000.
.0.0.

LPolice
black
.000.
.000.
0000.
.000.
.0.0.

RPolice
black
.000.
.000.
.0000
.000.
.0.0.

Crate
blue
..0..
00000
..0..
.000.
.0.0.

Wall
black

=======
LEGEND
=======

. = Background
player = UPolice or DPolice or LPolice or RPolice
i = UPolice
k = DPolice
j = LPolice
l = RPolice
c = Crate
@ = Crate and Target
t = Target
# = Wall

=========
SOUNDS
=========

Target move 59810707
Player move 86872107
startlevel 63638302
endlevel 86642102
endgame 63929902
showmessage 42200307
closemessage 42200307
startgame 94059902

================
COLLISIONLAYERS
================

Background
Target
Player, Crate, Wall

======
RULES
======

(The idea is simple: lots of Players, each of which only responds to a particular input direction.)

[ up RPolice ] -> [ RPolice ] 
[ down RPolice ] -> [ RPolice ] 
[ left RPolice ] -> [ RPolice ] 

[ down UPolice ] -> [ UPolice ] 
[ left UPolice ] -> [ UPolice ] 
[ right UPolice ] -> [ UPolice ] 

[ up LPolice ] -> [ LPolice ] 
[ down LPolice ] -> [ LPolice ] 
[ right LPolice ] -> [ LPolice ] 

[ up DPolice ] -> [ DPolice ] 
[ left DPolice ] -> [ DPolice ] 
[ right DPolice ] -> [ DPolice ] 

[ > Player | Crate ] -> [ > Player | > Crate ] 
[ > Crate | Crate ] -> [ > Crate | > Crate ] 

==============
WINCONDITIONS
==============

all target on Crate

=======
LEVELS
=======

message Let's see your ID

###############
#.............#
#.............#
#.....kkkk....#
#....l...cj...#
#....l.tt.j...#
#....lctt.j...#
#....lc..cj...#
#.....iiii....#
#.............#
#.............#
#.............#
###############

message Area Secure
message Gonna lock you up, sonny!

###############
#.............#
#.............#
#.....kkkk....#
#....l@@t.j...#
#....ltttcj...#
#....l@t@cj...#
#....lc.ccj...#
#.....iiii....#
#.............#
#.............#
#.............#
###############

message Area Secure
message We didn't see nothing!

###############
#.............#
#.............#
#....kkkkk....#
#...l@t@@cj...#
#...l@@ttcj...#
#...ltt@tcj...#
#...lt@@@.j...#
#...l.ccccj...#
#....iiiii....#
#.............#
#.............#
###############

message Area Secure
message You'll be late for class, eh!

###############
#.............#
#.............#
#....kkkkk....#
#...lc.c.cj...#
#...lc@tt.j...#
#...l.tt@cj...#
#...l.ttt.j...#
#...lc...cj...#
#....iiiii....#
#.............#
#.............#
###############

message Area Secure
message Ha! You shat yourself!

###############
#.............#
#.............#
#....kkkkkk...#
#...lc.c..cj..#
#...lcttt..j..#
#...l.t@@..j..#
#...l.ttt..j..#
#...lc...ccj..#
#....iiiiii...#
#.............#
#.............#
###############

message Area Secure
message Have to confiscate your camera!

###############
#.............#
#.............#
#....kkkkkk...#
#...lcc..ccj..#
#...l.tttt.j..#
#...l.t@@t.j..#
#...lcttt@cj..#
#...lct@ttcj..#
#...lcc..ccj..#
#....iiiiii...#
#.............#
###############

message Area Secure
message Keeping the peace is hard work

###############
#.............#
#...kkkkkkk...#
#..l..cc..cj..#
#..l.tttt..j..#
#..lc@t@t..j..#
#..lctttt.cj..#
#..l.@@tt..j..#
#..lc.....cj..#
#..lcc.c..cj..#
#...iiiiiii...#
#.............#
###############

message Area Secure
message You should've packed lunch!

###############
#.............#
#..kkkkkkkk...#
#.lccccc...j..#
#.lct@@tt.cj..#
#.l.t@t@t.cj..#
#.lc@@ttt.cj..#
#.lct@tttccj..#
#.l.tt@@t..j..#
#.l........j..#
#.lc..cc...j..#
#..iiiiiiii...#
###############

message Area Secure
message I want to hit you

###############
#.............#
#..kkkkkkkkk..#
#.lc.cccc...j.#
#.l.........j.#
#.lccttttt..j.#
#.lc.tt@tt.cj.#
#.lc.tt@@t.cj.#
#.l..@@tt@.cj.#
#.l..t@t@t..j.#
#.l.........j.#
#.lc.c.c.c.cj.#
#..iiiiiiiii..#
###############

message Area Secure
message Don't be naughty!

###############
#.kkkkkkkkkk..#
#lccc..c...cj.#
#lc.........j.#
#lc.@t@tt...j.#
#lc.t@@@t...j.#
#l..@ttt@..cj.#
#lc.tt@@t..cj.#
#l..ttttt...j.#
#l..........j.#
#l.........cj.#
#l....c.c.ccj.#
#.iiiiiiiiii..#
###############

message Area Secure
message I'm upholding human rights!

###############
#.kkkkkkkkkkk.#
#lccc.c.c...cj#
#l....c......j#
#lc.@ttttt...j#
#l..@ttt@t...j#
#l..t@@ttt..cj#
#lc.@tt@@t..cj#
#l..@@t@@t...j#
#l..t@t@tt..cj#
#lc..c.......j#
#lc....c.....j#
#lcc.c.c.c..cj#
#.iiiiiiiiiii.#
###############

message Area Secure
message I'm all out of lines, matey:
message It's game over for you


title Dungeon Janitor
author Farbs
homepage www.farbs.org
noundo
noaction
key_repeat_interval 0.1

========
OBJECTS
========

Background
DARKBLUE BLACK
11111
10000
10000
10000
10000

Entry
LIGHTBROWN BROWN DARKBROWN GRAY
30103
10101
10121
10101
10101

ExitClosed
LIGHTBROWN BROWN DARKBROWN GRAY
30103
10101
10121
10101
10101

ExitOpen
GRAY BLACK DARKBLUE
01110
11111
11111
11111
12221

Slime
LIGHTGREEN GREEN DARKGREEN
.....
.....
.....
.....
.....

Head
LIGHTGREEN GREEN DARKGREEN BLACK
.000.
03130
11111
23332
.222.

SlimeU
LIGHTGREEN GREEN DARKGREEN
11111
21112
.222.
.....
.....

SlimeD
LIGHTGREEN GREEN DARKGREEN
.....
.....
.000.
01110
11111

SlimeUD
LIGHTGREEN GREEN DARKGREEN
11111
11111
11111
11111
11111

SlimeL
LIGHTGREEN GREEN DARKGREEN
00...
110..
111..
112..
22...

SlimeR
LIGHTGREEN GREEN DARKGREEN
...00
..011
..111
..211
...22

SlimeLR
LIGHTGREEN GREEN DARKGREEN
00000
11111
11111
11111
22222

SlimeUL
LIGHTGREEN GREEN DARKGREEN
11111
11112
1112.
112..
22...

SlimeUR
LIGHTGREEN GREEN DARKGREEN
11111
21111
.2111
..211
...22

SlimeULR
LIGHTGREEN GREEN DARKGREEN
11111
11111
11111
11111
22222

SlimeDL
LIGHTGREEN GREEN DARKGREEN
00...
110..
1110.
11110
11111

SlimeDR
LIGHTGREEN GREEN DARKGREEN
...00
..011
.0111
01111
11111

SlimeDLR
LIGHTGREEN GREEN DARKGREEN
00000
11111
11111
11111
11111

SlimeUDLR
LIGHTGREEN GREEN DARKGREEN
11111
11111
11111
11111
11111

SlimeSolo
LIGHTGREEN GREEN DARKGREEN
.000.
01110
11111
21112
.222.

Wall
LIGHTGREY GREY
11111
01000
01000
11111
00010

PlayerA
Black Orange White Blue Brown
.....
.11..
2224.
333.4
3.322

PlayerB
Black Orange White Blue Brown
.....
.11..
.4222
4.333
223.3

Temp
PINK
00000
00000
00000
00000
00000

=======
LEGEND
=======

. = Background
# = Wall
E = Wall AND Entry
P = PlayerA
O = Slime AND Head
X = Wall AND ExitClosed

SlimeDecal = SlimeU OR SlimeD OR SlimeUD OR SlimeL OR SlimeR OR SlimeLR OR SlimeUL OR SlimeUR OR SlimeULR OR SlimeDL OR SlimeDR OR SlimeDLR OR SlimeUDLR OR SlimeSolo
Player = PlayerA OR PlayerB

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Slime
SlimeU, SlimeD, SlimeUD, SlimeL, SlimeR, SlimeLR, SlimeUL, SlimeUR, SlimeULR, SlimeDL, SlimeDR, SlimeDLR, SlimeUDLR, SlimeSolo, ExitOpen
Player, Wall, PlayerA, PlayerB, Temp
Head, Entry, ExitClosed

======
RULES
======

(Stomp Heads)
[ > Player | Head ] -> [ > Player | NO Head ]
[ > Player | Slime ] -> [ > Player | NO Slime ]

(New Head)
[ Head ] -> []
RANDOM [ Slime NO Player ] -> [ Slime Head ]

(Grow Slime)
[ Head | NO Slime NO Wall NO Player ] -> [ Head | Slime ]

(Clear Slime from under player)
[ > Player | Slime ] -> [ > Player | NO Slime ]
LATE [ Player Slime ] -> [ Player NO Head ]

(Make slime pretty)
[ SlimeDecal ] ->[ ]
VERTICAL [ Slime | Slime | Slime ] -> [ Slime | Slime SlimeUD | Slime ]
UP [ Slime NO SlimeUD | Slime ] -> [ Slime SlimeU | Slime ]
DOWN [ Slime NO SlimeUD | Slime ] -> [ Slime SlimeD | Slime ]
HORIZONTAL [ Slime | Slime NO SlimeU NO SlimeD NO SlimeUD | Slime ] -> [ Slime | Slime SlimeLR | Slime ]
LEFT [ Slime NO SlimeU NO SlimeD NO SlimeUD NO SlimeLR | Slime ] -> [ Slime SlimeL | Slime ]
RIGHT [ Slime NO SlimeU NO SlimeD NO SlimeUD NO SlimeLR | Slime ] -> [ Slime SlimeR | Slime ]
HORIZONTAL [ Slime | Slime SlimeU | Slime ] -> [ Slime | Slime SlimeULR | Slime ]
LEFT [ Slime SlimeU | Slime ] -> [ Slime SlimeUL | Slime ]
RIGHT [ Slime SlimeU | Slime ] -> [ Slime SlimeUR | Slime ]
HORIZONTAL [ Slime | Slime SlimeD | Slime ] -> [ Slime | Slime SlimeDLR | Slime ]
LEFT [ Slime SlimeD | Slime ] -> [ Slime SlimeDL | Slime ]
RIGHT [ Slime SlimeD | Slime ] -> [ Slime SlimeDR | Slime ]
[ Slime NO SlimeDecal ] -> [ Slime SlimeSolo ]

( Shut the door if slime still around)
( Have to open it first unfortunately )
[ ExitClosed Wall ] -> [ ExitOpen ]
[ ExitOpen ][ Slime ] -> [ ExitClosed Wall ][ Slime ]

(Player Anim)
[ LEFT PlayerB ] -> [ LEFT PlayerA ]
[ RIGHT PlayerA ] -> [ RIGHT PlayerB ]
LATE [ PlayerA ] -> [ Temp ]
LATE [ PlayerB ] -> [ PlayerA ]
LATE [ Temp ] -> [ PlayerB ]
==============
WINCONDITIONS
==============

ALL Player ON ExitOpen

=======
LEVELS
=======

MESSAGE It's Thursday.
MESSAGE Today I mop the slime pit.


##X##
##.##
##.##
##O##
##.##
##P##
##E##

MESSAGE Damn slime sticking to my shoes.

..####..
..#O.#..
###..###
#......#
#P#..#.#
#E#..#X#
..####..

MESSAGE Goes SHLUCK SHCLUK when I walk.

######X#############
###......#...#...###
###.##.#...#...#...#
##.....###########.#
#..#.#.....#.......#
#.##.###.#.##.######
#......#.#.#.O.....#
#.####.#.#.#.#####.#
#P.....#.#.........#
#E##################

MESSAGE Last room before home.
MESSAGE This one can take a while.

##########X#########
#..................#
#..................#
#..#######.######..#
#..#.#...#.#.#..#..#
#..#...#.#.#.#..#..#
#..#.....#.#.#..#..#
#..##....#.#...##..#
#..#............#..#
#..#......O.....#..#
#..#............#..#
#..#............#..#
#..##############..#
#.........P........#
##########E#########

MESSAGE Done for the day. Back tomorrow.
MESSAGE Fridays is rat scrubbing day.

title One player, unlimited rigidbodies
author increpare
homepage www.increpare.com

(

This is a sample of 

- Unlimited rigid bodies
- 1 Player (should be extendable)

It's made as an example for the extended rigid body tutorial here:

https://www.puzzlescript.net/Documentation/rigidbodies.html

)

========
OBJECTS
========

Background
green

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

 Crate
Yellow orange
00000
01110
01110
01110
00000


(The directional overlays bellow establish connections between crates)

 Crate_U
orange
.000.
.....
.....
.....
.....

 Crate_D
orange
.....
.....
.....
.....
.000.

 Crate_L
orange
.....
0....
0....
0....
.....

 Crate_R
orange
.....
....0
....0
....0
.....


=======
LEGEND
=======

. = Background
# = Wall
P = Player

w = Crate and Crate_U
x = Crate and Crate_D
a = Crate and Crate_L
d = Crate and Crate_R

e = Crate and Crate_U and Crate_L
z = Crate and Crate_D and Crate_L
q = Crate and Crate_U and Crate_R
c = Crate and Crate_D and Crate_R

r = Crate and Crate_L and Crate_R
t = Crate and Crate_D and Crate_U

1 = Crate and Crate_D and Crate_L and Crate_R
2 = Crate and Crate_U and Crate_L and Crate_R
3 = Crate and Crate_U and Crate_D and Crate_L
4 = Crate and Crate_U and Crate_D and Crate_R

0 = Crate and Crate_U and Crate_D and Crate_L and Crate_R

5 = Crate

 Crate_Connection = Crate_U or Crate_D or Crate_L or Crate_R

Pusher = Player or Crate
Pushable = Crate or Crate_connection

=======
SOUNDS
=======

 Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Player, Wall, Crate
 Crate_U
 Crate_D
 Crate_L
 Crate_R

======
RULES
======

(For making maps easily: uncomment out the next few lines + press action to discard all boundaries and recreate them)

( [ action player ] [ Crate_connection ] -> [ action player ] [ ] )
( up [ action player ] [ Crate | Crate ] -> [ action player ] [ Crate Crate_u | Crate Crate_d ] )
( right [ action player ] [ Crate | Crate ] -> [ action player ] [ Crate Crate_r | Crate Crate_l ] )

(Propagate movements within and between extended bodies)
[ > Pusher | Pushable ] -> [ > Pusher | > Pushable ]
+ up [ moving Crate Crate_U | Crate ] -> [ moving Crate Crate_U | moving Crate ]
+ down [ moving Crate Crate_D | Crate ] -> [ moving Crate Crate_D | moving Crate ]
+ left [ moving Crate Crate_L | Crate ] -> [ moving Crate Crate_L | moving Crate ]
+ right [ moving Crate Crate_R | Crate ] -> [ moving Crate Crate_R | moving Crate ]

(Move connections along with the crates themselves)
[ moving Crate stationary Crate_connection ] -> [ moving Crate moving Crate_connection ]

(If any crate can't move, cancel the turn. This works because there's only one player.)
[ > Crate | wall ] -> cancel

==============
WINCONDITIONS
==============

=======
LEVELS
=======

###############
#.............#
#..ca...ca.dz.#
#.ce..d1e...w.#
#.t....w......#
#.w.d1a..5.5..#
#....w....5...#
#......dz.....#
#...p...t..dz.#
#..x..cre...w.#
#..w..w...5...#
#.............#
###############

USER PROMPT:
Output the code for a complete PuzzleScript game.  First, reason about your task and determine the best plan of action. Then, write your code. Return your code in full, inside a ```plaintext code block.