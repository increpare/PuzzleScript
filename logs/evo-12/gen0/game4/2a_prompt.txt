SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title Sticky Candy Puzzle Saga
author Alan Hazelden
homepage www.draknek.org

run_rules_on_level_start

noaction

========
OBJECTS
========

Background
Black DarkGrey
00000
00000
00000
00000
00000


Wall
Pink #febfc6
10001
00011
00110
01100
11000

Player
Pink #febfc6
.000.
.111.
00100
.000.
.0.0.

Yellow
Yellow White Orange
.000.
01100
01000
00002
.022.

Red
Red Orange DarkRed
.000.
01100
01000
00002
.022.

Blue
LightBlue White #3468f7
.000.
01100
01000
00002
.022.

YellowTarget
Brown Yellow
00000
01110
01010
01110
00000

RedTarget
DarkBrown Red
00000
01110
01010
01110
00000

BlueTarget
Purple LightBlue
00000
01110
01010
01110
00000

Marker
White
.....
.....
.....
.....
.....

WallL
White
0....
0....
0....
0....
0....

WallR
White
....0
....0
....0
....0
....0

WallU
White
00000
.....
.....
.....
.....

WallD
White
.....
.....
.....
.....
00000

WallLR
White
0...0
0...0
0...0
0...0
0...0

WallUD
White
00000
.....
.....
.....
00000

JoinL
#febfc6
0....
.....
.....
.....
0....

JoinR
#febfc6
....0
.....
.....
.....
....0

JoinU
#febfc6
0...0
.....
.....
.....
.....

JoinD
#febfc6
.....
.....
.....
.....
0...0

JoinBoth
#febfc6
0...0
.....
.....
.....
0...0

Zero
Purple
.000.
.0.0.
.0.0.
.0.0.
.000.

One
Purple
.00..
..0..
..0..
..0..
.000.

Two
Purple
.000.
...0.
.000.
.0...
.000.

Three
Purple
.000.
...0.
.000.
...0.
.000.

Four
Purple
.0.0.
.0.0.
.000.
...0.
...0.

Five
Purple
.000.
.0...
.000.
...0.
.000.

Six
Purple
.000.
.0...
.000.
.0.0.
.000.

Seven
Purple
.000.
...0.
...0.
...0.
...0.

Eight
Purple
.000.
.0.0.
.000.
.0.0.
.000.

Nine
Purple
.000.
.0.0.
.000.
...0.
.000.



=======
LEGEND
=======

. = Background
# = Wall
P = Player
A = Red
B = Yellow
C = Blue
X = RedTarget
Y = YellowTarget
Z = BlueTarget

WallCoverLR = WallL or WallR or WallLR
WallCoverUD = WallU or WallD or WallUD

JoinCoverLR = JoinL or JoinR or JoinBoth
JoinCoverUD = JoinU or JoinD

Join = JoinCoverLR or JoinCoverUD

CoverLR = WallCoverLR or JoinCoverLR
CoverUD = WallCoverUD or JoinCoverUD

0 = Zero and Wall
1 = One and Wall
2 = Two and Wall
3 = Three and Wall
4 = Four and Wall
5 = Five and Wall
6 = Six and Wall
7 = Seven and Wall
8 = Eight and Wall
9 = Nine and Wall

Number = Zero or One or Two or Three or Four or Five or Six or Seven or Eight or Nine

Candy = Yellow or Red or Blue
Target = YellowTarget or RedTarget or BlueTarget


=======
SOUNDS
=======

sfx1 6045907 (candies sticking)
sfx2 27189102 (can't move)
sfx3 27189102 (can't push)
Candy MOVE 48059907
Player MOVE 11429307
EndLevel 85763108
Restart 65287309
Undo 84906107

================
COLLISIONLAYERS
================

Background
Marker
Target
Player, Wall, Candy
CoverUD
CoverLR, Number

======
RULES
======

[ >  Player | Candy ] -> [  >  Player | > Candy  ]
[ moving Candy | stationary Candy ] -> [ moving Candy | moving Candy ]

(Prevent movement in a way that triggers the "can't move" sound)

[ > Candy | Wall ] [ > Player | > Candy ] -> [ Candy | Wall ] [ > Player | Candy ]
[ > Player | stationary Candy ] [ moving Candy ] -> [ > Player | Candy ] [ Candy ]

(Trigger the "candies sticking" sound)

[ > Candy | no Candy ] -> [ > Candy | Marker ]

[ Marker | stationary Candy ] -> sfx1

[ Marker ] -> []

(Can't move sounds)

[ > Player | Wall ] -> sfx2
[ > Player | stationary Candy ] -> sfx3

(Wall borders)

left [ Wall no WallCoverLR | no Wall ] -> [ Wall WallL | ]
right [ Wall no WallCoverLR | no Wall ] -> [ Wall WallR | ]
right [ Wall WallL | no Wall ] -> [ Wall WallLR | ]

up [ Wall no WallCoverUD | no Wall ] -> [ Wall WallU | ]
down [ Wall no WallCoverUD | no Wall ] -> [ Wall WallD | ]
down [ Wall WallU | no Wall ] -> [ Wall WallUD | ]

(Candy joins)

late [ Join ] -> []

late left [ Candy | Candy ] -> [ Candy JoinL | Candy ]
late right [ Candy no JoinL | Candy ] -> [ Candy JoinR | Candy ]
late right [ Candy JoinL | Candy ] -> [ Candy JoinBoth | Candy ]

late up [ Candy no JoinBoth | Candy ] -> [ Candy JoinU | Candy ]
late down [ Candy no JoinU no JoinBoth | Candy ] -> [ Candy JoinD | Candy ]
late down [ Candy JoinU | Candy ] -> [ Candy JoinBoth | Candy ]

==============
WINCONDITIONS
==============

All Yellow on YellowTarget
All Red on RedTarget
All Blue on BlueTarget

=======
LEVELS
=======

message Transport the sticky candies to the collection areas.

#######
##...##
#..b.y#
#pb#.y#
#..b.y#
##...##
1######

#########
##.....##
#..a.#.x#
#pa....x#
#..a.#.x#
##.....##
2########

message Don't let different candies stick together!

########
#p...yy#
#.b.ayy#
#..b.a.#
#.a.b..#
#xxa.b.#
#xx....#
3#######

###########
#....p....#
#..b.c.b..#
#...c.c...#
###.zzz.###
####b.b####
####yyy####
#####y#####
4##########

#########
#.a...###
#.a...###
#..c.axx#
#zz.p.xx#
#zzc.a..#
###...c.#
###...c.#
5########

###########
##.......##
#y.#.p.#.z#
#yc.b.c.bz#
##.......##
###..#..###
6##########

#########
#####..z#
#...b.zz#
#.b.bb.z#
#p.###..#
#y.cc.c.#
#yy.c...#
#y..#####
7########

#########
##......#
#x.c.#by#
#.b.p..y#
#z.a.#by#
##......#
8########

#########
#yy.c.x.#
#y.b..xx#
#.b.....#
#...#..b#
#.a...c.#
#.aa.c.z#
#p....zz#
9########

#########
#yy###zz#
#...x...#
#.##a##.#
#..b.b..#
#..#.#..#
#..c.c..#
##..p..##
10#######

###########
#####y#####
####yyy####
###.b.b.###
###a...c###
#z...p...x#
#za.b.b.cx#
###.....###
11#########

###########
####...A###
##......###
#..#A#..xx#
#pA...A.xx#
#..#A#..xx#
##......###
####...A###
12#########

#########
#..xxx..#
#..#a#..#
#..b.b..#
##.#p#.##
##.aya.##
##..y..##
13#######

###########
##..zzz..##
#..a.p.b..#
#yy#ccc#xx#
#..b...a..#
##...#...##
14#########

#############
#...#####...#
#....c.b....#
#yy#..p..#zz#
#.y.cc.bb.z.#
####.....####
15###########

#########
#...#...#
#.a.a.a.#
#...x...#
#z#.x.#y#
#zc.x.by#
#.c.p.b.#
###...###
16#######

###########
##......###
##.#..c...#
#...c#..zz#
#pc..#c#zz#
#...c#..zz#
##.#..c...#
##......###
17#########

###############
#...###z###...#
#....bzzzb....#
#...c.c.c.c...#
#####.yyy.#####
#####.#b#.#####
#####.....#####
######.p.######
18#############

#############
###x#...#z###
#.xxa.c.azz.#
#..c.....c..#
###..#a#..###
####..p..####
19###########

###########
###.....###
##c.a.a.b##
#.cyy.zzb.#
#..yy.zz..#
#a..c.b..a#
#..xc.bx..#
#..xxxxx..#
##a..a..a##
###...p.###
20#########

message Thanks for playing! :)




title Cliched River Puzzle
author Weeble
homepage www.puzzlescript.net

========
OBJECTS
========

Grass
#559922 GREEN
11111
01111
11101
11111
10111


Player
Yellow
00.00
0...0
.....
0...0
00.00

Water1
#3333ff #5555ff #7777ff
00121
10100
12001
10010
01000

Water2
#3333ff #5555ff #7777ff
10000
00010
00121
01100
10001

Carrot
ORANGE #55cc00
11111
.000.
.000.
.000.
..0..

Fox
ORANGE WHITE BLACK
.2.2.
00000
12021
.111.
..2..

Rabbit
WHITE #ff88bb
00.00
.0.0.
.000.
.101.
.000.

EmptyBoat
BROWN DARKBROWN
.000.
01110
01110
01110
00000
Platform
GREY
.....
.....
.....
.....
.....

LeftB
BLUE
.....
.....
.....
.....
.....

RightB
RED
.....
.....
.....
.....
.....

Dude
BLACK
..0..
00000
..0..
.0.0.
0...0

Dock
YELLOW
.....
.....
.....
.....
.....

=======
LEGEND
=======

. = Grass and LeftB
, = Grass and RightB
~ = Water1 and LeftB
^ = Water1 and RightB
P = Player and LeftB
C = Carrot and Platform and LeftB
F = Fox and Platform and LeftB
R = Rabbit and Platform and LeftB
_ = EmptyBoat and LeftB and Water1
* = Platform and RightB
[ = Dude and Dock and LeftB
] = Dock and RightB
Water = Water1 or Water2
Background = Grass or Water1 or Water2
Object = Rabbit or Fox or Carrot
Bank = LeftB or RightB
=======
SOUNDS
=======

SFX0 81232704
SFX1 47027309
SFX2 92562507
SFX3 11245308

================
COLLISIONLAYERS
================

Background
EmptyBoat, Platform, Dock
LeftB, RightB
Carrot, Rabbit, Fox, Dude
Player

======
RULES
======

(Loading the boat)
[ action Player Object Platform ] [ EmptyBoat no Object ] -> [ Player Platform ] [ EmptyBoat Object ] sfx2

(Emptying the boat)
[ action Player Object EmptyBoat LeftB ] [ Platform no Object LeftB ] -> [ Player EmptyBoat LeftB ] [ Platform Object LeftB ] sfx2
[ action Player Object EmptyBoat RightB ] [ Platform no Object RightB ] -> [ Player EmptyBoat RightB ] [ Platform Object RightB ] sfx2

( Eating rules for stuff on the left bank )
late [ RightB Dude ] [ LeftB Rabbit ] [ LeftB Fox ] -> [ RightB Dude ] [ LeftB ] [ LeftB Fox ] sfx0 message Fox eats rabbit! Chomp!
late [ RightB Dude ] [ LeftB Carrot ] [ LeftB Rabbit ] -> [ RightB Dude ] [ LeftB ] [ LeftB Rabbit ] sfx1 message Rabbit eats carrot! Chomp!

( Same rules for stuff on the right bank )
late [ LeftB Dude ] [ RightB Rabbit ] [ RightB Fox ] -> [ LeftB Dude ] [ RightB ] [ RightB Fox ] sfx0 message Fox eats rabbit! Chomp!
late [ LeftB Dude ] [ RightB Carrot ] [ RightB Rabbit ] -> [ LeftB Dude ] [ RightB ] [ RightB Rabbit ] sfx1 message Rabbit eats carrot! Chomp!

( Animate the water )
[ Water ] -> [ random Water ]

( Row across the river with an object )
[ action Player Dude | EmptyBoat Object | ... | | Dock ] -> [ | | ... | EmptyBoat Object | Player Dude Dock ] sfx3

( Row across the river without an object )
[ action Player Dude | EmptyBoat no Object | ... | | Dock ] -> [ | | ... | EmptyBoat | Player Dude Dock ] sfx3

( Victory condition )
[ RightB Platform Rabbit ] [ RightB Platform Fox ] [ RightB Platform Carrot] -> win

==============
WINCONDITIONS
==============

=======
LEVELS
=======

.....~~~^^,,,,,
.....~~~^^,,,,,
...F.~~~^^,*,,,
..RP[_~~^^],*,,
...C.~~~~^,*,,,
.....~~~^^,,,,,
.....~~~^^,,,,,



title Modality
author Sean Barrett
homepage www.silverspaceship.com
background_color #603060
noaction

========
OBJECTS
========

White
White

Black
#202020


Grey
#505050 #a0a0a0
01010
10101
01010
10101
01010


Background
Red

Target
#3030a0
.....
.000.
.0.0.
.000.
.....

Player
Black Orange #c0c0c0 Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

. = Black
+ = White
g = Grey
p = Player and Black
Q = Player and White
* = Crate and Black
@ = Crate and White
t = Target and Black
U = Target and White

Nonwhite = Black or Grey
Nonblack = White or Grey

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
White, Black, Grey
Target
Player, Crate

======
RULES
======

[ >  Player Nonblack | Crate Nonblack ] -> [  > Player Nonblack | > Crate Nonblack ]
[ >  Player Black | Crate Black ] -> [  > Player Black | > Crate Black ]
[ > Player White | Black ] -> CANCEL
[ > Player Black | White ] -> CANCEL

==============
WINCONDITIONS
==============

All Crate on Target

=======
LEVELS
=======


u+...
@+g.p
+++..

++++q
+.*.+
u++.+
+g..+
+++++

......
.++.+.
.+q++.
..u.++
g.*.++
++++++

...t.......
...+.......
.+.+.+...++
.+.++++..++
q@g++..++++
.+.++...+++
.+.+....+++
.+......+++
.++++++++++



title Drop Swap
author Aaron Steed
homepage www.robotacid.com

key_repeat_interval 0.23
again_interval 0.05
noaction
noundo
background_color Black
text_color Grey
run_rules_on_level_start

========
OBJECTS
========

Background
Black

Player
#ffffff #000000 #626263 #3c3b3c
.222.
21112
23132
20102
.222.

DeadPlayer
#ffffff #000000 #626263 #3c3b3c
.222.
21112
21112
23132
.222.

BaseWall
#222222 #333333 #444444 #ffffff
11110
12220
12200
12000
00000

APill
#ff0000 #ff7374 #ff0000 #ff0000
.111.
11110
11010
11100
.000.

BPill
#00de00 #cffa92 #ff0000 #ff0000
.111.
10010
10110
11100
.000.

CPill
#7a35fc #b56bfd #ff0000 #ff0000
.111.
10110
11010
11100
.000.

DPill
#0094fc #79b5fd #ff0000 #ff0000
.111.
10100
11010
10100
.000.

EPill
#d1c200 #fff919 #ffffff #ff0000
.111.
10010
10010
11110
.000.

FPill
#ff33b1 #ff76d2 #ff0000 #ff0000
.111.
10100
11110
10110
.000.

AllPill
#eeee66 #ffffff #ffffcc #ffffff
.112.
11112
11012
21120
.220.

NoPill
#ffffff #000000 #ffde85 #ff0000
.000.
00000
21012
.000.
..2..

Bomb
#6e6e6e #000000 #ff0000 #fcf26b
.020.
02320
01210
01110
.000.

GemPill
#ffffff #000000 #626263 #3c3b3c
..0..
.002.
00022
.223.
..2..

Count
#ffffff #000000 #5a595a #ff0000
.....
.000.
.000.
.000.
.....

Marked
#ffffff #000000
.....
..1..
.101.
..1..
.....

CountTray
#333333

GemTray
#333333

StartGem
red

Collect
White

Debris1
#ffffff #0739f5 #1f0f42 #ffffff
.0.0.
00.00
..0..
00.00
.0.0.

Debris2
#ffffff #0739f5 #1f0f42 #ffffff
.....
.0.0.
..0..
.0.0.
.....

Empty
Black

OldPos
Black

StartCount
Black

DropWall1
#222222 #333333 #444444 #ffffff
11110
12220
12200
12200
12200

DropWall2
#222222 #333333 #444444 #ffffff
11110
12220
12200
12200
12200

DropWall3
#222222 #333333 #444444 #ffffff
11110
12220
12200
12200
12200

DropWall4
#222222 #333333 #444444 #ffffff
11110
12220
12200
12200
12200

DropMarker
Blue

Init
Purple

=======
LEGEND
=======

. = Background
@ = Player
p = DeadPlayer
# = BaseWall
a = APill
b = BPill
c = CPill
d = DPill
e = EPill
f = FPill
n = NoPill
x = AllPill
u = Count and CountTray
w = Count and CountTray and StartCount
g = GemPill
q = GemTray
o = GemTray and StartGem
p = Bomb
i = Init
j = DropWall1
k = DropWall2
l = DropWall3
m = DropWall4


MatchPill = APill or BPill or CPill or DPill or EPill or FPill or AllPill
Pill = MatchPill or NoPill or GemPill or Bomb
Debris = Debris1 or Debris2
AMatch = APill or AllPill
BMatch = BPill or AllPill
CMatch = CPill or AllPill
DMatch = DPill or AllPill
EMatch = EPill or AllPill
FMatch = FPill or AllPill
Wall = BaseWall or DropWall1 or DropWall2 or DropWall3 or DropWall4
Item = Pill or Debris or Wall or Player
Heavy = Pill or Player
Dropping = Marked or DropMarker
DropMix1 = APill or BPill or CPill or DPill or EPill
DropMix2 = APill or BPill or CPill or DPill or EPill or GemPill
DropMix3 = APill or BPill or CPill or DPill or EPill or NoPill
DropMix4 = APill or BPill or CPill or DPill or EPill or FPill or GemPill
DropMix5 = APill or BPill or CPill or DPill or EPill or FPill
DropWallMarked = DropWall1 or DropWall3

=======
SOUNDS
=======

sfx0 54236904 (illegal)
sfx1 5160507 (swap)
sfx2 19008103 (allpill)
sfx3 50665503 (crush 3)
sfx4 80820507 (land)
sfx5 2030908 (blast)
sfx6 3032303 (bomb)
sfx7 83704102 (dead)
sfx8 7194508 (gem collect)
EndLevel 16709508 (win)

================
COLLISIONLAYERS
================


Collect, StartCount, StartGem
Background
OldPos, CountTray, GemTray
Player, Wall, Pill, Empty, Count, DeadPlayer, Bomb, Init
Debris, Marked, DropMarker

======
RULES
======

(init)
[ DropWall1 ] [ Init ] -> [ DropWall1 ] [ random DropMix1 Marked ]
[ DropWall2 ] [ Init ] -> [ DropWall2 ] [ random DropMix1 ]
[ DropWall3 ] [ Init ] -> [ DropWall3 ] [ random DropMix1 Marked ]
[ DropWall4 ] [ Init ] -> [ DropWall4 ] [ random DropMix5 ]

(debris clean up)
[ Debris2 ] -> [ Empty ] again
[ Debris1 ] -> [ Debris2 ] again

(player - pill swapping)
[ > Player | Pill ] -> [ Pill OldPos | Player ] sfx1
[ Player Marked | OldPos ] -> [ Player | OldPos Marked ]

(blocked move)
[ > Player | No Pill ] -> [ Player | ] sfx0

(pill combos)
[ AMatch | AMatch | AMatch ] -> [ AMatch Collect | AMatch Collect | AMatch Collect ]
[ BMatch | BMatch | BMatch ] -> [ BMatch Collect | BMatch Collect | BMatch Collect ]
[ CMatch | CMatch | CMatch ] -> [ CMatch Collect | CMatch Collect | CMatch Collect ]
[ DMatch | DMatch | DMatch ] -> [ DMatch Collect | DMatch Collect | DMatch Collect ]
[ EMatch | EMatch | EMatch ] -> [ EMatch Collect | EMatch Collect | EMatch Collect ]
[ FMatch | FMatch | FMatch ] -> [ FMatch Collect | FMatch Collect | FMatch Collect ]
[ NoPill | NoPill | NoPill ] -> [ NoPill Collect | NoPill Collect | NoPill Collect ]

(bomb activate)
[ OldPos Bomb ] -> sfx5
up [ OldPos Bomb | Pill ] -> [ OldPos Bomb | Pill > Collect ]
right [ OldPos Bomb | Pill ] -> [ OldPos Bomb | Pill > Collect ]
down [ OldPos Bomb | Pill ] -> [ OldPos Bomb | Pill > Collect ]
left [ OldPos Bomb | Pill ] -> [ OldPos Bomb | Pill > Collect ]
[ OldPos Bomb ] -> [ OldPos Empty ]

(blast)
up [ Pill > Collect | Pill ] -> [ Pill Collect | Pill > Collect ]
right [ Pill > Collect | Pill ] -> [ Pill Collect | Pill > Collect ]
down [ Pill > Collect | Pill ] -> [ Pill Collect | Pill > Collect ]
left [ Pill > Collect | Pill ] -> [ Pill Collect | Pill > Collect ]
[ > Collect ] -> [ Collect ]
[ GemPill Collect ] -> [ GemPill ]

(reward matches)
(junction)
Right [ Collect | Collect ] -> [ > Collect | > Collect ]
Down [ Collect | Collect ] -> [ > Collect | > Collect ]
[ Down Collect | Right Collect ] [ Collect OldPos ] -> [ Collect | Collect ] [ Bomb OldPos ] sfx6
(4: create AllPill)
[ MatchPill Collect | MatchPill Collect | MatchPill Collect | MatchPill Collect ] [ Collect OldPos ] -> [ MatchPill Collect | MatchPill Collect | MatchPill Collect | MatchPill Collect ] [ AllPill OldPos ] sfx2

(count moves)
(subtract on move)
random right [ OldPos ] [ Count | No Count ] -> [ OldPos ] [ | ]
(fill on Debris)
[ Collect ] [ CountTray ] -> [ Collect ] [ CountTray Count ]

(dead?)
[ DeadPlayer ] -> cancel
[ No Count StartCount CountTray ] [ Player ] -> [ No Count StartCount CountTray ] [ DeadPlayer ] sfx7
[ Player | NoPill Collect ] -> [ DeadPlayer | NoPill ]
[ DeadPlayer ] [ CountTray Count ] -> [ DeadPlayer ] [ CountTray ] sfx7

(destroy matched)
[ APill Collect ] -> [ Debris1 ] sfx3 again
[ BPill Collect ] -> [ Debris1 ] sfx3 again
[ CPill Collect ] -> [ Debris1 ] sfx3 again
[ DPill Collect ] -> [ Debris1 ] sfx3 again
[ EPill Collect ] -> [ Debris1 ] sfx3 again
[ FPill Collect ] -> [ Debris1 ] sfx3 again
[ AllPill Collect ] -> [ Debris1 ] sfx3 again
[ Player ] [ NoPill Collect ] -> [ Player ] [ Debris1 ] sfx3 again

(destroy NoPills)
down [ NoPill | Wall ] -> [ Debris1 | Wall ] again

(collect gems)
down [ GemPill | Wall ] -> [ GemPill Collect | Wall ]
[ GemPill Collect ] [ StartGem No GemPill ] -> [ Debris1 ] [ StartGem GemPill ] again sfx8
right [ GemPill Collect ] [ GemPill GemTray | No GemPill GemTray ] -> [ Debris1 ] [ GemPill GemTray | GemPill GemTray ] again sfx8

(drop)
[ Heavy No Dropping ] -> [ Heavy DropMarker ]
down [ Heavy Dropping | Empty ] -> [ Empty | Heavy Dropping ]
[ DropMarker ] -> [ ]
down [ DropWall1 | Empty ] -> [ DropWall1 | random DropMix1 ] again
down [ DropWall2 | Empty ] -> [ DropWall2 | random DropMix2 ] again
down [ DropWall3 | Empty ] -> [ DropWall3 | random DropMix3 ] again
down [ DropWall4 | Empty ] -> [ DropWall4 | random DropMix4 ] again

(gem drop win)
right [ GemPill GemTray | Wall ] -> win

[ OldPos ] -> [ ]

==============
WINCONDITIONS
==============

No Marked
Some DropWallMarked

=======
LEVELS
=======

message collect orbs by matching 3
message match 3 to restore move-energy
message level 1: collect all marked orbs

wuuuuu#
#jjjjj#
#iiiii#
#iiiii#
#iiiii#
#iiiii#
#ii@ii#
#######

message level 2: collect all marked orbs

wuuuuuu#
#jjjjjj#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iii@ii#
########

message collect gray gems by getting them to the bottom
message level 3: collect 8 gems

oqqqqqqq#
wuuuuuuu#
#kkkkkkk#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iii@iii#
#########

message if you are next to 3 matching skulls you die
message skulls can be destroyed by getting them to the bottom
message level 4: collect all marked orbs

wuuuuuu#
#llllll#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iii@ii#
########

message level 5: collect 7 gems

oqqqqqqq#
wuuuuuuu#
#mmmmmmm#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iii@iii#
#########

message thanks for playing



title ScriptCross
author Dennis Au
homepage www.puzzlescript.net

========
OBJECTS
========

Background
White

Target
Black

Fill
Black

Cross
LightGrey
0...0
.0.0.
..0..
.0.0.
0...0


Player
Green
00000
0...0
0...0
0...0
00000

Border #
LightGrey

(Clues 0-9)
Clue0 0
LightGrey

Clue1 1
LightGrey Black
00000
0...0
0.1.0
0...0
00000

Clue2 2
LightGrey Black
00000
0..10
0...0
01..0
00000

Clue3 3
LightGrey Black
00000
0..10
0.1.0
01..0
00000

Clue4 4
LightGrey Black
00000
01.10
0...0
01.10
00000

Clue5 5
LightGrey Black
00000
01.10
0.1.0
01.10
00000

Clue6 6
LightGrey Black
00000
01.10
01.10
01.10
00000

Clue7 7
LightGrey Black
00000
01110
0.1.0
01110
00000

Clue8 8
LightGrey Black
00000
01110
0.110
01110
00000

Clue9 9
LightGrey Black
00000
01110
01110
01110
00000


=======
LEGEND
=======

. = Background
P = Player and Border
b = Target
Clue = Clue0 or Clue1 or Clue2 or Clue3 or Clue4 or Clue5 or Clue6 or Clue7 or Clue8 or Clue9
NoEmpty = Fill or Cross or Clue or Border

=======
SOUNDS
=======
EndLevel 37348103
EndGame 64173909

================
COLLISIONLAYERS
================

(switch order of target and background to see the solutions)
Target
Background
Border, Clue, Fill, Cross
Player


======
RULES
======

[Action Player no NoEmpty] -> [Player Fill]
[Action Player Fill] -> [Player Cross]
[Action Player Cross] -> [Player]



==============
WINCONDITIONS
==============

All Target on Fill
All Fill on Target

=======
LEVELS
=======

(P123456789)

message level 1 - oh so stable

P12
1.b
2bb

message level 2 - it is what it is
P#1###
##113#
##311#
3.bbb.
1...b.
3.bbb.
1.b...
3.bbb.

message level 3 - i see you!
P##1#1#
###111#
##.....
11.b.b.
##.....
#3.bbb.
##.....

message level 4 - that's ace
P#######1####
#######111###
######14141##
#####8111118#
###8.bbbbbbb.
##11.b.....b.
#131.b.bbb.b.
1111.b.b.b.b.
#131.b.bbb.b.
1111.b.b.b.b.
##11.b.....b.
###8.bbbbbbb.
####.........

message congratulations!


USER PROMPT:
The following PuzzleScript game code:
```plaintext
title Push the Box
author Your Name

========
OBJECTS
========

Background
lightgray

Player
blue
..0..
.000.
..0..
..0..
.....

Crate
yellow
.000.
00000
00000
.000.
.....

Target
red
.....
..0..
.000.
..0..
.....

Wall
black
.....
00000
00000
00000
.....

=======
LEGEND
=======

. = Background
P = Player
C = Crate
T = Target
W = Wall
# = Wall

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Target
Crate
Player, Wall

======
RULES
======

[ Player | Crate | No Crate No Wall ] -> [ > Player | > Crate ]
[ > Player | No Crate No Wall ] -> [ > Player ]

==============
WINCONDITIONS
==============

All Crate on Target

=======
LEVELS
=======

message Welcome to Push the Box! Move all crates onto targets.

#######
#.....#
#.P.C.#
#..W..#
#.C.T.#
#..T..#
#######


```
produced the following console output:
=================================
line 71 : In a rule, each pattern to match on the left must have a corresponding pattern on the right of equal length (number of cells).
line 72 : In a rule, each pattern to match on the left must have a corresponding pattern on the right of equal length (number of cells).
Errors detected during compilation; the game may not work correctly.
Return a repaired version of the code that addresses these errors. First, reason about your task and determine the best plan of action. Then, write your code.  Return your code in full, inside a ```plaintext code block.