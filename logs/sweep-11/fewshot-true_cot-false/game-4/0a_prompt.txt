SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title Spriteaporter
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Temp
black

Background
lightgreen green
11111
01111
11101
11111
10111

Target
darkblue
.....
.000.
.0.0.
.000.
.....

Wall
brown darkbrown
00010
11111
01000
11111
00010

Player
black orange white blue
.000.
.111.
22222
.333.
.3.3.

Sprite
pink blue red
.....
.000.
00100
01110
.202.

=======
LEGEND
=======

. = Background
# = Wall
P = Player
O = Sprite

=======
SOUNDS
=======

sfx0 67117309

================
COLLISIONLAYERS
================

Temp
Background
Target
Player, Wall, Sprite

======
RULES
======

late [ Sprite | ... | Player ] -> [ Temp | ... | Sprite ] 
late [ Temp ] -> [ Player ] 

==============
WINCONDITIONS
==============

=======
LEVELS
=======

#########
#.......#
#....O..#
#.P.....#
#.......#
#.......#
#########


title Simple Block Pushing Game
author David Skinner
homepage www.puzzlescript.net
zoomscreen 3x3
========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

[ >  Player | Crate ] -> [  >  Player | > Crate  ]

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======


####..
#.O#..
#..###
#@P..#
#..*.#
#..###
####..


######
#....#
#.#P.#
#.*@.#
#.O@.#
#....#
######


title Bouncers
author Tyler Glaiel
homepage twitter.com/tylerglaiel

========
OBJECTS
========

Bg
#111111 #222222
00010
11111
01000
01000
11111

Selection
white gray
..1..
..1..
11011
..1..
..1..

Wall
DarkBlue DarkGreen #333333
20002
22022
22122
21112
11111

Hole
DarkGray Gray Black
.111.
12221
10001
10001
.111.


Arrow
Red Red
..1..
..11.
11111
..11.
..1..


HudDivider
white black
00000
11111
11111
11111
11111

IconGO
green white
10111
10011
10001
10011
10111

Going
green white
10111
10011
10001
10011
10111

LeftBouncer
White White Blue
.221.
2.1.2
21..2
2.1.2
.221.

RightBouncer
White White Blue
.122.
2.1.2
2..12
2.1.2
.122.

UpBouncer
White White Blue
.222.
2.1.2
21.12
1...1
.222.

DownBouncer
White White Blue
.222.
1...1
21.12
2.1.2
.222.

HudBG
black

Empty
black

PlayEmpty
black
.....
.....
.....
.....
.....


Ball
#003300 Green
.000.
01110
01110
01110
.000.

WinBall
LightBlue LightGreen
.000.
01110
01110
01110
.000.



BallUp
#003300 Green Black
.000.
01210
01110
01110
.000.

BallDown
#003300 Green Black
.000.
01110
01110
01210
.000.

BallLeft
#003300 Green Black
.000.
01110
02110
01110
.000.

BallRight
#003300 Green Black
.000.
01110
01120
01110
.000.




=======
LEGEND
=======
Background = Bg or HudBG
. = bg and PlayEmpty
S = Selection and PlayEmpty
- = HudDivider and Wall
H = HudBG and PlayEmpty
A = Arrow and HudBG
G = IconGO
W = Wall
O = Hole

L = LeftBouncer and HudBG
R = RightBouncer and HudBG
U = UpBouncer and HudBG
D = DownBouncer and HudBG

B = BallLeft
N = BallRight
M = BallUp
K = BallDown

Player = Selection


PlacedLeftbouncer = LeftBouncer and Selection
PlacedRightbouncer = RightBouncer and Selection
PlacedUpbouncer = UpBouncer and Selection
PlacedDownbouncer = DownBouncer and Selection

Bouncer = LeftBouncer or RightBouncer or UpBouncer or DownBouncer

emptyPlayer = Selection and PlayEmpty


ballSwitchLeft = Ball and LeftBouncer
ballSwitchRight = Ball and RightBouncer
ballSwitchUp = Ball and UpBouncer
ballSwitchDown = Ball and DownBouncer

ballSwitchedLeft = BallLeft and LeftBouncer
ballSwitchedRight = BallRight and RightBouncer
ballSwitchedUp = BallUp and UpBouncer
ballSwitchedDown = BallDown and DownBouncer

gameGo = Selection and IconGO
gameGoing =  Going



WinL = BallLeft and Hole
WinR = BallRight and Hole
WinU = BallUp and Hole
WinD = BallDown and Hole

Win = WinBall

MovingBall = Ball or BallLeft or BallRight or BallUp or BallDown

=======
SOUNDS
=======
SFX0 41242705 (place bouncer)
SFX1 73889302 (press go)
MovingBall Move 68254307 (ball move)
SFX2 68257500 (pocket ball)
SFX3 82752503 (ball switch direction)

================
COLLISIONLAYERS
================

Background

Arrow, Empty, LeftBouncer, RightBouncer, UpBouncer, DownBouncer, PlayEmpty, IconGO, Going, Hole
WinBall
Wall, Ball, BallUp, BallDown, BallLeft, BallRight
Selection, HudDivider


======
RULES
======

(action button: take icon to the right of the arrow, place under selector)
[action selection PlayEmpty][Arrow|LeftBouncer] -> [PlacedLeftbouncer][Arrow|Empty] SFX0
[action selection PlayEmpty][Arrow|RightBouncer] -> [PlacedRightbouncer][Arrow|Empty] SFX0
[action selection PlayEmpty][Arrow|UpBouncer] -> [PlacedUpbouncer][Arrow|Empty] SFX0
[action selection PlayEmpty][Arrow|DownBouncer] -> [PlacedDownbouncer][Arrow|Empty] SFX0

(win conditions)
[WinL] -> [Win] again SFX2
[WinR] -> [Win] again SFX2
[WinU] -> [Win] again SFX2
[WinD] -> [Win] again SFX2

(slide unused icons over to the left if there's space between them and the arrow)
right [Empty | Bouncer] -> [Bouncer | Empty]

(selector on top of go button -> signals game to run)
[action selection iconGO] -> [gameGoing] again SFX1

(make genericball match the direction of the bouncer)
[ballSwitchLeft]->[ballSwitchedLeft] again
[ballSwitchRight]->[ballSwitchedRight] again
[ballSwitchUp]->[ballSwitchedUp] again
[ballSwitchDown]->[ballSwitchedDown] again

(if ball is going to move on top of a bouncer, switch it to generic ball first)
right [Going][Bouncer | BallLeft] -> [Going][Bouncer | Left Ball] again SFX3
left  [Going][Bouncer | BallRight] -> [Going][Bouncer | Right Ball] again SFX3
up    [Going][Bouncer | BallDown] -> [Going][Bouncer | Down Ball] again SFX3
down  [Going][Bouncer | BallUp] -> [Going][Bouncer | Up Ball] again SFX3



(move the ball if Going)
[Going][BallLeft] -> [Going][LEFT BallLeft] again
[Going][BallRight] -> [Going][RIGHT BallRight] again
[Going][BallUp] -> [Going][UP BallUp] again
[Going][BallDown] -> [Going][DOWN BallDown] again



[Going] -> [Going] again

==============
WINCONDITIONS
==============

no MovingBall

=======
LEVELS
=======

message place bouncers, hit play

......w.....
..o...w.....
......w.....
......w.....
......w..b..
......s.....
............
g-----------
-hhadluhhhhh

message Level 2

............
......o.....
..s.........
......w.....
...o.....b..
......m.....
............
g-----------
-hhalurhhhhh

message Level 3

............
............
..o.wwww....
..s.n..b....
....wwww.o..
............
............
g-----------
-hhaudrlhhhh

message Level 4

............
............
..o......o..
............
............
.s..bbbb....
............
............
..o......o..
............
g-----------
-hhalrudu...

message Level 5

.....n......w...
..wwww.k....o.w.
......b..m......
...o............
.......w........
.....b.w........
......wwo.......
w.ww.www........
.o..............
....s...........
..........wwwo..
g---------------
-hhaddddddrrr...

message I didn't feel like making more levels

title The Legend of Zokoban
author Joshua Minor
homepage pixelverse.org
flickscreen 9x7

========
OBJECTS
========

Background 
yellow orange
00000
01000
00001
00100
10000

Water 
blue lightblue
00000
01000
00001
00100
10000

Cave 
DarkBlue
00000
00000
00000
00000
00000

Goal 
DarkBlue Red
00000
00000
00100
00000
00000

RockWall 
BROWN Orange
01110
10000
10010
10000
00100

StoneWall 
darkgrey grey
01110
10000
10010
10000
00100

PlayerWithoutSword
GREEN lightgreen 
.00..
00000
.000.
.111.
00.00

PlayerWithSword
GREEN lightgreen white grey
.002.
00020
.0222
.110.
00.00

Sword 
grey white 
..1..
..1..
.111.
..0..
.....

Block  
BROWN Orange darkgrey
.111.
10001
10002
10002
.222.

EasyBaddie
white red 
.111.
10101
11111
11111
11.11

Money
white lightblue
..1..
.001.
10111
.111.
..1..

=======
LEGEND
=======

Wall = StoneWall or RockWall
Solid = Wall or Block or Water
Player = PlayerWithoutSword or PlayerWithSword
Obstacle = Wall or Block or Player or Water
Baddie = EasyBaddie

. = Background
# = RockWall
S = StoneWall
P = PlayerWithoutSword
* = Block
@ = Block and Cave
O = Cave
w = Water
1 = EasyBaddie
+ = Sword
G = Goal

=======
SOUNDS
=======

StartLevel 40430108 (music-ish)
SFX0 16358103 (power up)
SFX1 84057304 (push)
SFX2 74654508 (stairs)
SFX3 21293301 (fight)
SFX4 49521903 (hurt)
SFX5 47598700 (money)
ShowMessage 82693708 (you win!)

================
COLLISIONLAYERS
================

Background
Cave, Goal
Player, Wall, Block, Water, EasyBaddie
Sword, Money

======
RULES     
======     

(baddies move at random)
[ Stationary Baddie ] -> [ RandomDir Baddie ]

(player can push blocks)
[ > Player | Block ] -> [ > Player | > Block ] SFX1

(blocks pushed into water become land)
[ > Block | Water ] -> [ | ]

(baddies can attack the player w/o sword)
[ > Baddie | Player No Sword ] -> SFX4

(player can pick up sword)
([ > Player | Sword ] -> [ | Player Sword ] SFX0)
[ PlayerWithoutSword Sword ] -> [ PlayerWithSword ] SFX0

(player with sword can kill baddies & leave money)
[ > PlayerWithSword | Baddie ] -> [ PlayerWithSword | Money ] SFX3
(sword moves with player)
([ > Player Sword ] -> [ > Player > Sword ])
(sword doesn't move into obstacles)
([ > Sword | Stationary Obstacle ] -> [ Sword | Stationary Obstacle ])

(player can pick up money)
[ > Player | Money ] -> [ | Player ] SFX5

(blocks pushed into caves, teleport to other end of cave)
random [ Moving Block Cave | ... | Cave ] -> [ Cave | ... | Block Cave ]
(blocks sitting on cave exits will roll aside to let player follow them)
late random [ Block Cave | No Obstacle ] -> [ Cave | Block ]
(players can enter caves - teleporting to orthogonally matching cave)
late random [ Player Cave | ... | Cave ] -> [ Cave | ... | Player Cave ] SFX2
(sword follows player through the cave)
(late random [ Sword Cave | ... | Cave ] -> [ Cave | ... | Sword Cave ])


==============
WINCONDITIONS
==============

All Player on Goal     

=======     
LEVELS
=======

sssssssss#################wwwwwwwwww
ss.s1s..s#1#####.##..1..1.wwwwww..ww
s.......s#.#####.##.#.#.#.wwwww..*.w
s..*....o#.##o##.............ww....w
s.s....ss#..........#.*.#....ww..1.w
sss.s.sss##...............wwwwww..ww
sssssssss#####..#####.....wwwwwwwwww
sssssssss#####..#####.....wwwwwwwwww
ss1...sss#####..####.....wwww.....ww
s......os##o#....##...#.1www..s.s..w
s..+...ss#.......##*......ww...g...w
ss.....ss#...p..........#.ww..s.s.1w
ssss..sss##.....###..#.1..www..1..ww
sssssssss#################wwwwwwwwww

message Congratulations!

title Cake Monsters
author Matt Rix
homepage www.magicule.com
color_palette whitingjp
text_color #FF5588
background_color #050A0A
noaction
youtube Z6ih1aKeETk (tycho!)

========
OBJECTS
========

(-----------------------)
(----MONSTER COLOURS----)
(-----------------------)

RedMon
#D8144D #B00F3F BLACK WHITE
00000
020.3
000..
00000
1..1.

YellowMon
#F3F333 #C7C829 BLACK WHITE
00000
020.3
000..
00000
1..1.

BlueMon
#0366E3 #0A52BB BLACK WHITE
00000
020.3
000..
00000
1..1.

PurpleMon
#9E14E2 #800FBA BLACK WHITE
00000
020.3
000..
00000
1..1.

OrangeMon
#E68A25 #BB711E BLACK WHITE
00000
020.3
000..
00000
1..1.

GreenMon
#1CED55 #15C346 BLACK WHITE
00000
020.3
000..
00000
1..1.

TrashMon
#7C6953 #6D5239 BLACK WHITE
00000
020.3
000..
00000
1..1.


(------------------------)
(------CAKE COLOURS------)
(------------------------)

RedCake
#D8144D #B00F3F
.000.
00000
00000
10001
.111.

YellowCake
#F3F333 #C7C829
.000.
00000
00000
10001
.111.

BlueCake
#0366E3 #0A52BB
.000.
00000
00000
10001
.111.


PurpleCake
#9E14E2 #800FBA
.000.
00000
00000
10001
.111.

OrangeCake
#E68A25 #BB711E
.000.
00000
00000
10001
.111.

GreenCake
#1CED55 #15C346
.000.
00000
00000
10001
.111.

TrashCake
#7C6953 #6D5239
.000.
00000
00000
10001
.111.

(-----------------------)
(------OTHER STUFF------)
(-----------------------)

Destroyer
#001111 #223333 #384949
.222.
20202
12021
10101
.111.

Background
#152020 #192525
01101
10110
01101
11010
01010


Wall
#334444 #384949 #445555 #475959 #4A6B6B
34443
23332
12221
01110
00000

Blackness
#050A0A


=======
LEGEND
=======


. = Background
_ = Blackness
# = Wall
X = Destroyer

R = RedMon
Y = YellowMon
B = BlueMon
P = PurpleMon
O = OrangeMon
G = GreenMon
T = TrashMon

Monster = RedMon or YellowMon or BlueMon or PurpleMon or OrangeMon or GreenMon or TrashMon
Player = Monster

PrimaryMon = RedMon or YellowMon or BlueMon
SecondaryMon = PurpleMon or OrangeMon or GreenMon

(each cake char is the next letter in the alphabet after the monster char)
(except for orange, sadly)
S = RedCake
Z = YellowCake
C = BlueCake
Q = PurpleCake
A = OrangeCake
H = GreenCake
U = TrashCake

Cake = RedCake or YellowCake or BlueCake or PurpleCake or OrangeCake or GreenCake or TrashCake

=======
SOUNDS
=======

sfx0 8478107 (move)
sfx1 18590508 (eat other monster)
sfx2 61437309 (eat cake)
sfx3 89161102 (monster destroyed)
startlevel 563108
endlevel 76028308
undo 82948709
restart 22851101

================
COLLISIONLAYERS
================

Background
Blackness, Wall, Destroyer
Monster,Cake



======
RULES
======

(------------------------)
(----MONSTER MOVEMENT----)
(------------------------)

(movement sound)
[ > Monster ] -> [ > Monster ] sfx0

(stop the monster if it hits a wall)
[ > Monster | Wall ] -> [ Monster | Wall]

(if monster hits a destroyer, remove the monster)
[ > Monster | Destroyer ] -> [ | Destroyer] sfx3

(monster eats cakes of the same color)
[ > RedMon | RedCake] -> [ | RedMon] sfx2
[ > BlueMon | BlueCake] -> [ | BlueMon] sfx2
[ > YellowMon | YellowCake] -> [ | YellowMon] sfx2
[ > PurpleMon | PurpleCake] -> [ | PurpleMon] sfx2
[ > OrangeMon | OrangeCake] -> [ | OrangeMon] sfx2
[ > GreenMon | GreenCake] -> [ | GreenMon] sfx2
[ > TrashMon | TrashCake] -> [ | TrashMon] sfx2

(stop the monster if it hits a cake of a different color)
(note: this is necessary to make sure the monster is marked as stationary)
[ > RedMon | Cake no RedCake] -> [ RedMon | Cake no RedCake]
[ > BlueMon | Cake no BlueCake] -> [ BlueMon | Cake no BlueCake]
[ > YellowMon | Cake no YellowCake] -> [ YellowMon | Cake no YellowCake]
[ > PurpleMon | Cake no PurpleCake] -> [ PurpleMon | Cake no PurpleCake]
[ > OrangeMon | Cake no OrangeCake] -> [ OrangeMon | Cake no OrangeCake]
[ > GreenMon | Cake no GreenCake] -> [ GreenMon | Cake no GreenCake]
[ > TrashMon | Cake no TrashCake] -> [ TrashMon | Cake no TrashCake]

(--------------------------)
(---MONSTER COLOR MIXING---)
(--------------------------)

(red+blue = purple)
[ > BlueMon | stationary RedMon] -> [ | PurpleMon] sfx1
[ > RedMon | stationary BlueMon] -> [ | PurpleMon] sfx1

(red+yellow = orange)
[ > RedMon | stationary YellowMon] -> [ | OrangeMon] sfx1
[ > YellowMon | stationary RedMon] -> [ | OrangeMon] sfx1

(blue+yellow = green)
[ > BlueMon | stationary YellowMon] -> [ | GreenMon] sfx1
[ > YellowMon | stationary BlueMon] -> [ | GreenMon] sfx1

(primary color + secondary color = brown aka trash)
[ > PrimaryMon | stationary SecondaryMon] -> [ | TrashMon] sfx1
[ > SecondaryMon | stationary PrimaryMon] -> [ | TrashMon] sfx1

(green+orange = trash)
[ > GreenMon | stationary OrangeMon] -> [ | TrashMon] sfx1
[ > OrangeMon | stationary GreenMon] -> [ | TrashMon] sfx1

(purple+orange = trash)
[ > PurpleMon | stationary OrangeMon] -> [ | TrashMon] sfx1
[ > OrangeMon | stationary PurpleMon] -> [ | TrashMon] sfx1

(green+purple = trash)
[ > GreenMon | stationary PurpleMon] -> [ | TrashMon] sfx1
[ > PurpleMon | stationary GreenMon] -> [ | TrashMon] sfx1

(trash+anything = trash)
[ > TrashMon | stationary Monster] -> [ | TrashMon] sfx1
[ > Monster | stationary TrashMon] -> [ | TrashMon] sfx1

(monster + monster of the same color = monster of the same color)
[ > Monster | stationary Monster] -> [ | Monster] sfx1


==============
WINCONDITIONS
==============

No Cake


=======
LEVELS
=======


message WELCOME TO CAKE MONSTERS!
message MONSTERS LIKE EATING CAKE

_______
#######
#.....#
#.r...#
#...s.#
#.....#
#######
_______
(diff 0)

message YOU MOVE ALL OF THE MONSTERS

######_######
#....#_#z...#
#.r..#_#....#
#..s.#_#....#
#....#_#...y#
######_######
_____________
######_######
#.c..#_#....#
#....#_#...h#
#..b.#_#g...#
#....#_#....#
######_######
(diff 0)

message MONSTERS ONLY LIKE THEIR OWN CAKE
message REMEMBER: Z TO UNDO, R TO RESTART
_________
#########
#r.z....#
#######.#
#.......#
#.sy....#
#########
_________
(diff 1)

message MONSTERS MIX WHEN THEY GET EATEN
_________
#########
#.......#
#.#####.#
#.r.q.b.#
#.#####.#
#.......#
#########
_________
(diff 1)


message BROWN + ANY COLOR = BROWN
__________
##########
#........#
#.rybpog.#
#........#
#........#
#.szcqah.#
#........#
#........#
#.uuuuuu.#
#........#
##########
__________
(diff 1)


message HAVE FUN!
############
#y........c#
#b........z#
#####..#####
____#..#____
____#..#____
____#hh#____
____####____
(diff 1)

(----------------------------)


_____
#####
#RCQ#
#.CS#
#Q.B#
#####
_____
(diff 2)




______
######
#R...#
#B...#
#Q.QQ#
#CSCS#
######
______
(diff 2)

_______
#######
#R.H.B#
#.....#
#.....#
#Y.Q.B#
#######
_______
(diff 2)

_______
#######
#CCHCB#
#.....#
#YZHZZ#
#######
_______
(diff 2)


______
######
#R..B#
#S.C.#
#.A.C#
#Y.C.#
######
______
(diff 2)


message PIECE OF CAKE, RIGHT?


#########
#ccccccc#
#bcccccc#
######cc#
_____#ch#
######zz#
#yzzzzzz#
#yzzzzzz#
######zz#
_____#sa#
######ss#
#rssssss#
#sssssss#
#########
(diff 3, fun)

_______
#######
#R.B.Y#
#Y.B.R#
#R.B.Y#
#Y.B.R#
#AHQHA#
#######
_______
(diff 3)


_______
#######
#AR.BA#
#.#.#.#
#H...H#
##.A.##
#HY#YH#
#######
_______
(diff 3)



message X TILES DESTROY MONSTERS
_________
_###_###_
#...#...#
#.q.x.p.#
#...x...#
_#..x..#_
__#...#__
___#.#___
____#____
_________
(diff 0, destroyerz)


_____
#####
#RAR#
#RXY#
#YAY#
#####
_____
(diff 1, destroyerz)

______
######
#BHHB#
#YXXY#
#YXXY#
#BHHB#
######
______
(diff 1 destroyerz)

_______
#######
#RQXQR#
#SQXQS#
#BQXQB#
#######
_______
(diff 1 destroyerz)

___________
#####_#####
#sss#_#qqq#
#sss###qqq#
#x.......x#
##x.rxb.x##
#x.......x#
#qqq###ccc#
#qqq#_#ccc#
#####_#####
___________
(diff 2 destroyerz)

___________
###########
#...#q#.b.#
#sxr#.#xx.#
#.x.....c.#
###########
___________
(diff 3 destroyerz)



message YOU'RE CAKING THIS LOOK EASY!

______
######
#RZSZ#
#YSZS#
#..SZ#
#..ZS#
######
______
(diff 3)


#######
#Y#B#Y#
#.#R#.#
#.#.#.#
#.....#
#.###.#
#HHAHH#
#######
(diff 3, fun!)



####__####
#qc####cb#
#c......c#
##.####.##
_#.#xx#.#_
_#.c.r#.#_
##.####.##
#c......c#
#qc####cq#
####__####
(diff 3)




_#########_
_#rrr.sss#_
_#.......#_
_#yyy.zzz#_
_#.......#_
##bbb.ccc##
#.........#
#.q..a..h.#
#.........#
###########

______
######
#RA.Y#
#....#
#HH..#
#SBCY#
######
______
(diff 4)

message LET'S CAKE IT TO THE NEXT TIER!


#############
#...#.h.#...#
#.r.#...#.y.#
#...c#c#s...#
###c.....s###
#..#.#h#.#..#
#z.h.z.c.s.c#
#..#.#s#.#..#
###......z###
#....#z#z...#
#.b.#...#.g.#
#...#.s.#...#
#############
(diff 3, easy but fun)


_____
#####
#B.B#
#RRY#
#.Q.#
#S.S#
#CHC#
#####
_____
(diff 5 - fun!)


______
######
#HYBH#
#YBYB#
#BBBB#
#HHHH#
######
______
(diff 4)




______
######
#BR.Y#
#..A.#
#SCZA#
#CSZA#
######
______
(diff 4)




______
######
#CZG.#
#ZCR##
###..#
#SHY##
#HSB.#
######
______
(diff 5 - very fun!)


message YOU'RE GETTING BATTER!

______
######
#YYYY#
#...R#
#AAAA#
#ZZZZ#
######
______
(diff 5 - fun but surprisingly hard)


_______
#######
#Y..YA#
#.BB.H#
#R..RQ#
#Y..YH#
#######
_______
(diff 4)


message ICING ON THE CAKE!

______
######
#ZBBZ#
#YBYB#
#....#
#ZHCZ#
######
______
(diff 5)




_______
#######
#A.S.A#
#.Z.Z.#
#SYRYS#
#.ZYZ.#
#A.S.A#
#######
_______
(diff 5 - not too hard)



message WILL YOU CAKE IT OUT ALIVE?
______
######
#ZZZZ#
#SSSS#
#CCCC#
#YRB.#
######
______
(diff 6 - fun but tricky)




message THE FINAL CHALLENGE!
message DOUGH YOU HAVE WHAT IT CAKES?
_____
#####
#AYA#
#...#
#CBC#
#SZY#
#RZS#
#CBC#
#...#
#HOH#
#####
_____
(diff 8 - a pretty tough one)



message WELL, YOU REALLY TOOK THE CAKE!
message CONGRATS, YOU WIN! :)




title Simple Action Example
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.


Blood
DarkRed red white
.01.0
.1202
2.0.1
02.1.
.1.0.

Sheep O
black white gray
..11.
01111
01112
.212.
.0.0.


=======
LEGEND
=======

. = Background
# = Wall
P = Player


=======
SOUNDS
=======

Sheep MOVE 76935307
SFx0 52519108

================
COLLISIONLAYERS
================

Background
Blood
Player, Wall, Sheep

======
RULES
======

[ Action Player | Sheep ] -> [  Action  Player | > Sheep  ]

[ > Sheep |  Sheep ] -> [ blood | > Sheep ] Sfx0

==============
WINCONDITIONS
==============

=======
LEVELS
=======

#################
#...............#
#...............#
#......ooo......#
#########ooo....#
#P.o...o..ooo...#
#########oooo...#
#...oooooooo....#
#......oo.......#
#...............#
#################



title Sokobond: The Demake
author Jonah Ostroff
homepage www.sokobond.com
text_color #202020
background_color #FDFDFD
key_repeat_interval 0.25
run_rules_on_level_start
noaction
youtube 2R0sTlruOZc

========
OBJECTS
========

Background
#FFFFFF #F8F8F8
00000
01010
00000
01010
00000

Player
#FFBBEE
00000
0...0
0...0
0...0
00000

Wall
Yellow White
00000
00000
00000
00000
00000

Hydrogen
Red Black
.....
.111.
.101.
.111.
.....

Nitrogen
Green Black
.....
.111.
.101.
.111.
.....

Oxygen
Blue Black
.....
.111.
.101.
.111.
.....

Carbon
Yellow Black
.....
.111.
.101.
.111.
.....

Helium
White Black
.....
.111.
.101.
.111.
.....

1Electron
LightGray
.....
.0...
.....
.....
.....

2Electron
LightGray
.....
...0.
.....
.0...
.....

3Electron
LightGray
.....
.0.0.
.....
.0...
.....

4Electron
LightGray
.....
.0.0.
.....
.0.0.
.....

BondU
Black
..0..
.....
.....
.....
.....

BondL
Black
.....
.....
0....
.....
.....

BondR
Black
.....
.....
....0
.....
.....

BondD
Black
.....
.....
.....
.....
..0..


BondUL
Black
..0..
.....
0....
.....
.....

BondUR
Black
..0..
.....
....0
.....
.....

BondUD
Black
..0..
.....
.....
.....
..0..

BondLR
Black
.....
.....
0...0
.....
.....

BondLD
Black
.....
.....
0....
.....
..0..

BondRD
Black
.....
.....
....0
.....
..0..

BondULR
Black
..0..
.....
0...0
.....
.....

BondULD
Black
..0..
.....
0....
.....
..0..

BondURD
Black
..0..
.....
....0
.....
..0..

BondLRD
Black
.....
.....
0...0
.....
..0..

BondULRD
Black
..0..
.....
0...0
.....
..0..

Temp1
Red

Temp2 (These are placeholders for various things.)
Green

Temp3
Blue

Temp4
Yellow

=======
LEGEND
=======

Atom = Hydrogen or Oxygen or Nitrogen or Carbon or Helium
Orbital = 1Electron or 2Electron or 3Electron or 4Electron
UBond = BondU or BondUL or BondUR or BondUD or BondULR or BondULD or BondURD or BondULRD
LBond = BondL or BondUL or BondLR or BondLD or BondULR or BondULD or BondLRD or BondULRD
RBond = BondR or BondUR or BondLR or BondRD or BondULR or BondURD or BondLRD or BondULRD
DBond = BondD or BondUD or BondLD or BondRD or BondULD or BondURD or BondLRD or BondULRD
1Bond = BondU or BondL or BondR or BondD
2Bond = BondUL or BondUR or BondUD or BondLR or BondLD or BondRD
3Bond = BondULR or BondULD or BondURD or BondLRD
Bond = 1Bond or 2Bond or 3Bond or BondULRD
Temps = Temp1 or Temp2 or Temp3 or Temp4
AtomStuff = Bond or Orbital

. = Background
# = Wall
H = Hydrogen and 1Electron
O = Oxygen and 2Electron
N = Nitrogen and 3Electron
C = Carbon and 4Electron
E = Helium
0 = Helium  and Player
1 = Hydrogen and 1Electron and Player
2 = Oxygen and 2Electron and Player
3 = Nitrogen and 3Electron and Player
4 = Carbon and 4Electron and Player
P = Player

=======
SOUNDS
=======

Sfx1 64050702 (Bonding sound)
EndLevel 96545300 (End level)

================
COLLISIONLAYERS
================

Background
Player
Bond
Wall, Atom
Orbital
Temps

======
RULES
======

(This is the old way I did movement, and it was super inefficient.)
([> Player Atom] -> [> Player > Atom]

Up [ Moving Atom UBond | Atom] -> [ Moving Atom UBond | Moving Atom]
+ Left [ Moving Atom LBond | Atom] -> [ Moving Atom LBond | Moving Atom]
+ Right [ Moving Atom RBond | Atom] -> [ Moving Atom RBond | Moving Atom]
+ Down [ Moving Atom DBond | Atom] -> [ Moving Atom DBond | Moving Atom]
+ [> Atom | Atom] -> [> Atom | > Atom])

[Up Player] -> [Up Player Temp1]
[Left Player] -> [Left Player Temp2]
[Right Player] -> [Right Player Temp3]
[Down Player] -> [Down Player Temp4]

Up [Temps UBond | no Temps] -> [Temps Ubond | Temps]
+ Left [Temps LBond | no Temps] -> [Temps Lbond | Temps]
+ Right [Temps RBond | no Temps] -> [Temps Rbond | Temps]
+ DOwn [Temps DBond | no Temps] -> [Temps Dbond | Temps]
+ Up [Temp1 | Atom no Temp1] -> [Temp1 | Atom Temp1]
+ Left [Temp2 | Atom no Temp2] -> [Temp2 | Atom Temp2]
+ Right [Temp3 | Atom no Temp3] -> [Temp3 | Atom Temp3]
+ Down [Temp4 | Atom no Temp4] -> [Temp4 | Atom Temp4]

[Temp1 Atom] -> [Up Atom]
[Temp2 Atom] -> [Left Atom]
[Temp3 Atom] -> [Right Atom]
[Temp4 Atom] -> [Down Atom]

[ > Atom | Wall] -> Cancel

[ > Atom AtomStuff] -> [ > Atom > AtomStuff]

Late Right [Orbital no Temps no RBond|Orbital no Temps no LBond] -> [Orbital Temp1|Orbital Temp2 Sfx1] (Bond horizontally.)
+ Late [Temp1 no Bond] -> [Temp3 BondR]
+ Late [Temp1 BondU] -> [Temp3 BondUR]
+ Late [Temp1 BondL] -> [Temp3 BondLR]
+ Late [Temp1 BondD] -> [Temp3 BondRD]
+ Late [Temp1 BondUL] -> [Temp3 BondULR]
+ Late [Temp1 BondUD] -> [Temp3 BondURD]
+ Late [Temp1 BondLD] -> [Temp3 BondLRD]
+ Late [Temp1 BondULD] -> [Temp3 BondULRD]
+ Late [Temp2 no Bond] -> [Temp3 BondL]
+ Late [Temp2 BondU] -> [Temp3 BondUL]
+ Late [Temp2 BondR] -> [Temp3 BondLR]
+ Late [Temp2 BondD] -> [Temp3 BondLD]
+ Late [Temp2 BondUR] -> [Temp3 BondULR]
+ Late [Temp2 BondUD] -> [Temp3 BondULD]
+ Late [Temp2 BondRD] -> [Temp3 BondLRD]
+ Late [Temp2 BondURD] -> [Temp3 BondULRD]
+ Late [Temp3 1Electron] -> []
+ Late [Temp3 2Electron] -> [1Electron]
+ Late [Temp3 3Electron] -> [2Electron]
+ Late [Temp3 4Electron] -> [3Electron]

Late Down [Orbital no Temps no DBond|Orbital no Temps no UBond] -> [Orbital Temp1|Orbital Temp2 Sfx1] (Bond vertically.)
+ Late [Temp1 no Bond] -> [Temp3 BondD]
+ Late [Temp1 BondU] -> [Temp3 BondUD]
+ Late [Temp1 BondL] -> [Temp3 BondLD]
+ Late [Temp1 BondR] -> [Temp3 BondRD]
+ Late [Temp1 BondUL] -> [Temp3 BondULD]
+ Late [Temp1 BondUR] -> [Temp3 BondURD]
+ Late [Temp1 BondLR] -> [Temp3 BondLRD]
+ Late [Temp1 BondULR] -> [Temp3 BondULRD]
+ Late [Temp2 no Bond] -> [Temp3 BondU]
+ Late [Temp2 BondL] -> [Temp3 BondUL]
+ Late [Temp2 BondR] -> [Temp3 BondUR]
+ Late [Temp2 BondD] -> [Temp3 BondUD]
+ Late [Temp2 BondLR] -> [Temp3 BondULR]
+ Late [Temp2 BondLD] -> [Temp3 BondULD]
+ Late [Temp2 BondRD] -> [Temp3 BondURD]
+ Late [Temp2 BondLRD] -> [Temp3 BondULRD]
+ Late [Temp3 1Electron] -> []
+ Late [Temp3 2Electron] -> [1Electron]
+ Late [Temp3 3Electron] -> [2Electron]
+ Late [Temp3 4Electron] -> [3Electron]

==============
WINCONDITIONS
==============

No Orbital

=======
LEVELS
=======

Message This is a demake of Sokobond by Alan Hazelden and Harry Lee.

Message Get the full version at www.sokobond.com!

#########
####....#
####....#
#....#H.#
#...O#..#
#.1.....#
#....####
#########

Message Water atoms are icosahedral.

######
#H..O#
#....#
#1.###
######

Message The blue tint seen in large bodies of water comes from the pigment in the oxygen atoms.

########
###...##
#2.H.H.#
###...##
########

Message The Earth's surface and the human body are both 70% water. That CAN'T be a coincidence, right?

#######
#..O..#
#..#..#
#.1#H.#
#..#..#
#..O..#
#######

Message "One hydrogen per oxide" is defined as the speed at which atoms move.

#######
#.H.3.#
#.....#
#.H.H.#
#######

Message Ammonia is an inflammatory condition of the lungs.

#######
#.....#
#.....#
#H#H#H#
#.....#
#..3..#
#######

Message Hang on, I was thinking of "pneumonia".

#########
####4####
###...###
##.H.H.##
#.......#
##.H.H.##
###...###
####.####
#########

Message 90% of all the methane in the world is produced in Arizona.

#########
##H...H##
#...4...#
##H...H##
#########

Message Methane contains the name "Ethan". Do you think it was discovered by a guy named Ethan?

########
#......#
#...H..#
#O.H..3#
#...H..#
#......#
########

Message Hydroxylamine contains almost every vowel of the alphabet almost exactly once.

#########
#.......#
#..H.H..#
#.2...C.#
#..H.H..#
#.......#
#########

Message Methanol occurs naturally in peppermint oils.

#########
#.......#
#.1...E.#
###.H.###
###...###
###.O.###
#########

Message Helium is named after the Greek god Helios, who first discovered its existence.

#########
#.......#
#.E.E.E.#
###H.1###
###.H.###
###...###
###.N.###
#########

Message Inhaling helium contracts the ear drum, causing pitches to sound higher. Your voice is perceived normally by everyone else.

#######
###..##
##..H.#
#..N..#
#.H..H#
##..0.#
#######

Message The first helium balloons were used at the wedding of Queen Victoria and Prince Albert, popularizing the term "noble gas".

#######
#.....#
#..N..#
#..0..#
#H.H.H#
#.#.#.#
#.....#
#######

Message Helium does not form chemical bonds with other elements, but it's the bonds of friendship that really matter.

########
#......#
#...H#N#
#......#
##H#H.0#
##.....#
####...#
########

Message Scientists have been searching for centuries to discover the missing elements between hydrogen and helium on the periodic table.

Message Thanks for playing! Go get the full version, which has lots of cool stuff that I couldn't write in PuzzleScript: www.sokobond.com

USER PROMPT:
Output the code for a complete PuzzleScript game. Return your code in full, inside a ```plaintext code block.