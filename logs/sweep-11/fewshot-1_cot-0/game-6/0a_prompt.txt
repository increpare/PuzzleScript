SYSTEM PROMPT:
You are a game designer, familiar with the PuzzleScript game description language. Here are some example games, for inspiration (do not reproduce these games exactly):

title Drop Swap
author Aaron Steed
homepage www.robotacid.com

key_repeat_interval 0.23
again_interval 0.05
noaction
noundo
background_color Black
text_color Grey
run_rules_on_level_start

========
OBJECTS
========

Background
Black

Player
#ffffff #000000 #626263 #3c3b3c
.222.
21112
23132
20102
.222.

DeadPlayer
#ffffff #000000 #626263 #3c3b3c
.222.
21112
21112
23132
.222.

BaseWall
#222222 #333333 #444444 #ffffff
11110
12220
12200
12000
00000

APill
#ff0000 #ff7374 #ff0000 #ff0000
.111.
11110
11010
11100
.000.

BPill
#00de00 #cffa92 #ff0000 #ff0000
.111.
10010
10110
11100
.000.

CPill
#7a35fc #b56bfd #ff0000 #ff0000
.111.
10110
11010
11100
.000.

DPill
#0094fc #79b5fd #ff0000 #ff0000
.111.
10100
11010
10100
.000.

EPill
#d1c200 #fff919 #ffffff #ff0000
.111.
10010
10010
11110
.000.

FPill
#ff33b1 #ff76d2 #ff0000 #ff0000
.111.
10100
11110
10110
.000.

AllPill
#eeee66 #ffffff #ffffcc #ffffff
.112.
11112
11012
21120
.220.

NoPill
#ffffff #000000 #ffde85 #ff0000
.000.
00000
21012
.000.
..2..

Bomb
#6e6e6e #000000 #ff0000 #fcf26b
.020.
02320
01210
01110
.000.

GemPill
#ffffff #000000 #626263 #3c3b3c
..0..
.002.
00022
.223.
..2..

Count
#ffffff #000000 #5a595a #ff0000
.....
.000.
.000.
.000.
.....

Marked
#ffffff #000000
.....
..1..
.101.
..1..
.....

CountTray
#333333

GemTray
#333333

StartGem
red

Collect
White

Debris1
#ffffff #0739f5 #1f0f42 #ffffff
.0.0.
00.00
..0..
00.00
.0.0.

Debris2
#ffffff #0739f5 #1f0f42 #ffffff
.....
.0.0.
..0..
.0.0.
.....

Empty
Black

OldPos
Black

StartCount
Black

DropWall1
#222222 #333333 #444444 #ffffff
11110
12220
12200
12200
12200

DropWall2
#222222 #333333 #444444 #ffffff
11110
12220
12200
12200
12200

DropWall3
#222222 #333333 #444444 #ffffff
11110
12220
12200
12200
12200

DropWall4
#222222 #333333 #444444 #ffffff
11110
12220
12200
12200
12200

DropMarker
Blue

Init
Purple

=======
LEGEND
=======

. = Background
@ = Player
p = DeadPlayer
# = BaseWall
a = APill
b = BPill
c = CPill
d = DPill
e = EPill
f = FPill
n = NoPill
x = AllPill
u = Count and CountTray
w = Count and CountTray and StartCount
g = GemPill
q = GemTray
o = GemTray and StartGem
p = Bomb
i = Init
j = DropWall1
k = DropWall2
l = DropWall3
m = DropWall4


MatchPill = APill or BPill or CPill or DPill or EPill or FPill or AllPill
Pill = MatchPill or NoPill or GemPill or Bomb
Debris = Debris1 or Debris2
AMatch = APill or AllPill
BMatch = BPill or AllPill
CMatch = CPill or AllPill
DMatch = DPill or AllPill
EMatch = EPill or AllPill
FMatch = FPill or AllPill
Wall = BaseWall or DropWall1 or DropWall2 or DropWall3 or DropWall4
Item = Pill or Debris or Wall or Player
Heavy = Pill or Player
Dropping = Marked or DropMarker
DropMix1 = APill or BPill or CPill or DPill or EPill
DropMix2 = APill or BPill or CPill or DPill or EPill or GemPill
DropMix3 = APill or BPill or CPill or DPill or EPill or NoPill
DropMix4 = APill or BPill or CPill or DPill or EPill or FPill or GemPill
DropMix5 = APill or BPill or CPill or DPill or EPill or FPill
DropWallMarked = DropWall1 or DropWall3

=======
SOUNDS
=======

sfx0 54236904 (illegal)
sfx1 5160507 (swap)
sfx2 19008103 (allpill)
sfx3 50665503 (crush 3)
sfx4 80820507 (land)
sfx5 2030908 (blast)
sfx6 3032303 (bomb)
sfx7 83704102 (dead)
sfx8 7194508 (gem collect)
EndLevel 16709508 (win)

================
COLLISIONLAYERS
================


Collect, StartCount, StartGem
Background
OldPos, CountTray, GemTray
Player, Wall, Pill, Empty, Count, DeadPlayer, Bomb, Init
Debris, Marked, DropMarker

======
RULES
======

(init)
[ DropWall1 ] [ Init ] -> [ DropWall1 ] [ random DropMix1 Marked ]
[ DropWall2 ] [ Init ] -> [ DropWall2 ] [ random DropMix1 ]
[ DropWall3 ] [ Init ] -> [ DropWall3 ] [ random DropMix1 Marked ]
[ DropWall4 ] [ Init ] -> [ DropWall4 ] [ random DropMix5 ]

(debris clean up)
[ Debris2 ] -> [ Empty ] again
[ Debris1 ] -> [ Debris2 ] again

(player - pill swapping)
[ > Player | Pill ] -> [ Pill OldPos | Player ] sfx1
[ Player Marked | OldPos ] -> [ Player | OldPos Marked ]

(blocked move)
[ > Player | No Pill ] -> [ Player | ] sfx0

(pill combos)
[ AMatch | AMatch | AMatch ] -> [ AMatch Collect | AMatch Collect | AMatch Collect ]
[ BMatch | BMatch | BMatch ] -> [ BMatch Collect | BMatch Collect | BMatch Collect ]
[ CMatch | CMatch | CMatch ] -> [ CMatch Collect | CMatch Collect | CMatch Collect ]
[ DMatch | DMatch | DMatch ] -> [ DMatch Collect | DMatch Collect | DMatch Collect ]
[ EMatch | EMatch | EMatch ] -> [ EMatch Collect | EMatch Collect | EMatch Collect ]
[ FMatch | FMatch | FMatch ] -> [ FMatch Collect | FMatch Collect | FMatch Collect ]
[ NoPill | NoPill | NoPill ] -> [ NoPill Collect | NoPill Collect | NoPill Collect ]

(bomb activate)
[ OldPos Bomb ] -> sfx5
up [ OldPos Bomb | Pill ] -> [ OldPos Bomb | Pill > Collect ]
right [ OldPos Bomb | Pill ] -> [ OldPos Bomb | Pill > Collect ]
down [ OldPos Bomb | Pill ] -> [ OldPos Bomb | Pill > Collect ]
left [ OldPos Bomb | Pill ] -> [ OldPos Bomb | Pill > Collect ]
[ OldPos Bomb ] -> [ OldPos Empty ]

(blast)
up [ Pill > Collect | Pill ] -> [ Pill Collect | Pill > Collect ]
right [ Pill > Collect | Pill ] -> [ Pill Collect | Pill > Collect ]
down [ Pill > Collect | Pill ] -> [ Pill Collect | Pill > Collect ]
left [ Pill > Collect | Pill ] -> [ Pill Collect | Pill > Collect ]
[ > Collect ] -> [ Collect ]
[ GemPill Collect ] -> [ GemPill ]

(reward matches)
(junction)
Right [ Collect | Collect ] -> [ > Collect | > Collect ]
Down [ Collect | Collect ] -> [ > Collect | > Collect ]
[ Down Collect | Right Collect ] [ Collect OldPos ] -> [ Collect | Collect ] [ Bomb OldPos ] sfx6
(4: create AllPill)
[ MatchPill Collect | MatchPill Collect | MatchPill Collect | MatchPill Collect ] [ Collect OldPos ] -> [ MatchPill Collect | MatchPill Collect | MatchPill Collect | MatchPill Collect ] [ AllPill OldPos ] sfx2

(count moves)
(subtract on move)
random right [ OldPos ] [ Count | No Count ] -> [ OldPos ] [ | ]
(fill on Debris)
[ Collect ] [ CountTray ] -> [ Collect ] [ CountTray Count ]

(dead?)
[ DeadPlayer ] -> cancel
[ No Count StartCount CountTray ] [ Player ] -> [ No Count StartCount CountTray ] [ DeadPlayer ] sfx7
[ Player | NoPill Collect ] -> [ DeadPlayer | NoPill ]
[ DeadPlayer ] [ CountTray Count ] -> [ DeadPlayer ] [ CountTray ] sfx7

(destroy matched)
[ APill Collect ] -> [ Debris1 ] sfx3 again
[ BPill Collect ] -> [ Debris1 ] sfx3 again
[ CPill Collect ] -> [ Debris1 ] sfx3 again
[ DPill Collect ] -> [ Debris1 ] sfx3 again
[ EPill Collect ] -> [ Debris1 ] sfx3 again
[ FPill Collect ] -> [ Debris1 ] sfx3 again
[ AllPill Collect ] -> [ Debris1 ] sfx3 again
[ Player ] [ NoPill Collect ] -> [ Player ] [ Debris1 ] sfx3 again

(destroy NoPills)
down [ NoPill | Wall ] -> [ Debris1 | Wall ] again

(collect gems)
down [ GemPill | Wall ] -> [ GemPill Collect | Wall ]
[ GemPill Collect ] [ StartGem No GemPill ] -> [ Debris1 ] [ StartGem GemPill ] again sfx8
right [ GemPill Collect ] [ GemPill GemTray | No GemPill GemTray ] -> [ Debris1 ] [ GemPill GemTray | GemPill GemTray ] again sfx8

(drop)
[ Heavy No Dropping ] -> [ Heavy DropMarker ]
down [ Heavy Dropping | Empty ] -> [ Empty | Heavy Dropping ]
[ DropMarker ] -> [ ]
down [ DropWall1 | Empty ] -> [ DropWall1 | random DropMix1 ] again
down [ DropWall2 | Empty ] -> [ DropWall2 | random DropMix2 ] again
down [ DropWall3 | Empty ] -> [ DropWall3 | random DropMix3 ] again
down [ DropWall4 | Empty ] -> [ DropWall4 | random DropMix4 ] again

(gem drop win)
right [ GemPill GemTray | Wall ] -> win

[ OldPos ] -> [ ]

==============
WINCONDITIONS
==============

No Marked
Some DropWallMarked

=======
LEVELS
=======

message collect orbs by matching 3
message match 3 to restore move-energy
message level 1: collect all marked orbs

wuuuuu#
#jjjjj#
#iiiii#
#iiiii#
#iiiii#
#iiiii#
#ii@ii#
#######

message level 2: collect all marked orbs

wuuuuuu#
#jjjjjj#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iii@ii#
########

message collect gray gems by getting them to the bottom
message level 3: collect 8 gems

oqqqqqqq#
wuuuuuuu#
#kkkkkkk#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iii@iii#
#########

message if you are next to 3 matching skulls you die
message skulls can be destroyed by getting them to the bottom
message level 4: collect all marked orbs

wuuuuuu#
#llllll#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iiiiii#
#iii@ii#
########

message level 5: collect 7 gems

oqqqqqqq#
wuuuuuuu#
#mmmmmmm#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iiiiiii#
#iii@iii#
#########

message thanks for playing



title Ebony & Ivory
author Guilherme TÃ¶ws
homepage zarat.us

========
OBJECTS
========

Background
Purple

Gray
GRAY

White
WHITE

Black
BLACK

PlayerRed
Red
.....
.000.
.000.
.000.
.....

PlayerGold
Yellow
.....
.000.
.000.
.000.
.....


=======
LEGEND
=======

. = White
, = Black
# = Gray
: = PlayerRed and White
; = PlayerRed and Black
Player = PlayerRed or PlayerGold

=======
SOUNDS
=======

SFX0 72488907

================
COLLISIONLAYERS
================

Background
Black, White, Gray
Player

======
RULES
======

[ PlayerGold ] -> [ PlayerRed ]
[ >  Player White | Black ] -> [  > Player White | > Black ]
[ >  Player Black | White ] -> [  > Player Black | > White ]
[ > Black | White ] -> [ White | Black ] SFX0
[ > White | Black ] -> [ Black | White ] SFX0
[ >  Player White |  Black ] -> [  Player White |  Black ]
[ >  Player Black |  White ] -> [  Player Black |  White ]
[ >  Player |  Gray ] -> [  Player |  Gray ]
late [ Player | PlayerRed ] -> [ Player | PlayerGold ]

==============
WINCONDITIONs
==============

No PlayerRed

=======
LEVELS
=======

########
#..,,,,#
#.:.,.,#
#...,,,#
#.,.,;,#
#....,,#
########

title Octat
author increpare
homepage www.increpare.com

noundo
require_player_movement

(
    
A port of a flash game I made.  The original's here:

http://ded.increpare.com/~locus/octat/

)

========
OBJECTS
========

Background
purple
.....
.000.
.000.
.000.
.....

Activetile
pink red
00000
0...0
0...0
0...0
00000

Target
red
0...0
.0.0.
..0..
.0.0.
0...0

Player
white
.....
.000.
.000.
.000.
.....

Void 0
black

Temp
black

=======
LEGEND
=======

. = Background
P = Player and Activetile
a = Activetile
x = Target
y = target and Activetile

=========
SOUNDS
=========

================
COLLISIONLAYERS
================

Background
Void
Activetile
Player
Temp
Target

======
RULES
======

[ > Player | no Activetile ] -> cancel

late [ Activetile | Player ] -> [ Temp | Player ] 
late [ no Temp | Player ] -> [ Activetile | Player ] 
late [ Temp ] -> [ ] 

late [ Activetile Void ] -> [ Void ] 

==============
WINCONDITIONS
==============

some Player on target

=======
LEVELS
=======

message level 1 of 8

..........
..........
..........
..........
..........
.x...ap...
..........
..........
..........
..........

message level 2 of 8

...........
...........
...........
...........
...........
....xpa....
...........
...........
...........
...........

message level 3 of 8

...........
...........
...........
...........
..a........
.axaaap....
..a........
...........
...........
...........

message level 4 of 8

...........
...........
...a.......
..a.a......
.a.a.a.....
a.aya.a.ap.
.a.a.a.....
..a.a......
...a.......
...........

message level 5 of 8

...........
...........
...........
...........
aaaaaaaaaaa
...y..ap...
aaaaaaaaaaa
...........
...........
...........

message level 6 of 8

000000....0
000000..0.0
000000..0..
000000000..
....A...0.0
.AY.A...AAP
....A...0.0
000000000..
000000..0..
000000..0.0
000000....0

message level 7 of 8

..AAA...000
.AY.A...AP.
..AAA...000

message level 8 of 8

0AAAA..00
0AAAA..00
YAAAA..AP
0AAAA..00
0AAAA..00

message the end


title Slidings
author Alain Brobecker
homepage abrobecker.free.fr

========
OBJECTS
========

Background
#d8a96c #e5b679
11111
10101
11111
10101
11111

Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
#88bbbb #448888
11111
01111
11101
11111
10111

OverWall
#88bbbb #448888
11111
01111
11101
11111
10111

Count
#88bbbb #448888 White
11111
02221
12221
12221
10111

CountEnd
#88bbbb #448888
11111
01111
11101
11111
10111

Player
#88ff22 #44ff00 #11cc00
.112.
10012
10012
21122
.222.

PlayerHasMoved
#ffe02c #ffc700 #c87700
.112.
10012
10012
21122
.222.

DeadPlayer
#ffe02c #ffc700 #c87700
.112.
10012
10012
21122
.222.

BallA
#999999 #777777 #444444
.112.
10012
10012
21122
.222.

BallB
#999999 #777777 #444444
.112.
10012
10012
21122
.222.

BallC
#999999 #777777 #444444
.112.
10012
10012
21122
.222.


=======
LEGEND
=======

. = Background
# = Wall
A = BallA
B = BallB
C = BallC
D = BallB and OverWall
E = BallC and OverWall
P = Player
O = Target
m = Count
n = CountEnd

Item = Player or Wall or BallA or BallB or BallC or Count or CountEnd

=======
SOUNDS
=======

sfx0 5160507 (move)
sfx1 83704102 (wrong move)
sfx2 16709508
EndLevel 123413 (win)
EndGame 10475908

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, BallA, BallB, BallC, Count, CountEnd, DeadPlayer, PlayerHasMoved
OverWall

======
RULES
======

(Player movements)
[> Player | Item] -> [ Player | Item] sfx1
[> Player | No Item] -> [ | > Player] again sfx0
[> Player] -> [ PlayerHasMoved ]

(swap between balls)
[Action Player] [BallA] [BallB] [BallC] -> [BallC] [Player] [BallA] [BallB]
[ Player OverWall ] [BallA] [BallB] [BallC] -> [BallC OverWall ] [Player] [BallA] [BallB] again

(count remaining moves)
[ PlayerHasMoved ] [ Wall | CountEnd | Wall ] ->  [ DeadPlayer ] [Wall | Wall | Wall]
[ DeadPlayer ] -> [ DeadPlayer ] sfx2
[ DeadPlayer ] -> restart
[ PlayerHasMoved ] [ Count | CountEnd ] -> [Player] [CountEnd | Wall ]
[ PlayerHasMoved ] -> [ Player ]

==============
WINCONDITIONS
==============

All Player on Target

=======
LEVELS
=======

(you must always have a # before the first counter)

message Bring a ball on the spot.         X to control next sliding ball.                                                                          level 1: 3 moves
#mmmn#
#PA..#
#..O.#
D####E

message level 2: 4 moves
#mmmmn
#.O..#
#P####
#A####
D####E

message level 3: 4 moves
#mmmmn#
#PAO..#
##....#
D#####E

message level 4: 5 moves
#mmmmmn
#.....#
#..O..#
#PA####
D#####E

message level 5: 7 moves
##mmmmmmmn
##..O..P##
#.......A#
D########E

message level 6: 7 moves
#mmmmmmmn
#P.A....#
#.......#
#.......#
#..O#...#
#.......#
#.......#
#B......#
########E

message level 7: 8 moves
#mmmmmmm
#.O.#P.m
#...#ABn
#......#
#...#..#
#...#..#
#######E

message level 8: 5 moves
#mmmmmn
#....P#
#A.O..#
#B....#
#.C...#
#######

message level 9: 9 moves
#mmmmmmm
##..P..m
##.....m
##..O..n
##A....#
##....B#
#######E

message level 10: 9 moves
#mmmmmmm
###..##m
##...P#m
#..O...n
#......#
##.A..##
###..###
D######E

message level 11: 13 moves
mmmmmmmm
m.P....m
m...O..m
#A....nm
D######E



title Match 3 Block Push
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Background
lightgreen green
11111
01111
11101
11111
10111


Target
darkblue
.....
.000.
.0.0.
.000.
.....

Wall
brown darkbrown
00010
11111
01000
11111
00010

Player
black orange white blue
.000.
.111.
22222
.333.
.3.3.

Crate
orange yellow
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target


=======
SOUNDS
=======

Crate MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

[ >  Player | Crate ] -> [  >  Player | > Crate  ]

late [ Crate | Crate | Crate ] -> [ | | ]

==============
WINCONDITIONS
==============

all Crate on Target

=======
LEVELS
=======

#########
#.......#
#.*.*.@.#
#.P...O.#
#...*.*.#
#.......#
#########

.#######.
.#.....#.
.#O##..##
##.##.*.#
#.*..**.#
#.OOO.#.#
#.P.....#
#########


title Heroes of Sokoban
author Jonah Ostroff
text_color yellow
key_repeat_interval 0.25

========
OBJECTS
========

Background
#AAAAAA #DDDDDD
00000
00100
00000
00010
00000

Goal
Brown Yellow
.000.
00000
00010
00000
00000

Wall
Darkgrey #222222
01000
11111
00010
00010
11111

Fighter
#FF4444 Pink DarkBrown
.000.
.111.
00000
.000.
.2.2.

Wizard
#2266FF Pink
.000.
.010.
00000
.000.
00000

Thief
#00BB00 Pink DarkBrown
.000.
.010.
00000
.0.0.
.0.0.

SFighter
#880000 Pink DarkBrown
.000.
.111.
00000
.000.
.2.2.

SWizard
#000088 Pink
.000.
.010.
00000
.000.
00000

SThief
#006600 Pink DarkBrown
.000.
.010.
00000
.0.0.
.0.0.


Crate
Orange #552200
.111.
10001
10001
10001
.111.

YellowSwitch
Yellow Black
.....
.111.
.101.
.111.
.....

YellowOpen
Yellow
0...0
.....
.....
.....
0...0

YellowDoor
Yellow #AA7722
01010
10001
00000
10001
01010

PurpleSwitch
#6611CC Black
.....
.111.
.101.
.111.
.....

PurpleDoor
#6611CC #220088
01010
10001
00000
10001
01010

PurpleOpen
#6611CC
0...0
.....
.....
.....
0...0

Temp
Pink

ShadowDoor
Purple

=======
LEGEND
=======

Player = Fighter or Wizard or Thief
SPlayer = Swizard or Sthief or SFighter
Hero = Player or Splayer
ClosedDoor = PurpleDoor or YellowDoor
OpenDoor = YellowOpen or PurpleOpen
Switch = PurpleSwitch or YellowSwitch
Moveable = Crate or SPlayer
Static = Wall or ClosedDoor
Weighing = Crate or Player or SPlayer
. = Background
# = Wall
F = Fighter
W = Wizard
T = Thief
1 = SFighter
2 = SThief
3 = SWizard
* = Crate
G = Goal
[ = YellowDoor
] = PurpleDoor
{ = YellowSwitch
} = PurpleSwitch

=======
SOUNDS
=======

Moveable move 82056307
Sfx0 21221308
Endlevel 36315908
Sfx1 66399906
Sfx2 92172900
Sfx3 15129100
EndGame 86793500

================
COLLISIONLAYERS
================

Background
Temp
Goal, Switch, OpenDoor, ShadowDoor
ClosedDoor, Hero, Crate, Wall

======
RULES
======

[ >  Fighter | Moveable ] -> [  >  Fighter | > Moveable  ]
[ > Moveable | Moveable ] [ Fighter] -> [ > Moveable | > Moveable ] [ Fighter]

[ < Thief | Moveable ] -> [ < Thief | < Moveable ]

[ > Wizard ] -> [ Wizard > Temp]
[ > Temp | no Moveable no Static] -> [ | > Temp]
[ > Temp | Static] [Wizard] -> [ | Static ] [> Wizard]
[ > Temp | Moveable ] [Wizard] -> [ | Wizard Sfx0] [Moveable]

Late [Temp] -> []

[ Action Fighter] [SThief] -> [SFighter] [Thief Sfx1]
[ Action Thief] [SWizard] -> [SThief] [Wizard Sfx1]
[ Action Wizard] [SFighter] -> [SWizard] [Fighter Sfx1]
[ Action Fighter] [no SThief] [SWizard] -> [SFighter] [no SThief] [Wizard Sfx1]
[ Action Thief] [no Swizard] [SFighter] -> [SThief] [no SWizard] [Fighter Sfx1]
[ Action Wizard] [no SFighter] [SThief] -> [SWizard] [no SFighter] [Thief Sfx1]

late [Weighing  YellowSwitch] [YellowDoor] -> [Weighing  YellowSwitch] [ShadowDoor]
late [YellowSwitch no Weighing] [YellowOpen] -> [YellowSwitch] [YellowDoor sfx2]
late [YellowSwitch no Weighing] [ShadowDoor] -> [YellowSwitch] [YellowDoor]
late [ShadowDoor] -> [YellowOpen sfx3]

late [Weighing PurpleSwitch] [PurpleDoor] -> [Weighing PurpleSwitch] [ShadowDoor]
late [PurpleSwitch no Weighing] [PurpleOpen] -> [PurpleSwitch] [PurpleDoor sfx2]
late [PurpleSwitch no Weighing] [ShadowDoor] -> [PurpleSwitch] [PurpleDoor]
late [ShadowDoor] -> [PurpleOpen sfx3]

==============
WINCONDITIONS
==============

All Hero on Goal
All Goal on Hero

=======
LEVELS
=======

Message Walk to the exit.

#############
#...........#
#.F.......G.#
#...........#
#############

Message Red warrior pushes blocks.

#############
#...#.*.#.*.#
#.F...*.**G.#
#...#.*.#.*.#
#############

Message Hold down switches to open doors.

#############
#...#...#...#
#.F.*.{.#.G.#
#...#...[...#
#############

Message Press multiple switches at once.

#########
#.....#G#
#.#*..#[#
#.**.F..#
#.......#
#.{.{.{.#
#.......#
#########

Message Green thief pulls blocks.

#############
#...#..*#...#
#.T.*.{.#.G.#
#...#..*[...#
#############

#########
#.....#G#
#.#*..#[#
#.**.T..#
#.......#
#.{.{.{.#
#.......#
#########

#########
#...##G.#
#.#..##[#
#....{..#
#.#.#.#.#
#..*.T..#
#########

Message Blue wizard teleports to blocks.

#############
#...*.{.#...#
#.W.*.{.[.G.#
#...#...#...#
#############

#########
#.....#G#
#.#*..#[#
#.**.W..#
#.......#
#.{.{.{.#
#.......#
#########

#######
#W#G#*#
#.#.#.#
#*...*#
#.###.#
#*.*..#
#######

Message Press action to switch heroes.

#############
#...#...#...#
#.F...}.[.G.#
#...#...#...#
#############
#...#...#...#
#.3.].{...G.#
#...#...#...#
#############

Message Heroes can move each other.

###########
#*#..##G.G#
#.#.2.#...#
#.....#..##
#W###.*..*#
###########

###########
#F.*.#.*#{#
#..#...*..#
####...*.*#
#..#.##[###
#2...#*.GG#
###########

#############
#.###########
#*....{....3#
#.####.######
#.#..#.###..#
#.#G....[].G#
#.#..#.###..#
#.####.######
#.....}....T#
#############

#############
##G.#F#*#...#
#*..[.....{.#
##G.#3###...#
#############

###########
#W..{...[*#
#######.#[#
#G#G#G#.#2#
#.......#1#
###########

#########
#*......#
#######.#
#F23.]#.#
#..##G#.#
#..#GG#.#
##.####.#
#.......#
##}####.#
#########

#############
#...#G.G#...#
#.*.#F23#.G.#
#...#...#...#
##[###.###[##
#...#...#...#
#.{...{...{.#
#...#...#...#
#############

#######
#G***F#
#*****#
#G***2#
#*****#
#G***3#
#######

########
#3..G[*#
########
#F**G{{#
########
#2.[G..#
########

#############
#...#...#...#
#F#.*.#.*.#3#
#...#...#...#
######.######
#...#...#####
#2#.*.#...}*#
#...#...#####
######.######
#G..#...#...#
#G..].#...#.#
#G..#...#...#
#############

#############
#F.........G#
#############
#T.........G#
#############
#W.........G#
#############
Message Thanks for playing!


title constellation z
author increpare
homepage www.increpare.com

(

A port of the first few levels of my game, the full version of which is
http://ded.increpare.com/~locus/constellationz/

)

========
OBJECTS
========

Background
black

BorderTile
red

Teleport
white

Target
blue
.....
.000.
.000.
.000.
.....

Player
pink
.....
.000.
.000.
.000.
.....

AltPlayer
black

=======
LEGEND
=======

# = BorderTile
. = Background
P = Player
O = Teleport
t = Target

=========
SOUNDS
=========

sfx1 44641500 (Teleport)
startgame 26858107
startlevel 34443107
endlevel 34292905

================
COLLISIONLAYERS
================

Background
Teleport,BorderTile, Target
Player, AltPlayer

======
RULES
======

late [ Player Teleport ] -> [ AltPlayer Teleport ] sfx1

late [ AltPlayer Teleport ] [ Teleport no AltPlayer ] -> [ AltPlayer Teleport ] [ Teleport Player ] 
late [ AltPlayer ] -> [ ] 

late [ Player BorderTile ] -> cancel

==============
WINCONDITIONS
==============

some Player
all Player on Target
all Target on Player

=======
LEVELS
=======

##############
#............#
#............#
#............#
#............#
#............#
#............#
#..P....t....#
#............#
#............#
#............#
#............#
#............#
##############

##############
#............#
#............#
#............#
#............#
#............#
#............#
#..P.o..O..t.#
#............#
#............#
#............#
#............#
#............#
##############

##############
#............#
#............#
#............#
#............#
#.......O.t..#
#............#
#..P....O....#
#............#
#.......O.t..#
#............#
#............#
#............#
##############

##############
#............#
#............#
#............#
#............#
#............#
#............#
#..P...O...t.#
#............#
#............#
#............#
#............#
#............#
##############

##############
#............#
#............#
#.......O.t..#
#............#
#.......O.t..#
#............#
#..P....O.t..#
#............#
#.......O.t..#
#............#
#.......O....#
#............#
##############

###################################
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#..P.P.......OOOOOOOOO............#
#................O........T.T.....#
#..P.P.......O.O.O.O.O............#
#.........................T.T.....#
#............O.O.O.O.O............#
#............O.......O............#
#............OOOOOOOOO............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
#............O...O...O............#
###################################

####################################
#..................................#
#...........p......................#
#..................................#
#.................o................#
#..................................#
#..................................#
#..................................#
#..................................#
#............o....O....t.........t.#
#..................................#
#..................................#
#..................................#
#..................................#
#..................................#
#..................................#
#..................................#
####################################

####################################
#..................................#
#..................................#
#....p.............................#
#..................................#
#..................................#
#.......o....o....t....t....t......#
#..................................#
#..................................#
#..................................#
#..................................#
#.......o....o....t.........t......#
#..................................#
#..................................#
#..................................#
#..................................#
#..................................#
####################################

##############
#............#
#............#
#............#
#......o.....#
#............#
#......o.....#
#............#
#....o...o...#
#............#
#............#
#............#
#............#
#......P.....#
#............#
#............#
#....t...t...#
#............#
#....t.t.t...#
#............#
#....t...t...#
#............#
#............#
#............#
##############

####################################
#..................................#
#..............tt...tt...tt........#
#..................................#
#..................................#
#..................................#
#..................................#
#..................................#
#.............o....o..o............#
#..................................#
#..................................#
#..................................#
#..................................#
#..................................#
#................pppp..............#
#..................................#
#..................................#
####################################

##############
#............#
#............#
#............#
#............#
#............#
#.p..........#
#.p..........#
#.p..........#
#.p..........#
#.p....o.....#
#.p..........#
#.p....o..t..#
#.p..........#
#.p..........#
#.p....o.....#
#.p..........#
#.p..........#
#.p..........#
#.p..........#
#............#
#............#
#............#
#............#
#............#
##############

message congratulations!


title BROKEN Naive Extended Bodies
author increpare
homepage www.puzzlescript.net

(
naive/broken implementation of rigid bodies
see https://www.puzzlescript.net/Documentation/rigidbodies.html#Introduction
)

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

BigBlock
Orange Yellow
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = BigBlock
@ = BigBlock and Target
O = Target


=======
SOUNDS
=======

BigBlock MOVE 36772507

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, BigBlock

======
RULES
======

[ >  Player | BigBlock ] -> [  >  Player | > BigBlock  ]
[ moving BigBlock | BigBlock ] -> [moving BigBlock|moving BigBlock]

==============
WINCONDITIONS
==============

=======
LEVELS
=======

.#######.
#.......#
#..***..#
#..p*...#
#.......#
.#######.


title Multiple Patterns
author increpare
homepage www.puzzlescript.net

========
OBJECTS
========

Background
lightgreen green
11111
01111
11101
11111
10111

Wall
brown darkbrown
00010
11111
01000
11111
00010

Player
black orange white blue
.000.
.111.
22222
.333.
.3.3.

Sumo
pink white
..0..
00000
.000.
.111.
.0.0.

=======
LEGEND
=======

. = Background
# = Wall
P = Player
S = Sumo

=======
SOUNDS
=======

Sumo MOVE 48878907

================
COLLISIONLAYERS
================

Background
Player, Wall, Sumo

======
RULES
======

[ > Player ] [ Sumo ] -> [ > Player ] [ > Sumo ] 

==============
WINCONDITIONS
==============

=======
LEVELS
=======

########
#......#
#......#
#.P..S.#
#......#
#......#
########


USER PROMPT:
Output the code for a complete PuzzleScript game. Return your code in full, inside a ```plaintext code block.