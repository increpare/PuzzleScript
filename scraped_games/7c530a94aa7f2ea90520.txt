title Puzzle Script Tutorial
author ozmike.
homepage www.puzzlescript.net

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111



ExitOpen
Black DarkBrown
01000
01010
01010
01010
01000

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

WallDarkRoom
BROWN DARKBROWN
00010
11111
01000
11111
00010

PlayerNoGravity
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

PlayerGravity
Black Orange White Blue
.000.
.111.
22222
.333.
33.33


PlayerJumpL
Black Orange White Blue
0000.
.111.
22222
.333.
33.33

PlayerJumpR
Black Orange White Blue
.0000
.111.
22222
.333.
33.33

PlayerGravityLadder
Black Orange White Blue
.000.
.111.
22222
.333.
33.33



PlayerWithTorch
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Object
Yellow
00000
0...0
0...0
0...0
00000

ObjectGravity
Yellow
00000
0..00
0.0.0
00..0
00000

KeyPickup
Yellow Red Orange
.....
000..
0.000
000.0
.....

KeyMoveable
Orange
.....
000..
0.000
000.0
.....


KeyCarry
Red
.....
000..
0.000
000.0
.....

KeyCarryL
blue
.....
..000
000.0
0.000
.....

KeyCarryR
blue
.....
000..
0.000
000.0
.....

BulletLR
Orange Red
.....
.....
.110.
.....
.....

BulletTB
Orange Red
.....
..1..
..1..
..0..
.....

Gun
Orange Red
.....
00000
0.0
000..
.....




Potion
Yellow Red Grey
..02.
..02.
.0.02
.0102
.0002

Entry
LIGHTBROWN BROWN DARKBROWN GRAY
30103
10101
10121
10101
10101

DoorLocked
LIGHTBROWN BROWN DARKBROWN GRAY
30103
10101
10121
10101
10101

MirrorTLBR
Orange Red
0....
.0...
..0..
...0.
....0

MirrorTLBRHitLeft
Orange Red
0....
.0...
..0..
...0.
....0

Ladder
DarkBrown
0...0
00000
0...0
00000
0...0

Message1
Yellow Black
.000.
0...0
...0.
..0..
..1..

MessageHidden
Transparent
.000.
0...0
...0.
..0..
..0..


MessageExit
Transparent
0....
.0...
..0..
...0.
....0

MessageGun
Transparent
0....
.0...
..0..
...0.
....0

MessageHallway
Transparent
0....
.0...
..0..
...0.
....0

HallwayHidden
Black
00000
00000
00000
00000
00000


HallwayWallHidden
Black
00000
00000
00000
00000
00000

RoomHidden
Black
00000
00000
00000
00000
00000

RoomUnhidden
Transparent
00000
00000
00000
00000
00000

RoomWallHidden
Black
00000
00000
00000
00000
00000

RoomWallunHidden
Black
00000
00000
00000
00000
00000

BAckGroundHidden
Black
00000
00000
00000
00000
00000

WallCornerDarkRoom
Black
00000
00000
00000
00000
00000

3DFloor
White Red
11111
11111
11111
11111
00000

3DFloorRight
White Black Red Yellow
22220
22201
22011
20111
01111


3DFloorLeft
White Black Red
11110
11102
11022
10222
00000

3DSideRight
White Black Red
01110
01102
01022
00222
00000

3DSideRightBottom
White Black Red
01110
01101
01011
00111
01111

3DSideEndR
Black White
10000
10000
10000
10000
10000

3DWallBaseCnrL
Black White
10000
10000
10000
10000
11111



3DWallFloorTopCnrR
Black White
00000
00000
00000
00000
10000

3DWallFloorTopR
Black White
00000
00000
00000
00000
11111

Degree
Yellow Red Black
.000.
.122.
.000.
.222.
.....

=======
LEGEND 
=======

. = Background
, = BackgroundHidden
# = Wall
% = RoomWallHidden
+ = HallwayWallHidden
P = PlayerNoGravity

O = ExitOpen
K = KeyPickUp
C = Object
t = Potion
L = DoorLocked
e = KeyMoveable
B = bulletLR
X = bulletTB
G = Gun
M = MirrorTLBR
Y = ObjectGravity
Z = PlayerGravity
S = MessageExit 
U = MessageGun
Q = MessageHallway
H = HallwayHidden
R = RoomHidden
J = PlayerWithTorch
@ = WallCornerDarkRoom
A = Ladder
I = PlayerGravityLadder
N = Message1
W = MessageHidden
F = KeyCarry
$ = KeyCarryR
: = 3dFloor
/ = 3dfloorLeft
! = 3dfloorRight
{ = 3dWallBaseCnrL
} = 3DSideEndR
* = 3DSideRight
? = 3DSideRightBottom
- = 3DWallFloorTopCnrR
_ = 3DWallFloorTopR
& = Degree

Ladders = Ladder

Bullet = BulletLR or BulletTB

Mirror = MirrorTlBR or MirrorTLBRHitLeft

keys = KeyPickup or KeyMoveable or KeyCarry or KeyCarryL or KeyCarryR

CarryObject = KeyCarryL or KeyCarryR or KeyCarry

Player = PlayerGravity or PlayerNoGravity or PlayerWithTorch or PlayerGravityLadder or PlayerJumpL or PlayerJumpR 

MoveableObject = Object or ObjectGravity or KeyMoveable or KeyCarry or KeyCarryL or KeyCarryR

Obstacles = Wall or WallDArkRoom or   Object or   Potion or  DoorLocked or Keys or gun or ObjectGravity or HallwayHidden  or RoomHidden or HallwayWallHidden 

Messages = MessageExit or MessageGun or MessageHallway or Message1 or MessageHidden or degree

Hidden = HallwayHidden or RoomHidden or RoomUnhidden or RoomWallHidden or BackgroundHidden or HallwayWallHidden or RoomWallUnhidden or WallCornerDarkRoom

3DLayer = 3dFloor or 3dfloorLeft or 3dfloorRight or 3dWallBaseCnrL or 3DSideEndR or 3DSideRight or  3DSideRightBottom or 3DWallFloorTopCnrR or 3DWallFloorTopR

=======
SOUNDS
=======

potion destroy 61838100

================
COLLISIONLAYERS
================

Background
ExitOpen
Ladders
Messages,
Hidden
3DLayer
Player, Obstacles, bullet , mirror



Entry

======
RULES
======

(  display a message )
late [ Player Message1  ]  -> [  Player  no Message1 ] message When a Player moves onto the Message1 tile this code runs CODE：late [ Player Message1  ]  -> [  Player  no Message1 ]　 message Hello World

late [ Player MessageHidden  ]  -> [  Player  no MessageExit ] message When a Player moves onto a Transaprent  MessageHidden tile on the Message layer show message, see CODE


( player on MsgExit display a message )
late [ Player MessageExit  ]  -> [  Player  no MessageExit ] message When Player moves onto door this code runs Code:All Player on ExitOpen ........in WINCONDITIONS 

( ==================
  Push Object
  ================== )


( push crate/key , player touch object, push object)
[ >  Player | MoveableObject ] -> [  >  Player | > MoveableObject  ]


( ==================
  Consume Object
  ================== )

( Pick up object )
[ >  Player | Potion ] -> [  >  Player | No Potion ] message potion picked up, sound plays see SOUNDS Section 

( ==================
  Keys
  ================== )
 
( Player pic up key open door )
[ >  Player | KeyPickup ] [ DoorLocked] -> [  >  Player | No KeyPickup ][ ExitOpen] 

( Player push key into door to open door )
[ Keys |  DoorLocked] -> [   No Keys | ExitOpen] 

( move/push key note key pushed if it is a moveableObject)

( ==================
  Carry Keys/Objects 
  ================== )

( pull object with player , note if hit obstacle may lose it..! not a rigid body)
[   < Player | KeyCarry ]  -> [  <  Player | < KeyCarry ]

( carry  key in the  direction of travel  ,  face object left or right.  note if hit obstacle may lose it..!)
 
[   | left Player | KeyCarryR ]  -> [  right KeyCarryL |left Player |  ]
[  KeyCarryL | right Player |  ]  -> [   | Player |  KeyCarryR ]

( general carry code for up/down)
[   perpendicular Player | CarryObject ]  -> [  perpendicular  Player | perpendicular CarryOBject ]



( ==================
  Guns & Bullets
  ================== )
  
( player on MessageGun display a message )
late [ Player MessageGun  ]  -> [  Player  no MessageGun ] message When Player presses right the gun fires CODE : right  [ right  Player | Gun | ] -> [  Player | Gun |     BulletLR ]

(Fire Bullet, gun on right ,player moves right)
right  [ right  Player | Gun | ] -> [  Player | Gun |     BulletLR ]

( Move bullet right )
[ BulletLR ] -> [ right BulletLR ] again


( bullet hit mirror from left )
right [ BulletLR |  MirrorTLBR  ]  -> [   | MirrorTLBRHitLeft   ] 

( reflect down )
down  [ MirrorTLBRHitLeft |   ]  -> [ MirrorTLBR  |  BulletTB   ]

( Move bullet down )
[ BulletTB ] -> [ down BulletTB ] again

( open door when bullet hits )
[ >  Bullet | DoorLocked ] -> [  | ExitOpen]

( Clean up Bullet, only one bullet )
late [ ExitOpen | Bullet ] -> [ExitOpen | no bulletLR ]

( ==================
  Gavity & Ladders & Jumping
  ================== )

( Player with gravity and no ladders )
down [  stationary PlayerGravity ] -> [ down PlayerGravity ] 


( Player jump at wall )
right [  right PlayerGravity | Wall ] -> [ up PlayerJumpR | wall ] 
left [  left PlayerGravity | Wall ] -> [ up PlayerJumpL | wall ] 

( Player jump at moveable object that hit wall )
left [  left PlayerGravity | MoveableObject | Wall ] -> [ up PlayerJumpL | MoveableObject | wall ]
right [  right PlayerGravity | MoveableObject | Wall ] -> [ up PlayerJumpR | MoveableObject | wall ]

(complete diagonal Jump move after going up )
[ stationary PlayerJumpR ] -> [ right PlayerGravity ]
[ stationary PlayerJumpL ] -> [ left PlayerGravity ]



( Player with gravity and ladder, ie NO gravity if on or touch ladder  )
down [  stationary PlayerGravityLadder no ladder | no ladder ] -> [ down PlayerGravityLadder no ladder | no ladder ] 


( Object with gravity , fall to ground)
down [  ObjectGravity ] -> [ down ObjectGravity ]   again


( move ObjectGravity player push crate as normal)
[ >  Player | ObjectGravity ] -> [  >  Player | > ObjectGravity  ]

( ==================
  Hidden HAllways
  ================== )

( Unhide hallway)
[ > Player | HallwayHidden ] -> [ >  Player | no HallwayHidden  ]


( player on MsgHallway display a message )
late [ Player MessageHallway  ]  -> [  Player  no MessageHallway ] message When Player is touches  HallwayHidden then unhide Hallway CODE : [ > Player | HallwayHidden ] -> [ >  Player | no HallwayHidden  ] 


( ==================
  Hidden Rooms
  ================== )


(** Dark Room reveal as you go **)
late [ PlayerWithTorch | RoomHidden ] -> [   PlayerWithTorch |  no RoomHidden  ]

( Unhide Room Wall As you go Dark Room  )
late [ PlayerWithTorch | RoomWallHidden ] -> [   PlayerWithTorch |  WallDarkRoom  ]

( Unhide Room Wall Corers As you go Dark Room  )
late [ PlayerWithTorch | WallCornerDarkRoom ] -> [   PlayerWithTorch |  WallDarkRoom  ]


( Unhide Room Wall Corner Tiles  )
late [ WallDarkRoom | WallCornerDarkRoom   ] -> [   WallDarkRoom | WallDarkRoom   ]


(*** Unhide whole room **)
( Unhide Room)
late [ Player | RoomHidden ] -> [   Player |  RoomUnHidden  ]

( Unhide Whole Room)
late [ RoomUnhidden | RoomHidden ] -> [   RoomUnhidden |  RoomUnHidden  ]

( Unhide Room Walls)
late [ RoomUnhidden | RoomWallHidden ] -> [   RoomUnhidden | Wall   ]

( Unhide Whole Room Walls )
late [ RoomWallhidden | Wall ] -> [   Wall | Wall  ]

( Unhide Hallway Tiles Nearby )
late [ PlayerWithTorch | HallwayHidden ] -> [   PlayerWithTorch |  no HallwayHidden  ]

( Unhide Hallway Tiles Nearby )
late [ Player  Degree ] -> [   Player  no Degree  ] message You now have a degree in puzzle script!    Go and get a real job.


==============
WINCONDITIONS
==============

Some Player on ExitOpen

=======
LEVELS
=======

message Welcome to the ...Puzzlescript.University....    .......More lectures?............. get me out of here!



message "You want out? ..first you must learn to make an exit.."


######
#....#
#....#
#.p..#
#....#
#..#So
######





message How to move an object ? ...............Rule:........... [>Player|MoveableObject]->[>Player|>MOveableObject]

######
#pC..#
##.###
#...o#
#....#
#....#
######




message "How to pick up object {potion}? ........  Code:........ [>Player|Potion]->[>Player|No Potion]"

######
#t...#
#....#
#.p..#
#....o
#....#
######

message "By the way ..how do you display a message !   ?.."


######
#p...#
####N#
#....#
#....#
#....o
######

message "By the way ..how do you display a Hidden message !  Use a Transparent Tile and a separete LAYER under player .."


######
#p...#
####W#
#....#
#....#
#....o
######


message Open a door when a key picked up? Code: [>Player|Key][DoorLocked]-> [>Player|No Key][ExitOpen] in RULES

######
#k...#
#....#
#.p..#
#....L
#....#
######




message "How move/push a key into lock  Code: [> KeyMoveable| DoorLocked]->[no KeyMoveable|ExitOpen] in RULES"

######
#....#
#..e.#
#.p..#
#....L
#....#
######

message "How to CARRY key into lock  .. see code ...in RULES"

######
#....#
#...F#
#.p.##
#....L
#....#
######

message "How to CARRY a key in the direction of travel  .. see code ...in RULES"

######
#....#
#...$#
#.p.##
#....L
#....#
######


message "How to fire a bullet right?   Code:[ Bullet ] -> [ right Bullet ] again ........in rules"

#######
#.p...#
#..#..#
#Ug...L
###...#
#.....#
#######

message "How to do a mirror?   See Code ........in rules"

########
#p.....#
#.g...M#
###....#
###....#
#......#
######L#



message "Gravity On Player? ......Code: [ stationary PlayerGravity ] -> [ down PlayerGravity ]  ........in rules"


######
#Z...#
####.#
#....#
#....o
#...Y#
######



message "Gravity On Object? ......Code: down[ObjectGravity]->[ down ObjectGravity]again ........in rules"


######
#Z.Y.#
####.#
#....#
#....o
#....#
######

message "Ladders with gravity? ......see Code:   ........in rules"


#######
#..Y..#
##A##.#
#.A#..#
#.A#..o
#I.#..#
#######

message "How to do a basic jump with gravity? ......Code: see code ........in rules"


#########
#..Y....#
####....#
#.......#
#.......o
#Z.#....#
#########

message "How do you jump onto a moveable object that has hit a wall? Code: see code ........in rules"


#########
#..Y....#
####....#
#.......#
#.......o
#Z.Y.#..#
#########


message Unhide Hidden Hallway (like rougue) Hint: use a transaparent tile an add a "hidden" LAYER. 

######,,+HHH
#..###+++H+H
#..#.QHHHH+H
#.p..#+++L+H
#....#,,+,+H
#....#,,+o+H
######,,+HHH


message "Unhide whole room See Code: in RULES"

#####,,%%%%
#...#++%RR%
#....HHHRR%
#..##++%%R%
#.p#,%o,+H+
#..#,%R%%H%
#..#,%RRRR%
####,%%%%%%

message "Unhide dark room as you go (like Rogue) See Code: in RULES"

#####,,@%%@
#...#++%RR%
#....HHHRR%
#..##++@%R@
#..#,%o,+H+
#..#,%R%%H%
#J.#,%RRRR%
####,@%%%%@


message "Can you do 3d in puzzle script ? don't think so! "


,,,,,,,,#Z.#,
,,,,,,,,,#.#,
,,,__-,,,#.#,
,,/:!{_-,#.#,
,,{_*:!},#.#,
,/:!{_?,,#o#,
,{_?,,,,,#Y#,  


message "Ye have escaped the puzzle script Uni! Collect your degree on the way out!"


######
#....#
#....#
#....#
#Z...#
#Y..&o
######  

message As you leave the puzzle script uni you ponder ?..... When is a script Not a puzzle.. ?  

   






