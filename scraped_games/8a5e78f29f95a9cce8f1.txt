title Festive Lights
author Madball

========
OBJECTS
========

Active
Transparent

Background
Brown Green 
01010
10101
00000
00000
00000

Lamp
LightGray DarkGray Black
.000.
01120
01220
02220
.000.

LampY
LightGray White Yellow
.000.
01120
01220
02220
.000.

LampP
LightGray White Pink
.000.
01120
01220
02220
.000.

LampB
LightGray White Blue
.000.
01120
01220
02220
.000.

LampO
LightGray White Orange
.000.
01120
01220
02220
.000.

WireRight
Gray
.....
.....
..000
.....
.....

WireLeft
Gray
.....
.....
000..
.....
.....

WireUp
Gray
..0..
..0..
..0..
.....
.....

WireDown
Gray
.....
.....
..0..
..0..
..0..

Player4
Pink
00.00
0...0
.....
0...0
00.00

Player3
LightRed
00...
0....
.....
0...0
00.00

Player2
Red
00...
0....
.....
0....
00...

Player1
DarkRed
00...
0....
.....
.....
.....

=======
LEGEND
=======

Player = Player1 or Player2 or Player3 or Player4
Wire = WireRight or WireLeft or WireUp or WireDown
LampLit = LampY or LampP or LampB or LampO
LampAny = Lamp or LampLit
. = Background
# = Player4
@ = Lamp and WireRight and WireLeft and WireUp and WireDown
H = WireRight and WireLeft
V = WireUp and WireDown
1 = WireRight and WireUp
2 = WireRight and WireDown
3 = WireLeft and WireDown
4 = WireLeft and WireUp
5 = WireRight and WireUp and WireDown
6 = WireDown and WireRight and WireLeft
7 = WireLeft and WireUp and WireDown
8 = WireUp and WireRight and WireLeft

=========
SOUNDS
=========

sfx0 71529500
sfx1 17019902
endlevel 81546102

================
COLLISIONLAYERS
================

Active
Background
WireRight
WireLeft
WireUp
WireDown
LampAny
Player


======
RULES
======



[ Action Player no LampAny ] -> [ Player ]
[ Action Player LampAny ] -> [ Action Player Active ]

startloop
Right [ Active WireRight no LampAny | WireLeft ] -> [ Active WireRight | Active WireLeft ]
Left [ Active WireLeft no LampAny | WireRight ] -> [ Active WireLeft | Active WireRight ]
Up [ Active WireUp no LampAny | WireDown ] -> [ Active WireUp | Active WireDown ]
Down [ Active WireDown no LampAny | WireUp ] -> [ Active WireDown | Active WireUp ]
endloop

[ Action Player ] -> [ Action Player Lamp ]
[ Active Lamp ] -> [ random LampLit ]

[ Action Player4 ] -> [ Player3 ] sfx0
[ Action Player3 ] -> [ Player2 ] sfx0
[ Action Player2 ] -> [ Player1 ] sfx0
[ Action Player1 ] [ Lamp ] -> [ ][ Lamp ] sfx1
[ Action Player1 ] -> [ ] sfx0

==============
WINCONDITIONS
==============

no Lamp

=======
LEVELS
=======

@H@H@
..V..
#.@..
..V..
@H@H@

#......
.......
@.@.@.@
V.V.V.V
@H@H@H@

#...@
.....
@.@.@
@H@.V
@.@.@

#.....
.@@@@.
.@@@@.
.@@@@.
.@@@@.
......

....@H@.
...24.13
.@H@...@
24.13.24
@.#.@H@.
13.24.13
.@H@...@
...13.24
....@H@.

.@H@...@H@.
24.13.24.13
@...@H@...@
13.24.13.24
.@H@.#.@H@.
...13.24...
....@H@....

@H@.@H@
V.V.@.@
@H@H@H@
..@.@..
#.@H@..

2@@3
@@@@
1@@4
#@@.
2@@3
@@@@
1@@4

#.@@@....
..1@4..2@
...@6@@@@
@3.5@7.1@
@@@@8@...
@4...@...

2HH@HH3
V2H@H3V
VV@@3VV
VV#@VVV
@@@@@@@
VVV@@VV
VV1@4VV
V1H@H4V
1HH@HH4

message Happy New Year!