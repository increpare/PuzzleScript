title Knightoban
author Nick Sperry

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
red 
00000
0...0
0...0
0...0
00000

knight_marker
blue

possible_crate
darkred 
0.0.0
.....
0...0
.....
0.0.0

selected_crate
darkred
00000
0...0
0...0
0...0
00000

indicator_selecting
transparent

zero_choices
transparent
one_choice
transparent
two_choices
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target

object = Crate or wall or player
crates = Crate
ghostcrate = possible_crate or selected_crate
indicator = zero_choices or one_choice or two_choices or indicator_selecting


=======
SOUNDS
=======

sfx0 50376306
sfx1 67537905
sfx2 92863706

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate, possible_crate, selected_crate
knight_marker
indicator

======
RULES
======

(push crates and create choices)
[> player no indicator | Crate | | ] -> [ | player zero_choices | | > knight_marker]
[^ knight_marker | no object] -> [^ knight_marker | possible_crate one_choice]
[v knight_marker | no object] -> [v knight_marker | possible_crate one_choice]
[ knight_marker ] -> []

(add them up on the player)
[player zero_choices][no player one_choice] -> [player one_choice][]
[player one_choice][no player one_choice] -> [player two_choices][]

(handle each possibility)
[zero_choices] -> cancel
[one_choice][possible_crate] -> [][crate]
[two_choices][possible_crate] -> [indicator_selecting][selected_crate]

(exit selection)
[action player][possible_crate][player indicator_selecting] -> [action player][][player indicator_selecting]
[action player][selected_crate][player indicator_selecting] -> [action player][Crate][player] sfx1

(pick crate)
up [up player][selected_crate | ... | possible_crate] -> [player][possible_crate | ... | selected_crate] sfx2
down [down player][selected_crate | ... | possible_crate] -> [player][possible_crate | ... | selected_crate] sfx2
left [left player][selected_crate | ... | possible_crate] -> [player][possible_crate | ... | selected_crate] sfx2
right [right player][selected_crate | ... | possible_crate] -> [player][possible_crate | ... | selected_crate] sfx2
[player indicator_selecting] -> [stationary player indicator_selecting]


==============
WINCONDITIONS
==============

All Target on crates

=======
LEVELS
=======

message 0/5 (Tutorial)

#########
#.......#
#.......#
#.p.*.o.#
#.......#
#.......#
#########

message Microban levels by D. Skinner:

message 1/5 (Microban 1)

####..
#.o#..
#..###
#@P..#
#..*.#
#..###
####..


message 2/5 (Microban 5)

.#######
.#.....#
.#.O*O.#
##.*P*.#
#..O*O.#
#......#
########

message 3/5 (Microban 6)

######.#####
#....###...#
#.**.....#P#
#.*.#OOO...#
#...########
#####.......

message 4/5 (Microban 7)

#######
#.....#
#.O*O.#
#.*O*.#
#.O*O.#
#.*O*.#
#..P..#
#######

message 5/5 (Microban 13)

####...
#o.##..
#op.#..
#o.*#..
##*.###
.#.*..#
.#....#
.#..###
.####..

message YOU ARE NOW A KNIGHT
