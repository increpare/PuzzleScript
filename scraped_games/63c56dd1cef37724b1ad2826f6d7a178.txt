title Son of Pacman
author Josh Reichental
homepage https://cheifemu.itch.io/
youtube BxYzjjs6d1s

========
OBJECTS
========

Background
Black
00000
00000
00000
00000
00000

WallV
Blue
.0.0.
.0.0.
.0.0.
.0.0.
.0.0.

WallH
Blue
.....
00000
.....
00000
.....


PlayerR
YELLOW
.0000
0000.
000..
0000.
.0000

PlayerL
YELLOW
0000.
.0000
..000
.0000
0000.

PlayerU
YELLOW
0...0
00.00
00000
00000
.000.

PlayerD
YELLOW
.000.
00000
00000
00.00
0...0

Pellet
Yellow
.....
.....
..0..
.....
.....

Ghost
Pink Blue
.000.
01010
00000
00000
0.0.0


=======
LEGEND
=======

Player = PlayerR or PlayerL or PlayerU or PlayerD

. = Background
- = WallH
! = WallV
P = PlayerR
* = Pellet
G = Ghost



=======
SOUNDS
=======

restart 83744502
endlevel 83744503
startgame 92244503

================
COLLISIONLAYERS
================

Background
Pellet
Player, WallH, WallV, Ghost

======
RULES
======

[ >  Ghost | Pellet ] -> [  >  Ghost |  ]
[ > Player | ... | Ghost ] -> [ > Player | ... | > Ghost ]
[ >  Player | Ghost ] -> restart

UP [ UP Player ] -> [ UP PlayerU ]
Down [ Down Player ] -> [ Down PlayerD ]
right [ right Player ] -> [ right PlayerR ]
left [ left Player ] -> [ left PlayerL ]

==============
WINCONDITIONS
==============

All ghost on Pellet

=======
LEVELS
=======

message Eat that pellet ghosty!

...---
...!*!
...!.!
...!g!
!---.!
!p...!
!----!


message Don't touch me! You're working for US now!

..---..
--!.!-.
!*!..!.
!.!g.--
!.p...!
!..----
!.!....
---....

message Look at me now Dad, look at me now...

.----....
-!*.!---.
!..g...!.
--p....--
.!...g..!
.---!.*--
....----.

message Is this my life now?

.-----..
-!p..!-.
!*.*.*!.
!ggggg!.
!.....--
!.*..*.!
-!.!---.
.---....

message Who am I?
message I am the Son...
message The Son of Pacman