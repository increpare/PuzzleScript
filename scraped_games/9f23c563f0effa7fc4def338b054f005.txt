title Memory Push
author Le Slo
homepage www.puzzlescript.net

background_color #243247
text_color #98399c

========
OBJECTS
========

Background
#243247 #243247
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

LitTarget01
#446f99
.....
.000.
.0.0.
.000.
.....

LitTarget02
#647fa9
.....
.000.
.0.0.
.000.
.....

LitTarget03
#848fb9
.....
.000.
.0.0.
.000.
.....

LitTarget04
#a49fc9
.....
.000.
.0.0.
.000.
.....

LitTarget05
#c4afd9
.....
.000.
.0.0.
.000.
.....

Wall
#5f2b88 #5f2b88 #243247
00010
11111
01000
11111
00010

Wall01
#5f2b88 #5f2b88 #243247
20010
11111
01000
11111
00010

Wall02
#5f2b88 #5f2b88 #243247
00010
11111
01000
11111
20010

Wall03
#5f2b88 #5f2b88 #243247
00010
11111
01000
11111
00012

Wall04
#5f2b88 #5f2b88 #243247
00012
11111
01000
11111
00010

Player
#98399c #c042a5
.000.
00100
01110
00100
.000.

Crate
#c042a5
00000
0...0
0...0
0...0
00000

CrateR
#c042a5
00000
0...0
0..00
0...0
00000

CrateU
#c042a5
00000
0.0.0
0...0
0...0
00000

CrateL
#c042a5
00000
0...0
00..0
0...0
00000

CrateD
#c042a5
00000
0...0
0...0
0.0.0
00000

push
transparent

object
transparent

object2
transparent

object3
transparent


ACrateR
#e93f64
00000
0...0
0..00
0...0
00000

ACrateU
#e93f64
00000
0.0.0
0...0
0...0
00000

ACrateL
#e93f64
00000
0...0
00..0
0...0
00000

ACrateD
#e93f64
00000
0...0
0...0
0.0.0
00000

ACross
#e96f64
.....
.0.0.
..0..
.0.0.
.....

end
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target
1 = Wall01
2 = Wall02 
3 = Wall03
4 = Wall04

MCrate = CrateR or CrateU or CrateL or CrateD
walls = Wall or Wall01 or Wall02 or Wall03 or Wall04
crates = Crate or MCrate
collision = crates or walls
aux = object or push or object2 or object3 or end
litTargets = LitTarget01 or LitTarget02 or LitTarget03 or LitTarget04 or LitTarget05
animations = ACrateR or ACrateU or ACrateL or ACrateD or ACross


=======
SOUNDS
=======

Crate MOVE 36772507
animations create 93865908
sfx0 20765508
restart 10342508
undo 63171308
closemessage 78310508
crates move 1789707
startgame 88668308

================
COLLISIONLAYERS
================

Background
object, object2, object3, end
push
Target
LitTargets
Player, Walls, Crates
animations

======
RULES
======

[LitTarget04] -> [LitTarget05]
[LitTarget03] -> [LitTarget04]
[LitTarget02] -> [LitTarget03]
[LitTarget01] -> [LitTarget02]

[ mcrate animations] -> [crate]
[animations] -> []

right [ >  Player | Crates | no collision] -> [  >  Player push | > CrateR object | no collision object]
left [ >  Player | Crates | no collision] -> [  >  Player push | > CrateL object | no collision object ]
up [ >  Player | Crates | no collision ] -> [  >  Player push | > CrateU object | no collision object ]
down [ >  Player | Crates | no collision ] -> [  >  Player push | > CrateD object | no collision object ]

right [stationary crateR | no object no collision no object2 no object3] [moving player push] -> [right crateR | object2] [moving player push]
left [stationary crateL | no object no collision no object2 no object3] [moving player push] -> [left crateL | object2] [moving player push]
up [stationary crateU | no object no collision no object2 no object3] [moving player push] -> [up crateU | object2] [moving player push]
down [stationary crateD | no object no collision no object2 no object3] [moving player push] -> [down crateD | object2] [moving player push] 

right [stationary crateR | no object no collision object2] [moving player push] -> [right crateR | object3] [moving player push]
left [stationary crateL | no object no collision object2] [moving player push] -> [left crateL | object3] [moving player push]
up [stationary crateU | no object no collision object2] [moving player push] -> [up crateU | object3] [moving player push]
down [stationary crateD | no object no collision object2] [moving player push] -> [down crateD | object3] [moving player push] 

[player push][stationary crateR] -> [player push][crateR ACrateR]
[player push][stationary crateL] -> [player push][crateL ACrateL]
[player push][stationary crateU] -> [player push][crateU ACrateU]
[player push][stationary crateD] -> [player push][crateD ACrateD]

right 	[moving crateR | object3]  -> [ crateR | object3 Across] 
left 	[moving crateL | object3]  -> [ crateL | object3 Across] 
up 		[moving crateU | object3]  -> [ crateU | object3 Across] 
down	[moving crateD | object3]  -> [ crateD | object3 Across]

late[target no crates] -> [target end]
late[target no LitTargets crates] -> [target LitTarget01 crates]
late[LitTargets][end] -> [][end]

late[animations] -> again
late[litTargets] -> again sfx0
[aux] -> []

==============
WINCONDITIONS
==============

All Target on LitTarget05
(No animations)

=======
LEVELS
=======

(Debug)
(
############.
#........###.
#....*....##.
#.......*o###
#p*.......###
#.........###
#.........##.
############.)
message The past is in the future. Push carefully.

message >.... ....

1#####4
#.....#
#.o...#
#...o.#
#..o..#
#.***.#
#..p..#
#.....#
2#####3

message >>... ....

1#######4
#.......#
#.**poo.#
#.......#
2###4...#
....2###3

message >>>.. ....

1######4.
#......#.
#.*p.#.24
##...oo.#
#.*..#.13
#......#.
2######3.

message >>>>. ....

1#######4
#...#...#
#...*o..#
#.po*...#
#...*o..#
#...#...#
2#######3


message >>>>> ....

1#####4......
#.....24.....
#...p..2####4
#.***.o.o.o.#
#......1####3
#.....13.....
2#####3......

message >>>>> >...

.1######4.
.#..o*..#.
13.####.24
#..*p.*..#
#........#
#...oo...#
#........#
2########3

message >>>>> >>..

1#####4
#.....#
#.**..#
#..*..#
#...#o#
#p..o.#
2#4o1#3
..#.#..
..2#3..

message >>>>> >>>.

1######4....
#......#....
#..*..p#....
#..*...2##4.
#..*....o.24
#......o.o.#
#....##.1##3
2##4.o*.#...
...2####3...

message >>>>> >>>>

..1######4
1#3..##..#
#..*.##oo#
#.1#.23p.#
#.#...*..#
#.....*..#
2########3

message The push is in the future. Rest now.




