title Yin and Yang
author Andrea Gilbert
homepage http://www.clickmazes.com
key_repeat_interval 5
again_interval 0.2

========
OBJECTS
========

Background
black
wall
transparent

onYin
white
..0..
.0.0.
0.0.0
.0.0.
..0..

offYin
grey 
..0..
.0.0.
0.0.0
.0.0.
..0..

onYang
white
.0.0.
0.0.0
.0.0.
0.0.0
.0.0.

offYang
grey
.0.0.
0.0.0
.0.0.
0.0.0
.0.0.


(-------------------------)

cellYB
lightgreen blue
.....
.101.
.0.0.
.101.
.....

cellBY
blue lightgreen
.....
.101.
.0.0.
.101.
.....

cellRY
red lightgreen
.....
.101.
.0.0.
.101.
.....

cellYR
lightgreen red
.....
.101.
.0.0.
.101.
.....

cellRB
red blue
.....
.101.
.0.0.
.101.
.....

cellBR
blue red
.....
.101.
.0.0.
.101.
.....

(-------------------------)

cellRR
red
.....
.000.
.0.0.
.000.
.....

cellBB
blue
.....
.000.
.0.0.
.000.
.....

cellYY
lightgreen
.....
.000.
.0.0.
.000.
.....

(-------------------------)

flag
transparent

yy1
white lightgrey grey
11110
10010
10210
10000
11111

yy2
lightgrey grey darkgrey
11111
10001
10201
10111
10000

yy3
grey darkgrey black
.111.
00001
01201
01001
.111.

yy4
darkgrey black white
00001
11101
10201
10001
11111

bliss
orange yellow white
2.2.2
.212.
21012
.212.
2.2.2

=======
LEGEND
=======
. = wall

1 = cellRR
2 = cellBB
3 = cellYY

4 = cellBY
5 = cellYB
6 = cellRY
7 = cellYR
8 = cellRB
9 = cellBR

yin = onYin or offYin
yang = onYang or offYang
yinyang = yin or yang
finale = yy1 or yy2 or yy3 or yy4 or bliss

p = onYin and cellRR
q = offYang and cellBB
s = onYin and cellBB
t = offYang and cellRR
m = onYin and cellRY
n = offYang and cellYB

pIsR = cellRR or cellRY or cellRB 
pIsB = cellBB or cellBR or cellBY 
pIsY = cellYY or cellYR or cellYB 
qIsR = cellRR or cellYR or cellBR
qIsB = cellBB or cellRB or cellYB 
qIsY = cellYY or cellRY or cellBY 

grid = pIsR or pIsB or pIsY

(valid goal is any solid ring of one colour)
goal = cellYY or cellRR or cellBB

onX = onYin or onYang
offX = offYin or offYang
player = onX

=======
SOUNDS
=======

sfx0 24586100
sfx1 44697300
EndLevel 20374703
Cancel 83097304

================
COLLISIONLAYERS
================

background
grid, wall
yin
yang
finale
flag

======
RULES
======

[ flag ] -> []
[ offYang qIsR ][ > onYin | ... | qIsR ] -> [ onYang flag qisR ][ | ... | offYin qIsR ] sfx0
[ offYang qIsB ][ > onYin | ... | qIsB ] -> [ onYang flag qIsB ][ | ... | offYin qIsB ] sfx0
[ offYang qIsY ][ > onYin | ... | qIsY ] -> [ onYang flag qisY ][ | ... | offYin qIsY ] sfx0
[ offYin pIsR ][ > onYang | ... | pIsR ] -> [ onYin flag pisR ][ | ... | offYang pIsR ] sfx1
[ offYin pIsB ][ > onYang | ... | pIsB ] -> [ onYin flag pIsB ][ | ... | offYang pIsB ] sfx1
[ offYin pIsY ][ > onYang | ... | pIsY ] -> [ onYin flag pisY ][ | ... | offYang pIsY ] sfx1

[yy4] -> [ bliss ] again 
[yy3] -> [yy4] again
[yy2] -> [yy3] again
[yy1] -> [yy2] again
[yin yang goal ] -> [yy1] again

late [ onX no flag ] -> cancel
late [ yin yang no goal ] -> cancel

==============
WINCONDITIONS
==============

some bliss

=======
LEVELS
=======

Message Yin meet Yang - 1 of 3
(solve in 11 to Y - DLRRULDUURL)
.......
.s8978.
.5...9.
.9.3.7.
.8...4.
.6958t.
.......

Message Yin meet Yang - 2 of 3
(solve in 13 to B - RLLRDUULRUUUL)

......
.s748.
.4596.
.9847.
.569t.
......

Message Yin meet Yang - 3 of 3
(solve in 15 to either R or B - no solve for Y)

.......
.p.486.
.84.9..
.5.3.7.
..9.68.
.759.q.
.......

(Message Yin meet Yang (original)
.........
.p567948.
.4978694.
.86...56.
.94.3.78.
.67...84.
.5879465.
.764589q.
.........)

Message Thank you for playing :)


