title Line Force
author Joel Fox - level by jc

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

PlayerU
Black Orange White Blue
.111.
22222
.333.
.3.3.
.....

PlayerD
Black Orange White Blue
.....
.111.
22222
.333.
.3.3.

PlayerL
Black Orange White Blue
.11..
.22..
2222.
.333.
.3.3.

PlayerR
Black Orange White Blue
..11.
..22.
.2222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000


=======
LEGEND
=======

Player = PlayerU or PlayerD or PlayerL or PlayerR
Blocking = wall or crate

. = Background
# = Wall
P = PlayerU
* = Crate
@ = Crate and Target
O = Target
X = playerU and Target

=======
SOUNDS
=======

Crate MOVE 36772507
endlevel 83744503
startgame 92244503

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate

======
RULES
======

up [> Player] -> [> PlayerU]
down [> Player] -> [> PlayerD]
left [> Player] -> [> PlayerL]
right [> Player] -> [> PlayerR]

(rotate without moving)
(
up [> PlayerU | no blocking] -> [|PlayerU]
up [> Player] -> [PlayerU]
down [> PlayerD | no blocking] -> [|PlayerD]
down [> Player] -> [PlayerD]
left [> PlayerL | no blocking] -> [|PlayerL]
left [> Player] -> [PlayerL]
right [> PlayerR | no blocking] -> [|PlayerR]
right [> Player] -> [PlayerR]
)

up [action PlayerU|...|crate] -> [action PlayerU|...|> crate]
up [crate|...|action PlayerU] -> [> crate|...|action PlayerU]
down [action PlayerD|...|crate] -> [action PlayerD|...|> crate]
down [crate|...|action PlayerD] -> [> crate|...|action PlayerD]
left [action PlayerL|...|crate] -> [action PlayerL|...|> crate]
left [crate|...|action PlayerL] -> [> crate|...|action PlayerL]
right [action PlayerR|...|crate] -> [action PlayerR|...|> crate]
right [crate|...|action PlayerR] -> [> crate|...|action PlayerR]

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======

message Use the force of... um... lines

#########
#....P..#
#.#####.#
#.#O*O#.#
#.#*.*#.#
#.#O*O#.#
#.#.###.#
#.###...#
#.....###
#########


message congratulations!
