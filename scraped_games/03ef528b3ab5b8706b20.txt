Title Sticky v0.901
Author Connorses
homepage connorses.deviantart.com
background_color #181927
text_color green
again_interval 0.3
========
OBJECTS
========
BG
#848484 grey darkgrey
(#464646 #4a4467 #3a3a3a #dd1f00)
00200
21110
01112
21112
22222

wall
purple darkblue darkgrey
12112
11001
11001
12112
11001

wall2
(#848484 grey darkgrey)
#464646 #4a4467 #3a3a3a #dd1f00
22222
20002
20002
21002
22222

starground
white black
11111
11101
01111
11111
11011

sticky_On
blue white
.000.
01010
.000.
0...0
.0.0.

sticky_Off
lightblue grey
.000.
01010
.000.
0...0
.0.0.

robot_off
#dba293 #5e504f darkred #cbcbcb 
..2..
..3..
.000.
02020
10101

robot_on
#dba293 #5e504f red #cbcbcb 
..2..
..3..
.000.
02020
10101

airlock
yellow black
00000
01110
01110
01110
00000

crate
#9c1301 grey #e16411 #ff0000 
00200
02110
21212
01120
00200
(#9c1301 #b2b2b2 #ff6600 #ff0000 
00100
01330
13131
03310
00100)

crateh
brown darkgrey darkblue
21112
10011
10101
11001
21112
h2
darkgrey black darkred
11111
10001
10201
10001
11111
h3
black darkgrey
00000
00000
00100
00000
00000

fence
blue
0.0.0
.....
0...0
.....
0.0.0

target
black yellow
01010
1...1
0...0
1...1
01010

hole
black
=======
LEGEND
=======
player = sticky_on or robot_on
player_off = sticky_off or robot_off

sticky = sticky_on or sticky_off
robot = robot_on or robot_off

. = BG
; = BG and starground
+ = wall2
# = wall and wall2
p = sticky_on and wall2
s = sticky_on and BG
c = crate
& = crate and target
* = target
O = hole
A = airlock
H = sticky_on and airlock
X = fence and hole
y = fence and BG
z = fence and bg and target
w = fence and c
! = robot_off
background = BG
blocking = wall or wall2 or airlock or starground or robot or fence
blocking2 = wall2 or crate or wall or airlock or starground
high = starground or fence
=======
SOUNDS
=======
Sfx0 47065502 (falling)
Sfx1 53385102 (crash)
Sfx2 63989908 (?)
Sfx3 137502   (stick)
crate move 93027907
startgame 97120108
startlevel 1180308
endlevel 44514508
crate cantmove 137502 (unstick)

robot_on create 21785706
robot_off create 5960106

undo 58948109
restart 84808509
================
COLLISIONLAYERS
================
background
hole, crateh, h2, h3
wall2, crate, airlock
target
player, player_off
wall,starground,fence
======
RULES
======
[> sticky no crate|crate] -> [> sticky|crate] Sfx3
[> sticky crate|no blocking] -> [> sticky > crate|]
[> sticky > crate|crate|no robot] ->[> sticky > crate|> crate|]
[h3] -> [hole]
[h2] -> [h3] Sfx1 again
[crateh] -> [h2] again
late [crate hole no sticky] -> [crateh] Sfx0 again

[> player|player_off] -> [player|player_off] message [Press 'X' to switch between Player and Robot.]

(extra collisions)
[> player|wall ] -> [player|wall ]
[> sticky|high] -> [sticky|high]
[> robot |wall2] -> [robot |wall2]
[> player|starground] -> cancel

[> robot|crate] -> [> robot|> crate]
[> robot|crate|blocking2] ->[robot|crate|blocking2]

[robot_off][action sticky_on] -> [robot_on][sticky_off]
[action robot_on][sticky_off] -> [robot_off][sticky_on]
late [robot hole][sticky] -> [h2][sticky_on] Sfx0 again
==============
WINCONDITIONS
==============
all target on crate
all player on airlock
=======
LEVELS
=======
message Welcome, Employee, to Space Station Hollistar.
message Please proceed to airlock B7, on your left.
;#;;#;;#;
#########
++++++++p
+++++++++
##.######
##.....a#
#########
message Remember, all containers must be stored securely in the Safety Zones.
#######
###H###
+++++++
++c..++
+&...*+
+++++++
#######
#######
message Unsecured containers are a safety hazard.
#H###+++
#**.+;;+
#...+;;+
#+..+;;+
#...+;;+
#cc.+;;+
#+++#+++
message Containers must be secured to prevent accidents.
.........
###ha####
;;++++;;;
;++.c+;;;
;+#**#++;
;++**.c+;
;;+...c+;
;;+c.+++;
;;++++;;;
;;;;;;;;;
message Mind the holes. Please do not lose our containers.
message [Remember, you can press 'Z' to undo,]
message [and 'R' to restart.]
#.s.....#
#....c..#
#.......#
###ooo###
#++++c++#
#++*.c++#
a++**.++#
a+++++++#
message All employees are held responsible for loose containers in their work area.
#H#########;
#+#++++oo+;;
#ccoo**oo+;;
#ccooo**o+;;
#+++++ooo+;;
###########;

+###A#
+#.**#
##.#.#
#co..#
#c.s##
#####+

;###h##
;#o***o
;#.#.##
;#....#
;#ccc##
;#####+
message Aliens who can work comfortably in a vacuum are very useful to us.
;;;;;;;;
;;;;;;;;
oooooo;;
o*scoo#;
###oc*#;
#*##c.#;
#.#.*.##
#c.&cc*#
#...*o.#
####a###
message The worker robots are very useful.
message [Press 'X' to switch between Player and Robot.]
##h#########
#....cy....#
#oooooxoooo#
#.+.c.y...*#
#.!...y...*#
##ooo#######
########;;;;;
message Please try not to damage the robots.
#ap+####
##++y!.#
##xxy..#
##.c.c##
##.c..##
#*..#.##
#**...##
########

##h##########
#oooooy.c...#
#*o*ooy..!c.#
#+*+ooy..c..#
#############

#####;;;o;;o;
##aa#######;;
#++p+#**c.#;;
#+&++#+.+.#o;
#+*!cyc...#;o
#+&o+#..+##o;
#++++#+++#;;;
##########o;o

message You will be held responsible if containers you handled are found floating outside the space station.
;;;;;;;
;xxxx;;
;x..x;;
;x.*xxx
;x&...x
;x..c&x
;x..x+x
;xxxxp#
;;;;#..
;;;###a
message All levels complete!