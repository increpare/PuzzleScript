title Magnetoban
author Mrquaotta
homepage www.puzzlescript.net

run_rules_on_level_start

========
OBJECTS
========

Background
Blue DarkBlue
11111
10001
10001
10001
11111

Wall
DarkGreen #5c5c8a
10001
01100
11000
00011
01010

Cover1
DarkGreen #5c5c8a #4db8ff
.....
.....
.....
.....
22222

Cover2
DarkBlue Blue #4db8ff
22222
.....
.....
.....
.....

Cover3
DarkGreen #5c5c8a
.....
.....
.....
.....
01010

Cover4
#4db8ff
.....
.....
.....
00000
00000

Empty
Black

PlayerD
Orange DarkBrown Purple DarkGray
..0..
.222.
02220
01110
.1.1.

PlayerL
Orange DarkBrown Purple
..0..
.222.
.220.
.110.
.1.1.

PlayerR
Orange DarkBrown Purple
..0..
.222.
.022.
.011.
.1.1.

Crate
LightBrown Brown Yellow
.222.
.222.
.222.
.111.
.111.

MagnetD
DarkRed Red DarkGray Gray
.111.
11111
11011
33.33
22.22

MagnetU
DarkRed Red DarkGray Gray
33.33
11.11
11111
01110
.000.

MagnetR
DarkRed Red DarkGray Gray
.1113
11002
11113
01113
.0002

MagnetL
DarkRed Red DarkGray Gray
3111.
20011
31111
31110
2000.

TargetD
LightBlue
.....
.000.
00000
00.00
00.00

TargetU
LightBlue
.....
00.00
00.00
00000
.000.

TargetR
LightBlue
.....
.0000
00...
00000
.0000

TargetL
LightBlue
.....
0000.
...00
00000
0000.

Detect1
Orange

Detect2
Orange

Detect3
Orange

Detect4
Orange

WaveD
Purple

WaveU
LightBrown

WaveL
LightRed

WaveR
LightGreen

Center1
Transparent

Center2
Transparent

=======
LEGEND
=======

. = Background
# = Wall
- = Empty
P = PlayerD
* = Crate
a = MagnetD
b = MagnetU
c = MagnetR
d = MagnetL
e = TargetD
f = TargetU
g = TargetR
h = TargetL

Player = PlayerD or PlayerL or PlayerR
Moveable = MagnetD or MagnetU or MagnetR or MagnetL or Crate
Target = TargetD or TargetU or TargetL or TargetR
Object = Moveable or Wall or Player
MagnetDU = MagnetD or MagnetU
MagnetLR = MagnetL or MagnetR
MagnetDLR = MagnetD or MagnetL or MagnetR
MagnetULR = MagnetU or MagnetL or MagnetR
MagnetDUR = MagnetD or MagnetU or MagnetR
MagnetDUL = MagnetD or MagnetU or MagnetL
Magnet = MagnetD or MagnetU or MagnetR or MagnetL




=======
SOUNDS
=======

Player Move 77225307
Moveable Move 18218307
Undo 84326902
Restart 60582902
EndLevel 25938102

================
COLLISIONLAYERS
================

Background
WaveD, Detect3, Detect4, Empty
WaveU
WaveL, Detect1, Detect2
WaveR
Center1
Center2
Cover2
Target
Object
Cover1, Cover3, Cover4


======
RULES
======

( Depth )
Down [ Wall | no Wall ] -> [ Cover1 Wall | Cover2 no Wall ]
Up [ Wall | no Wall ] -> [ Wall | Cover3 no Wall ]


( Movement )
[ Left PlayerD ] -> [ Left PlayerL ]
[ Right PlayerD ] -> [ Right PlayerR ]

[ Vertical PlayerL ] -> [ Vertical PlayerD ]
[ Right PlayerL ] -> [ Right PlayerR ]

[ Vertical PlayerR ] -> [ Vertical PlayerD ]
[ Left PlayerR ] -> [ Left PlayerL ]

[ > Player | Moveable ] -> [ > Player | > Moveable ] 
[ Moving Player ] -> [ Moving Player ] again




Down [ Detect3 | MagnetLR ] -> [ Detect3 | < MagnetLR ] again
Up [ Detect4 | MagnetLR ] -> [ Detect4 | < MagnetLR ] again
Left [ Detect2 | MagnetDU ] -> [ Detect2 | < MagnetDU ] again
Right [ Detect1 | MagnetDU ] -> [ Detect1 | < MagnetDU ] again

Down [ no Object | MagnetDLR WaveD ] -> [ no Object | < MagnetDLR WaveD ] again
Up [ no Object | MagnetULR WaveU ] -> [ no Object | < MagnetULR WaveU ] again
Left [ no Object | MagnetDUL WaveL ] -> [ no Object | < MagnetDUL WaveL ] again
Right [ no Object | MagnetDUR WaveR ] -> [ no Object | < MagnetDUR WaveR ] again

Down [ no Object | MagnetD | Center2 ] -> [ no Object | < MagnetD | Center2 ] again
Up [ no Object | MagnetU | Center2 ] -> [ no Object | < MagnetU | Center2 ] again
Left [ no Object | MagnetL | Center1 ] -> [ no Object | < MagnetL | Center1 ] again
Right [ no Object | MagnetR | Center1 ] -> [ no Object | < MagnetR | Center1 ] again


Down Late [ MagnetD | ] -> [ MagnetD | WaveD ]
Down Late [ WaveD | ] -> [ WaveD | WaveD ]
[ WaveD ] -> [ ]

Up Late [ MagnetU | ] -> [ MagnetU | WaveU ]
Up Late [ WaveU | ] -> [ WaveU | WaveU ]
[ WaveU ] -> [ ]

Left Late [ MagnetL | ] -> [ MagnetL | WaveL ]
Left Late [ WaveL | ] -> [ WaveL | WaveL ]
[ WaveL ] -> [ ]

Right Late [ MagnetR | ] -> [ MagnetR | WaveR ]
Right Late [ WaveR | ] -> [ WaveR | WaveR ]
[ WaveR ] -> [ ]

Late [ WaveR WaveL ] -> [ Center1 ]
[ Center1 ] -> [ ]

Late [ WaveD WaveU ] -> [ Center2 ]
[ Center2 ] -> [ ]





Down Late [ MagnetU Center2 | Center2 Center1 | no Object ] -> [ MagnetU Center2 | Center2 Center1 | Crate ]
Up Late [ MagnetD Center2 | Center2 Center1 | no Object ] -> [ MagnetD Center2 | Center2 Center1 | Crate ]

Left Late [ MagnetR Center1 | Center2 Center1 | no Object ] -> [ MagnetR Center1 | Center2 Center1 | Crate ]
Right Late [ MagnetL Center1 | Center2 Center1 | no Object ] -> [ MagnetL Center1 | Center2 Center1 | Crate ]

Down Late [ MagnetU WaveD | Center1 WaveD | no Object ] -> [ MagnetU WaveD | Center1 WaveD | Crate ]
Up Late [ MagnetD WaveD | Center1 WaveD | no Object ] -> [ MagnetD WaveD | Center1 WaveD | Crate ]

Down Late [ MagnetU WaveU | Center1 WaveU | no Object ] -> [ MagnetU WaveU | Center1 WaveU | Crate ]
Up Late [ MagnetD WaveU | Center1 WaveU | no Object ] -> [ MagnetD WaveU | Center1 WaveU | Crate ]

Left Late [ MagnetR Center1 | Center1 WaveD | no Object ] -> [ MagnetR WaveD | Center1 WaveD | Crate ]
Right Late [ MagnetL Center1 | Center1 WaveD | no Object ] -> [ MagnetL WaveD | Center1 WaveD | Crate ]

Left Late [ MagnetR Center1 | Center1 WaveU | no Object ] -> [ MagnetR WaveU | Center1 WaveU | Crate ]
Right Late [ MagnetL Center1 | Center1 WaveU | no Object ] -> [ MagnetL WaveU | Center1 WaveU | Crate ]

Down Late [ MagnetU Center2 | Center2 WaveR | no Object ] -> [ MagnetU Center2 | Center2 WaveR | Crate ]
Up Late [ MagnetD Center2 | Center2 WaveR | no Object ] -> [ MagnetD Center2 | Center2 WaveR | Crate ]

Down Late [ MagnetU Center2 | Center2 WaveL | no Object ] -> [ MagnetU Center2 | Center2 WaveL | Crate ]
Up Late [ MagnetD Center2 | Center2 WaveL | no Object ] -> [ MagnetD Center2 | Center2 WaveL | Crate ]

Left Late [ MagnetR WaveR | Center2 WaveR | no Object ] -> [ MagnetR WaveR | Center2 WaveR | Crate ]
Right Late [ MagnetL WaveR | Center2 WaveR | no Object ] -> [ MagnetL WaveR | Center2 WaveR | Crate ]

Left Late [ MagnetR WaveL | Center2 WaveL | no Object ] -> [ MagnetR WaveL | Center2 WaveL | Crate ]
Right Late [ MagnetL WaveL | Center2 WaveL | no Object ] -> [ MagnetL WaveL | Center2 WaveL | Crate ]

Left Late [ MagnetR WaveL | WaveL WaveD | no Object ] -> [ MagnetR WaveL | WaveL WaveD | Crate ]
Right Late [ MagnetDUL WaveL | WaveL WaveD | no Object ] -> [ MagnetDUL WaveL | WaveL WaveD | Crate ]

Left Late [ MagnetDUR WaveR | WaveR WaveD | no Object ] -> [ MagnetDUR WaveR | WaveR WaveD | Crate ]
Right Late [ MagnetL WaveR | WaveR WaveD | no Object ] -> [ MagnetL WaveR | WaveR WaveD | Crate ]

Down Late [ MagnetU WaveD | WaveR WaveD | no Object ] -> [ MagnetU WaveD | WaveR WaveD | Crate ]
Down Late [ MagnetU WaveD | WaveL WaveD | no Object ] -> [ MagnetU WaveL | WaveR WaveD | Crate ]

Up Late [ MagnetD WaveU | WaveL WaveU | no Object ] -> [ MagnetD WaveU | WaveL WaveU | Crate ]
Up Late [ MagnetD WaveU | WaveR WaveU | no Object ] -> [ MagnetD WaveU | WaveR WaveD | Crate ]

Left Late[ MagnetDUR WaveR | WaveR WaveU | no Object ] -> [ MagnetDUR WaveR | WaveR WaveU | Crate ]
Right Late [ MagnetL WaveR | WaveR WaveU | no Object ] -> [ MagnetL WaveR | WaveR WaveU | Crate ]

Left Late [ MagnetR WaveL | WaveL WaveU | no Object ] -> [ MagnetR WaveL | WaveL WaveU | Crate ]
Right Late [ MagnetDUL WaveL | WaveL WaveU | no Object ] -> [ MagnetDUL WaveL | WaveL WaveU | Crate ]



Right Late [ no MagnetR | MagnetDU Center1 | no MagnetL ] -> [ Detect1 | MagnetDU Center1 | Detect2 ]
Right Late [ | Detect1 no MagnetR | ... | Detect2 no MagnetL | ] -> [ Detect1 | no MagnetR | ... | no MagnetL | Detect2 ]
Late [ Detect1 no MagnetR ] -> [ no MagnetR ]
Late [ Detect2 no MagnetL ] -> [ no MagnetL ]
Left Late [ no MagnetDU | Detect1 ] -> [ Detect1 | Detect1 ]
Right Late [ no MagnetDU | Detect2 ] -> [ Detect2 | Detect2 ]
Right Late [ Detect1 | MagnetDU | Detect2 ] -> [ | MagnetDU | ]
[ Detect1 ] -> [ ]
[ Detect2 ] -> [ ]

Down Late [ no MagnetD | MagnetLR Center2 | no MagnetU ] -> [ Detect3 | MagnetLR Center2 | Detect4 ]
Down Late [ | Detect3 no MagnetD | ... | Detect4 no MagnetU | ] -> [ Detect3 | no MagnetD | ... | no MagnetU | Detect4 ]
Late [ Detect3 no MagnetD ] -> [ no MagnetD ]
Late [ Detect4 no MagnetU ] -> [ no MagnetU ]
Up Late [ no MagnetLR | Detect3 ] -> [ Detect3 | Detect3 ]
Down Late [ no MagnetLR | Detect4 ] -> [ Detect4 | Detect4 ]
Down Late [ Detect3 | MagnetLR | Detect4 ] -> [ | MagnetLR | ]
[ Detect3 ] -> [ ]
[ Detect4 ] -> [ ]













==============
WINCONDITIONS
==============

All TargetU on MagnetU
All TargetD on MagnetD
All TargetR on MagnetR
All TargetL on MagnetL

=======
LEVELS
=======

Message Level 1
-#######
-#....e#
##.c####
#...##g#
#...p.a#
##....##
-#..###-
-####---

Message Level 2
#######---
#..ge.####
#.....cpa#
###.#..###
--#.#.##--
--#.#.####
--#.#..ea#
--#...####
--#####---

Message Level 3
--#######
--#.....#
--#..a.f#
####....#
#..p..be#
#....####
#....#---
######---

Message Level 4
-######-
##....##
#..af..#
#.c##p.#
#.g##..#
#..eb..#
##....##
-######-

Message Level 5
###########
#..g.e....#
#..######.#
#..#.*..#.#
#..*..a.*.#
#.c#.*..#.#
#.p######.#
#.........#
###.......#
--#########

Message Level 6
###########
#.........#
#...bpd...#
#..f###h..#
#..#b..#..#
#..#.f.#..#
#..#......#
#...###...#
#.........#
#.........#
###########

Message Level 7
############
#..........#
#....p*....#
#..######..#
#..#.gh.#..#
#..*cadb#f.#
#..*....#..#
#.......#e.#
############

Message Level 8
######---
#...e#---
#.ga*#---
##..h####
###.....#
#..p.d..#
#.c.#####
#####----

Message Level 9
-----#####
-----#...#
-#####.#.#
-#..##...#
-#...*.p##
##...#d.#-
#dg.c#hh#-
##...#..#-
-########-

Message Level 10
----###-----
#####a#####-
#..#..##..#-
#..#..##..##
#.cc**...gg#
#.....p....#
############

Message Level 11
##########
#....e#h.#
#.####...#
#b.......#
######..##
#fdpa...#-
#.......#-
#......##-
########--

Message Level 12
-####-####
-#..###..#
-#c..*...#
##p.###*.#
#..*.g#..#
#..*.a#*.#
##..###..#
-##..e...#
--########




