title Puzzle Defender
author Xavier Direz
homepage www.lafaceobscuredupixel.fr
realtime_interval 0.2
flickscreen 19x12
noundo
========
OBJECTS
========

Background 
GREEN 
 
Wall
BROWN

pointedCellFlag
blue
.....
.....
.....
.....
.....

countedFlag
blue

rotatedFlag
blue

toolDemolish
white black
01110
10101
11011
10101
01110

toolRepair
white black
00110
01110
00100
00100
00100


toolLaserTower
black white
00100
01010
01110
01110
01110

toolThunderTower
black white
10101
10101
01110
01110
01110

toolWebTower
black white
10101
10101
01110
10101
10101

lifeSlot
black
.....
.0.0.
00000
.000.
..0..

Life1
#ff0000 #b90000 #ff5555 #ff9595  
.....
.0...
110..
.11..
..1..

Life2
#ff0000 #b90000 #ff5555 #ff9595  
.....
...2.
...32
...0.
.....

playerLifeSlot
black
.....
.0.0.
00000
.000.
..0..

playerLife
#ff0000 #b90000 #ff5555 #ff9595  
.....
.0.2.
11032
.110.
..1..

laserDown
white
..0..
..0..
..0..
..0..
..0..

laserUp
white
..0..
..0..
..0..
..0..
..0..

laserLeft
white
.....
.....
00000
.....
.....

laserRight
white
.....
.....
00000
.....
.....

laserHitUp
white
.....
..0..
.0.0.
..0..
..0..

laserHitDown
white
..0..
..0..
.0.0.
..0..
.....

laserHitLeft
white
.....
..0..
.0.00
..0..
.....

laserHitRight
white
.....
..0..
00.0.
..0..
.....

thunder4
white

thunder3
lightgray

thunder2
gray

thunder1
darkgray

web5
white
00000
0...0
0...0
0...0
00000

web4
white
00000
0...0
0...0
0...0
00000

web3
lightgray
00000
0...0
0...0
0...0
00000

web2
gray
00000
0...0
0...0
0...0
00000

web1
darkgray
00000
0...0
0...0
0...0
00000

towerHit
white
.....
..0..
.0.0.
..0..
.....

Player
Yellow
00.00
0...0
.....
0...0
00.00

CursorNormal
white
00.00
0...0
.....
0...0
00.00


cursorUp1B
white
.....
.....
.....
.....
00.00

cursorUp1A
white
0...0
.....
0...0
00.00
.....

cursorUp2B
white
.....
.....
.....
00.00
0...0

cursorUp2A
white
.....
0...0
00.00
.....
.....

cursorUp3B
white
.....
.....
00.00
0...0
.....

cursorUp3A
white
0...0
00.00
.....
.....
.....

cursorUp4B
white
.....
00.00
0...0
.....
0...0

cursorUp4A
white
00.00
.....
.....
.....
.....


cursorDown1B
white
00.00
.....
.....
.....
.....

cursorDown1A
white
.....
00.00
0...0
.....
0...0

cursorDown2B
white
0...0
00.00
.....
.....
.....

cursorDown2A
white
.....
.....
00.00
0...0
.....

cursorDown3B
white
.....
0...0
00.00
.....
.....

cursorDown3A
white
.....
.....
.....
00.00
0...0

cursorDown4B
white
0...0
.....
0...0
00.00
.....

cursorDown4A
white
.....
.....
.....
.....
00.00

cursorRight1B
white
0....
0....
.....
0....
0....

cursorRight1A
white
.00.0
.0...
.....
.0...
.00.0

cursorRight2B
white
00...
.0...
.....
.0...
00...

cursorRight2A
white
..00.
..0..
.....
..0..
..00.

cursorRight3B
white
.00..
..0..
.....
..0..
.00..

cursorRight3A
white
...00
...0.
.....
...0.
...00

cursorRight4B
white
0.00.
...0.
.....
...0.
0.00.

cursorRight4A
white
....0
....0
.....
....0
....0

cursorLeft1B
white
....0
....0
.....
....0
....0

cursorLeft1A
white
0.00.
...0.
.....
...0.
0.00.

cursorLeft2B
white
...00
...0.
.....
...0.
...00

cursorLeft2A
white
.00..
..0..
.....
..0..
.00..

cursorLeft3B
white
..00.
..0..
.....
..0..
..00.

cursorLeft3A
white
00...
.0...
.....
.0...
00...

cursorLeft4B
white
.00.0
.0...
.....
.0...
.00.0

cursorLeft4A
white
0....
0....
.....
0....
0....

noCursor
black
.....
.....
.....
.....
.....

gauge0
orange
.....
.....
.....
.....
.....

gauge1
orange
.....
.....
.....
.....
.....

gauge2
orange
.....
.....
.....
.....
.....


gauge3
orange
.....
.....
.....
.....
.....

gauge4
orange
.....
.....
.....
.....
.....

gauge5
orange
.....
.....
.....
.....
.....

gauge6
orange
.....
.....
.....
.....
.....

gauge7
orange
.....
.....
.....
.....
.....

gauge8
orange
.....
.....
.....
.....
.....

gauge9
orange
.....
.....
.....
.....
.....

gauge10
orange
.....
.....
.....
.....
.....

destroySprt1
red

destroySprt2
pink

(standing)
sprtStandingUp
red lightblue
..1..
.111.
00000
.000.
..0..

sprtStandingDown
red lightblue
..0..
.000.
00000
.111.
..1..

sprtStandingLeft
red lightblue
..0..
.100.
11000
.100.
..0..

sprtStandingRight
red lightblue
..0..
.001.
00011
.001.
..0..

sprtSpawn
yellow

spawnDecoration
brown

sprtEnd
yellow

sprtBasicTarget
darkblue

sprtTargetOne
pink

sprtTargetTwo
lightgreen

sprtTargetTree
darkblue

roadDownRight
blue darkblue
11111
10000
10000
10000
10001

roadDownLeft
blue darkblue
11111
00001
00001
00001
10001

roadUpLeft
blue darkblue
10001
00001
00001
00001
11111

roadUpRight
blue darkblue
10001
10000
10000
10000
11111

roadUpDown
blue darkblue
10001
10001
10001
10001
10001

roadLeftRight
blue darkblue
11111
00000
00000
00000
11111

EntryUp
blue darkblue yellow
10201
12221
10201
10201
11111

ExitDown
blue darkblue yellow
10201
10201
12221
10201
11111

( right )
sprtRight1A
red lightblue
...0.
..001
.0001
..001
...0.

sprtRight1B
red lightblue
.....
.....
1....
.....
.....

sprtRight2A
red lightblue
....0
...00
..000
...00
....0

sprtRight2B
red lightblue
.....
1....
11...
1....
.....

sprtRight3A
red lightblue
.....
....0
...00
....0
.....

sprtRight3B
red lightblue
0....
01...
011..
01...
0....

sprtRight4A
red lightblue
.....
.....
....0
.....
.....

sprtRight4B
red lightblue
.0...
001..
0011.
001..
.0...

( left )
sprtLeft1B
red lightblue
.....
.....
....1
.....
.....

sprtLeft1A
red lightblue
.0...
100..
1000.
100..
.0...

sprtLeft2B
red lightblue
.....
....1
...11
....1
.....

sprtLeft2A
red lightblue
0....
00...
000..
00...
0....

sprtLeft3B
red lightblue
....0
...10
..110
...10
....0

sprtLeft3A
red lightblue
.....
0....
00...
0....
.....

sprtLeft4B
red lightblue
...0.
..100
.1100
..100
...0.

sprtLeft4A
red lightblue
.....
.....
0....
.....
.....

(down)
sprtDown1A
red lightblue
.....
..0..
.000.
00000
.111.

sprtDown1B
red lightblue
..1..
.....
.....
.....
.....

sprtDown2A
red lightblue
.....
.....
..0..
.000.
00000


sprtDown2B
red lightblue
.111.
..1..
.....
.....
.....

sprtDown3A
red lightblue
.....
.....
.....
..0..
.000.

sprtDown3B
red lightblue
00000
.111.
..1..
.....
.....

sprtDown4A
red lightblue
.....
.....
.....
.....
..0..


sprtDown4B
red lightblue
.000.
00000
.111.
..1..
.....

( up )
sprtUp1B
red lightblue
.....
.....
.....
.....
..1..


sprtUp1A
red lightblue
.111.
00000
.000.
..0..
.....

sprtUp2B
red lightblue
.....
.....
.....
..1..
.111.

sprtUp2A
red lightblue
00000
.000.
..0..
.....
.....

sprtUp3B
red lightblue
.....
.....
..1..
.111.
00000

sprtUp3A
red lightblue
.000.
..0..
.....
.....
.....

sprtUp4B
red lightblue
.....
..1..
.111.
00000
.000.

sprtUp4A
red lightblue
..0..
.....
.....
.....
.....


TowerLaserReady
lightblue white
.....
.010.
.111.
.010.
.000.

TowerLaserCoolDown3
lightblue white
.....
.000.
.000.
.000.
.000.

TowerLaserCoolDown2
lightblue white
.....
.000.
.010.
.000.
.000.

TowerLaserCoolDown1
lightblue white
.....
.010.
.101.
.010.
.000.

TowerThunderReady
yellow white
.....
.010.
.111.
.010.
.000.

TowerThunderCoolDown10
yellow white
.....
.000.
.000.
.000.
.000.
TowerThunderCoolDown9
yellow white
.....
.000.
.000.
.000.
.000.
TowerThunderCoolDown8
yellow white
.....
.000.
.000.
.000.
.000.
TowerThunderCoolDown7
yellow white
.....
.000.
.000.
.000.
.000.
TowerThunderCoolDown6
yellow white
.....
.000.
.000.
.000.
.000.
TowerThunderCoolDown5
yellow white
.....
.000.
.000.
.000.
.000.

TowerThunderCoolDown4
yellow white
.....
.000.
.000.
.000.
.000.

TowerThunderCoolDown3
yellow white
.....
.000.
.010.
.000.
.000.

TowerThunderCoolDown2
yellow white
.....
.010.
.101.
.010.
.000.

TowerThunderCoolDown1
red white
.....
.010.
.111.
.010.
.000.

TowerWebReady
red white
.....
.010.
.111.
.010.
.000.

TowerWebCoolDown10
red white
.....
.000.
.000.
.000.
.000.

TowerWebCoolDown9
red white
.....
.000.
.000.
.000.
.000.

TowerWebCoolDown8
red white
.....
.000.
.000.
.000.
.000.

TowerWebCoolDown7
red white
.....
.000.
.000.
.000.
.000.

TowerWebCoolDown6
red white
.....
.000.
.000.
.000.
.000.

TowerWebCoolDown5
red white
.....
.000.
.000.
.000.
.000.

TowerWebCoolDown4
red white
.....
.000.
.000.
.000.
.000.

TowerWebCoolDown3
red white
.....
.000.
.000.
.000.
.000.

TowerWebCoolDown2
red white
.....
.000.
.010.
.000.
.000.

TowerWebCoolDown1
red white
.....
.010.
.101.
.010.
.000.

letterE
lightgrey
.000.
.00..
.0...
.000.
.....

letterI
lightgrey
.000.
..0..
..0..
.000.
.....

letterN
lightgrey
.00..
.0.0.
.0.0.
.0.0.
.....
letterO
lightgrey
.00..
.0.0.
.0.0.
..00.
.....

letterR
lightgrey
.00..
.0.0.
.00..
.0.0.
.....

letterT
lightgrey
.000.
..0..
..0..
..0..
.....
letterU
lightgrey
.0.0.
.0.0.
.0.0.
..00.
.....

letterW
lightgrey
.0.0.
.0.0.
.000.
.000.
.....
letterSpace
lightgrey
.....
.....
.....
.....
.....

k
white black
10001
10101
10101
10001
11111

1
white black
11011
10011
11011
10001
11111
2
white black
11001
10001
10111
10001
11111
3
white black
10001
11001
11101
10001
11111
4
white black
10111
10101
10001
11101
11111
5
white black
10011
10001
11101
10001
11111
6
white black
10001
10011
10101
10001
11111
7
white black
10001
11101
11011
10111
11111
8
white black
10001
10001
10101
10001
11111
9
white black
10001
10101
11001
10001
11111
0
white black
10001
10101
10101
10001
11111

goldSpawn
yellow #fffbce orange
.011.
00001
20001
20000
.220.

gold
yellow #fffbce orange
.011.
00001
20001
20000
.220.

goldLogo
yellow #fffbce orange
.011.
00001
20001
20000
.220.

goldSlot
black
.000.
00000
00000
00000
.000.

doneFlag
blue

movedFlag
red

queuePointer
yellow
.....
..0..
.0.0.
..0..
.....

queueSlot
black

alertWave
orange

info
green

gameStatusSlot 
black

gameOverFlag
blue
=======
LEGEND
=======

. = Background
# = Wall
p = Player
* = sprtSpawn and sprtBasicTarget and nocursor and entryUp
_ = sprtBasicTarget
- = sprtBasicTarget and sprtEnd and exitDown
i = info
L = lifeSlot
@ = playerLifeSlot and playerLife
£ = goldSlot
$ = goldSlot and gold
€ = goldLogo

a = roadDownRight and sprtBasicTarget
b = roadDownLeft and sprtBasicTarget
c = roadUpLeft and sprtBasicTarget
d = roadUpRight and sprtBasicTarget
e = roadUpDown and sprtBasicTarget
f = roadLeftRight and sprtBasicTarget

! = alertWave and queueSlot
+ = queueSlot and sprtStandingUp
, = queueSlot
§ = queueSlot and queuePointer
? = gameStatusSlot
o = queueSlot and gold
alert = alertWave

sprtPath = sprtBasicTarget or sprtTargetOne or sprtTargetTwo or sprtTargetTree

sprtStanding = sprtStandingUp or sprtStandingDown or sprtStandingLeft or sprtStandingRight
sprt = sprtStanding or sprtRight1A or sprtRight1B or sprtRight2A or sprtRight2B or sprtRight3A or sprtRight3B or sprtRight4A or sprtRight4B or sprtLeft1B or sprtLeft1A or sprtLeft2B or sprtLeft2A or sprtLeft3B or sprtLeft3A or sprtLeft4B or sprtLeft4A or sprtDown1A or sprtDown1B or sprtDown2A or sprtDown2B or sprtDown3A or sprtDown3B or sprtDown4A or sprtDown4B or sprtUp1B or sprtUp1A or sprtUp2B or sprtUp2A or sprtUp3B or sprtUp3A or sprtUp4B or sprtUp4A

cursor = cursorNormal or cursorUp1B or cursorUp2B or cursorUp3B or cursorUp4B or cursorDown1B or cursorDown2B or cursorDown3B or cursorDown4B or cursorRight1B or cursorRight2B or cursorRight3B or cursorRight4B or cursorLeft1B or cursorLeft2B or cursorLeft3B or cursorLeft4B

cursorA = cursorUp1A or cursorUp2A or cursorUp3A or cursorUp4A or cursorDown1A or cursorDown2A or cursorDown3A or cursorDown4A or cursorRight1A or cursorRight2A or cursorRight3A or cursorRight4A or cursorLeft1A or cursorLeft2A or cursorLeft3A or cursorLeft4A
selection = cursor or nocursor

letter = letterU or letterN or letterI or letterT or letterO or letterW or letterE or letterR or letterSpace

sprtRightA = sprtRight1A or sprtRight2A or sprtRight3A or sprtRight4A
sprtRightB = sprtRight1B or sprtRight2B or sprtRight3B or sprtRight4B
sprtRight = sprtRightA or sprtRightB

sprtLeftA = sprtLeft1A or sprtLeft2A or sprtLeft3A or sprtLeft4A
sprtLeftB = sprtLeft1B or sprtLeft2B or sprtLeft3B or sprtLeft4B
sprtLeft = sprtLeftA or sprtLeftB

sprtUpA = sprtUp1A or sprtUp2A or sprtUp3A or sprtUp4A
sprtUpB = sprtUp1B or sprtUp2B or sprtUp3B or sprtUp4B
sprtUp = sprtUpA or sprtUpB

sprtDownA = sprtDown1A or sprtDown2A or sprtDown3A or sprtDown4A
sprtDownB = sprtDown1B or sprtDown2B or sprtDown3B or sprtDown4B
sprtDown = sprtDownA or sprtDownB

sprtA =  sprtRight1A or sprtRight2A or sprtRight3A or sprtRight4A or sprtLeft1A  or sprtLeft2A  or sprtLeft3A  or sprtLeft4A or sprtDown1A or sprtDown2A or  sprtDown3A  or sprtDown4A or sprtUp1A or sprtUp2A or sprtUp3A or sprtUp4A

sprtB =  sprtStanding or sprtRight1B or sprtRight2B or sprtRight3B or sprtRight4B or sprtLeft1B  or sprtLeft2B  or sprtLeft3B  or sprtLeft4B or sprtDown1B or sprtDown2B or  sprtDown3B  or sprtDown4B or sprtUp1B or sprtUp2B or sprtUp3B or sprtUp4B

gauge = gauge0 or gauge1 or gauge2 or gauge3 or gauge4 or gauge5 or gauge6 or gauge7 or gauge8 or gauge9 or gauge10

towerLaser = towerLaserReady or towerLaserCoolDown3 or towerLaserCoolDown2 or towerLaserCoolDown1
towerThunder = towerThunderReady or towerThunderCoolDown10 or towerThunderCoolDown9 or towerThunderCoolDown8 or towerThunderCoolDown7 or towerThunderCoolDown6 or towerThunderCoolDown5 or towerThunderCoolDown4 or towerThunderCoolDown3 or towerThunderCoolDown2 or towerThunderCoolDown1
towerWeb = towerWebReady or TowerWebCoolDown10 or TowerWebCoolDown9 or TowerWebCoolDown8 or TowerWebCoolDown7 or TowerWebCoolDown6 or towerWebCoolDown5 or towerWebCoolDown4 or towerWebCoolDown3 or towerWebCoolDown2 or towerWebCoolDown1
web = web5 or web4 or web3 or web2 or web1
thunder = thunder4 or thunder3 or thunder2 or thunder1
laser = laserUp or laserDown or laserLeft or laserRight
laserHit = laserHitUp or laserHitDown or laserHitLeft or laserHitRight
bullet = laserHit or towerHit or thunder

tower = towerLaser or towerThunder or towerWeb

unity = tower or sprt

life = life1 or life2

destroySprt = destroySprt1 or destroySprt2

tool = pointedCellFlag or toolDemolish or toolRepair or toolLaserTower or toolThunderTower or toolWebTower

digit = 0 or 1 or 2 or 3 or 4 or 5 or 6 or 7 or 8 or 9 or k

constructionBlocker = wall or tower or sprtPath or digit

=======
SOUNDS
=======
sfx0 78533300
sfx1 79495502
sfx2 42606709
================
COLLISIONLAYERS
================

Background
countedFlag
rotatedFlag

gameStatusSlot
gameoverFlag
queueSlot
doneFlag
movedFlag

alertWave
queuePointer


sprtSpawn, sprtEnd
sprtPath
entryUp, exitDown

roadDownRight, roadDownLeft, roadUpLeft, roadUpRight, roadUpDown, roadLeftRight
Wall, sprtStanding, sprtRight1A, sprtRight1B, sprtRight2A,sprtRight2B, sprtRight3A, sprtRight3B, sprtRight4A, sprtRight4B, sprtLeft1A, sprtLeft1B, sprtLeft2A, sprtLeft2B, sprtLeft3A, sprtLeft3B, sprtLeft4A, sprtLeft4B, sprtDown1A, sprtDown1B, sprtDown2A, sprtDown2B, sprtDown3A, sprtDown3B, sprtDown4A, sprtDown4B, sprtUp1A, sprtUp1B, sprtUp2A, sprtUp2B, sprtUp3A, sprtUp3B, sprtUp4A, sprtUp4B, destroySprt

spawnDecoration

info, lifeSlot, playerLifeSlot
letter, k, 0, 1, 2, 3, 4, 5, 6, 7, 8, 9
goldSlot
goldLogo
life2
life1
playerLife
tower

gauge
web
thunder
laser, laserHit
towerHit
goldSpawn
gold
cursor, noCursor
cursorA

pointedCellFlag, toolDemolish, toolRepair, toolLaserTower, toolThunderTower, toolWebTower
Player

======
RULES     
======     

( INIT )
( Init targets ) 
[stationary Player][sprtSpawn sprtBasicTarget]->[Player][sprtSpawn sprtTargetOne]

startloop
	[stationary Player][sprtTargetOne | sprtBasicTarget]->[Player][sprtTargetOne | sprtTargetTwo]
	[stationary Player][sprtTargetTwo | sprtBasicTarget]->[Player][sprtTargetTwo | sprtTargetTree]
	[stationary Player][sprtTargetTree | sprtBasicTarget]->[Player][sprtTargetTree | sprtTargetOne]
endloop

( LOOT MANAGEMENT )

[stationary Player][goldSpawn no gold]->[Player][no goldSpawn gold]
random [stationary Player][sprtSpawn gold][goldSlot no gold]->[Player][sprtSpawn no gold][goldSlot gold] sfx0
[stationary Player][sprtSpawn gold]->[Player][sprtSpawn no gold] sfx0

( moving gold )
[stationary player][movedFlag]->[Player][no movedFlag]
[stationary Player][gold sprtTargetOne no movedFlag |no gold sprtTargetTree]->[Player][no gold sprtTargetOne| gold sprtTargetTree movedFlag]
[stationary Player][gold sprtTargetTwo no movedFlag|no gold sprtTargetOne]->[Player][no gold sprtTargetTwo| gold sprtTargetOne movedFlag]
[stationary Player][gold sprtTargetTree no movedFlag|no gold sprtTargetTwo]->[Player][no gold sprtTargetTree| gold sprtTargetTwo movedFlag]

(GOLD COUNTING)

startloop
right [stationary Player][goldSlot no gold| goldSlot gold]->[Player][goldSlot gold| goldSlot no gold]
endloop

[ stationary Player ][gold countedFlag]->[Player][gold no countedFlag]
RIGHT [ stationary Player ][digit| digit| goldLogo] -> [Player][0 |0 | goldLogo]

startloop
( tens )

RIGHT [ stationary Player ][8 |9| goldLogo] -> [Player][9 |k | goldLogo]
RIGHT [ stationary Player ][7 |9| goldLogo] -> [Player][8 |k | goldLogo]
RIGHT [ stationary Player ][6 |9| goldLogo] -> [Player][7 |k | goldLogo]
RIGHT [ stationary Player ][5 |9| goldLogo] -> [Player][6 |k | goldLogo]
RIGHT [ stationary Player ][4 |9| goldLogo] -> [Player][5 |k | goldLogo]
RIGHT [ stationary Player ][3 |9| goldLogo] -> [Player][4 |k | goldLogo]
RIGHT [ stationary Player ][2 |9| goldLogo] -> [Player][3 |k | goldLogo]
RIGHT [ stationary Player ][1 |9| goldLogo] -> [Player][2 |k | goldLogo]
RIGHT [ stationary Player ][k |9| goldLogo] -> [Player][0 |9 | goldLogo]
RIGHT [ stationary Player ][0 |9| goldLogo] -> [Player][1 |k | goldLogo]

( units )

RIGHT [ stationary Player ][gold goldSlot no countedFlag][8 | goldLogo] -> [Player][gold goldSlot countedFlag][9 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][7 | goldLogo] -> [Player][gold goldSlot countedFlag][8 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][6 | goldLogo] -> [Player][gold goldSlot countedFlag][7 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][5 | goldLogo] -> [Player][gold goldSlot countedFlag][6 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][4 | goldLogo] -> [Player][gold goldSlot countedFlag][5 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][3 | goldLogo] -> [Player][gold goldSlot countedFlag][4 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][2 | goldLogo] -> [Player][gold goldSlot countedFlag][3 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][1 | goldLogo] -> [Player][gold goldSlot countedFlag][2 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][0 | goldLogo] -> [Player][gold goldSlot countedFlag][1 | goldLogo]
RIGHT [ stationary Player ][gold goldSlot no countedFlag][k | goldLogo] -> [Player][gold goldSlot countedFlag][0 | goldLogo]
endloop

( DAMAGE MANAGEMENT )
( no matter what type of bullet or what unity match, the gauge is affected)
[stationary Player][bullet gauge1] -> [Player][bullet gauge0]
[stationary Player][bullet gauge2] -> [Player][bullet gauge1]
[stationary Player][bullet gauge3] -> [Player][bullet gauge2]
[stationary Player][bullet gauge4] -> [Player][bullet gauge3]
[stationary Player][bullet gauge5] -> [Player][bullet gauge4]
[stationary Player][bullet gauge6] -> [Player][bullet gauge5]
[stationary Player][bullet gauge7] -> [Player][bullet gauge6]
[stationary Player][bullet gauge8] -> [Player][bullet gauge7]
[stationary Player][bullet gauge9] -> [Player][bullet gauge8]
[stationary Player][bullet gauge10] -> [Player][bullet gauge9]

( DESTRUCTION )

[stationary Player][destroySprt1 no selection]->[Player][no destroySprt1 nocursor goldSpawn]
[stationary Player][destroySprt1 selection]->[Player][no destroySprt1 nocursor goldSpawn]

[stationary Player][destroySprt2 no selection]->[Player][no destroySprt2 nocursor ]
[stationary Player][destroySprt2 selection]->[Player][no destroySprt2 nocursor ]

up [stationary Player][sprtUpA | sprtUpB gauge0]->[Player][destroySprt2 no sprt no gauge0 | destroySprt1 no sprt no gauge0]

down [stationary Player][sprtDownA  | sprtDownB gauge0]->[Player][destroySprt2 no sprt no gauge0 | destroySprt1 no sprt no gauge0]

left [stationary Player][sprtLeftA  | sprtLeftB gauge0]->[Player][destroySprt2 no sprt no gauge0 | destroySprt1 no sprt no gauge0]

right [stationary Player][sprtRightA  | sprtRightB gauge0]->[Player][destroySprt2 no sprt no gauge0 | destroySprt1 no sprt no gauge0]

( SPRT ATTACKING TOWER)
[stationary Player][towerHit]->[Player][no towerHit]


up [stationary Player][sprtUp4B no web| tower]->[Player][sprtUp4B| tower towerHit]
down [stationary Player][sprtDown4B no web| tower]->[Player][sprtDown4B| tower towerHit]
left [stationary Player][sprtLeft4B no web| tower]->[Player][sprtLeft4B| tower towerHit]
right [stationary Player][sprtRight4B no web| tower]->[Player][sprtRight4B | tower towerHit]

up [stationary Player][sprtStandingUp web| tower]->[Player][sprtStandingUp web| tower towerHit]
down [stationary Player][sprtStandingDown web| tower]->[Player][sprtStandingDown web| tower towerHit]
left [stationary Player][sprtStandingLeft web| tower]->[Player][sprtStandingLeft web| tower towerHit]
right [stationary Player][sprtStandingRight web| tower]->[Player][sprtStandingRight web| tower towerHit]

[stationary Player][tower gauge0]->[Player][no tower no gauge]

( WEB ATTACK MANAGEMENT )

(web lifetime)
[stationary Player][web1] -> [Player][no web]
[stationary Player][web2] -> [Player][web1]
[stationary Player][web3] -> [Player][web2]
[stationary Player][web4] -> [Player][web3]
[stationary Player][web5] -> [Player][web4]

(towerWeb cooldown)
[stationary Player][towerWebCoolDown1] -> [Player][towerWebReady]
[stationary Player][towerWebCoolDown2] -> [Player][towerWebCoolDown1]
[stationary Player][towerWebCoolDown3] -> [Player][towerWebCoolDown2]
[stationary Player][towerWebCoolDown4] -> [Player][towerWebCoolDown3]
[stationary Player][towerWebCoolDown5] -> [Player][towerWebCoolDown4]
[stationary Player][towerWebCoolDown6] -> [Player][towerWebCoolDown5]
[stationary Player][towerWebCoolDown7] -> [Player][towerWebCoolDown6]
[stationary Player][towerWebCoolDown8] -> [Player][towerWebCoolDown7]
[stationary Player][towerWebCoolDown9] -> [Player][towerWebCoolDown8]
[stationary Player][towerWebCoolDown10] -> [Player][towerWebCoolDown9]

[stationary Player][towerWebReady | sprtPath] -> [Player][towerWebReady | sprtPath web5]
[stationary Player][towerWebReady | web5] -> [Player][towerWebCoolDown10 | web5]

( THUNDER ATTACK MANAGEMENT )

( thunder lifetime )
[stationary Player][thunder1] -> [Player][no thunder]
[stationary Player][thunder2] -> [Player][thunder1]
[stationary Player][thunder3] -> [Player][thunder2]
[stationary Player][thunder4] -> [Player][thunder3]

[stationary Player][towerThunderReady | sprtPath no thunder4] -> [Player][towerThunderReady | sprtPath thunder4]

[stationary Player][towerThunderReady | sprtPath] -> [Player][towerThunderCoolDown10 | sprtPath thunder4]

(spawning secondary thunder)
[stationary Player][thunder2 | sprtPath] -> [Player][thunder2 | sprtPath thunder1]
[stationary Player][thunder3 | sprtPath] -> [Player][thunder3 | sprtPath thunder2]

[stationary Player][towerThunderCoolDown1] -> [Player][towerThunderReady]
[stationary Player][towerThunderCoolDown2] -> [Player][towerThunderCoolDown1]
[stationary Player][towerThunderCoolDown3] -> [Player][towerThunderCoolDown2]
[stationary Player][towerThunderCoolDown4] -> [Player][towerThunderCoolDown3]
[stationary Player][towerThunderCoolDown5] -> [Player][towerThunderCoolDown4]
[stationary Player][towerThunderCoolDown6] -> [Player][towerThunderCoolDown5]
[stationary Player][towerThunderCoolDown7] -> [Player][towerThunderCoolDown6]
[stationary Player][towerThunderCoolDown8] -> [Player][towerThunderCoolDown7]
[stationary Player][towerThunderCoolDown9] -> [Player][towerThunderCoolDown8]
[stationary Player][towerThunderCoolDown10] -> [Player][towerThunderCoolDown9]

( LASER ATTACK MANAGEMENT )
(free lasers)
[stationary Player][laser]->[Player][no laser]

(free laser hits)
[stationary Player][laserHit] -> [Player][no laserHit]

(tower laser cool Down)
[stationary Player][towerLaserCoolDown1] -> [Player][towerLaserReady]
[stationary Player][towerLaserCoolDown2] -> [Player][towerLaserCoolDown1]
[stationary Player][towerLaserCoolDown3] -> [Player][towerLaserCoolDown2]

up [stationary Player][towerLaserReady | sprtPath] -> [Player][towerLaserReady  | sprtPath laserUp]
down [stationary Player][towerLaserReady | sprtPath] -> [Player][towerLaserReady  | sprtPath laserDown]
left [stationary Player][towerLaserReady | sprtPath] -> [Player][towerLaserReady  | sprtPath laserLeft]
right [stationary Player][towerLaserReady | sprtPath] -> [Player][towerLaserReady  | sprtPath laserRight]

( direct hit on sprt near the tower )
up [stationary Player][towerLaserReady | sprtB] -> [Player][towerLaserReady| sprtB laserHitUp]
down [stationary Player][towerLaserReady | sprtB] -> [Player][towerLaserReady| sprtB laserHitDown]
left [stationary Player][towerLaserReady | sprtB] -> [Player][towerLaserReady| sprtB laserHitLeft]
right [stationary Player][towerLaserReady | sprtB] -> [Player][towerLaserReady| sprtB laserHitRight]

[stationary Player][towerLaserReady]->[Player][towerLaserCoolDown3]

(draw lasers)
startloop
	up [stationary Player][laserUp sprtPath| sprtPath no sprtB no laserUp] -> [Player][laserUp  sprtPath| sprtPath laserUp]
	down [stationary Player][laserDown sprtPath| sprtPath no sprtB no laserDown] -> [Player][laserDown  sprtPath| sprtPath laserDown]
	left [stationary Player][laserLeft sprtPath| sprtPath no sprtB no laserLeft] -> [Player][laserLeft  sprtPath| sprtPath laserLeft]
	right [stationary Player][laserRight sprtPath| sprtPath no sprtB no laserRight] -> [Player][laserRight  sprtPath| sprtPath laserRight]
endloop

(create laser collision on each sprt)
up [stationary Player][laserUp | sprtB] -> [Player][laserUp| sprtB laserHitUp]
down [stationary Player][laserDown | sprtB] -> [Player][laserDown| sprtB laserHitDown]
left [stationary Player][laserLeft | sprtB] -> [Player][laserLeft| sprtB laserHitLeft]
right [stationary Player][laserRight | sprtB] -> [Player][laserRight| sprtB laserHitRight]


( QUEUE MANAGEMENT )
( next queueSlot )
[stationary Player][movedFlag]->[player][no movedFlag]
right [stationary Player][queuePointer doneFlag no movedFlag| queueSlot no doneflag]->[Player][doneFlag no queuePointer | queuePointer queueSlot movedFlag]
left [stationary Player][queuePointer doneFlag no movedFlag| queueSlot no doneFlag]->[Player][doneFlag no queuePointer | queuePointer queueSlot movedFlag]
down [stationary Player][queuePointer doneFlag no movedFlag| queueSlot no doneFlag]->[Player][doneFlag no queuePointer | queuePointer queueSlot movedFlag]
up [stationary Player][queuePointer doneFlag no movedFlag| queueSlot no doneFlag]->[Player][doneFlag no queuePointer | queuePointer queueSlot movedFlag]

( spawn current sprt )
[stationary Player][queuePointer sprt no doneFlag][sprtSpawn no sprt]->[Player][queuePointer doneFlag no sprt][sprtSpawn sprt gauge5 noCursor] sfx2
( skip empty slot )
[stationary Player][queuePointer no sprt no alert no gold no doneFlag]->[Player][queuePointer doneFlag]
( gold bonus )
right [stationary Player][gold goldSlot|wall][queuePointer gold no doneFlag]->[Player][gold goldSlot|wall][queuePointer no gold doneFlag]
random [stationary Player][queuePointer gold no doneFlag][goldSlot no gold]->[Player][queuePointer doneFlag no gold][goldSlot gold] sfx0

( display Alert Slot )
[stationary Player][queuePointer alertWave no doneFlag]->[Player][queuePointer alertWave doneFlag] message Warning ! Ennemy wave approching...
( add the different messages here )

( SPRT MOVE )

( spawn sprt on sprtSpawn)
([ stationary Player ][sprtSpawn sprtPath no sprt]->[Player][sprtSpawn sprtPath sprtStandingUp gauge5 noCursor])

( hit player life when sprt is on sprtEnd )
random [ stationary Player ][sprtEnd sprtStanding selection][playerLifeSlot playerLife]->[Player][no sprtStanding sprtEnd no selection no gauge][playerLifeSlot no playerLife] sfx1

startloop
	right [stationary Player][playerLifeSlot no playerLife | playerLifeSlot playerLife]->[Player][playerLifeSlot playerLife | playerLifeSlot no playerLife]
endloop

( sprt in web behaviour )
[stationary Player][rotatedFlag]->[Player][no rotatedFlag]
[stationary Player][sprtStandingUp web no rotatedFlag]->[Player][sprtStandingLeft web rotatedFlag]
[stationary Player][sprtStandingDown web no rotatedFlag]->[Player][sprtStandingRight web rotatedFlag]
[stationary Player][sprtStandingLeft web no rotatedFlag]->[Player][sprtStandingDown web rotatedFlag]
[stationary Player][sprtStandingRight web no rotatedFlag]->[Player][sprtStandingUp web rotatedFlag]

( move the sprite to its target )
up [ stationary Player ][sprtUp4A|sprtUp4B no movedFlag] -> [ stationary Player ][|sprtStandingUp movedFlag]
up [ stationary Player ][sprtUp3A|sprtUp3B no movedFlag] -> [ stationary Player ][sprtUp4A|sprtUp4B movedFlag]
up [ stationary Player ][sprtUp2A|sprtUp2B no movedFlag] -> [ stationary Player ][sprtUp3A|sprtUp3B movedFlag]
up [ stationary Player ][sprtUp1A|sprtUp1B no movedFlag] -> [ stationary Player ][sprtUp2A|sprtUp2B movedFlag]
up [ stationary Player ][sprtStanding sprtTargetOne selection no web | sprtTargetTwo no sprt no movedFlag] -> [ stationary Player ][sprtUp1A sprtTargetOne |sprtUp1B sprtTargetTwo selection movedFlag]
up [ stationary Player ][sprtStanding sprtTargetTwo selection no web | sprtTargetTree no sprt no movedFlag] -> [ stationary Player ][sprtUp1A sprtTargetTwo |sprtUp1B sprtTargetTree selection movedFlag]
up [ stationary Player ][sprtStanding sprtTargetTree selection no web | sprtTargetOne no sprt no movedFlag] -> [ stationary Player ][sprtUp1A sprtTargetTree |sprtUp1B sprtTargetOne selection movedFlag]

down [ stationary Player ][sprtDown4A|sprtDown4B no movedFlag] -> [ stationary Player ][|sprtStandingDown movedFlag]
down [ stationary Player ][sprtDown3A|sprtDown3B no movedFlag] -> [ stationary Player ][sprtDown4A|sprtDown4B movedFlag]
down [ stationary Player ][sprtDown2A|sprtDown2B no movedFlag] -> [ stationary Player ][sprtDown3A|sprtDown3B movedFlag]
down [ stationary Player ][sprtDown1A|sprtDown1B no movedFlag] -> [ stationary Player ][sprtDown2A|sprtDown2B movedFlag]
down [ stationary Player ][sprtStanding sprtTargetOne selection no web | sprtTargetTwo no sprt no movedFlag] -> [ stationary Player ][sprtDown1A sprtTargetOne |sprtDown1B sprtTargetTwo selection movedFlag]
down [ stationary Player ][sprtStanding sprtTargetTwo selection no web | sprtTargetTree no sprt no movedFlag] -> [ stationary Player ][sprtDown1A sprtTargetTwo |sprtDown1B sprtTargetTree selection movedFlag]
down [ stationary Player ][sprtStanding sprtTargetTree selection no web | sprtTargetOne no sprt no movedFlag] -> [ stationary Player ][sprtDown1A sprtTargetTree |sprtDown1B sprtTargetOne selection movedFlag]

[stationary player][movedFlag]->[Player][no movedFlag]
left [ stationary Player ][sprtLeft4A|sprtLeft4B no movedFlag] -> [ stationary Player ][|sprtStandingLeft movedFlag]
left [ stationary Player ][sprtLeft3A|sprtLeft3B no movedFlag] -> [ stationary Player ][sprtLeft4A|sprtLeft4B movedFlag]
left [ stationary Player ][sprtLeft2A|sprtLeft2B no movedFlag] -> [ stationary Player ][sprtLeft3A|sprtLeft3B movedFlag]
left [ stationary Player ][sprtLeft1A|sprtLeft1B no movedFlag] -> [ stationary Player ][sprtLeft2A|sprtLeft2B movedFlag]
left [ stationary Player ][sprtStanding sprtTargetOne selection no web | sprtTargetTwo no sprt no movedFlag] -> [ stationary Player ][sprtLeft1A sprtTargetOne |sprtLeft1B sprtTargetTwo selection movedFlag]
left [ stationary Player ][sprtStanding sprtTargetTwo selection no web | sprtTargetTree no sprt no movedFlag] -> [ stationary Player ][sprtLeft1A sprtTargetTwo |sprtLeft1B sprtTargetTree selection movedFlag]
left [ stationary Player ][sprtStanding sprtTargetTree selection no web | sprtTargetOne no sprt no movedFlag] -> [ stationary Player ][sprtLeft1A sprtTargetTree |sprtLeft1B sprtTargetOne selection movedFlag]

right [ stationary Player ][sprtRight4A|sprtRight4B no movedFlag] -> [ stationary Player ][|sprtStandingRight movedFlag]
right [ stationary Player ][sprtRight3A|sprtRight3B no movedFlag] -> [ stationary Player ][sprtRight4A|sprtRight4B movedFlag]
right [ stationary Player ][sprtRight2A|sprtRight2B no movedFlag] -> [ stationary Player ][sprtRight3A|sprtRight3B movedFlag]
right [ stationary Player ][sprtRight1A|sprtRight1B no movedFlag] -> [ stationary Player ][sprtRight2A|sprtRight2B movedFlag]
right [ stationary Player ][sprtStanding sprtTargetOne selection no web | sprtTargetTwo no sprt no movedFlag] -> [ stationary Player ][sprtRight1A sprtTargetOne |sprtRight1B sprtTargetTwo selection movedFlag]
right [ stationary Player ][sprtStanding sprtTargetTwo selection no web | sprtTargetTree no sprt no movedFlag] -> [ stationary Player ][sprtRight1A sprtTargetTwo |sprtRight1B sprtTargetTree selection movedFlag]
right [ stationary Player ][sprtStanding sprtTargetTree selection no web | sprtTargetOne no sprt no movedFlag] -> [ stationary Player ][sprtRight1A sprtTargetTree |sprtRight1B sprtTargetOne selection movedFlag]





[stationary Player][gauge sprtA sprtTargetOne | sprtB sprtTargetTwo]->[Player][sprtA sprtTargetOne| gauge sprtB sprtTargetTwo]
[stationary Player][gauge sprtA sprtTargetTwo | sprtB sprtTargetTree]->[Player][sprtA sprtTargetTwo| gauge sprtB sprtTargetTree]
[stationary Player][gauge sprtA sprtTargetTree | sprtB sprtTargetOne]->[Player][sprtA sprtTargetTree| gauge sprtB sprtTargetOne]

( CURSOR MOVE )

[cursorA]->[no cursorA]

( move cursor when a sprt is selected to follow it)

up [sprtUp4A cursor| no cursor]->[sprtUp4A no cursor| cursor]
up [sprtUp3A cursor| no cursor]->[sprtUp3A no cursor| cursor]
up [sprtUp2A cursor| no cursor]->[sprtUp2A no cursor| cursor]
up [sprtUp1A cursor| no cursor]->[sprtUp1A no cursor| cursor]

up [sprtUp1A | cursor]->[sprtUp1A cursorUp1A|cursorUp1B]
up [sprtUp2A | cursor]->[sprtUp2A cursorUp2A|cursorUp2B]
up [sprtUp3A | cursor]->[sprtUp3A cursorUp3A|cursorUp3B]
up [sprtUp4A | cursor]->[sprtUp4A cursorUp4A|cursorUp4B]

down [sprtDown4A cursor| no cursor]->[sprtDown4A no cursor| cursor]
down [sprtDown3A cursor| no cursor]->[sprtDown3A no cursor| cursor]
down [sprtDown2A cursor| no cursor]->[sprtDown2A no cursor| cursor]
down [sprtDown1A cursor| no cursor]->[sprtDown1A no cursor| cursor]

down [sprtDown1A | cursor]->[sprtDown1A cursorDown1A|cursorDown1B]
down [sprtDown2A | cursor]->[sprtDown2A cursorDown2A|cursorDown2B]
down [sprtDown3A | cursor]->[sprtDown3A cursorDown3A|cursorDown3B]
down [sprtDown4A | cursor]->[sprtDown4A cursorDown4A|cursorDown4B]

left [sprtLeft4A cursor| no cursor]->[sprtLeft4A no cursor| cursor]
left [sprtLeft3A cursor| no cursor]->[sprtLeft3A no cursor| cursor]
left [sprtLeft2A cursor| no cursor]->[sprtLeft2A no cursor| cursor]
left [sprtLeft1A cursor| no cursor]->[sprtLeft1A no cursor| cursor]

left [sprtLeft1A | cursor]->[sprtLeft1A cursorLeft1A|cursorLeft1B]
left [sprtLeft2A | cursor]->[sprtLeft2A cursorLeft2A|cursorLeft2B]
left [sprtLeft3A | cursor]->[sprtLeft3A cursorLeft3A|cursorLeft3B]
left [sprtLeft4A | cursor]->[sprtLeft4A cursorLeft4A|cursorLeft4B]

right [sprtRight4A cursor| no cursor]->[sprtRight4A no cursor| cursor]
right [sprtRight3A cursor| no cursor]->[sprtRight3A no cursor| cursor]
right [sprtRight2A cursor| no cursor]->[sprtRight2A no cursor| cursor]
right [sprtRight1A cursor| no cursor]->[sprtRight1A no cursor| cursor]

right [sprtRight1A | cursor]->[sprtRight1A cursorRight1A|cursorRight1B]
right [sprtRight2A | cursor]->[sprtRight2A cursorRight2A|cursorRight2B]
right [sprtRight3A | cursor]->[sprtRight3A cursorRight3A|cursorRight3B]
right [sprtRight4A | cursor]->[sprtRight4A cursorRight4A|cursorRight4B]

[cursor sprtStanding]->[cursorNormal sprtStanding]

( DISPLAY INFO )

right [sprt cursor][info|info|info|info|info]->[sprt cursor][info letterU|info letterN|info letterI|info letterT|info letterSpace]
right [tower cursor][info|info|info|info|info]->[tower cursor][info letterT|info letterO|info letterW|info letterE|info letterR]
right [no sprt no tower cursor][info|info|info|info|info]->[cursor][info letterSpace|info letterSpace|info letterSpace|info letterSpace|info letterSpace]


right [lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[lifeSlot no life1 no life2|lifeSlot no life1 no life2|lifeSlot no life1 no life2|lifeSlot no life1 no life2|lifeSlot no life1 no life2]

right [gauge1 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge1 cursor][lifeSlot life1|lifeSlot no life|lifeSlot no life|lifeSlot no life|lifeSlot no life]

right [gauge2 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge2 cursor][lifeSlot life1 life2|lifeSlot no life|lifeSlot no life|lifeSlot no life|lifeSlot no life]

right [gauge3 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge3 cursor][lifeSlot life1 life2|lifeSlot life1|lifeSlot no life|lifeSlot no life|lifeSlot no life]

right [gauge4 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge4 cursor][lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot no life|lifeSlot no life|lifeSlot no life]

right [gauge5 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge5 cursor][lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1|lifeSlot no life|lifeSlot no life]
right [gauge6 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge5 cursor][lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot no life|lifeSlot no life]
right [gauge7 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge5 cursor][lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1|lifeSlot no life]
right [gauge8 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge5 cursor][lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot no life]
right [gauge9 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge5 cursor][lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1]
right [gauge10 cursor][lifeSlot|lifeSlot|lifeSlot|lifeSlot|lifeSlot]->[gauge5 cursor][lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2|lifeSlot life1 life2]

( MENUS )

(delete contextual Menu when leaving it)
[ > Player | no tool][tool]->[> Player|][no tool]
[Action Player][Player pointedCellFlag][tool]->[Player][Player no pointedCellFlag][no tool]

( Construction Menu )
right [Action Player][no tool | player no sprtpath no tower no tool | no tool]->[Action Player][toolThunderTower | player pointedCellFlag | toolWebTower]
down [Action Player][no tool | player pointedCellFlag]->[Player][toolLaserTower | player pointedCellFlag]

( adding a laser Tower)
right [Action Player][pointedCellFlag no constructionBlocker][Player toolLaserTower][goldSlot gold| goldSlot gold| goldSlot gold|no gold] ->  [Player][Player no pointedCellFlag towerLaserCoolDown3 gauge5][toolLaserTower][goldSlot no gold|goldSlot no gold|goldSlot no gold|no gold]

( adding a thunder Tower)
[Action Player][pointedCellFlag no constructionBlocker][Player toolThunderTower][goldSlot gold|goldSlot gold|goldSlot gold|goldSlot gold|gold|no gold] ->  [Player][Player no pointedCellFlag towerThunderCoolDown10 gauge5][toolThunderTower][goldSlot no gold|goldSlot no gold|goldSlot no gold|goldSlot no gold|goldSlot no gold|no gold]

( adding a web Tower)
[Action Player][pointedCellFlag no constructionBlocker][Player toolWebTower][goldSlot gold|goldSlot gold|goldSlot gold|no gold] ->  [Player][Player no pointedCellFlag towerWebCoolDown10 gauge5][toolWebTower][goldSlot no gold|goldSlot no gold|goldSlot no gold|no gold]

( Maintenance Menu )
right [Action Player][no tool | player no sprtpath tower no tool | no tool]->[Action Player][toolRepair | player tower pointedCellFlag | toolDemolish]

( repair a laser Tower)
[Action Player][pointedCellFlag towerLaser gauge][Player toolRepair][goldSlot gold|goldSlot gold|no gold] ->  [Player][Player no pointedCellFlag towerLaserCoolDown3 gauge5][toolLaserTower][goldSlot no gold|goldSlot no gold|no gold]

( repair a thunder Tower)
[Action Player][pointedCellFlag towerThunder][Player toolRepair][goldSlot gold|goldSlot gold|no gold] -> [Player][Player no pointedCellFlag towerThunderCoolDown10 gauge5][toolThunderTower][goldSlot no gold|goldSlot no gold|no gold]

( repair a web Tower)
[Action Player][pointedCellFlag towerWeb][Player toolRepair][goldSlot gold|goldSlot gold|no gold] ->  [Player][Player no pointedCellFlag towerWebCoolDown10 gauge5][toolWebTower][goldSlot no gold|goldSlot no gold|no gold]

( demolish a Tower)
[Action Player][pointedCellFlag tower gauge][Player toolDemolish][goldSlot gold|goldSlot gold|no gold] ->  [Player][Player no pointedCellFlag no tower no gauge][toolLaserTower][goldSlot no gold|goldSlot no gold|no gold]

(delete contextual Menu when player is not on any tool)
[ player no tool][tool]->[ player][no tool]

( SELECT A TOWER OR A SPRT )
[Action Player unity][cursor|cursorA]->[Player cursorNormal unity][noCursor| no cursorA]
[Action Player unity][cursor]->[Player cursorNormal unity][noCursor]
[Action Player no cursor unity]->[Player cursorNormal unity]

( LOOSING )

right [stationary Player][wall|playerlifeSlot no playerLife][gameStatusSlot  gameOverFlag]-> restart
right [stationary Player][wall|playerlifeSlot no playerLife][gameStatusSlot no gameOverFlag]-> [stationary Player][wall|playerlifeSlot no playerLife][gameStatusSlot gameOverFlag] message Try again
==============
WINCONDITIONS
==============

no sprt
    

=======     
LEVELS
=======

###################
#iiiii#...afffffb.#
#lllll#...e.....e.#
#.....#affc...afc.#
#.....#e......e...#
#.....#dffb...e...#
#.....#...e...dfb.#
#.....#..ac.....e.#
#.....#..*..p...-.#
###################
#@@@@##########00€#
###################
#$$$$$$$$$$$$$££££#
#?#################
#§,,,,,,,,,,,,,!,,#
#+++,+++ooo+++++++#
#+++,,ooo,,+++++++#
#+++,,,+,,,+++++++#
###################
