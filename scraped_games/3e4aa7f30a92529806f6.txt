title Impasse
author RatoLibre1 [Wanderlands's Impasse demake]
homepage https://www.facebook.com/RatoLibre1Games
noaction

========
OBJECTS
========

Background
GREY

Border
BLACK

Player
BLACK WHITE
.111.
11011
10001
11011
.111.

Fixed
DARKGREY
.000.
00000
00000
00000
.000.

Goal
BLACK LIGHTGREEN
.111.
11101
10101
11011
.111.

Upper
BLACK ORANGE
.111.
11011
10101
10101
.111.

Lower
BLACK PINK
.111.
10101
10101
11011
.111.

Vanisher1
BLACK LIGHTBLUE
.111.
11111
10001
11111
.111.

VanisherTemp
BLACK LIGHTBLUE
.111.
11111
10001
11111
.111.

Vanisher2
LIGHTBLUE
.0.0.
0...0
.....
0...0
.0.0.

PerpUp
BLACK YELLOW
.111.
11011
10101
10101
.111.

PerpDown
BLACK PURPLE
.111.
10101
10101
11011
.111.

Blocker1
BLACK RED
.111.
10101
11011
10101
.111.

BlockerTemp
BLACK RED
.111.
10101
11011
10101
.111.

Blocker2
RED
.0.0.
0...0
.....
0...0
.0.0.

Switch
BLACK GREEN
.111.
11011
10101
11011
.111.

=======
LEGEND
=======

Obstacle = Fixed or Upper or Lower or Vanisher1 or VanisherTemp or PerpUp or PerpDown or Blocker1 or BlockerTemp
Mover = Player or Upper or Lower or PerpUp or PerpDown

.=Background
+=Border
P=Player
*=Fixed
U=Upper
L=Lower
G=Goal
Q=Vanisher1
W=Vanisher2
Y=PerpUp
S=PerpDown
B=Blocker1
N=Blocker2
/=Switch

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Border
Fixed
Goal
Vanisher1, Vanisher2, VanisherTemp
Blocker1, Blocker2, BlockerTemp
Switch
Upper
Lower
PerpUp
PerpDown
Player

======
RULES
======

[ vertical Player ][ Upper ] -> [ vertical Player ][ up Upper ]
[ vertical Player ][ Lower ] -> [ vertical Player ][ down Lower ]
[ vertical Player ][ Vanisher1 ] -> [ vertical Player ][ VanisherTemp ]
[ vertical Player ][ Vanisher2 ] -> [ vertical Player ][ Vanisher1 ]
[ horizontal Player ][ PerpUp ] -> [ horizontal Player ][ up PerpUp ]
[ horizontal Player ][ PerpDown ] -> [ horizontal Player ][ down PerpDown ]

(scroll border)
late [Border Mover | ... | | Border ] -> [Border | ... | Mover | Border ]


(post-movement changes)
late [ Player Switch ][ Blocker1 ] -> [ Player Switch ][ BlockerTemp ]
late [ Player Switch ][ Blocker2 ] -> [ Player Switch ][ Blocker1 ]
late [ VanisherTemp ] -> [ Vanisher2 ]
late [ BlockerTemp ] -> [ Blocker2 ]

(death)
late [ Player Obstacle ] -> restart

([action Player] -> win)

==============
WINCONDITIONS
==============

All Player on Goal

=======
LEVELS
=======

message Demake of the Flash game 'Impasse', by Wanderlands

++++++++++
..*.......
p.*....*.g
.......*..
++++++++++

++++++++++
****...***
p.*..*.*.g
*...**...*
++++++++++

++++++++++
..*....*..
p.******.g
....**....
++++++++++

++++++++++
....uu.u..
p.u....u.g
..u.uu....
++++++++++

++++++++++
.*...*...*
p*u*u..*ug
..u*u***u*
++++++++++

++++++++++
....u*....
p.u*u..u*g
.*u...uu**
++++++++++

++++++++++
..l.l.*.l.
pul*lu*ulg
.u.*.u.u..
++++++++++

++++++++++
*l..u*l..*
p.ulull*ug
***l...uu*
++++++++++

++++++++++
.l.u**l.u.
pluuulllug
..*....*..
++++++++++

++++++++++
..*.*.q..*
p.q.*.*qqg
..*.q.*..*
++++++++++

++++++++++
.*.q.u.*.*
pqu*lu*qlg
.*.ql*qql*
++++++++++

++++++++++
*uq*uqu*.*
pq..q.q.lg
.uq*uq.*l*
++++++++++

++++++++++
...wq.qw..
p.qw.wq..g
..q.qw.w..
++++++++++

++++++++++
.w.q.*...*
p*lqu*lu*g
.*l*w.w*q.
++++++++++

++++++++++
.*qq.*.u.q
p*u*wul**g
.ww*wuwu.q
++++++++++

++++++++++
...*.y*..*
p*y.ly...g
.*..l.*yy*
++++++++++

++++++++++
.*yyyyyyy*
p......u.g
.*.q.l.u.*
++++++++++

++++++++++
...**.yq..
p*y*.lyuyg
.*.q.l.uy*
++++++++++

++++++++++
..ysulys..
p........g
..ysulys..
++++++++++

++++++++++
.qs.lwy.u*
p*syl*ysug
.w.y.q.s.*
++++++++++

++++++++++
u.l.u.l.u.
p*ly**ly*g
usq.usq.us
++++++++++

++++++++++
../b.uw*..
p..b./ssbg
../b.u./*.
++++++++++

++++++++++
..wsu./**.
p*ysuys/bg
..y/by.**.
++++++++++