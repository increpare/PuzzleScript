title Flower Dance
author quat
background_color lightbrown
text_color darkbrown

require_player_movement

color_palette 14

again_interval 0.1

noaction

========
OBJECTS
========

Background .
lightbrown

Player
red lightred
.110.
10000
00000
00000
.000.

Marker
transparent

Check &
brown
.....
.000.
.0...
.000.
.....

Bud
red green lightgreen
.....
.....
...2.
...1.
...1.

Plant
red green lightgreen
..0..
.020.
..01.
..11.
...1.

Wall #
darkgrey
(darkgrey grey
00000
00100
01110
00100
00000)

End W
brown
.....
.000.
.0.0.
.000.
.....

=======
LEGEND
=======

Collidable = Wall or Plant

P = Player and Marker

=======
SOUNDS
=======

sfx7 34533704
sfx1 78722500
sfx2 38016702
EndLevel 82634105

================
COLLISIONLAYERS
================

Background
End
Check
Marker
Bud
Player, Wall, Plant


======
RULES
======

[> Player | No Collidable] -> [ | > Player] sfx7

[Player] -> [Marker Player]

[Bud Player] -> sfx2

[Bud] -> [Plant]
[Marker no Player] -> [Bud]

[Player Check] -> [Player] checkpoint sfx1

==============
WINCONDITIONS
==============

Some Player
All Player on End

=======
LEVELS
=======

Message Arrow keys to run across the garden.
Message Plant flowers in the dirt.
Message Don't land on the flower buds...

......................#............#.............
............#......#..#..#...#...................
...........###..##...#...#....#....#.............
...........##.##....#.#..#.#..#...#....#.........
....##....#..##...##..#..#....#..#.#...##........
........###..######....##.##..##.#.#.##..........
...#######.#.###.#######.######..##..##.#........
...#.#.###############.#####.##..##.#......#.....
.....#.######.####.############.#####.###.#......
.....#####.#..#..#..#.########..#.#######..#.....
.......##.###########.#...#..###########.##......
......######......###.#...#..##.....###.##.......
..##.####.#........####...#####.###.###..#....#..
.....#..###.........###...#####.###.#####.####...
.#...######&....................###.########.....
..#..##.###.........###...#####..&..###.###......
...#.#.####........####...######....#####.....#..
...#.#######......#####...#######.#######..#...##
...#.#.#######.########...#######.#####.##.###...
....#.####...#.#######.....##...#.##.###.#.#.....
..###.#.#.####.##...#.......#...#.####.####.#....
....#####.#..#.##.P.....w......##.##..#.###...#.#
....#.###.#..#.##...#.......#...#.####...###.#...
......########.#######.....##...#.#######........
..##.##.######.########...###...#.###...###......
.....##.##..........####.####.#.#.#######.#......
.....#.##...........####.####...#.....#.#####....
.....###............####.####...###...##..##...#.
...#####................&####...###...####..##...
.....###............#########.........####...#...
.......##...........#########.........#.#.#......
.##....#.#....&.....####################.#..#....
....####.#############..##.####.#.####.##.#......
.......###...#.##.##...####.###.#.###......#.#...
..#.#.##.##.####.#####.##.#.#.#.##.#.##.##.#.....
......#####......####.##.#.##.#####...#.####.....
.....############.####.#..#...#.#.#.......#......
.....####.##...#..#..........#...##...#..........
.....#...#.....##.#..####...#...#..#....#........
........#...#.#...##.....#.#.........#...........
...........#..#...#.......#.....#................
..............#....##....##.....#....#...........
