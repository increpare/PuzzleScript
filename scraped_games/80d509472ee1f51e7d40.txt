title Heroes of Sokoban and Other Tales: The Rogue
author Connorses [original HoS by Jonah Ostroff]
homepage http://www.puzzlescript.net/play.html?p=6860122
text_color blue
key_repeat_interval 0.25

========
OBJECTS
========

Background 
#989898 #C8C8C8
00000
00100
00000
00010
00000

Goal 
Brown Yellow
.000.
00000
00010
00000
00000

Wall
Darkgrey #222222
01000
11111
00010
00010
11111

Fighter (Warrior sounds better in-game, but I want different abbreviations for the classes in the editor.)
#FF4444 Pink DarkBrown
.000.
.111.
00000
.000.
.2.2.

Wizard
#2266FF Pink
.000.
.010.
00000
.000.
00000

Thief
#66DD66 Pink DarkBrown
.000.
.010.
00000
.0.0.
.0.0.

Priest
#FFFFAA Pink
.000.
.111.
00000
.000.
00000

Rogue
pink #0e60b4 darkbrown
.111.
.101.
02220
.111.
.2.2.

SFighter 
#880000 Pink DarkBrown
.000.
.111.
00000
.000.
.2.2.

SWizard
#000088 Pink
.000.
.010.
00000
.000.
00000

SThief
#006600 Pink DarkBrown
.000.
.010.
00000
.0.0.
.0.0.

SPriest
#B8B800 Pink
.000.
.111.
00000
.000.
00000

SRogue 
pink darkblue darkbrown
.111.
.101.
02220
.111.
.2.2.

Crate 
Orange #552200
.111.
10001
10001
10001
.111.

YellowSwitch
Yellow Black
.....
.111.
.101.
.111.
.....

YellowOpen
Yellow
0...0
.....
.....
.....
0...0

YellowDoor
Yellow #AA7722
01010
10001
00000
10001
01010

PurpleSwitch
#6611CC Black
.....
.111.
.101.
.111.
.....

PurpleDoor
#6611CC #220088
01010
10001
00000
10001
01010

PurpleOpen
#6611CC
0...0
.....
.....
.....
0...0

MonsterDoor
DarkGrey #222222 Red
21002
22122
20212
20012
21112

Warpball
Blue

ShadowDoor
Purple

DragonLeft
Green Red Orange Yellow DarkGreen
.....
1100.
3210.
1100.
...4.

DragonRight
Green Red Orange Yellow DarkGreen
.....
.0011
.0123
.0011
.4...

Goblin
Green DarkBrown DarkGreen
0.2.0
00000
..0..
.111.
.1.1.

Dying
White

Saved
#D8D800
..0..
.000.
..0..
..0..
.....

HasSaved
Yellow
.....
.....
.....
.....
.....

Corpse
#CCC
.000.
.000.
00000
.0.0.
.0.0.

LFireball
Orange

RFireball
Orange

DidNotMove
Purple

=======
LEGEND
=======

Dragon = DragonLeft or DragonRight
Player = Fighter or Wizard or Thief or Priest or Rogue
SPlayer = Swizard or Sthief or SFighter or SPriest or SRogue
Hero = Player or Splayer
Healer = Priest or SPriest (Unlike the other heroes, priests must function even when inactive. "Healer" is the generic term for an active or inactive priest.)
ClosedDoor = PurpleDoor or YellowDoor or MonsterDoor
OpenDoor = YellowOpen or PurpleOpen
Switch = PurpleSwitch or YellowSwitch
Monster = Dragon or Goblin
Moveable = Crate or SPlayer or Monster
Static = Wall or ClosedDoor
Weighing = Crate or Hero or Monster
Vulnerable = Hero or Monster
Invulnerable = Crate or Static
Fireball = LFireball or RFireball
. = Background
# = Wall
F = Fighter
W = Wizard
T = Thief
P = Priest
\ = Rogue
1 = SFighter
2 = SThief
3 = SWizard
4 = SPriest
5 = SRogue
* = Crate
G = Goal
[ = YellowDoor
] = PurpleDoor
{ = YellowSwitch
} = PurpleSwitch
L = DragonLeft
R = DragonRight
O = Goblin
M = MonsterDoor

=======
SOUNDS
=======

Moveable move 82056307
Sfx0 21221308 (teleport)
Endlevel 36315908
Sfx1 66399906 (switch player)
Sfx2 92172900 (close door)
Sfx3 15129100 (open door)
Sfx4 69860105 (saved)
Sfx5 63194902 (kill enemy)
Sfx6 81996702 (dying player)
EndGame 86793500

================
COLLISIONLAYERS
================

Background
Warpball, Dying
Goal, Switch, OpenDoor, ShadowDoor
ClosedDoor, Hero, Crate, Wall, Monster, Corpse
RFireball, Saved, DidNotMove
LFireball, HasSaved

======
RULES     
======     
[Saved] [Wall] -> [] [Wall HasSaved] (Remembering whether we saved someone last round by attaching a note to some wall.)

[Corpse] -> Restart

(Adds a DidNotMove to where the player currently is, to cancel moves where they bump into a wall.)

[Player] -> [Player DidNotMove]

[ >  Fighter | Moveable ] -> [  >  Fighter | > Moveable  ]     
[ > Moveable | Moveable ] [ Fighter] -> [ > Moveable | > Moveable ] [ Fighter] (Fighters push.)

[ < Thief | Moveable ] -> [ < Thief | < Moveable ] (Thieves pull.)

[ > Wizard ] -> [ Wizard > Warpball]
[ > Warpball | no Moveable no Static] -> [ | > Warpball]
[ > Warpball | Static] [Wizard] -> [ | Static ] [> Wizard]
[ > Warpball | Moveable ] [Wizard] -> [ | Wizard Sfx0] [Moveable] (Wizards telexport. What they really do is shoot a "warpball", which causes them to move normally if it hits a wall and warp if it hits something they can warp with.)

(The Rogue pulls blocks that are to her side)
[^ Rogue | Moveable] -> [ ^ Rogue | ^ Moveable]
[v Rogue | Moveable] -> [ v Rogue | v Moveable]

(The following mess is for changing characters.)
[ Action Fighter] [SThief] -> [SFighter] [Thief Sfx1]
[ Action Thief] [SWizard] -> [SThief] [Wizard Sfx1]
[ Action Wizard] [SPriest] -> [SWizard] [Priest Sfx1]
[ Action Priest] [SRogue] -> [SPriest] [Rogue Sfx1]
[ Action Rogue] [SFighter] -> [SRogue] [Fighter Sfx1]

[ Action Fighter] [no SThief  ] [SWizard ] -> [SFighter] [ ] [Wizard Sfx1]
[ Action Thief  ] [no Swizard ] [SPriest ] -> [SThief  ] [ ] [Priest Sfx1]
[ Action Wizard ] [no SPriest ] [SRogue  ] -> [SWizard ] [ ] [Rogue Sfx1]
[ Action Priest ] [no SRogue  ] [SFighter] -> [SPriest ] [ ] [Fighter Sfx1]
[ Action Rogue  ] [no SFighter] [SThief  ] -> [SRogue  ] [ ] [Thief Sfx1]

[ Action Fighter] [no SWizard] [SPriest] -> [SFighter] [ ] [Priest Sfx1]
[ Action Thief  ] [no SPriest] [SRogue] -> [SThief] [ ] [Rogue Sfx1]
[ Action Wizard] [no SRogue]  [SFighter] -> [SWizard] [ ] [Fighter Sfx1]
[ Action Priest] [no SFighter] [SThief] -> [SPriest] [ ] [Thief Sfx1]
[ Action Rogue] [no SThief] [SWizard] -> [SRogue] [ ] [Wizard Sfx1]

[ Action Fighter ] [no SPriest ] [SRogue] -> [SFighter] [ ] [Rogue]
[ Action Thief   ] [no SRogue  ] [SFighter] -> [SThief] [ ] [Fighter]
[ Action Wizard  ] [no SFighter] [SThief] -> [SWizard] [ ] [Thief]
[ Action Priest  ] [no SThief  ] [SWizard] -> [SPriest] [ ] [Wizard]
[ Action Rogue   ] [no SWizard ] [SPriest] -> [SRogue] [ ] [Priest]

(Cancels the move if the player is in the same place that they started. Notice that switching characters negates this. Only matters in levels where one dragon has multiple targets.)

Late [Player DidNotMove] -> Cancel
Late [DidNotMove] -> []

(Now enemies attack.)

Late [DragonLeft] -> [LFireball DragonLeft]
Late [DragonRight] -> [RFireball DragonRight]

Late RIGHT [RFireball | No Vulnerable no Invulnerable] -> [ | RFireball]
Late LEFT [LFireball | No Vulnerable no Invulnerable] -> [ | LFireball]
Late RIGHT [RFireball | Vulnerable] -> [ | Dying Vulnerable]
Late Right [RFireball | Invulnerable] -> [ | Invulnerable] 
Late Left [LFireball | Vulnerable] -> [ | Dying Vulnerable]
Late LEFT [LFireball | Invulnerable] -> [ | Invulnerable]

Late [Goblin | Vulnerable] -> [Goblin | Dying Vulnerable]

(Vulnerable objects that are attacked are designated as "Dying", but heroes can be saved by the Priest. Then the stuff that didn't get saved dies.)

Late [Dying Healer] -> [Saved Healer]
Late [Healer | Dying Hero] -> [Healer | Saved Hero]
Late [Dying Monster] -> [Sfx5]
Late [Dying Hero] -> [Corpse Sfx6]

(Doors opening.)

late [Weighing  YellowSwitch] [YellowDoor] -> [Weighing  YellowSwitch] [ShadowDoor]
late [YellowSwitch no Weighing] [YellowOpen] -> [YellowSwitch] [YellowDoor sfx2]
late [YellowSwitch no Weighing] [ShadowDoor] -> [YellowSwitch] [YellowDoor]
late [ShadowDoor] -> [YellowOpen sfx3]

late [Weighing PurpleSwitch] [PurpleDoor] -> [Weighing PurpleSwitch] [ShadowDoor]
late [PurpleSwitch no Weighing] [PurpleOpen] -> [PurpleSwitch] [PurpleDoor sfx2]
late [PurpleSwitch no Weighing] [ShadowDoor] -> [PurpleSwitch] [PurpleDoor]
late [ShadowDoor] -> [PurpleOpen sfx3]

late [MonsterDoor] -> [ShadowDoor]
late [ShadowDoor] [Monster] -> [MonsterDoor] [Monster]
late [ShadowDoor] -> [sfx3]

(Looks for a HasSaved on a wall and moves it to the Priest. Then, if someone was saved this turn, plays the sound only if the Priest doesn't have the HasSaved note. Finally, cleans that up.)

late [Wall HasSaved] [Healer] -> [Wall] [Healer HasSaved]
late [Saved] [Healer no HasSaved] -> [Saved sfx4] [Healer]
late [HasSaved] -> []

==============
WINCONDITIONS
==============

All Hero on Goal     
All Goal on Hero

=======     
LEVELS
=======
Message Welcome.

Message [You've played Jonah Ostroff's excellent original, yes?]
Message [If not, this might be confusing.]
Message [The original HoS is in the link below.]

(***
 *** Prologue
 ***)

(Temple of Sokoban)
#############
####3.F.2####
###.......###
###.##.##.###
#.*{#.{*#{.*#
#...#...#...#
#...#...#...#
###[##[##[###
##.G..G..G.##
#############

(***
 *** Act I: The Rogue
 ***)

Message The Blue Rogue slides blocks next to her.

#############
#...#*..#...#
#.\....*..g.#
#...#...#...#
#############

#############
#...#..{#...#
#.\.....#.g.#
#...#*..[...#
#############

#############
#..*.*.{#...#
#.\.....#.g.#
#...#...[...#
#############

#######
#\....#
#..*..#
#..##.#
#..{..#
#.....#
###[###
###g###
#######

########
#g#.{..#
#[*....#
##....\#
##*....#
###.{..#
########

#########
#.....#G#
#.#*..#[#
#.**.\..#
#.......#
#.{.{.{.#
#.......#
#########

Message Heroes can move each other.

###########
#*#..##g.g#
#...5.#...#
#..#...#.##
#w.*#..*.*#
###########

#########
#*#######
#.....#g#
#\.*.{#g#
#.#####.#
#.#.#...#
#*..[.3.#
##..#...#
#########

#############
########gg###
#.........#w#
#.*###*#..#.#
#..#.#.#..#.#
r...........#
#.....*.....#
#.....5.....#
r***********#
#o#o#o#o#o#o#

#############
#g..[...#####
#...#.*.{..*#
#g..#5{t#####
#############

#############
#.......#g.g#
#########[#[#
#2..#...#.{.#
#.\...*.....#
#...#...#...#
#############

###########
#f.*...*#{#
#..#...*..#
####...*.*#
#..#.##[###
#5...#*.gg#
###########

#######
#gg.\.#
#.....#
####*.#
#r....#
#r....#
#r....#
####..#
#.1...#
#.....#
#######

####o#o#o####
#g.........4#
#g.........\#
#####o#o#####

#########
##.....##
#...#..##
#{..#*.o#
#...o..##
#{.*#..o#
##..o..##
###[#..\#
#g..#...#
#.gg#4.2#
#########

#########
#...[..g#
#{.{#...#
r...#..g#
#...l#.##
r*..##.##
#*..l#.##
#...#...#
#.\.#...#
#..2[...#
#########

#############
#F.........G#
#############
#T.........G#
#############
#W.........G#
#############
#\.........G#
#############
#P.........G#
#############

Message Thanks to Jonah for his cool game
Message [And the forums people who helped us mod it. Haha]