title Slippy Penguin
author Ryan Woods
homepage www.puzzlescript.net

debug

color_palette 14

background_color BLACK

noundo

noaction

youtube 6ByKY6UowDs

========
OBJECTS
========
Player_Still
black orange LIGHTGRAY orange
.000.
.010.
00200
02220
.3.3.

PlayerL
black orange LIGHTGRAY orange
..00.
00001
0000.
10001
.200.

PlayerR
black orange LIGHTGRAY orange
.00..
10000
.0000
10001
.002.

PlayerU
black orange LIGHTGRAY orange
.000.
.000.
00000
00000
.3.3.

PlayerD
black orange LIGHTGRAY orange
.3.3.
00000
00000
.000.
.000.

Rock
GRAY WHITE DARKGRAY
.00..
0201.
00001
02000
22020

Background
LIGHTBLUE white
11001
10011
00111
01110
11100

Goal1
LIGHTGRAY blue LIGHTGRAY DARKBLUE
.000.
01310
31131
23112
.222.

Goal2
LIGHTGRAY blue LIGHTGRAY DARKBLUE
.000.
01130
13113
21312
.222.

Fish
white DARKBROWN ORANGE BLACK
.222.
20322
22212
.222.
22.22

Fish2
white DARKBROWN ORANGE BLACK
.222.
20322
22212
.222.
22.22

Fish3
white DARKBROWN ORANGE BLACK
.222.
20322
22212
.222.
22.22

=======
LEGEND
=======

P = Player_Still
. = Background
R = Rock
G = Goal1
F = Fish
2 = Fish2
3 = Fish3

Player = Player_Still or PlayerL or PlayerR or PlayerU or PlayerD

Goal = Goal1 or Goal2

Obstacle = Rock

=======
SOUNDS
=======

endlevel 53546508
Player move 359308

================
COLLISIONLAYERS
================

Background
Goal
Player, Rock, Fish, Fish2, Fish3


======
RULES
======

(Player movement and Sprite Changing)

[LEFT Player ] -> [ PlayerL ]
left [ PlayerL | Obstacle ] -> [ Player_Still | Obstacle ]
[ PlayerL ] -> [ LEFT PlayerL ] again


[right Player ] -> [ PlayerR ]
right [ PlayerR | Obstacle ] -> [ Player_Still | Obstacle ]
[ PlayerR ] -> [ right PlayerR ] again

[up Player ] -> [ PlayerU ]
up [ PlayerU | Obstacle ] -> [ Player_Still | Obstacle ]
[ PlayerU ] -> [up PlayerU ] again

[down Player ] -> [ PlayerD ]
down [ PlayerD | Obstacle ] -> [ Player_Still | Obstacle ]
[ PlayerD ] -> [ down PlayerD ] again


(Fish follow)

[fish |...| Player] -> [ > Fish |...| Player]

[fish2 |...| Fish] -> [ > Fish2 |...| Fish]

[fish3 |...| Fish2] -> [ > Fish3 |...| Fish2]

==============
WINCONDITIONS
==============

All Player on Goal1

=======
LEVELS
=======

message Help Slippy the Penguin get back to the water!

RRRRRRRR
R.....GR
R..RRRRR
R.....PR
RRRRRRRR

message No Slippy! What are you doing on land again!

RRRRRRRR
R..RG..R
R..R...R
R......R
RR.....R
RR...RRR
RR....PR
RRRRRRRR


rrrrrrrrrrrrrrrr
r.........r.g..r
r....rr........r
r..r...r.......r
r..........r..rr
r.......r......r
r.....r..rr....r
rr...r.......rpr
rrrrrrrrrrrrrrrr


rrrrrrrrrrrrrrrr
r........r.....r
r...r..........r
r.........r....r
r.r............r
rr.......r.....r
r.............rr
r......r.......r
r..r..........gr
r.............rr
r.......r......r
r.....r...r....r
r.............pr
rrrrrrrrrrrrrrrr

message Oh! Your catching fish! That makes sense!

rrrrrrrrrrrrrrrr
rg....r.....rrrr
r...........rrrr
r.....r........r
r.r....r....r..r
rr......r......r
rr........r....r
r..rr..r.....r.r
r..............r
r....r......r..r
r.r............r
r.............Pr
r......r.r..rrFr
rrrrrrrrrrrrrrrr

message Another fish huh?

rrrrrrrrrrrrrrrr
r...r..........r
r........r.r...r
r....r..r......r
r......rr...r..r
r..r.r.rr.r....r
r.....rg..r..r.r
r..r...r...r...r
r....r.........r
r.....r....r..rr
rP..r......rr..r
rF...r.........r
r2r......r.....r
rrrrrrrrrrrrrrrr

message Three fish?! You greedy Penguin! Back in the water with you!

rrrrr
r...r
r.g.r
r...r
r...r
r...r
r...r
r.p.r
r.F.r
r.2.r
r.3.r
rrrrr



