title Simple Block Pushing Game
author Stephen Lavelle
homepage www.puzzlescript.net

========
OBJECTS
========

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

Crate
Orange Yellow
00000
0...0
0...0
0...0
00000

Hot
Red Darkred
01011
10110
01001
00100
10001

=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target
! = hot
& = target and hot
? = crate and hot
Â¿ = crate and hot and target


=======
SOUNDS
=======

Crate MOVE 36772507
sfx0 97363508

================
COLLISIONLAYERS
================

Background
hot
Target
Player, Wall, Crate

======
RULES
======

[ >  Player no hot| Crate ] -> [  >  Player | > Crate  ]
late [player hot] -> [player hot] sfx0

==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======
#######
#.....#
#...!.#
#p.?.o#
#...!.#
#.....#
#######

#######
#.!o.!#
#!!!*p#
#!.!!!#
#!.!..#
#!.!!!#
#######

#######
####.o#
##.#..#
#&*.*.#
#.!*!.#
#.po..#
#######

#######
#.....#
###!*.#
#oo*!p#
##.!..#
#.....#
#######

#######
##..P.#
##.#!.#
#..*@.#
#..O@.#
##....#
#######
