title ironic game
homepage by noa
background_color darkgreen
text_color white
flickscreen 9x7
again_interval 0.5

run_rules_on_level_start

========
OBJECTS
========

background .
lightgray

player p
green lightgreen
.000.
00000
01010
00000
.000.

wall #
black

button b
green gray
11111
10001
10001
10001
11111

ebutton e
gray green red blue yellow purple orange lightgreen darkblue pink
00000
03890
07520
01460
00000

door d
orange darkred
10001
10001
10001
10001
10001

open
gray

anim
transparent

next
transparent

line l
gray
.....
.....
00000
.....
.....

box x
orange darkred
11111
10001
10001
10001
11111

ured
darkred
00000
.....
.....
.....
.....

dred
darkred
.....
.....
.....
.....
00000

dot +
pink
.....
.....
.000.
.....
.....

dot2 2
pink
.....
..0..
..0..
..0..
.....

dh h
pink gray
11111
11.11
10001
11.11
11111

dv v
pink gray
11111
11011
1.0.1
11011
11111

safespot
gray #DDBBDD

slime
#C5C9BB

nmode o
yellow

noff
black

ndeco g
gray

zap z
gray transparent
11111
10001
10001
10001
11111

zon
orange black
11111
10001
10001
10001
11111

zg
yellow

zmark
transparent

off
darkgray

mg
lightblue

sbox
darkgreen
00000
00000
0.0.0
00000
00000

shut s
transparent

le1 4
blue
.000.
..0..
..0..
..0..
..0..

le2 5
pink
.0.0.
.000.
.0.0.
.0.0.
.....

le3 6
red
.000.
.0...
.000.
.0...
.000.

le4 7
green
.000.
.0...
.000.
.0...
.000.

le5 8
lightgreen
.....
.000.
.0.0.
.0.0.
.0.0.

le6 9
orange
.00..
.0.0.
.0.0.
.0.0.
.00..

=======
LEGEND
=======

pusher = player or box
col = pusher or wall or door or zg or sbox
dc = dh or dv
letter = le1 or le2 or le3 or le4 or le5 or le6


=======
SOUNDS
=======

sfx0 95349704
sfx1 43656302
sfx2 21164709

================
COLLISIONLAYERS
================

background
off, mg
safespot, slime

noff
letter
button, line, dot, dot2, dh, dv, nmode, zap, zon, ndeco, ebutton

open
col

ured, dred

anim
next
zmark
shut

======
RULES    
======  
(shut)
late down [shut | no shut no col] -> [shut | shut]
late [player shut] [pusher button] -> [player shut] [button]
[> player shut] [open] -> [player shut] [open]

(power)
late [player nmode] -> [player noff] sfx1
late [noff] [zon] -> [noff] [zap]
late [noff] [zg] -> [noff] []
late [noff] [no col] -> [noff] [off]
late [noff] [slime] -> [noff] [sbox]
late [noff] [dc] -> [noff] [noff dc]

vertical [> player no dot | sbox] -> [> player | > sbox] sfx0
horizontal [> player no dot2 | sbox] -> [> player | > sbox] sfx0
vertical   [no col | > player dot  | sbox] -> [sbox | > player dot  | ] sfx1
horizontal [no col | > player dot2 | sbox] -> [sbox | > player dot2 | ] sfx1

(dhdv)
[> player | dv no col] -> [| dh player] sfx0
[> player | dh no col] -> [| dv player] sfx0
late [player dh] [dot2] -> [player dh] [dot]
late [player dv] [dot] -> [player dv] [dot2]

vertical   [ > player | dot2] -> [player | dot2] sfx1
horizontal [ > player | dot] ->  [player | dot ] sfx1

(zap)
late [player dc no noff] [zap] -> [player dc] [zon]
late [player dc] [zon | no col] -> [player dc] [zon | zg]
late horizontal [no zon no zmark | zg no zmark | no zon no zmark] -> [zg | zg zmark | zg]
[> player | zg] -> [player | zg] sfx1

(box)
vertical [> player no dot | box] -> [> player | > box] sfx0
horizontal [> player no dot2 | box] -> [> player | > box] sfx0
vertical   [no col | > player dot  | box] -> [box | > player dot  | ] sfx1
horizontal [no col | > player dot2 | box] -> [box | > player dot2 | ] sfx1

(door)
late up [door | wall] -> [door ured | wall]
late down [door | wall] -> [door dred | wall]

late [pusher button | line | ... | door] -> [pusher button | line | ... | door anim]
late [door no anim | door anim | door no anim] -> [door anim next| door anim | door anim]

late [door anim next] -> [open anim] sfx0
late [open anim |door no next] -> [open anim|door next] again
late [open anim] -> [open]

late [button no pusher] [door|door|open] -> [button no pusher] [door|door|door] sfx1
late [button no pusher] [door|open|open] -> [button no pusher] [door|door|open] sfx1
late [button no pusher] [open|open|open] -> [button no pusher] [door|open|open] sfx1

(slime)
late [wall | | wall] -> [wall | safespot | wall]
late [player] [nmode] -> [player slime] [nmode]

(end)
late [nmode] [letter] -> [nmode] [letter ndeco]
late [player ebutton] -> [player ebutton] sfx2
late [player ebutton] [ndeco] -> [player ebutton][]
late [player ebutton] [line] ->       [player ebutton][]
late [player ebutton] [sbox] ->       [player ebutton][]
late [player ebutton] [noff] -> [player ebutton][]
late [player ebutton] [off] -> [player ebutton][mg]

==============
WINCONDITIONS 
==============

no ebutton

=======    
LEVELS
=======

##################
#.......##.......#
#...h...##..ggg..#
#.......+...gog..#
#...+...##..ggg..#
#.z...z.##.......#
####.#############
####.#############
#.......##.g.g.g.#
#...+...##g.g.g.g#
#...x...##.g.g.g.#
#...+...##g.g.g.g#
#.......##.g.g.g.#
####.#############
####.#############
#.......##s......#
#.......dds.456..#
#...blllddllgeg..#
#.......dds.789..#
#.......##s......#
####.#############
####.#############
##.g.g.###.g.g.g.#
##g.g.g###g.g.g.g#
##.gpg.###.g.g.g.#
##g.g.g###g.g.g.g#
##.g.g.###.g.g.g.#
##################






