title AutoTrom
author Marcos Donnantuoni
homepage https://marcosd.itch.io

again_interval 0.25
key_repeat_interval 0.17
text_color grey

========
OBJECTS
========

background
gray #888
00000
00000
00100
00000
00000

wall
brown

player
blue
00.00
0...0
.....
0...0
00.00

domL
green darkgreen
00000
01111
01111
01111
00000

domH
green darkgreen
00000
11111
11111
11111
00000

domR
green darkgreen
00000
11110
11110
11110
00000

domU
green darkgreen
00000
01110
01110
01110
01110

domV
green darkgreen
01110
01110
01110
01110
01110

domD
green darkgreen
01110
01110
01110
01110
00000

adomL
yellow brown
00000
01111
01111
01111
00000

adomH
yellow brown
00000
11111
11111
11111
00000

adomR
yellow brown
00000
11110
11110
11110
00000

adomU
yellow brown
00000
01110
01110
01110
01110

adomV
yellow brown
01110
01110
01110
01110
01110

adomD
yellow brown
01110
01110
01110
01110
00000

part1
darkgreen
.....
.000.
.000.
.000.
.....

part2
darkgreen
.....
.000.
.000.
.000.
.....

dos
transparent

=======
LEGEND
=======

. = Background
# = wall
@ = Player
u = domU
d = domD
l = domL
r = domR
dom = domL or domH or domR or domU or domV or domD or adomL or adomH or adomR or adomU or adomV  or adomD
lim = dom or wall

=======
SOUNDS
=======

domH create 4873700
adomV create 95340100

================
COLLISIONLAYERS
================

background
wall
dom
part1, part2
player
dos

======
RULES
======

[ action player lim ] -> cancel
[ action player no part1 | no dom | part1 ] -> [ player part2 | | part2 no part1 ]
[ action player | | no part1 ] [ part1 ] -> [ player part1 | | no part1 ] [ no part1 ]
[ action player no part1 ] -> [ player part1 ]
[ action player part1 ] -> [ player no part1 ]

right [ part2 | | part2 ] -> [ no part2 domL | domH | no part2 domR ]
down  [ part2 | | part2 ] -> [ no part2 domU | domV | no part2 domD ]

late [ dos ] -> []
late [ lim | no lim | lim ] -> [ lim | dos | lim ]
late [ lim | no lim  | no lim | lim ] -> [ lim | dos | dos| lim ]
late right [ lim | dos | no lim | no lim ] -> [ lim | no dos adomL | no dos adomH | no dos adomR ] again
late right [ lim | no lim | dos | no lim | lim] -> [ lim | no dos adomL | no dos adomH | no dos adomR|lim ] again
late right [ no lim | no lim | dos | lim ] -> [ no dos adomL | no dos adomH | no dos adomR | lim ] again
late down  [ lim | dos | no lim | no lim ] -> [ lim | no dos adomU | no dos adomV | no dos adomD ] again
late down  [ no lim | no lim | dos | lim ] -> [ no dos adomU | no dom adomV | no dos adomD | lim ] again
late down  [ lim | no lim | dos |no lim| lim ] -> [ lim| no dos adomU | no dom adomV | no dos adomD | lim ] again


==============
WINCONDITIONS
==============


=======
LEVELS
=======

message you must fill the board
message use X to draw green trominoes
message yellow trominoes will appear in obvious places
message use as few green trominoes as possible

###########
#.........#
#.........#
#.........#
#.........#
#....@....#
#.........#
#.........#
#.........#
#.........#
###########

