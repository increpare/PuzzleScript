title Sokolor EX
author ncrecc
homepage www.puzzlescript.net
require_player_movement
noaction

========
OBJECTS
========

Background
grey darkgray
11111
11101
11111
10111
11111

Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
White Lightgrey Grey
00001
00012
00122
01222
12222

Recession
Darkgray Gray
11111
11101
11111
10111
11111

Player
Orange White Black
.222.
.000.
10001
.111.
22.22

Blue
Blue Purple
00001
0.011
0.1.1
011.1
11111

Red
Red Darkred
00001
000.1
0.1.1
0.111
11111

Yellow
Yellow Lightbrown
00001
00..1
00111
0..11
11111

Green
Lightgreen Green
00001
0..11
00111
01..1
11111

Connector
White lightgray
00001
00.11
0.1.1
01.11
11111

Difficult
lightgray Gray
00001
0.0.1
00111
0.1.1
11111

Pin
Lightred
0....
.....
..0..
.....
....0

Inactive
Black

Active
Pink

=======
LEGEND
=======

. = Background
, = Recession
# = Wall
P = Player
O = Target
1 = Blue and Inactive
2 = Red and Inactive
3 = Yellow and Inactive
4 = Green and Inactive
5 = Connector and Inactive
6 = Difficult and Inactive
A = Blue and Pin and Inactive
B = Red and Pin and Inactive
C = Yellow and Pin and Inactive
D = Green and Pin and Inactive
E = Connector and Pin and Inactive
F = Difficult and Pin and Inactive

Block = Blue or Red or Yellow or Green or Connector or Difficult
Color = Blue or Red or Yellow or Green
Backing = Inactive or Active

=======
SOUNDS
=======

Player move 58745307
Block MOVE 33628907
restart 222506502
undo 62190104
endlevel 71736700

================
COLLISIONLAYERS
================

Background
Recession, Target
Inactive
Active
Player, Wall, Block
Pin

======
RULES
======

[Active] -> []

[> Player|Block no Pin] -> [> Player|> Block]
[> Block|Recession] -> [Block|Recession]

random [> Block] [Blue] -> [> Block] [Blue Active] 
random [> Block] [Red] -> [> Block] [Red Active]
random [> Block] [Yellow] -> [> Block] [Yellow Active]
random [> Block] [Green] -> [> Block] [Green Active]
(difficults will not activate unless they receive energy from a universal connector)

[Backing > Block] -> [> Backing > Block]
startloop
late [Active Block|Connector] -> [Active Block|Active Connector]
late [Active Connector|Block] -> [Active Connector|Active Block]

late [Active Blue|Blue] -> [Active Blue|Active Blue]
late [Active Red|Red] -> [Active Red|Active Red]
late [Active Yellow|Yellow] -> [Active Yellow|Active Yellow]
late [Active Green|Green] -> [Active Green|Active Green]
late [Active Difficult|Difficult] -> [Active Difficult|Active Difficult]
endloop
==============
WINCONDITIONS
==============

All Target on Block
All Color on Active


=======
LEVELS
=======

message Every push you make, one random block per color will light up.

message Light spreads to adjacent blocks of the same color instantaneously.

message You win when all blocks are lit.

..##1##..
.##111##.
##11#11##
#.......#
###...###
.#.1.1.#.
.#.1p1.#.
.#.....#.
.#######.

########
#,,,.,2#
#,p,.,.#
#,,,3..#
#..24,.#
#,,.,,.#
#3....4#
########

#######
##...##
#.2...#
#p1#1.#
#...2.#
##...##
#######

#########
#..4,3..#
#..1,1..#
#..3.4..#
##..p..##
#########

#########
#..p....#
#.......#
#.4.5.4.#
#.......#
#.66666.#
#.5.#.5.#
##4###4##