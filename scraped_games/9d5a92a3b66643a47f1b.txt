title No Fault of My Own
author Jonah Ostroff
Background_Color Black
Run_Rules_On_Level_Start
norepeat_action

========
OBJECTS
========
Background
White

Cursor
Red
00.00
0...0
.....
0...0
00.00

Laying
Red Blue
00.00
01110
.111.
01110
00.00

FaultR
#ECC
....0
....0
....0
....0
....0

FaultL
#ECC
0....
0....
0....
0....
0....

FaultU
#ECC
00000
.....
.....
.....
.....

FaultD
#ECC
.....
.....
.....
.....
00000

DominoR
Blue
.....
.0000
.0000
.0000
.....

DominoL
Blue
.....
0000.
0000.
0000.
.....

DominoU
Blue
.000.
.000.
.000.
.000.
.....

DominoD
Blue
.....
.000.
.000.
.000.
.000.

Monomino
Black
=======
LEGEND
=======
Player = Cursor or Laying

Domino = DominoR or DominoL or DominoD or DominoU or Monomino
Fault = FaultR or FaultL or FaultD or FaultU

RThing = DominoR or FaultR
LThing = DominoL or FaultL
UThing = DominoU or FaultU
DThing = DominoD or FaultD

FaultV = FaultU or FaultD
FaultH = FaultL or FaultR

. = Background
@ = Cursor
M = Monomino
=======
SOUNDS
=======
EndLevel 17593700
Sfx1 81815307
Sfx2 13053302
================
COLLISIONLAYERS
================
Background
LThing
RThing
UThing
DThing
Monomino
Player
======
RULES
======
(Switch between movement and laying dominoes.)
[Action Cursor no Domino] -> [Laying]
[Action Laying] -> [Cursor]
[Action Cursor Domino] -> [Cursor Action Domino]

Right[Action DominoR|DominoL] -> [|] Sfx2
Left[Action DominoL|DominoR] -> [|] Sfx2
Down[Action DominoD|DominoU] -> [|] Sfx2
Up[Action DominoU|DominoD] -> [|] Sfx2

(Lay a domino, if there's room.)
Right [> Laying|no Domino] -> [DominoR|DominoL Cursor] Sfx1
Left [> Laying|no Domino] -> [DominoL|DominoR Cursor] Sfx1
Up [> Laying|no Domino] -> [DominoU|DominoD Cursor] Sfx1
Down [> Laying|no Domino] -> [DominoD|DominoU Cursor] Sfx1
[> Laying] -> Cancel

(Add fault lines...)
late Left [no LThing|] -> [FaultL|]
late Right [no RThing|] -> [FaultR|]
late Up [no UThing|] -> [FaultU|]
late Down [no DThing|] -> [FaultD|]

(...and then erase the broken ones.)
late Horizontal[FaultU|no FaultU] -> [|]
late Horizontal[FaultD|no FaultD] -> [|]
late Vertical[FaultR|no FaultR] -> [|]
late Vertical[FaultL|no FaultL] -> [|]

==============
WINCONDITIONS
==============
no Fault
all Background on Domino
=======
LEVELS
=======
@.......
........
........
........
........
........
........
........

@......
.......
.......
...M...
.......
.......
.......

@.....
......
......
......
......
......

Message Cheater!