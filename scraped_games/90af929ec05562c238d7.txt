(-
 - PuzzleTech
 - version 0.73
-)

title PuzzleTech 0.73
author 0x77
homepage 0x77.bit

background_color #777
text_color #333

realtime_interval .1

========
OBJECTS
========
( Background )
BACKGROUND .
BLACK

( Cursor )
PLAYER @
#111
00.00
0...0
.....
0...0
00.00
CURSOR +
#700
00.00
0...0
.....
0...0
00.00

( HUD )
HUD_BG _
#555
HUD_BR ;
#555
0....
0....
0....
0....
0....

( Icons )
I_DEL
#500
.....
.0.0.
..0..
.0.0.
.....
I_SPRK
YELLOW
.....
.0.0.
..0..
.0.0.
.....
I_CPWR
#A65626
.....
.000.
.0...
.000.
.....
I_PSWR
#C96C5A
.....
.000.
.000.
.0...
.....
I_NGWR
#6C87A4
.....
.000.
.0.0.
.0.0.
.....
I_LASR
#A3CE27
.....
.0...
.0...
.000.
.....
I_SAND
#E6E0C7
.....
..00.
..0..
.00..
.....
I_FLTR
#1F2587
.....
.000.
.00..
.0...
.....
I_WATR
#428BDD
.....
.0.0.
.000.
.000.
.....
I_SLCN
#A9A9A9
.....
.000.
.000.
.000.
.....
I_IWRE
#033
.....
.000.
..0..
.000.
.....
I_OXYG
#B5D8EE
.....
.000.
.0.0.
.000.
.....

( Entities )
END /
TRANSPARENT
CPWR
#A65626
PSWR
#C96C5A
NGWR
#6C87A4
LASR
#A3CE27
SAND
#E6E0C7
FLTR
#1F2587
WATR
#428BDD
WTR_L
#428BDD
WTR_R
#428BDD
WTR_D
#428BDD
SLCN
#A9A9A9
IWRE
#033
OXYG
#B5D8EE

( Properties )
SPRK
ORANGE
DCAY
YELLOW

RAY0
#FFF
RAY1
#DDD
RAY2
#BBB
RAY3
#999
RAY4
#777

( Tokens )
X
TRANSPARENT
Z
TRANSPARENT
Q
TRANSPARENT
W
TRANSPARENT
U
TRANSPARENT
D
TRANSPARENT
L
TRANSPARENT
R
TRANSPARENT

Y
#777 #FFF
.....
.101.
.101.
.101.
.....

PAUSE !
#FFF
.....
..0..
..00.
..0..
.....
STEP
#FFF
.....
..0..
...0.
..0..
.....

SELT
TRANSPARENT
CTMP =
TRANSPARENT

=======
LEGEND
=======
WIRE = CPWR or PSWR or NGWR or IWRE
HUD  = HUD_BG or HUD_BR
ICON = I_DEL or I_SPRK or I_CPWR or I_PSWR or I_NGWR or I_LASR or I_SAND or I_FLTR or I_WATR or I_SLCN or I_IWRE or I_OXYG
PROP = SPRK or DCAY
RAY  = RAY0 or RAY1 or RAY2 or RAY3 or RAY4
TOKN = PAUSE or STEP or SELT or CTMP
SPEC = Q or W
DIR  = U or D or L or R

WATER = WATR or WTR_L or WTR_R or WTR_D
GAS   = OXYG
SOLID = HUD_BG or WIRE or LASR or RAY or SAND or SLCN
THING = GAS or SOLID or FLTR
FALLS = SAND
WCLL  = THING or WATER
COND  = WIRE OR WATER
RPWR  = PSWR

( HUD )
0 = HUD_BG and I_SPRK
1 = HUD_BG and I_CPWR
2 = HUD_BG and I_PSWR
3 = HUD_BG and I_NGWR
4 = HUD_BG and I_LASR
5 = HUD_BG and I_SAND
6 = HUD_BG and I_FLTR
7 = HUD_BG and I_WATR
8 = HUD_BG and I_SLCN
9 = HUD_BG and I_IWRE
A = HUD_BG and I_OXYG

, = 0 and PLAYER
- = HUD_BG and SELT
' = HUD_BG and PAUSE
: = HUD_BG and STEP
" = HUD_BG and CURSOR
? = HUD_BG and I_DEL

=======
SOUNDS
=======

================
COLLISIONLAYERS
================
( BG )
BACKGROUND
END

( Entities )
RAY
WATER GAS WIRE LASR SAND SLCN FLTR
PROP

( Cursor )
CURSOR

( HUD )
HUD
ICON
PLAYER

( Special )
TOKN
X Y Z
SPEC
DIR

======
RULES
======
( Play/pause/step )
[ ACTION @ ! NO Y ] -> [ @ ! Y ]
[ ACTION @ !    Y ] -> [ @ ! ]

[ : X ] [ ! Y ] -> [:] [!]
[ - NO X ] [ ACTION @ : NO X ] [ ! NO Y ] -> [ - NO X ] [ @ : X ] [ ! Y ]

( Gas physics )
[! Y] [ STATIONARY GAS ] -> [! Y] [ RANDOMDIR GAS ]

( Water physics )
[! Y] [ > WATER SPEC ] -> [! Y] [ > WATER > SPEC ]
[! Y] [ > WATER PROP ] -> [! Y] [ > WATER > PROP ]
[! Y] [ WATER ] -> [! Y] [ STATIONARY WATR ]
DOWN [! Y] [ WATER | NO WCLL NO WATER ] -> [! Y] [ DOWN WTR_D | ]
DOWN [! Y] [ WTR_D | WCLL ] -> [! Y] [ STATIONARY WATR | WCLL ]
RIGHT [! Y] [ WATER | WTR_D ] -> [! Y] [ RIGHT WTR_R | WTR_D ]
RIGHT [! Y] [ WTR_D | WATR ] -> [! Y] [ WTR_D | LEFT WTR_L ]
RIGHT [! Y] [ WATR | WTR_R ] -> [! Y] [ RIGHT WTR_R | WTR_R ]
RIGHT [! Y] [ WTR_L | WATR ] -> [! Y] [ WTR_L | LEFT WTR_L ]
DOWN [! Y] [ WTR_D | WATR ] -> [! Y] [ WTR_D | RIGHT WATR ]
RIGHT [! Y] [ RIGHT WATR | WATR ] -> [! Y] [ LEFT WATR | WATR ]
HORIZONTAL [! Y] [ WCLL | WATR | NO WCLL ] -> [! Y] [ WCLL | > WATR | ]
RANDOM HORIZONTAL [! Y] [ NO WCLL | STATIONARY WATR | NO WCLL ] -> [! Y] [ | > WATR | ]
LATE DOWN [! Y] [ WATER | NO WCLL NO WATER ] -> [! Y] [ WATER | ]

( Gravity )
LATE [! Y] [ GAS   U ] -> [! Y] [ SAND ]
LATE [! Y] [ WATER U ] -> [! Y] [ SAND ]

DOWN [! Y] [ SAND | GAS   ] -> [! Y] [ RANDOMDIR GAS   | GAS   U ]
DOWN [! Y] [ SAND | WATER ] -> [! Y] [ RANDOMDIR WATER | WATER U ]

DOWN [! Y] [ FALLS | NO THING ] -> [! Y] [ DOWN FALLS | ]

( LASR logic )
LATE [! Y] [RAY4] -> [! Y] []
LATE [! Y] [RAY3] -> [! Y] [RAY4]
LATE [! Y] [RAY2] -> [! Y] [RAY3]
LATE [! Y] [RAY1] -> [! Y] [RAY2]
LATE [! Y] [RAY0] -> [! Y] [RAY1]

LATE UP    [! Y] [ LASR U | FLTR ] -> [! Y] [ LASR U | FLTR RAY0 U ]
LATE DOWN  [! Y] [ LASR D | FLTR ] -> [! Y] [ LASR D | FLTR RAY0 D ]
LATE LEFT  [! Y] [ LASR L | FLTR ] -> [! Y] [ LASR L | FLTR RAY0 L ]
LATE RIGHT [! Y] [ LASR R | FLTR ] -> [! Y] [ LASR R | FLTR RAY0 R ]
LATE UP    [! Y] [ LASR U | NO SOLID ] -> [! Y] [ LASR U | RAY0 U ]
LATE DOWN  [! Y] [ LASR D | NO SOLID ] -> [! Y] [ LASR D | RAY0 D ]
LATE LEFT  [! Y] [ LASR L | NO SOLID ] -> [! Y] [ LASR L | RAY0 L ]
LATE RIGHT [! Y] [ LASR R | NO SOLID ] -> [! Y] [ LASR R | RAY0 R ]
[! Y] [ LASR DIR ] -> [! Y] [ LASR ]

UP    [ RAY0 U | FLTR ] -> [ RAY0 | FLTR RAY0 U ]
DOWN  [ RAY0 D | FLTR ] -> [ RAY0 | FLTR RAY0 D ]
LEFT  [ RAY0 L | FLTR ] -> [ RAY0 | FLTR RAY0 L ]
RIGHT [ RAY0 R | FLTR ] -> [ RAY0 | FLTR RAY0 R ]
LATE UP    [ RAY0 U | FLTR ] -> [ RAY0 | FLTR RAY0 U ]
LATE DOWN  [ RAY0 D | FLTR ] -> [ RAY0 | FLTR RAY0 D ]
LATE LEFT  [ RAY0 L | FLTR ] -> [ RAY0 | FLTR RAY0 L ]
LATE RIGHT [ RAY0 R | FLTR ] -> [ RAY0 | FLTR RAY0 R ]
UP    [ RAY0 U | NO SOLID ] -> [ RAY0 | RAY0 U ]
DOWN  [ RAY0 D | NO SOLID ] -> [ RAY0 | RAY0 D ]
LEFT  [ RAY0 L | NO SOLID ] -> [ RAY0 | RAY0 L ]
RIGHT [ RAY0 R | NO SOLID ] -> [ RAY0 | RAY0 R ]
LATE UP    [ RAY0 U | NO SOLID ] -> [ RAY0 | RAY0 U ]
LATE DOWN  [ RAY0 D | NO SOLID ] -> [ RAY0 | RAY0 D ]
LATE LEFT  [ RAY0 L | NO SOLID ] -> [ RAY0 | RAY0 L ]
LATE RIGHT [ RAY0 R | NO SOLID ] -> [ RAY0 | RAY0 R ]

UP    [! Y] [ SPRK | LASR NO DIR | FLTR ] -> [! Y] [ SPRK | LASR U | FLTR ]
DOWN  [! Y] [ SPRK | LASR NO DIR | FLTR ] -> [! Y] [ SPRK | LASR D | FLTR ]
LEFT  [! Y] [ SPRK | LASR NO DIR | FLTR ] -> [! Y] [ SPRK | LASR L | FLTR ]
RIGHT [! Y] [ SPRK | LASR NO DIR | FLTR ] -> [! Y] [ SPRK | LASR R | FLTR ]
UP    [! Y] [ SPRK | LASR NO DIR | NO RAY ] -> [! Y] [ SPRK | LASR U |]
DOWN  [! Y] [ SPRK | LASR NO DIR | NO RAY ] -> [! Y] [ SPRK | LASR D |]
LEFT  [! Y] [ SPRK | LASR NO DIR | NO RAY ] -> [! Y] [ SPRK | LASR L |]
RIGHT [! Y] [ SPRK | LASR NO DIR | NO RAY ] -> [! Y] [ SPRK | LASR R |]

( SPRK logic )
LATE [ PROP NO COND ] -> []

LATE [! Y] [ WIRE  X ] -> [! Y] [ WIRE  SPRK ]
LATE [! Y] [ WATER Q ] -> [! Y] [ WATER SPRK ]

[! Y] [ SPRK | WIRE  NO PROP ] -> [! Y] [ SPRK | WIRE  X ]
[! Y] [ SPRK | WATER NO PROP ] -> [! Y] [ SPRK | WATER Q ]
[! Y] [ SPRK | IWRE  NO PROP ] -> [! Y] [ SPRK | IWRE  X SPRK ]

UP    [ RAY0 U | PSWR ] -> [ RAY0 U | PSWR X ]
DOWN  [ RAY0 D | PSWR ] -> [ RAY0 D | PSWR X ]
LEFT  [ RAY0 L | PSWR ] -> [ RAY0 L | PSWR X ]
RIGHT [ RAY0 R | PSWR ] -> [ RAY0 R | PSWR X ]

[! Y] [ COND   DCAY ] -> [! Y] [ COND ]
[! Y] [ COND   SPRK ] -> [! Y] [ COND DCAY ]
[! Y] [ COND X SPRK ] -> [! Y] [ COND SPRK ]

( Cursor )
[ - X ] [ > @ ] [ + ICON ] -> [ - X ] [ @ ] [ > + > ICON ]
[ > @ | ; ] [ - NO X ] [+] [=] -> [ @ | ; + I_DEL ] [ - X ] [=] []

[ > + ICON | _ NO @ ] [ - X ] [=] [@] -> [ = | @ _ ] [-] [+] []
[ > + ICON | _ @ ]    [ - X ] [=]     -> [ = | @ _ ] [-] [+]

[ - NO X ] [ ACTION @ 0 ] [+] [=] -> [ - X ] [ @ 0 ] [=] [ + I_SPRK ]
[ - NO X ] [ ACTION @ 1 ] [+] [=] -> [ - X ] [ @ 1 ] [=] [ + I_CPWR ]
[ - NO X ] [ ACTION @ 2 ] [+] [=] -> [ - X ] [ @ 2 ] [=] [ + I_PSWR ]
[ - NO X ] [ ACTION @ 3 ] [+] [=] -> [ - X ] [ @ 3 ] [=] [ + I_NGWR ]
[ - NO X ] [ ACTION @ 4 ] [+] [=] -> [ - X ] [ @ 4 ] [=] [ + I_LASR ]
[ - NO X ] [ ACTION @ 5 ] [+] [=] -> [ - X ] [ @ 5 ] [=] [ + I_SAND ]
[ - NO X ] [ ACTION @ 6 ] [+] [=] -> [ - X ] [ @ 6 ] [=] [ + I_FLTR ]
[ - NO X ] [ ACTION @ 7 ] [+] [=] -> [ - X ] [ @ 7 ] [=] [ + I_WATR ]
[ - NO X ] [ ACTION @ 8 ] [+] [=] -> [ - X ] [ @ 8 ] [=] [ + I_SLCN ]
[ - NO X ] [ ACTION @ 9 ] [+] [=] -> [ - X ] [ @ 9 ] [=] [ + I_IWRE ]
[ - NO X ] [ ACTION @ A ] [+] [=] -> [ - X ] [ @ A ] [=] [ + I_OXYG ]

[ ACTION @ ] [ + I_DEL WCLL ]          -> [ @ ] [ + I_DEL ]
[ ACTION @ ] [ + I_SPRK COND NO PROP ] -> [ @ ] [ + I_SPRK COND SPRK ]
[ ACTION @ ] [ + I_CPWR ]              -> [ @ ] [ + I_CPWR CPWR ]
[ ACTION @ ] [ + I_PSWR ]              -> [ @ ] [ + I_PSWR PSWR ]
[ ACTION @ ] [ + I_NGWR ]              -> [ @ ] [ + I_NGWR NGWR ]
[ ACTION @ ] [ + I_LASR ]              -> [ @ ] [ + I_LASR LASR ]
[ ACTION @ ] [ + I_SAND ]              -> [ @ ] [ + I_SAND SAND ]
[ ACTION @ ] [ + I_FLTR ]              -> [ @ ] [ + I_FLTR FLTR ]
[ ACTION @ ] [ + I_WATR ]              -> [ @ ] [ + I_WATR WATR ]
[ ACTION @ ] [ + I_SLCN ]              -> [ @ ] [ + I_SLCN SLCN ]
[ ACTION @ ] [ + I_IWRE ]              -> [ @ ] [ + I_IWRE IWRE ]
[ ACTION @ ] [ + I_OXYG ]              -> [ @ ] [ + I_OXYG OXYG ]

==============
WINCONDITIONS
==============

=======
LEVELS
':_;=.....................
___;......................
,12;......................
345;......................
678;......................
9A_;......................
___;......................
___;......................
___;......................
___;......................
___;......................
___;......................
___;......................
___;......................
-"_;......................
