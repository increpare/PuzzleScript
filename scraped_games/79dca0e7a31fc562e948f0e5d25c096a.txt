title Metal and Grass
author bregehr
homepage www.puzzlescript.net
realtime_interval 0.25
run_rules_on_level_start
youtube Gk3tXQzCeJA

========
OBJECTS
========

Background
#1d8213

grassfull
#259e1a

grassedge
#259e1a #18770e
00000
00000
00000
00000
11111

shadowgrass
#16680d
....0
....0
....0
....0
....0

shadowflower
#137708
....0
....0
....0
....0
....0

Wall
darkgrey

walledge
#4c4b4b
00000
00000
.....
.....
.....

shadowwall
#16680d
...00
...00
...00
...00
...00

doublewallshadow
#16680d #373837 #4c4b4b
22111
22111
...00
...00
...00

shadowwallcorner
#16680d
...00
...00
.....
.....
.....

Player
transparent

mowerup
white
.....
..0..
.000.
.000.
.....

mowerdown
white
.....
.000.
.000.
..0..
.....

mowerleft
white
.....
..00.
.000.
..00.
.....

mowerright
white
.....
.00..
.000.
.00..
.....

mowerout
gray
.....
.000.
.000.
.000.
.....

fuel
#ff2828 #ad0f0f #f7dd18 #e5cc12 #ff4949
...32
102..
1004.
1004.
1114.

forflowers
white yellow
.....
.010.
.101.
.010.
.....

flowers
#ffff00 #e9e9e9 #106a0b #c3c3c3 
21210
10131
31303
22123
21012

flowers2
#ffff00 #e9e9e9 #106a0b #c3c3c3 
22303
13031
01210
23101
30321

flowers3
#ffff00 #e9e9e9 #106a0b #c3c3c3 
32302
10132
31030
23203
10330

flowers4
#ffff00 #e9e9e9 #106a0b #c3c3c3 
03020
30212
12310
01031
21203

flowershadow
#16680d
00000
.....
.....
.....
.....

levelstart
transparent

fuelnext
transparent

refuel
transparent

refillhere
transparent

notrandomized
transparent

fuelbarcapfront
white
.....
0....
0....
0....
.....

fuelbar1
white red
00000
11111
11111
11111
00000

fuelbar2
white red
00000
1111.
1111.
1111.
00000

fuelbar3
white red
00000
111..
111..
111..
00000

fuelbar4
white red
00000
11...
11...
11...
00000

fuelbar5
white red
00000
1....
1....
1....
00000

fuelbarempty
white red
00000
.....
.....
.....
00000

fuelbarcapback
white
.....
....0
....0
....0
.....




=======
LEGEND
=======

. = Background
# = Wall
R = Player and mowerright and levelstart
D = Player and mowerdown and levelstart
U = Player and mowerup and levelstart
L = Player and mowerleft and levelstart
F = forflowers
` = grassfull
1 = fuelbar1 and wall
2 = fuelbar2 and wall
3 = fuelbar3 and wall
4 = fuelbar4 and wall
5 = fuelbar5 and wall
{ = fuelbarcapback and wall
} = fuelbarcapfront and wall
mower = mowerleft or mowerright or mowerup or mowerdown
markers = levelstart or fuelnext or refuel or notrandomized
fuelbarstuff = fuelbar1 or fuelbar2 or fuelbar3 or fuelbar4 or fuelbar5 or fuelbarcapfront or fuelbarcapback or fuelbarempty
fuelbarmain = fuelbar1 or fuelbar2 or fuelbar3 or fuelbar4 or fuelbar5 or fuelbarempty
grass = grassfull or grassedge
allflowers = flowers or flowers2 or flowers3 or flowers4


=======
SOUNDS
=======

mower move 24896102
grass destroy 38733908
allflowers destroy 38622302
fuel destroy 92088702


================
COLLISIONLAYERS
================

Background
flowershadow
walledge, shadowgrass, shadowwall, shadowwallcorner, doublewallshadow, shadowflower
Player
mower, grass, wall, mowerout, allflowers, forflowers
fuel
fuelbarstuff
markers
refillhere

======
RULES
======

(preparing level)
[levelstart][fuelbar1] -> [levelstart][fuelbar1 refillhere]
[levelstart][forflowers] -> [levelstart][forflowers notrandomized]
startloop
[levelstart][forflowers notrandomized] -> [levelstart][random allflowers]
endloop
random [levelstart][no fuel][no fuel no wall no mower no allflowers] -> [levelstart][][fuel]
[levelstart] -> []

(fuel bar)
right [stationary player][fuelbar5] -> [player][fuelbarempty]
right [stationary player][fuelbar4] -> [player][fuelbar5]
right [stationary player][fuelbar3] -> [player][fuelbar4]
right [stationary player][fuelbar2] -> [player][fuelbar3]
right [stationary player][fuelbar1 fuelnext] -> [player][fuelbar2]
right [stationary player][fuelbar1 | no fuelbar1 no fuelbar2 no fuelbar3 no fuelbar4 no fuelbar5] -> [player][fuelbar1 fuelnext| ]
[fuelbarcapback | fuelbarempty][mower] -> [fuelbarcapback | fuelbarempty][mowerout]

(moving mower)
[stationary player][stationary mowerup   ] -> [player][up    mowerup   ]
[stationary player][stationary mowerleft ] -> [player][left  mowerleft ]
[stationary player][stationary mowerright] -> [player][right mowerright]
[stationary player][stationary mowerdown ] -> [player][down  mowerdown ]
random [> mower | fuel][no fuel no wall no mower no allflowers][fuelbarcapfront] -> [> mower | ][fuel][fuelbarcapfront refuel]
[refuel][fuelbarmain] -> [refuel][]
[refuel][fuelnext] -> [refuel][]
[refuel][refillhere] -> [refuel][refillhere fuelbar1]
[refuel] -> []

(changing mower direction)
[up    player][mower] -> [player][mowerup   ]
[left  player][mower] -> [player][mowerleft ]
[right player][mower] -> [player][mowerright]
[down  player][mower] -> [player][mowerdown ]

(mowing grass)
[> mower | grass] -> [> mower | ]

(flowers)
[> mower | allflowers] -> [ | mowerout]

(making things pretty)
right [no allflowers no wall no grass | allflowers] -> [shadowflower | allflowers]
right [shadowflower | no allflowers] -> [|] 
down [grassfull | no grass no wall no allflowers] -> [grassedge | ]
left [grass | no grass no wall] -> [grass | shadowgrass]
right [shadowgrass | no grass] -> [|]
down [wall | no grass no wall] -> [wall | walledge]
down [allflowers | no grass no wall no allflowers] -> [allflowers | flowershadow]
up [flowershadow | no allflowers] -> [ | ]
left [wall | no grass no wall no walledge] -> [wall | shadowwall]
right [walledge | wall] -> [doublewallshadow | wall]
left [walledge no allflowers| no grass no wall no walledge] -> [walledge | shadowwallcorner]

(spawning fuel)








==============
WINCONDITIONS
==============

no grass

=======
LEVELS
=======


##############
#````````````#
#````````````#
#````````````#
#````````````#
#```R````````#
#````````````#
#````````````#
#````````````#
#````````````#
####{1111}####

#########
#```````#
#```d```#
#```````#
#```````#
#```````#
#``###``#
#``###``#
#``###``#
#```````#
#```````#
#```````#
#```````#
#```````#
#{11111}#

###################
#ff````f###f````ff#
#f```d`fffff`````f#
#f```````````````f#
#f``````fff``````f#
#f`````f###f`````f#
#f`````f###f`````f#
#f`````f###f`````f#
#f`````f###f`````f#
#f``````fff``````f#
#f```````````````f#
#f`````fffff`````f#
#ff````f###f````ff#
######{11111}######

###############
#`````````````#
#`f```fff```f`#
#``r``````````#
#fff`f`f`f`fff#
#`````````````#
#`f```fff```f`#
#`````````````#
#####{111}#####

############
#ffffffffff#
#f``f`````f#
#fr````f``f#
#f``f`````f#
#ffffffffff#
####{11}####

###############
#fffffffffffff#
#ffr````````ff#
#ff``f`f`f``ff#
#fff```````fff#
#ff``f`f`f``ff#
#fff```````fff#
#ff``f`f`f``ff#
#ff`````````ff#
#fffffffffffff#
#####{1111}####

################
###ff##ff##ff###
###``##`d##``###
#f````````````f#
#f````````````f#
#f`##``##``##`f#
#f`##``##``##`f#
#f````````````f#
#f````````````f#
###``##``##``###
###ff##ff##ff###
#####{1111}#####

#############
#```f```f```#
#`d`f```f```#
#```f```f```#
#```f```f```#
#```````````#
#```````````#
#```f```f```#
#```f```f```#
#```f```f```#
#```f```f```#
####{111}####
