title MicroChip
author Guilherme Tows
homepage zarat.us

========
OBJECTS
========

Player @
White Green #888800
.222.
.000.
11111
.111.
.1.1.

Background .
White LightGray
00001
01111
01111
01111
11111

Wall #
#888888 #666666 #444444
00001
01112
01112
01112
12222

Chip *
DarkGray Black
.....
.101.
.101.
.101.
.....

Lock ?
DarkGray Black
00000
01110
01.10
01110
00000

Exit !
Blue LightBlue
00000
01110
01010
01110
00000

Number0 0
Black White
01100
10010
10010
01100
00000

Number1 1
Black White
00100
01100
00100
00100
00000

Number2 2
Black White
11100
00110
01000
11110
00000

Number3 3
Black White
11110
00100
10010
01100
00000

Number4 4
Black White
01010
10010
11110
00010
00000

Number5 5
Black White
11110
01100
00010
11100
00000

Number6 6
Black White
01110
11100
10010
01100
00000

Number7 7
Black White
11110
00010
00100
01000
00000

Number8 8
Black White
01100
11110
10010
01100
00000

Number9 9
Black White
01100
10010
01110
11100
00000

NumberNone
Black White
00000
00000
11110
00000
00000

Status _
Black

RedDoor r
LightRed Red DarkRed
00001
01112
01112
01112
12222

GreenDoor g
LightGreen Green DarkGreen
00001
01112
01112
01112
12222

YellowDoor y
White Yellow Orange
00001
01112
01112
01112
12222

BlueDoor b
LightBlue Blue DarkBlue
00001
01112
01112
01112
12222

RedKey d
LightRed Red DarkRed
.11..
1.1..
111..
...1.
...11

GreenKey n
LightGreen Green DarkGreen
.11..
1.1..
111..
...1.
...11

YellowKey w
White Yellow Orange
.11..
1.1..
111..
...1.
...11

BlueKey e
LightBlue Blue DarkBlue
.11..
1.1..
111..
...1.
...11

Water ~
Blue DarkBlue
00000
01000
10100
00011
00000

Crate [
#88ff00 #66cc00 #44aa00
00001
01112
01112
01112
12222

Bee
Yellow Black White
.222.
.000.
21112
.000.
..1..

Mark
Black

Dirt
Brown

Dead
Black White
10001
01010
00100
10001
10101

ForceN "
Green LightGreen
00100
01110
11011
10001
00000

ForceE '
Green LightGreen
01100
00110
00011
00110
01100

ForceS ˜
Green LightGreen
00000
10001
11011
01110
00100

ForceW `
Green LightGreen
00110
01100
11000
01100
00110

AquaShoe a
Blue DarkBlue
..000
...11
00000
.0000
00...

FireShoe f
Red DarkRed
..000
...11
.0000
00000
11111

IceShoe i
LightBlue Blue DarkGray
..000
...11
.0000
00000
22222

PushShoe p
LightGreen Green
..000
...11
.0000
00000
11.11

Fire %
Yellow Red
.0...
.000.
00100
01110
.000.

IceX /
LightBlue White
01000
10000
00000
00001
00010

IceSW ▙
LightBlue White DarkGray
01000
20000
20000
22001
22220

IceNW ▛
LightBlue White DarkGray
22220
22000
20000
20001
00010

IceNE ▜
LightBlue White DarkGray
02222
10022
00002
00002
00010

IceSE ▟
LightBlue White DarkGray
01000
10002
00002
00022
02222

DirN
Black
.....
.....
.....
.....
.....

DirE
Black
.....
.....
.....
.....
.....

DirS
Black
.....
.....
.....
.....
.....


DirW
Black
.....
.....
.....
.....
.....

Forced
Black
.....
.....
.....
.....
.....

Invinco
Black
.....
.....
.....
.....
.....

=======
LEGEND
=======

Number = Number0 or Number1 or Number2 or Number3 or Number4 or Number5 or Number6 or Number7 or Number8 or Number9 or NumberNone
Door = RedDoor or BlueDoor or YellowDoor or GreenDoor
Item = RedKey or BlueKey or YellowKey or GreenKey or AquaShoe or FireShoe or IceShoe or PushShoe
Obstacle = Door or Wall or Lock or Crate
Enemy = Bee
Force = ForceN or ForceE or ForceS or ForceW
Ice = IceX or IceNE or IceNW or IceSE or IceSW
â = Bee and DirN
á = Bee and DirE
à = Bee and DirW
ã = Bee and DirS
Dir = DirN or DirE or DirS or DirW
DirNPObject = Bee or Crate
DirObject = DirNPObject or Player
ObjectObstacle = Obstacle or Dirt or Crate

=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background
Status
Exit, Chip, Item, Water, Force, Fire, Ice
Wall, Lock, Number, Door, Bee, Crate, Dirt
Player
Dir, Forced, Invinco
Mark, Dead

======
RULES
======

(Dir is only used on player when next move is involuntary)

[ Dir Player Ice ] [ IceShoe Status ] -> [ Player Ice ] [ IceShoe Status ]
[ Dir Player no Ice ] -> [ Player ]
[ Dir Crate no Ice ] -> [ Crate ]

right [ DirObject DirN ] -> [ ^ DirObject ]
right [ DirObject DirE ] -> [ > DirObject ]
right [ DirObject DirS ] -> [ v DirObject ]
right [ DirObject DirW ] -> [ < DirObject ]

(autonomous player movements)

[ > Player | RedDoor ] [ Status RedKey ] -> [ | Player ] [ Status ]
[ > Player | BlueDoor ] [ Status BlueKey ] -> [ | Player ] [ Status ]
[ > Player | YellowDoor ] [ Status YellowKey ] -> [ | Player ] [ Status ]
[ > Player | GreenDoor ] [ Status GreenKey ] -> [ | Player ] [ Status GreenKey ]

(ice and force floors)

right [ ^ DirObject IceNE ] -> [ > DirObject IceNE ]
right [ < DirObject IceNE ] -> [ v DirObject IceNE ]
right [ ^ DirObject IceNW ] -> [ > DirObject IceNW ]
right [ < DirObject IceNW ] -> [ v DirObject IceNW ]
right [ V DirObject IceSE ] -> [ > DirObject IceSE ]
right [ < DirObject IceSE ] -> [ ^ DirObject IceSE ]
right [ V DirObject IceSW ] -> [ > DirObject IceSW ]
right [ < DirObject IceSW ] -> [ ^ DirObject IceSW ]
[ > DirObject Ice no Mark | Obstacle ] -> [ < DirObject Ice Mark | Obstacle ]

right [ DirObject ForceN no Forced ] -> [ ^ DirObject ForceN Forced ]
right [ DirObject ForceE no Forced ] -> [ > DirObject ForceE Forced ]
right [ DirObject ForceS no Forced ] -> [ v DirObject ForceS Forced ]
right [ DirObject ForceW no Forced ] -> [ < DirObject ForceW Forced ]

(player movements)

[ > Player no Mark | Crate no Mark | no ObjectObstacle ] -> [ | > Player Mark | > Crate Mark ]
[ > Player no Mark | no Obstacle ] -> [ | > Player Mark ]
[ Player Mark Ice ] -> [ Player Ice ] again
[ Player Mark Force ] -> [ Player Force ] again
[ > Player Force Forced | no Obstacle ] -> [ Force | > Player ] again

(player dead test 1)

[ Player Enemy ] -> [ Dead ]

(other object movements)

[ > Bee | ObjectObstacle ] -> [ v Bee | ObjectObstacle ]
[ no ObjectObstacle | ^ Bee no Mark ] -> [ no ObjectObstacle | < Bee Mark ]
[ Bee Mark ] -> [ Bee No Mark ]

[ > DirNPObject no Mark | no ObjectObstacle ] -> [ | > DirNPObject Mark ]
[ > DirNPObject Force Forced | no ObjectObstacle ] -> [ Force | > DirNPObject ]

(object on top of object interactions)

[ Crate Water ] -> [ stationary Dirt ]
[ Player Dirt ] -> [ Player ]
[ Player Enemy ] -> [ Dead ]
[ Player Water no Invinco ] [ AquaShoe Status ] -> [ Invinco Player Water ] [ AquaShoe Status ]
[ Player Fire no Invinco ] [ FireShoe Status ] -> [ Invinco Player Fire ] [ FireShoe Status ]
[ no Invinco Player Water ] -> [ Dead ]
[ no Invinco Player Fire ] -> [ Dead ]

(chips)

right [ Player Chip ] [ stationary Number  | no Number  ] -> [ Player ] [ action Number  | ]
[ Player Item ] [ Status no Item ] -> [ Player ] [ Status Item ]

right [ Number | action Number0 ] -> [ action Number | Number9 ]

[ action Number1 ] -> [ Number0 ]
[ action Number2 ] -> [ Number1 ]
[ action Number3 ] -> [ Number2 ]
[ action Number4 ] -> [ Number3 ]
[ action Number5 ] -> [ Number4 ]
[ action Number6 ] -> [ Number5 ]
[ action Number7 ] -> [ Number6 ]
[ action Number8 ] -> [ Number7 ]
[ action Number9 ] -> [ Number8 ]

right [ Number0 | Number0 | Number0 ] -> [ NumberNone | NumberNone | NumberNone ]
[NumberNone] [ Lock ] -> [ NumberNone] []

(final cleanups)

right [ ^ DirObject ] -> [ DirN DirObject ]
right [ > DirObject ] -> [ DirE DirObject ]
right [ v DirObject ] -> [ DirS DirObject ]
right [ < DirObject ] -> [ DirW DirObject ]

[ Invinco ] -> [ ]
[ Mark ] -> [ ]
[ Forced ] -> [ ]

==============
WINCONDITIONS
==============
all Player on Exit
no Dead

=======
LEVELS
=======

*011_____________
.................
...#####.#####...
...#...###...#...
...#.*.#!#.*.#...
.#####G#?#G#####.
.#.w.B.....R.w.#.
.#.*.#e...d#.*.#.
.#####*.@.*#####.
.#.*.#e...d#.*.#.
.#...R..*..B...#.
.######Y#Y######.
.....#..#..#.....
.....#.*#*.#.....
.....#..#n.#.....
.....#######.....
.................

*004________________
....................
...#######..........
...#..*..#..........
...#.....#..........
...#..#..##########.
.###..#â..~~.....*#.
.#!?..##â.~~.[[@..#.
.###..#â..~~.....*#.
...#..#..##########.
...#.....#..........
...#..*..#..........
...#######..........
....................

*004______________
..................
........###.......
........#!#.......
....#####?####....
....#˜```````#....
....#˜######"#....
.####˜#~~~~#"####.
.#˜```#~i*~#"```#.
.#˜####~~~~####"#.
.#˜#▛//....%%%#"#.
.#˜#/f#....%p%#"#.
.#˜#/*#.@..%*%#"#.
.#˜#▙//..a.%%%#"#.
.#˜####....####"#.
.#'''''"""'''''"#.
.######"*""######.
......#""""#......
......######......
..................
