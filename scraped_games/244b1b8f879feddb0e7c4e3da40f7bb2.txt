title AutoDom
author Marcos Donnantuoni
homepage https://marcosd.itch.io

again_interval 0.25
key_repeat_interval 0.17
text_color grey

========
OBJECTS
========

background
gray #888
00000
00000
00100
00000
00000

wall
brown

player
blue
00.00
0...0
.....
0...0
00.00

domL
green darkgreen
00000
01111
01111
01111
00000

domR
green darkgreen
00000
11110
11110
11110
00000

domU
green darkgreen
00000
01110
01110
01110
01110

domD
green darkgreen
01110
01110
01110
01110
00000

adomL
yellow brown
00000
01111
01111
01111
00000

adomR
yellow brown
00000
11110
11110
11110
00000

adomU
yellow brown
00000
01110
01110
01110
01110

adomD
yellow brown
01110
01110
01110
01110
00000

part1
darkgreen
.....
.000.
.000.
.000.
.....

part2
transparent

dos
transparent

=======
LEGEND
=======

. = Background
# = wall
@ = Player
u = domU
d = domD
l = domL
r = domR
dom = domL or domR or domU or domD or adomL or adomR or adomU or adomD
lim = dom or wall

=======
SOUNDS
=======

domL create 4873700
domU create 4873700
adomL create 95340100
adomU create 95340100

================
COLLISIONLAYERS
================

background
wall
dom
part1, part2
player
dos

======
RULES
======

[ action player lim ] -> cancel
[ action player no part1 | part1 ] -> [ player part2 | part2 no part1 ]
[ action player | no part1 ] [ part1 ] -> [ player part1 | no part1 ] [ no part1 ]
[ action player no part1 ] -> [ player part1 ]
[ action player part1 ] -> [ player no part1 ]

right [ part2 | part2 ] -> [ no part2 domL | no part2 domR ]
down  [ part2 | part2 ] -> [ no part2 domU | no part2 domD ]

late [ dos ] -> []
late [ lim | no lim | lim ] -> [ lim | dos | lim ]
late right [ lim | dos | no lim ] -> [ lim | no dos adomL | no dos adomR ] again
late right [ no lim | dos | lim ] -> [ no dos adomL | no dos adomR | lim ] again
late down  [ lim | dos | no lim ] -> [ lim | no dos adomU | no dos adomD ] again
late down  [ no lim | dos | lim ] -> [ no dos adomU | no dos adomD | lim ] again



==============
WINCONDITIONS
==============


=======
LEVELS
=======

message you must fill the board
message use X to draw green dominoes
message yellow dominoes will appear in obvious places
message use as few green dominoes as possible

##########
#........#
#........#
#........#
#...@....#
#........#
#........#
#........#
#........#
##########




