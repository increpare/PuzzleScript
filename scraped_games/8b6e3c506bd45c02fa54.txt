title Slide Rule
author Jim Palmeri
homepage www.twitter.com/JimPalmeri
again_interval .07
run_rules_on_level_start

========
OBJECTS
========

Background 
#ebebeb #e0e0e0 #d4d4d4 #c8c8c8 #c0c0c0
43334
32123
31013
32123
43334

Wall 
#383838 #303030 #282828 #202020 #181818
43334
32123
31013
32123
43334

Corner
Green
.....
.....
.....
.....
.....

PlayerHeavy
Black Orange Green Blue
.000.
.111.
22222
.333.
.3.3.

PlayerLight
Black Orange White
.000.
.111.
22222
.222.
.222.

PLeft
Red
.....
.....
.....
.....
.....

PRight
Green
.....
.....
.....
.....
.....

PUp
Red
.....
.....
.....
.....
.....

PDown
Red
.....
.....
.....
.....
.....

FixedL
Blue
.....
.....
.....
.....
.....

FixedR
Blue
.....
.....
.....
.....
.....

FixedU
Blue
.....
.....
.....
.....
.....

FixedD
Blue
.....
.....
.....
.....
.....

FinishX
Red
.....
.0.0.
..0..
.0.0.
.....

FinishL
Red
..0..
.0...
00000
.0...
..0..

FinishR
Red
..0..
...0.
00000
...0.
..0..

FinishU
Red
..0..
.000.
0.0.0
..0..
..0..

FinishD
Red
..0..
..0..
0.0.0
.000.
..0..

Source
#888888
.....
.000.
.000.
.000.
.....

RopeL
#888888
.....
.....
000..
.....
.....
RopeR
#888888
.....
.....
..000
.....
.....

RopeU
#888888
..0..
..0..
..0..
.....
.....

RopeD
#888888
.....
.....
..0..
..0..
..0..

C01
#303030 #505050 #e0e0e0
00000
00000
00100
00000
00000

C11
#303030 #505050 #e0e0e0
00000
00000
00200
00000
00000

C02
#303030 #505050 #e0e0e0
00000
00010
00000
01000
00000

C12
#303030 #505050 #e0e0e0
00000
00020
00000
01000
00000

C22
#303030 #505050 #e0e0e0
00000
00020
00000
02000
00000

C03
#303030 #505050 #e0e0e0
00000
00100
00000
01010
00000

C13
#303030 #505050 #e0e0e0
00000
00200
00000
01010
00000

C23
#303030 #505050 #e0e0e0
00000
00200
00000
02010
00000

C33
#303030 #505050 #e0e0e0
00000
00200
00000
02020
00000

C04
#303030 #505050 #e0e0e0
00000
01010
00000
01010
00000

C14
#303030 #505050 #e0e0e0
00000
02010
00000
01010
00000

C24
#303030 #505050 #e0e0e0
00000
02020
00000
01010
00000

C34
#303030 #505050 #e0e0e0
00000
02020
00000
02010
00000

C44
#303030 #505050 #e0e0e0
00000
02020
00000
02020
00000

C05
#303030 #505050 #e0e0e0
00000
01010
00100
01010
00000

C15
#303030 #505050 #e0e0e0
00000
02010
00100
01010
00000

C25
#303030 #505050 #e0e0e0
00000
02020
00100
01010
00000

C35
#303030 #505050 #e0e0e0
00000
02020
00100
02010
00000

C45
#303030 #505050 #e0e0e0
00000
02020
00100
02020
00000

C55
#303030 #505050 #e0e0e0
00000
02020
00200
02020
00000

DecMarker
Yellow
.....
.....
.....
.....
.....

IncMarker
Yellow
.....
.....
.....
.....
.....

Pillow
White
.....
00000
.000.
.000.
.....

PillowStand
Brown
..0..
00000
..0..
..0..
.000.

PillowMarker
Purple
.....
.....
.....
.....
.....

StandMarker
Purple
.....
.....
.....
.....
.....

Pullback
#888888 #484848
.1.1.
11011
.000.
11011
.1.1.

PullbackUseMarker
Orange
.....
.....
.....
.....
.....

PullbackMarker
Yellow
.....
.....
.....
.....
.....

Header
#181818 #202020 #282828
00000
11111
22222
11111
00000

N0
White
.00..
0..0.
0..0.
0..0.
.00..

N1
White
.00..
..0..
..0..
..0..
..0..

N2
White
.00..
0..0.
..0..
.0...
0000.

N3
White
.00..
0..0.
..0..
...0.
.00..

N4
White
0..0.
0..0.
0000.
...0.
...0.

N5
White
0000.
0....
000..
...0.
000..

N6
White
.00..
0....
000..
0..0.
.00..

N7
White
0000.
0..0.
...0.
..0..
..0..

N8
White
.00..
0..0.
.00..
0..0.
.00..

N9
White
.00..
0..0.
0..0.
.000.
...0.

LetterO
White
.....
.00..
0..0.
0..0.
.00..

LetterF
White
..00..
.0...
0000..
.0...
.0...

=======
LEGEND
=======

. = Background
# = Wall
P = PlayerHeavy
F = FinishX
S = PillowStand
W = Pillow and PillowStand
C = Corner and Wall
1 = C11 and Source
2 = C22 and Source
3 = C33 and Source
4 = C44 and Source
5 = C55 and Source
0 = Header and N0
6 = Header and N6
7 = Header and N7
8 = Header and N8
9 = Header and N9
B = Pullback
H = Header
O = Header and LetterO
E = Header and LetterF

PMarker = PLeft or PRight or PUp or PDown
FixedMarker = FixedL or FixedR or FixedU or FixedD
Finish = FinishX or FinishL or FinishR or FinishU or FinishD
Crate = C01 or C11 or C02 or C12 or C22 or C03 or C13 or C23 or C33 or C04 or C14 or C24 or C34 or C44 or C05 or C15 or C25 or C35 or C45 or C55
CEmpty = C01 or C02 or C03 or C04 or C05
Solid = Wall or Crate or Header
Rope = RopeL or RopeR or RopeU or RopeD
CrateBlock = Solid or Rope or Finish or PillowStand or Pullback
Player = PlayerLight or PlayerHeavy

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background

Finish
Source
RopeL
RopeR
RopeU
RopeD
Pullback
PullbackUseMarker
PullbackMarker
StandMarker
PillowMarker
PillowStand
Pillow
Player, Wall, Crate
Header
N0, N1, N2, N3, N4, N5, N6, N7, N8, N9, LetterO, LetterF
PMarker, Corner
DecMarker, IncMarker
FixedL
FixedR
FixedU
FixedD

======
RULES     
======

(Make level numbers look nice)
left [ Header | no Header ] -> [ Header | Header ]
[ C11 Source Header ] -> [ N1 Header ]
[ C22 Source Header ] -> [ N2 Header ]
[ C33 Source Header ] -> [ N3 Header ]
[ C44 Source Header ] -> [ N4 Header ]
[ C55 Source Header ] -> [ N5 Header ]
[ Header ] -> [ Header Wall ]

(Make finish look nice)
down [ Corner | ... | FinishX ] -> [ Corner | ... | FinishL ]
up [ Corner | ... | FinishX ] -> [ Corner | ... | FinishR ]
left [ Corner | ... | FinishX ] -> [ Corner | ... | FinishD ]
right [ Corner | ... | FinishX ] -> [ Corner | ... | FinishU ]

(Player weight and pillows)
[ PillowStand StandMarker no Player ] -> [ PillowStand ]
[ Player ] -> [ Player PillowMarker ]
[ Player PillowMarker PillowStand StandMarker ] -> [ Player PillowStand StandMarker ]
[ PlayerHeavy PillowMarker Pillow ] -> [ PlayerLight StandMarker ]
[ PlayerLight PillowMarker PillowStand ] -> [ PlayerHeavy PillowStand Pillow ]
[ PillowStand Player no StandMarker ] -> [ PillowStand Player StandMarker ]
[ PillowMarker ] -> [ ]

(Compute which walls are fixed)
(Clear markers)
[ FixedMarker ] -> [ ]
(Fix walls)
[ Wall ] -> [ Wall FixedL FixedR FixedU FixedD ]
(Fix crates that are (a) empty and (b) can't move in a particular direction)
[ CEmpty no RopeL ] -> [ CEmpty FixedL ]
[ CEmpty no RopeR ] -> [ CEmpty FixedR ]
[ CEmpty no RopeU ] -> [ CEmpty FixedU ]
[ CEmpty no RopeD ] -> [ CEmpty FixedD ]
(Fix crates that can't move due to being blocked by something)
left [ Crate | CrateBlock ] -> [ Crate FixedL | CrateBlock ]
right [ Crate | CrateBlock ] -> [ Crate FixedR | CrateBlock ]
up [ Crate | CrateBlock ] -> [ Crate FixedU | CrateBlock ]
down [ Crate | CrateBlock ] -> [ Crate FixedD | CrateBlock ]

(Player movement)
(If nothing is in the player's way, mark player for sliding)
left [ > Player no PMarker | no Solid ] -> [ Player PLeft | ]
right [ > Player no PMarker | no Solid ] -> [ Player PRight | ]
up [ > Player no PMarker | no Solid ] -> [ Player PUp | ]
down [ > Player no PMarker | no Solid ] -> [ Player PDown | ]

(If the player is on a pullback space, mark all crates for pullback)
[ Pullback PullbackUseMarker no Player ] -> [ Pullback ]
[ Player Pullback no PullbackUseMarker ] [ Crate ] -> [ Player Pullback ] [ Crate PullbackMarker ]
[ Player Pullback no PullbackUseMarker ] -> [ Player Pullback PullbackUseMarker ]

(If the player tries to move into an adjacent movable crate, nudge the crate Note: Decrement only)
left [ > Player no PlayerLight | Crate no FixedL no RopeL | ] -> [ | Player RopeL | Crate DecMarker RopeR ]
right [ > Player no PlayerLight | Crate no FixedR no Roper | ] -> [ | Player RopeR | Crate DecMarker RopeL ]
up [ > Player no PlayerLight | Crate no FixedU no RopeU | ] -> [ | Player RopeU | Crate DecMarker RopeD ]
down [ > Player no PlayerLight | Crate no FixedD no RopeD | ] -> [ | Player RopeD | Crate DecMarker RopeU ]

(Nudge crates back to increment)
left [ > Player no PlayerLight | Crate RopeL | RopeR ] -> [ | Player | Crate IncMarker ]
right [ > Player no PlayerLight | Crate RopeR | RopeL ] -> [ | Player | Crate IncMarker ]
up [ > Player no PlayerLight | Crate RopeU | RopeD ] -> [ | Player | Crate IncMarker ]
down [ > Player no PlayerLight | Crate RopeD | RopeU ] -> [ | Player | Crate IncMarker ]

(Move marked player in the proper direction if there's nothing immovable in their way IF the player is heavy.)
left [ Player no PlayerLight PLeft | no FixedL ] -> [ > Player > PLeft | ] again
right [ Player no PlayerLight PRight | no FixedR ] -> [ > Player > PRight | ] again
up [ Player no PlayerLight PUp | no FixedU ] -> [ > Player > PUp | ] again
down [ Player no PlayerLight PDown | no FixedD ] -> [ > Player > PDown | ] again

(If the player is light, cannot push objects)
left [ PlayerLight PLeft | no Solid ] -> [ > PlayerLight > PLeft | ] again
right [ PlayerLight PRight | no Solid ] -> [ > PlayerLight > PRight | ] again
up [ PlayerLight PUp | no Solid ] -> [ > PlayerLight > PUp | ] again
down [ PlayerLight PDown | no Solid ] -> [ > PlayerLight > PDown | ] again

(If the moving player hits a non-fixed crate to decrement it, move the crate, decrement it and place a rope)
random left [ Player no PlayerLight PLeft | Crate no FixedL no RopeL | ] -> [ | Player PLeft RopeL | Crate DecMarker RopeR ]
random right [ Player no PlayerLight PRight | Crate no FixedR no RopeR | ] -> [ | Player PRight RopeR | Crate DecMarker RopeL ]
random up [ Player no PlayerLight PUp | Crate no FixedU no RopeU| ] -> [ | Player PUp RopeU | Crate DecMarker RopeD ]
random down [ Player no PlayerLight PDown | Crate no FixedD no RopeD | ] -> [ | Player PDown RopeD | Crate DecMarker RopeU ]

(If the player hits a crate which can be pushed back along its rope, move the crate, increment it, and remove the rope)
random left [ Player no PlayerLight PLeft | Crate RopeL | RopeR ] -> [ | Player PLeft | Crate IncMarker ]
random right [ Player no PlayerLight PRight | Crate RopeR | RopeL ] -> [ | Player PRight | Crate IncMarker ]
random up [ Player no PlayerLight PUp | Crate RopeU | RopeD ] -> [ | Player PUp | Crate IncMarker ]
random down [ Player no PlayerLight PDown | Crate RopeD | RopeU ] -> [ | Player PDown | Crate IncMarker ]

(Pull all marked crates back one unit)
left [ | Crate PullbackMarker RopeL | RopeR ] -> [ | | Crate IncMarker ]
right [ | Crate PullbackMarker RopeR | RopeL ] -> [ | | Crate IncMarker ]
up [ | Crate PullbackMarker RopeU | RopeD ] -> [ | | Crate IncMarker ]
down [ | Crate PullbackMarker RopeD | RopeU ] -> [ | | Crate IncMarker ]

(Decrement marked crates)
[ C11 DecMarker ] -> [ C01 ] again
[ C12 DecMarker ] -> [ C02 ] again
[ C22 DecMarker ] -> [ C12 ] again
[ C13 DecMarker ] -> [ C03 ] again
[ C23 DecMarker ] -> [ C13 ] again
[ C33 DecMarker ] -> [ C23 ] again
[ C14 DecMarker ] -> [ C04 ] again
[ C24 DecMarker ] -> [ C14 ] again
[ C34 DecMarker ] -> [ C24 ] again
[ C44 DecMarker ] -> [ C34 ] again
[ C15 DecMarker ] -> [ C05 ] again
[ C25 DecMarker ] -> [ C15 ] again
[ C35 DecMarker ] -> [ C25 ] again
[ C45 DecMarker ] -> [ C35 ] again
[ C55 DecMarker ] -> [ C45 ] again

(Increment marked crates)
[ C01 IncMarker ] -> [ C11 ] again
[ C02 IncMarker ] -> [ C12 ] again
[ C12 IncMarker ] -> [ C22 ] again
[ C03 IncMarker ] -> [ C13 ] again
[ C13 IncMarker ] -> [ C23 ] again
[ C23 IncMarker ] -> [ C33 ] again
[ C04 IncMarker ] -> [ C14 ] again
[ C14 IncMarker ] -> [ C24 ] again
[ C24 IncMarker ] -> [ C34 ] again
[ C34 IncMarker ] -> [ C44 ] again
[ C05 IncMarker ] -> [ C15 ] again
[ C15 IncMarker ] -> [ C25 ] again
[ C25 IncMarker ] -> [ C35 ] again
[ C35 IncMarker ] -> [ C45 ] again
[ C45 IncMarker ] -> [ C55 ] again

(Remove the player's marker if they hit a fixed object and are heavy)
left [ Player no PlayerLight PLeft | FixedL ] -> [ Player | FixedL ]
right [ Player no PlayerLight PRight | FixedR ] -> [ Player | FixedR ]
up [ Player no PlayerLight PUp | FixedU ] -> [ Player | FixedU ]
down [ Player no PlayerLight PDown | FixedD ] -> [ Player | FixedD ]

(Remove the player's marker if they hit a solid object and are light)
left [ PlayerLight PLeft | Solid ] -> [ PlayerLight | Solid ]
right [ PlayerLight PRight | Solid ] -> [ PlayerLight | Solid ]
up [ PlayerLight PUp | Solid ] -> [ PlayerLight | Solid ]
down [ PlayerLight PDown | Solid ] -> [ PlayerLight | Solid ]

(Win detection)
[ Player Finish no FinishX ] -> win
[ stationary Player FinishX ] -> win

==============
WINCONDITIONS
==============



=======     
LEVELS
=======

message Move to slide, slide to escape, escape to win

H01HOEH30
C########
#.......#
#.......F
##..P...#
########C

H02HOEH30
C########
#....#..#
##......#
#..P..#.#
#.......F
#.#.....#
########C

message A push and a shove

H03HOEH30
C#####F##
#...#...#
#.P...1.#
#...#...#
########C

H04HOEH30
c##f#####
##......#
#...1...#
#.......#
#.1.p1..#
#.......#
########c

H05HOEH30
c##f#####
#...2...#
#.......#
#.1.p1..#
#.......#
########c

H06HOEH30
c########
#.......#
#.p.3...#
#.......#
#3..1.2.#
#....#..#
#.1.2...#
#.......#
####F###c

message Please be gentle

H07HOEH30
C###F####
#......##
#..#....#
#P..3..##
#2#.....#
#.......#
#.#######
########C

H08HOEH30
c########
#.......#
#.p..3..#
#.2.....#
#..2...3#
#...3...#
#.......#
#.......#
#####f##c

message Please come to a full stop

H09HOEH30
C########
#....F..#
#.......#
#.....#P#
########C

H10HOEH30
C########
#.....#.#
#..#F...#
#......##
#.#.P...#
#....#.##
########C

HHH11HOEH30
c##########
#.2...3...#
#.........#
#...2p...##
#.........#
#.2..f.4..#
#..2......#
#......1..#
##########c

HHHH12HOEH30
c###########
#.....4....#
#..1.......#
#2......2..#
#....p.....#
#..........#
#...1......#
#.f2....3..#
###########c

message Back the way you came

HHHHH13HOEH30
c###f########
##......#..##
##..........#
#.p...3.....#
#..........##
##..#......##
############c

HHHHH14HOEH30
c############
##.....#....#
#...2..#3...#
#.....1#....#
#.2....f..1.#
#........####
#....4......#
#..........p#
############c

HHHHH15HOEH30
c############
#p.#.......##
#......2....#
##.....3....#
###F...2....#
#....4.....1#
#...........#
#...........#
############c

HHHHH16HOEH30
c############
#.#....#....#
#......4..p.#
#.....#....1#
#.4...f.2...#
#...........#
#.........c.#
#.....3.....#
############c

message Don't cross me

H17HOEH30
c###f####
###.....#
###.....#
#...3...#
#.....3.#
#.......#
#p.....##
########c

H18HOEH30
c########
#..2...##
#.......#
#..33...#
#.......#
#...pf..#
#..4....#
##......#
########c

HHH19HOEH30
c##########
#p........#
#....4....#
#......4..#
#........1#
#..5......#
#.f.......#
#....1....#
##########c

HHHHH20HOEH30
c############
###.....5.###
##.........##
#.....3.....#
#...2.......#
#.#......1..#
#......p....#
#...........#
##f...#....##
##..2.....###
############c

message Light as a feather

H21HOEH30
C####F###
#.......#
#..W..1.#
##..P...#
########C

HHH22HOEH30
c####f#####
#..#......#
#.........#
#w......#s#
#....1..#.#
#p#.....#.#
##########c

HHH23HOEH30
c#####f####
#..2......#
#.........#
#.........#
#2.s.w.s.2#
#.........#
#.........#
#..p......#
##########c

HHH24HOEH30
c##########
#.#.s...###
#.....4...#
#..4....#.#
#3...w...3#
#......fs.#
#...4.....#
#.#..p..#.#
##########c

message Two steps forward, one step back

H25HOEH30
c########
#..b..#.#
#.......f
#p..2...#
########c

HHH26HOEH30
c##########
#.........#
#.1b......#
#.........#
#######w.2#
#.........#
#.1f......#
#p....3...#
##########c

HHHHH27HOEH30
c############
#.....1.....#
#...p...f...#
#.....w.....#
#3....b....3#
#...3...3...#
#....222....#
#.s.......s.#
############c

HHHHH28HOEH30
c############
##.........b#
#.......34#.#
#........w4.#
#.3s..f...3.#
#.4p........#
#.#43.......#
#b.........##
############c

HHHH29HOEH30
c###########
#########.##
###s.w4....#
#..p...5..##
#.........##
#s...b..f.##
#.........##
#..3...3..##
#........###
###########c

HHHHHH30HOEH30
c#############
#..2......2..#
#.1.w####s.1.#
#...#.##.#...#
#.p.######.f.#
#...##..##...#
#...##..##...#
#.1.s####s.1.#
#..2......2..#
#############c

####1337#oe#30
c#############
#............#
#....#..#....#
#....#..#....#
##...#..#...##
#p..........f#
##..######..##
#...######...#
#....####....#
#............#
#############c

message Game complete. Congratulations!
message Thanks for playing!