title Schleimban
author increpare [+ ein Level von David Skinners Microban]
homepage www.increpare.com

run_rules_on_level_start

( One level taken from David Skinner's Microban - www.sneezingtiger.com/sokoban/levels/microbanText.html )

text_color lightblue
color_palette 6

========
OBJECTS
========

Background
black darkgray
00000
00000
00100
00000
00000

Wall
darkgray gray darkblue
00000
01112
01002
01002
02222

wall_up
darkgray gray darkblue
.1..2
..00.
.....
.....
.....

wall_down
darkgray gray darkblue
.....
.....
.....
.....
.1002

wall_left
darkgray gray darkblue
.....
1....
.0...
.0...
2....

wall_right
darkgray gray darkblue
.....
....1
....0
....0
.....

Target
yellow
.....
.....
..0..
.....
.....


Crate
gray darkblue
.000.
01110
01.10
01110
.000.

Player
darkred red black
.000.
01110
01.10
01110
.000.

player_up
darkred red
0...0
.....
.....
.....
.....

player_down
darkred red
.....
.....
.....
.....
0...0

player_left
darkred red
0....
.....
.....
.....
0....

player_right
darkred red
....0
.....
.....
.....
....0


player_up_o
darkred red
01110
.....
.....
.....
.....

player_down_o
darkred red
.....
.....
.....
.....
01110

player_left_o
darkred red
0....
1....
1....
1....
0....

player_right_o
darkred red
....0
....1
....1
....1
....0

player_ur
darkred red
....1
.....
.....
.....
.....

player_ul
darkred red
1....
.....
.....
.....
.....

player_dr
darkred red
.....
.....
.....
.....
....1

player_dl
darkred red
.....
.....
.....
.....
1....


ok
darkred



sprt_0_0  1
black

sprt_1_0 2
lightgray darkgray red lightred
...01
...01
...00
...22
..223

sprt_2_0 3
darkgray lightgray red lightred
00001
00001
11111
22222
33332

sprt_3_0 4
red black
11111
11111
11111
11111
01111

sprt_4_0 5
black

sprt_0_1 6
lightgray red darkgray black
33333
33333
00001
22201
22201

sprt_1_1 7
red lightred darkblue black
30011
00111
01111
11111
11122

sprt_2_1 8
lightred darkblue
00000
00000
00000
00000
00011

sprt_3_1 9
lightred red
011..
0011.
00011
00001
00001

sprt_4_1 0
black

sprt_0_2 -
darkgray lightgray red
00012
00012
00012
00012
00012

sprt_1_2 =
lightred darkblue
00011
00000
00000
00100
00010

sprt_2_2 q
lightred darkblue
00011
00000
00000
00000
00000

sprt_3_2 w
lightred red darkblue
00001
00000
00000
02000
20000

sprt_4_2 e
red lightgray lightred darkgray
0....
0....
00111
20133
20133

sprt_0_3 r
darkgray lightgray red
00012
00012
11112
.....
.....

sprt_1_3 t
lightred darkblue red
00001
00000
20000
22000
.2200

sprt_2_3 y
lightred darkblue
00001
11110
00000
00000
00000

sprt_3_3 u
lightred red
00000
00000
00000
00000
00001

sprt_4_3 i
lightred red lightgray darkgray
01233
01233
01233
11222
1....

sprt_0_4 z
black

sprt_1_4 x
red lightgray darkgray
..000
..111
..122
..122
..122

sprt_2_4 a
red lightgray darkgray
00000
11111
22222
22222
22222

sprt_3_4 s
red lightgray darkgray
00000
11111
22221
22221
22221

sprt_4_4 d
black


=======
LEGEND
=======

. = Background
# = Wall
P = Player
* = Crate
@ = Crate and Target
O = Target

obstacle = crate or wall
player_stick = player_up or player_down or player_left or player_right or player_up_o or player_down_o or player_left_o or player_right_o or player_ur or player_ul or player_dr or player_dl

=======
SOUNDS
=======

Crate MOVE 36772507
endlevel 83744503
startgame 92244503

================
COLLISIONLAYERS
================

Background
Target
Player, Wall, Crate
ok
player_up,player_up_o
player_down,player_down_o
player_left,player_left_o
player_right,player_right_o
player_ur
player_ul
player_dr
player_dl

wall_up
wall_down
wall_left
wall_right
1,2,3,4,5,6,7,8,9,0,sprt_0_2,sprt_1_2,q,w,e,r,t,y,u,i,z,x,a,s,d

======
RULES
======

right [ wall | wall ] -> [wall_right wall | wall_left wall ]
down [ wall | wall ] -> [ wall_down wall | wall_up wall]

[ >  Player | Crate ] -> [  >  Player | > Crate  ]

late [ player | obstacle ] -> [ player ok | obstacle ]
late [ player no ok ] -> cancel
late [ player ok ] -> [ player ]

late [player_stick]->[]

late down [ crate | player ] -> [ crate player_down | Player player_up_o]
late up [ crate | player ] -> [ crate player_up | Player player_down_o]
late left [ crate | player ] -> [ crate player_left | Player player_right_o]
late right [ crate | player ] -> [ crate player_right | Player player_left_o]

late up [ player | wall ]-> [player player_up_o|wall]
late down [ player | wall ]-> [player player_down_o|wall]
late left [ player | wall ]-> [player player_left_o|wall]
late right [ player | wall ]-> [player player_right_o|wall]

late [ player_up_o player_right_o ] -> [ player_up_o player_right_o player_ur]
late [ player_up_o player_left_o ] -> [ player_up_o player_left_o player_ul]
late [ player_down_o player_right_o ] -> [ player_down_o player_right_o player_dr]
late [ player_down_o player_left_o ] -> [ player_down_o player_left_o player_dl]
==============
WINCONDITIONS
==============

All Target on Crate

=======
LEVELS
=======


message Level 1 von 5

..#####..
..#...#..
..#.o.#..
..#...#..
####.####
#.......#
#...*...#
#.......#
#.......#
#...p...#
#########


message Level 2 von 5 [ von Microban ]

..####...
###..####
#.....*.#
#.#..#*.#
#.O.O#P.#
#########

message Level 3 von 5

#########
#.......#
#..o.o..#
#.......#
##.###.##
#.......#
#.*.p.*.#
#...#...#
#########

message Level 4 von 5

########
#o.....#
#p**...#
##.....#
.##....#
..##...#
...##..#
....##o#
.....###

message Level 5 von 5


#########
#.......#
#.....#.#
#.......#
#.....###
#..**.oo#
#....####
#.p..#...
######...


message Herzlichen Gl√ºckwunsch!


12345
67890
-=qwe
rtyui
zxasd


(



..####...
###..####
#.....*.#
#.#..#*.#
#.O.O#P.#
#########


.#######
.#.....#
.#.O*O.#
##.*P*.#
#..O*O.#
#......#
########

#############
#......##...#
#...**..p.#.#
#.....#oo...#
#.....#######
#######......

######.#####
#....###...#
#.**.....#P#
#.*.#OOO...#
#...########
#####.......

#########
#.......#
#..o.o..#
#.......#
##.###.##
#.......#
#.......#
#.......#
#.*.p.*.#
#...#...#
#########

..#####..
..#...#..
..#.o.#..
..#...#..
####.####
#.......#
#...*...#
#.......#
#.......#
#...p...#
#########


#######
#.....#
#.o.o.#
#.....#
##.#.##
#.....#
#.*.*.#
#..p..#
#######

)