title Boxes
author Pichusuperlover
homepage www.puzzlescript.net
noaction
require_player_movement
========
OBJECTS
========

Background 
White 

Target 
Green  

Wall 
Red

Player 
Blue   

DownGhost
Blue

UpGhost
Blue 

RightGhost
Blue  

LeftGhost
Blue  

Life
Pink
.....
.000.
.000.
.000.
.....

DownArrow
darkgreen
..0..
..0..
00000
.000.
..0..

DownArrowUsed
darkgreen
..0..
..0..
00000
.000.
..0..

UpArrow
darkgreen
..0..
.000.
00000
..0..
..0..

UpArrowUsed
darkgreen
..0..
.000.
00000
..0..
..0..

RightArrow
darkgreen
..0..
..00.
00000
..00.
..0..

RightArrowUsed
darkgreen
..0..
..00.
00000
..00.
..0..

LeftArrow
darkgreen
..0..
.00..
00000
.00..
..0..

LeftArrowUsed
darkgreen
..0..
.00..
00000
.00..
..0..

Ring
orange
.....
.000.
.0.0.
.000.
.....

LifeUp
Pink
.....
.000.
.000.
.000.
.....

Portal1
darkblue blue
00100
01100
10100
00100
11111

Portal2
darkblue blue
01110
10001
00110
01000
11111

Portal3
darkblue blue
01110
10001
00010
10001
01110

Portal4
darkblue blue
00010
00110
01010
11111
00010

Portal5
darkblue blue
11111
10000
11110
00001
11110

Portal6
darkblue blue
01110
10000
11110
10001
01110

Portal7
darkblue blue
11111
00010
00100
00100
00100

Portal8
darkblue blue
01110
10001
01110
10001
01110

Portal9
darkblue blue
01110
10001
01111
00001
01110

Gate1
red darkred
00100
01100
10100
00100
11111

Gate2
red darkred
01110
10001
00110
01000
11111

Gate3
red darkred
01110
10001
00010
10001
01110

Gate4
red darkred
00010
00110
01010
11111
00010

Gate5
red darkred
11111
10000
11110
00001
11110

Key1
Darkred
..0..
.00..
0.0..
..0..
00000

Key2
DarkRed
.000.
0...0
..00.
.0...
00000

Key3
DarkRed
.000.
0...0
...0.
0...0
.000.

Key4
DarkRed
...0.
..00.
.0.0.
00000
...0.

Key5
DarkRed
00000
0....
0000.
....0
0000.

Temp
transparent

Air
transparent

Marker
transparent

ToggleOff
transparent

ToggleOn
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = Player and Air
G = Target and Air
H = Life
A = Air
D = DownArrow
U = UpArrow
R = RightArrow
L = LeftArrow
O = Ring and Air
M = LifeUp and Air
T = ToggleOff
* = Marker
1 = Portal1 and air
2 = Portal2 and air
3 = Portal3 and air
4 = Portal4 and air
5 = Portal5 and air
6 = Portal6 and air
7 = Portal7 and air
8 = Portal8 and air
9 = Portal9 and air
I = Gate1
Q = Gate2
; = Gate3
{ = Gate4
: = Gate5
K = Key1 and air
? = Key2 and air
' = Key3 and air
} = Key4 and air
" = Key5 and air
Gate = Gate1 or Gate2 or Gate3 or Gate4 or Gate5
Key = Key1 or Key2 or Key3 or Key4 or Key5
AllWalls = Gate or Wall
Portal = Portal1 or Portal2 or Portal3 or Portal4 or Portal5 or Portal6 or Portal7 or Portal8 or Portal9
Ghost = DownGhost or UpGhost or RightGhost or LeftGhost
Arrow = UpArrow or DownArrow or RightArrow or LeftArrow
ArrowUsed = UpArrowUsed or DownArrowUsed or RightArrowUsed or LeftArrowUsed

=======
SOUNDS
=======

================
COLLISIONLAYERS
================

Background
Air, AllWalls
Ring, LifeUp, Portal, Key
Target
Player, Ghost, Life, Arrow, ArrowUsed, Marker, Temp, ToggleOff, ToggleOn
======
RULES     
======        

[ Player AllWalls ] -> restart 
[ Ghost AllWalls ] -> restart 

(life refill)
late [ Air | life ] -> [ Life | Life ]

(life drain)
[ Life | Air ] -> [ Temp | Air ]
[ Life | Marker ] -> [ Temp | Marker ]
late [ Temp ] -> [ Air ]

(rings)
late Right [ Air | Marker ] [ Player Ring ] [ ToggleOff ] -> [ Life | Marker ] [ Player Ring ] [ ToggleOn ]
late [ ToggleOn ][ Air | life ] -> [ ToggleOn ] [ Life | Life ]
late [ ToggleOn ] -> [ ToggleOff ]

(life up)
late Right [ Marker | Air ] [ Player LifeUp ] [ ToggleOff ] -> [ Air | Marker ] [ Player ] [ ToggleOn ]
late [ ToggleOn ] [ Life | Air ] -> [ ToggleOff ] [ Life | Life ]
late [ ToggleOn ] [ Arrow | Air ] -> [ ToggleOff ] [ Arrow | Life ]

(fall)
startloop
(fall down)
[ Player ] [ DownArrow | Air ] [ Air | Marker ] -> [ DownGhost ] [ DownArrowUsed | Air ]  [ Life | Marker ]
[ Player ] [ DownArrow | ArrowUsed ] -> [ DownGhost ] [ DownArrowUsed | ArrowUsed ] 
Down [ DownGhost Air | Air ] -> [ Air | Player Air ]
Down [ DownGhost AllWalls | Air ] -> [ AllWalls | Player Air ]
Down [ DownGhost Allwalls | AllWalls ] -> [ AllWalls | Player AllWalls ]
Down [ DownGhost air | AllWalls ] -> [ Air | Player AllWalls ]

(fall up)
[ Player ] [ UpArrow | Air ] [ Air | Marker ] -> [ UpGhost ] [ UpArrowUsed | Air ]  [ Life | Marker ]
[ Player ] [ UpArrow | ArrowUsed ] -> [ UpGhost ] [ UpArrowUsed | ArrowUsed ] 
Up [ UpGhost Air | Air ] -> [ Air | Player Air ]
Up [ UpGhost AllWalls | Air ] -> [ AllWalls | Player Air ]
Up [ UpGhost Allwalls | AllWalls ] -> [ AllWalls | Player AllWalls ]
Up [ UpGhost air | AllWalls ] -> [ Air | Player AllWalls ]

(fall right)
[ Player ] [ RightArrow | Air ] [ Air | Marker ] -> [ RightGhost ] [ RightArrowUsed | Air ]  [ Life | Marker ]
[ Player ] [ RightArrow | ArrowUsed ] -> [ RightGhost ] [ RightArrowUsed | ArrowUsed ] 
Right [ RightGhost Air | Air ] -> [ Air | Player Air ]
Right [ RightGhost AllWalls | Air ] -> [ AllWalls | Player Air ]
Right [ RightGhost Allwalls | AllWalls ] -> [ AllWalls | Player AllWalls ]
Right [ RightGhost air | AllWalls ] -> [ Air | Player AllWalls ]

(fall left)
[ Player ] [ LeftArrow | Air ] [ Air | Marker ] -> [ LeftGhost ] [ LeftArrowUsed | Air ]  [ Life | Marker ]
[ Player ] [ LeftArrow | ArrowUsed ] -> [ LeftGhost ] [ LeftArrowUsed | ArrowUsed ] 
Left [ LeftGhost Air | Air ] -> [ Air | Player Air ]
Left [ LeftGhost AllWalls | Air ] -> [ AllWalls | Player Air ]
Left [ LeftGhost Allwalls | AllWalls ] -> [ AllWalls | Player AllWalls ]
Left [ LeftGhost air | AllWalls ] -> [ Air | Player AllWalls ]
endloop

(reset arrows)
late [ DownArrowused ] -> [ DownArrow ]
late [ UpArrowused ] -> [ UpArrow ]
late [ RightArrowused ] -> [ RightArrow ]
late [ LeftArrowused ] -> [ LeftArrow ]

(portals)

(Portal 1)
late [ Player Portal1 ] [ Portal1 No Player ] [ ToggleOff ] -> [ Portal1 ] [ Player Portal1 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Portal 2)
late [ Player Portal2 ] [ Portal2 No Player ] [ ToggleOff ] -> [ Portal2 ] [ Player Portal2 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Portal 3)
late [ Player Portal3 ] [ Portal3 No Player ] [ ToggleOff ] -> [ Portal3 ] [ Player Portal3 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Portal 4)
late [ Player Portal4 ] [ Portal4 No Player ] [ ToggleOff ] -> [ Portal4 ] [ Player Portal4 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Portal 5)
late [ Player Portal5 ] [ Portal5 No Player ] [ ToggleOff ] -> [ Portal5 ] [ Player Portal5 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Portal 6)
late [ Player Portal6 ] [ Portal6 No Player ] [ ToggleOff ] -> [ Portal6 ] [ Player Portal6 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Portal 7)
late [ Player Portal7 ] [ Portal7 No Player ] [ ToggleOff ] -> [ Portal7 ] [ Player Portal7 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Portal 8)
late [ Player Portal8 ] [ Portal8 No Player ] [ ToggleOff ] -> [ Portal8 ] [ Player Portal8 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Portal 9)
late [ Player Portal9 ] [ Portal9 No Player ] [ ToggleOff ] -> [ Portal9 ] [ Player Portal9 ] [ ToggleOn ]
late [ ToggleOn ] -> [ ToggleOff ]

(Gates)

(Gate 1)
late [ Gate1 ] [ Player Key1 ] ->  [ Air ] [ Player Key1 ]
late [ Player Key1 ] -> [ Player ]

(Gate 2)
late [ Gate2 ] [ Player Key2 ] ->  [ Air ] [ Player Key2 ]
late [ Player Key2 ] -> [ Player ]

(Gate 3)
late [ Gate3 ] [ Player Key3 ] ->  [ Air ] [ Player Key3 ]
late [ Player Key3 ] -> [ Player ]

(Gate 4)
late [ Gate4 ] [ Player Key4 ] ->  [ Air ] [ Player Key4 ]
late [ Player Key4 ] -> [ Player ]

(Gate 5)
late [ Gate5 ] [ Player Key5 ] ->  [ Air ] [ Player Key5 ]
late [ Player Key5 ] -> [ Player ]

==============
WINCONDITIONS
==============

all Target on Player

=======     
LEVELS
=======

DHH*AAAT
########
#AAAAAA#
#AAAAGA#
#AAAAAA#
#AAAAAA#
#APAAAA#
#AAAAAA#
########

DHH*AAAT
########
#AAAAAA#
#PAAAAG#
########

DHH*AT
######
###AG#
##AA##
#AAA##
#AA###
#P####
######

DHH*AT
######
##A###
#AAAA#
#PA#G#
######

DH*AAT
######
#A####
#PA###
##AA##
###AA#
####G#
######

DHHH*AAAT
#########
#AAAAAAA#
#P#A#A#G#
#########

DHHH*AAT
########
####AAA#
#GAAA#A#
######A#
######A#
####AAA#
####P#A#
########

DHH*AAAAAT
##########
#PAA####G#
###AAOAOA#
##########

DH*AAAT
#######
#####G#
#PA##O#
##AOOO#
#######

DHHH*AAAAT
##########
#AAAAAAAA#
#PAAAAA#G#
###O######
##########

DHHH*AAAT
#########
#####AAA#
#AAA#O#G#
#P#AAA###
#########

DHHH*AAAAT
##########
#AAA#A#A##
#G#AAA#A##
###A#AAA##
###A#A#A##
###P#A#AO#
##########

DH*AAAAAAT
##########
#GO#######
#AO#AAOOO#
#OO#AA##O#
#OAOAA##O#
#OAAAA##O#
#AAAAA##O#
#AOOOO##O#
#OO##OOAO#
#PA##AAAO#
##########

UHH*AAAT
########
#PAAAAG#
#AAAAAA#
########

RH*AAT
######
###G##
###OO#
####O#
####O#
###AA#
##AA##
#AA###
#P####
######

RDHHH*AAAT
##########
#AAAAAA###
#P##A##AG#
##########

LDHHHH*AAAT
###########
#######AAA#
####A#A#AA#
###AAAA##A#
###A#A##AA#
###A#AA#AA#
##AP#A##A##
#G###AAOA##
###########

DHHH*AAT
########
#AAAAAA#
#P#M##G#
########

DHHHH*AAAT
##########
##AA######
#MPAAAAAG#
##########

DHHH*AAAAAT
###########
#AAA#######
#A#AAAAAA##
#AMA##A#AG#
#P#########
###########

DDH*AAAAAT
##########
####APMA##
##########
####MAAA##
##########
#MAAAA#M##
##########
###MAAAAG#
##########

DDDDDDH*AAAAAT
##############
#MAAAMAO#GAAA#
#OAAAAAA#AAAO#
##OAAAAAO###A#
#OAAAAAAA###A#
#AAAPAAAAOAAA#
#OMOOAAOAAAAO#
##############

DHHH*AAAAAAAAAAAAT
##################
#AAAAAAG##########
#A################
#A####A###A##A####
#A##AAA#AAAA#AAAM#
#MAAA#AAA##AAA##A#
########A#########
##A##AA#P#AAAA####
#MAAAAAAAAA##AAAM#
##########A#######
##################

DHHH*T
######
#AAA1#
#P#A##
######
#1AAA#
##A#G#
######

DDHHHH*AAAAAAAT
###############
###A#A###A#####
###A#AAAAA#####
#1AAAA###AAAAG#
###P#A#AAA#####
############1##
###############

DHHHH*AT
########
#AAM####
#1######
#A#AAA2#
#A#1####
#A###2##
#P###AG#
########

DDHHHHHH*AAAAAT
###############
######7###5####
#GA9#7A3#6A2#2#
######8########
###############
######5###6####
#PA1#1A3#4A9#4#
######8########
###############

DHH*AT
######
##K###
##A###
#PAIG#
######

DDHHHH*AAAAAAAAAT
#################
#G#AIA###A#####K#
#A###A###A#####A#
#AIPAAAAAAAAAA#A#
#####I###A###I#A#
#####A###AIA#AAA#
#################

DHHHHH*AAAAAAAAAT
#################
##########"######
########AAA######
###'AA##{##A###}#
#####QAAAA;AAAAA#
######AAAA#######
######APAA#AAA###
###AAAAAAAIA#?###
#KAA##1AA1#######
######:##########
######G##########
#################