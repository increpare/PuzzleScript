title 1D M3
author Le Slo
homepage www.puzzlescript.net
run_rules_on_level_start
background_color #5c4f49
again_interval 0.18
text_color #def0ff


========
OBJECTS
========

Top
#5c4f49 #646556 #8da089 #a9cbc4 #def0ff
00000
11111
22222
33333
44444
Bottom
#def0ff #a9cbc4 #8da089 #646556 #5c4f49
00000
11111
22222
33333
44444

LeftB
#5c4f49 #646556 #8da089 #a9cbc4 #def0ff
01234
01234
01234
01234
01234

RightB
#def0ff #a9cbc4 #8da089 #646556 #5c4f49
01234
01234
01234
01234
01234

C1
#5c4f49 #646556 #8da089 #a9cbc4 #def0ff
00000
01111
01222
01233
01234

C2
#5c4f49 #646556 #8da089 #a9cbc4 #def0ff
00000
11110
22210
33210
43210

C3
#5c4f49 #646556 #8da089 #a9cbc4 #def0ff
01234
01233
01222
01111
00000

C4
#def0ff #a9cbc4 #8da089 #646556 #5c4f49
01234
11234
22234
33334
44444

Nothing
#def0ff #5c4f49
00000
00000
00000
00000
00000

One
#def0ff #5c4f49 
00000
00000
00100
00000
00000

Two
#def0ff #5c4f49 #def0ff
00000
01000
00000
00010
00000

Three
#def0ff #5c4f49 #def0ff
00000
00100
00000
01010
00000

Four
#def0ff #5c4f49 #def0ff
00000
01010
00000
01010
00000

Five
#def0ff #5c4f49 #def0ff
00000
01010
00100
01010
00000

Snothing
#e2b2f3 #5c4f49 
00000
00000
00000
00000
00000

SOne
#e2b2f3 #5c4f49 #e2b2f3
00000
00000
00100
00000
00000

STwo
#e2b2f3 #5c4f49 #e2b2f3
00000
01000
00000
00010
00000

SThree
#e2b2f3 #5c4f49 #e2b2f3
00000
00100
00000
01010
00000

SFour
#e2b2f3 #5c4f49 #e2b2f3
00000
01010
00000
01010
00000

SFive
#e2b2f3 #5c4f49 #e2b2f3
00000
01010
00100
01010
00000

Nnothing
#B4592C #5c4f49 #B4592C
00000
00000
00000
00000
00000

NOne
#B4592C #5c4f49 #B4592C
00000
00000
00100
00000
00000

NTwo
#B4592C #5c4f49 #B4592C
00000
01000
00000
00010
00000

NThree
#B4592C #5c4f49 #B4592C
00000
00100
00000
01010
00000

NFour
#B4592C #5c4f49 #B4592C
00000
01010
00000
01010
00000

Nfive
#B4592C #5c4f49 #B4592C
00000
01010
00100
01010
00000

Cursor
#7b98ba
00.00
0...0
.....
0...0
00.00

CursorSelected
#7b98ba
0...0
.....
.....
.....
0...0

CursorFirst
#abc8fb
00.00
0...0
.....
0...0
00.00

P1
transparent

P2
transparent

P3
transparent

P4
transparent

P5
transparent

Background
#5c4f49 #def0ff
00000
00000
00000
00000
00000


Target
DarkBlue
.....
.000.
.0.0.
.000.
.....

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010


Crate
Orange Yellow
00000
0...0
0...0
0...0
00000

randomT
red Yellow
00000
0...0
0...0
0...0
00000

AuxMove
transparent

init
#7b98ba
.0.0.
0...0
.....
0...0
.0.0.

NAnim02
transparent

NAnim01
#E47C7C #def0ff
00000
00000
00000
00000
00000

NAnim04
transparent

NAnim03
#E47C7C #def0ff
00000
00000
00000
00000
00000

FAnim02
transparent

FAnim01
#E47C7C #def0ff
.....
.0.0.
..0..
.0.0.
.....

FAnim04
transparent

FAnim03
#E47C7C #def0ff
.....
.0.0.
..0..
.0.0.
.....

GAnim01
#74EC7C #def0ff
00000
00000
00000
00000
00000

GAnim02
#def0ff #5c4f49
00000
00000
00000
00000
00000

GAnim03
#74EC7C #def0ff
00000
00000
00000
00000
00000

GAnim04
#def0ff #5c4f49
00000
00000
00000
00000
00000

initL
transparent

initR
transparent

OK
transparent

FinalNothing
#a00000
.....
..0..
.....
.....
.....

FinalOne
#5c4f49
.....
.....
..0..
.....
.....

FinalTwo
#5c4f49
.....
.0...
.....
...0.
.....

FinalThree
#5c4f49
.....
..0..
.....
.0.0.
.....

FinalFour
#5c4f49
.....
.0.0.
.....
.0.0.
.....

FinalFive
#5c4f49
.....
.0.0.
..0..
.0.0.
.....

FinalB
#def0ff #5c4f49
00000
00000
00000
00000
00000

FinalBOK
#74EC7C #5c4f49
00000
00000
00000
00000
00000

start
transparent

Hone
transparent

Htwo
transparent

Hthree
transparent

Hfour
transparent

Hfive
transparent

T1
transparent

T2
transparent

T3
transparent

T4
transparent

T5
transparent

=======
LEGEND
=======

. = Background
# = Wall
P = CursorFirst
* = Crate
@ = Crate and Target
O = Target
X = randomT
0 = Nothing
1 = FinalOne 
2 = FinalTwo 
3 = FinalThree
4 = FinalFour
5 = FinalFive
a = nothing and Hone
b = nothing and Htwo
c = nothing and Hthree and CursorFirst
d = nothing and Hfour
e = nothing  and hfive
s = start

PlayableTiles = One or Two or Three or Four or Five or nothing
selectedTiles = SOne or Snothing or STwo or SThree or SFour or SFive
Tiles = One or Two or Three or Four or Five or None	or NTwo or NThree or NFour or Nfive or Nnothing
AllTiles = Tiles or Nothing or randomT
Player = Cursor or CursorSelected or CursorFirst

auxDirs = auxMove or auxMove

board = Top or Bottom or LeftB or RightB or C1 or C2 or C3 or C4
animations = NAnim01 or NAnim02 or NAnim03 or NAnim04 or FAnim01 or FAnim02 or FAnim03 or FAnim04 or GAnim01 or GAnim03 or GAnim02 or GAnim04
ones = one or none
twos = two or ntwo
threes = three or NThree
fours = four or nfour
fives = five or nfive
finalNumbers = FinalNothing or FinalOne or FinalTwo or FinalThree or FinalFour or FinalFive
finalBack = FinalB or FinalBOK
texture = randomT or finalBack or PlayableTiles
hiddentiles = Hone or Htwo or Hthree or Hfour or Hfive
PTiles = P1 or p2 or p3 or p4 or p5
tutorial = T1 or T2 or T3 or T4 or T5
=======
SOUNDS
=======

Crate MOVE 36772507
FinalBOK create 25013308
startgame 20419108
NAnim01 create 25759908
FAnim01 create 25759908
GAnim01 create 79236108
restart 83339308
player move 35113107
CursorSelected create  23947508
cursor create 82445508

sfx0 1014701
sfx1 37749901

================
COLLISIONLAYERS
================

Background
finalB, FinalBOK, ptiles, tutorial
finalNumbers
ok
initL, initR
board
auxMove
Target
AllTiles
selectedTiles
init
Player
Wall, Crate
animations
start, hiddentiles

======
RULES
======

(START GAME)

right [start | ] -> [start | p1]
right [p1 | ] -> [p1 | p2]
right [p2 | ] -> [p2 | p3]
right [p3 | ] -> [p3 | p4]
right [p4 | ] -> [p4 | p5]

[finalNumbers no FinalBack] -> [finalNumbers finalB]


up[texture | ] -> [texture | top]
down[texture | ] -> [texture | bottom]
left[texture | no texture] -> [texture | leftB]
right[texture | no texture] -> [texture | RightB]

up[start][RightB | no RightB] -> [start][RightB | c2]
down [start][RightB | no RightB] -> [start][RightB | c4]

up[start][leftB | no leftB] -> [start][leftB | c1]
down [start][leftB | no leftB] -> [start][leftB | c3]

startLoop
[randomT] -> [random PlayableTiles]

[start][One | One | One ] -> [start][randomT | randomT | randomT]
[start][Two | Two | Two ] -> [start][randomT | randomT | randomT]
[start][Three | Three | Three ] -> [start][randomT | randomT | randomT]
[start][Four | Four  | Four  ] -> [start][randomT | randomT | randomT]
[start][Five | Five  | Five  ] -> [start][randomT | randomT | randomT]

endLoop

[start] -> []

(PLACEMENT OF TILES)
vertical [> player] -> [stationary player]
right [ right player | no AllTiles] -> [stationary player | ]
left [ left player | no AllTiles] -> [stationary player | ]

[action CursorFirst nothing][p1] -> [CursorFirst one][]
[action CursorFirst nothing][p2] -> [CursorFirst two][]
[action CursorFirst nothing][p3] -> [CursorFirst three][]
[action CursorFirst nothing][p4] -> [CursorFirst four][]
[action CursorFirst nothing][p5] -> [Cursor five][ ]

([t1] -> [t2] 	 message Press X to select a tile. 



[action player][ t5] -> [action player][] 	
[moving player][ t4] -> [moving player][t5] 
[moving player][ t3] -> [moving player][t4] 
[action player][ t2] -> [action player][t3] 
[action player][ t1] -> [action player][t2] )




(MOVEMENT)





[action cursor nothing] 	-> [cursorSelected snothing init]
[action cursor one] 		-> [cursorSelected sone init]
[action cursor two] 		-> [cursorSelected stwo init]
[action cursor three] 		-> [cursorSelected sthree init]
[action cursor four] 		-> [cursorSelected sfour init]
[action cursor five] 		-> [cursorSelected sfive init]

[action cursorSelected no init Snothing] 	-> [cursor nothing]
[action cursorSelected no init Sone] 		-> [cursor one]
[action cursorSelected no init Stwo] 		-> [cursor two]
[action cursorSelected no init Sthree] 		-> [cursor three]
[action cursorSelected no init Sfour] 		-> [cursor four]
[action cursorSelected no init Sfive] 		-> [cursor five]

[action cursorSelected init Snothing] 	-> [cursor nothing]
[action cursorSelected init Sone] 		-> [cursor one]
[action cursorSelected init Stwo] 		-> [cursor two]
[action cursorSelected init Sthree] 	-> [cursor three]
[action cursorSelected init Sfour] 		-> [cursor four]
[action cursorSelected init Sfive] 		-> [cursor five]




[cursor][init] -> [cursor][]
[cursor][initR] -> [cursor][]
[cursor][initL] -> [cursor][]

[CursorSelected InitL] -> [CursorSelected]
[CursorSelected InitR] -> [CursorSelected]


[left cursorSelected init] -> [left CursorSelected init initL]
[right cursorSelected init] -> [right CursorSelected init initR]

(STOP Cursor)

[NAnim04] -> []
[NAnim03] -> [NAnim04]
[NAnim02] -> [NAnim03]
[NAnim01] -> [NAnim02]

[FAnim04] -> []
[FAnim03] -> [FAnim04]
[FAnim02] -> [FAnim03]
[FAnim01] -> [FAnim02]

[GAnim04] -> [randomT]
[GAnim03] -> [GAnim04]
[GAnim02] -> [GAnim03]
[GAnim01] -> [GAnim02]

left[initL] [left cursorSelected | nothing]-> [initL][ CursorSelected | nothing NAnim01]
right[initR][right cursorSelected | nothing]-> [initR][ CursorSelected | nothing NAnim01]

left[initL] [left cursorSelected | nnothing]-> [initL][ CursorSelected | nnothing NAnim01]
right[initR][right cursorSelected | nnothing]-> [initR][ CursorSelected | nnothing NAnim01]

left[initL] [left cursorSelected SFive]-> [initL][ CursorSelected SFive FAnim01]
right[initR][right cursorSelected SFive]-> [initR][ CursorSelected SFive FAnim01]

(ADD Cursor)

left[initL no OK] [left cursorSelected snothing| no nothing]-> [initL OK][left CursorSelected left SOne 	| right auxMove] sfx0
left[initL no OK] [left cursorSelected SOne| no nothing] 	-> [initL OK][left CursorSelected left STwo 	| right auxMove] sfx0
left[initL no OK] [left cursorSelected STwo| no nothing] 	-> [initL OK][left CursorSelected left SThree 	| right auxMove] sfx0
left[initL no OK] [left cursorSelected SThree| no nothing] 	-> [initL OK][left CursorSelected left SFour 	| right auxMove] sfx0
left[initL no OK] [left cursorSelected SFour| no nothing] 	-> [initL OK][left CursorSelected left SFive 	| right auxMove] sfx0

right[initR no OK][right cursorSelected snothing| no nothing]-> [initR OK][right CursorSelected right SOne 		| left auxMove] sfx0
right[initR no OK][right cursorSelected SOne| no nothing] 	-> [initR OK][right CursorSelected right STwo 		| left auxMove] sfx0
right[initR no OK][right cursorSelected STwo| no nothing] 	-> [initR OK][right CursorSelected right SThree 	| left auxMove] sfx0
right[initR no OK][right cursorSelected SThree| no nothing] 	-> [initR OK][right CursorSelected right SFour 	| left auxMove] sfx0
right[initR no OK][right cursorSelected SFour| no nothing] 	-> [initR OK][right CursorSelected right SFive 	| left auxMove] sfx0

(Subtract Cursor)


left[initR no OK] [left cursorSelected SOne| ] 	-> [initR OK][left CursorSelected left Snothing| right auxMove]sfx1
left[initR no OK] [left cursorSelected STwo| ] 	-> [initR OK][left CursorSelected left SOne 	| right auxMove]sfx1
left[initR no OK] [left cursorSelected SThree| ] 	-> [initR OK][left CursorSelected left STwo 	| right auxMove]sfx1
left[initR no OK] [left cursorSelected SFour| ] 	-> [initR OK][left CursorSelected left SThree 	| right auxMove]sfx1
left[initR no OK] [left cursorSelected SFive| ] 	-> [initR OK][left CursorSelected left SFour 	| right auxMove]sfx1

right[initL no OK][right cursorSelected SOne| ] 	-> [initL OK][right CursorSelected right Snothing 	| left auxMove]sfx1
right[initL no OK][right cursorSelected STwo| ] 	-> [initL OK][right CursorSelected right SOne 		| left auxMove]sfx1
right[initL no OK][right cursorSelected SThree| ] 	-> [initL OK][right CursorSelected right STwo 		| left auxMove]sfx1
right[initL no OK][right cursorSelected SFour| ] 	-> [initL OK][right CursorSelected right SThree 	| left auxMove]sfx1
right[initL no OK][right cursorSelected SFive| ] 	-> [initL OK][right CursorSelected right SFour 	| left auxMove]sfx1

[OK] -> []

(Subtract NUMBERS)

[initR no OK][left auxMove One] 	-> [initR OK][left auxMove left nothing]
[initR no OK][left auxMove Two] 	-> [initR OK][left auxMove left One]
[initR no OK][left auxMove Three] 	-> [initR OK][left auxMove left Two]
[initR no OK][left auxMove Four] 	-> [initR OK][left auxMove left Three]
[initR no OK][left auxMove Five] 	-> [initR OK][left auxMove left Four]

[initR no OK][left auxMove NOne]	-> [initR OK][left auxMove left nnothing]
[initR no OK][left auxMove NTwo] 	-> [initR OK][left auxMove left NOne]
[initR no OK][left auxMove NThree] 	-> [initR OK][left auxMove left NTwo]
[initR no OK][left auxMove NFour] 	-> [initR OK][left auxMove left NThree]
[initR no OK][left auxMove NFive] 	-> [initR OK][left auxMove left NFour]

[initL no OK][right auxMove One] 	-> [initL OK][right auxMove right nothing]
[initL no OK][right auxMove Two] 	-> [initL OK][right auxMove right One]
[initL no OK][right auxMove Three] 	-> [initL OK][right auxMove right Two]
[initL no OK][right auxMove Four] 	-> [initL OK][right auxMove right Three]
[initL no OK][right auxMove Five] 	-> [initL OK][right auxMove right Four]

[initL no OK][right auxMove NOne] 	-> [initL OK][right auxMove right nnothing]
[initL no OK][right auxMove NTwo] 	-> [initL OK][right auxMove right NOne]
[initL no OK][right auxMove NThree]	-> [initL OK][right auxMove right NTwo]
[initL no OK][right auxMove NFour] 	-> [initL OK][right auxMove right NThree]
[initL no OK][right auxMove NFive] 	-> [initL OK][right auxMove right NFour]

(ADD NUMBERS)

[initR no OK][right auxMove nothing] 	-> [initR OK][right auxMove right One]
[initR no OK][right auxMove One] 		-> [initR OK][right auxMove right Two]
[initR no OK][right auxMove Two] 		-> [initR OK][right auxMove right Three]
[initR no OK][right auxMove Three] 		-> [initR OK][right auxMove right Four]
[initR no OK][right auxMove Four] 		-> [initR OK][right auxMove right Five]

[initR no OK][right auxMove Nnothing] 	-> [initR OK][right auxMove right NOne]
[initR no OK][right auxMove NOne]		-> [initR OK][right auxMove right NTwo]
[initR no OK][right auxMove NTwo] 		-> [initR OK][right auxMove right NThree]
[initR no OK][right auxMove NThree] 	-> [initR OK][right auxMove right NFour]
[initR no OK][right auxMove NFour] 		-> [initR OK][right auxMove right NFive]

[initL no OK][left auxMove nothing] 	-> [initL OK][left auxMove left One]
[initL no OK][left auxMove One] 		-> [initL OK][left auxMove left Two]
[initL no OK][left auxMove Two] 		-> [initL OK][left auxMove left Three]
[initL no OK][left auxMove Three] 		-> [initL OK][left auxMove left Four]
[initL no OK][left auxMove Four] 		-> [initL OK][left auxMove left Five]

[initL no OK][left auxMove Nnothing] 	-> [initL OK][left auxMove left NOne]
[initL no OK][left auxMove NOne] 		-> [initL OK][left auxMove left NTwo]
[initL no OK][left auxMove NTwo] 		-> [initL OK][left auxMove left NThree]
[initL no OK][left auxMove NThree] 		-> [initL OK][left auxMove left NFour]
[initL no OK][left auxMove NFour] 		-> [initL OK][left auxMove left NFive]

[OK] -> []
[auxMove] -> []
[auxMove] -> []

late [randomT Hone] -> [one Hone]
late [randomT HTwo] -> [two Htwo]
late [randomT Hthree] -> [three Hthree]
late [randomT Hfour] -> [four Hfour]
late [randomT Hfive] -> [five Hfive]

late [randomT] -> [Nothing]

late [randomT] -> [random PlayableTiles]

startloop

late [cursor][Ones | Ones | Ones ][FinalOne ] -> [cursor][Ones GAnim01 | Ones GAnim01 | Ones GAnim01][FinalOne FinalBOK]
late [cursor][Twos | Twos | Twos ][FinalTwo ] -> [cursor][Twos GAnim01 | Twos GAnim01 | Twos GAnim01][FinalTwo FinalBOK]
late [cursor][Threes | Threes | Threes ][FinalThree ] -> [cursor][Threes GAnim01 |Threes GAnim01 | Threes GAnim01][FinalThree FinalBOK]
late [cursor][Fours | Fours  | Fours  ][FinalFour ] -> [cursor][Fours GAnim01 | Fours GAnim01 | Fours GAnim01][FinalFour FinalBOK]
late [cursor][Fives | Fives  | Fives  ][FinalFive ] -> [cursor][Fives GAnim01 | Fives GAnim01 | Fives GAnim01][FinalFive FinalBOK]


late [cursor][Ones | Ones | Ones ] -> [cursor][GAnim01 | GAnim01 | GAnim01]
late [cursor][Twos | Twos | Twos ] -> [cursor][GAnim01 | GAnim01 | GAnim01]
late [cursor][Threes | Threes | Threes ] -> [cursor][GAnim01 | GAnim01 | GAnim01]
late [cursor][Fours | Fours  | Fours  ] -> [cursor][GAnim01 | GAnim01 | GAnim01]
late [cursor][Fives | Fives  | Fives  ] -> [cursor][GAnim01 | GAnim01 | GAnim01]
endloop


late [animations] -> again

==============
WINCONDITIONS
==============

No FinalB
No animations

=======
LEVELS
=======



message Press X to place a number from 1 to 5.	
message Then, press X to select a tile.
message Move the selected tile to swap it with other tiles.
message The selected tile robs pegs when swaping.
message Press X again to confirm your move.
message Make a match of each number.


s..........
...........
...........
..0abcde0..
...........
...........
...........
..0123450..
...........
...........
...........

message !