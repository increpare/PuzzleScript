title VEXT EDIT
author JACK

run_rules_on_level_start
norepeat_action

========
OBJECTS
========

Background
#eeeeee

PlayerTarget1mark
#0f0 #ff0
.000.
.000.
00100
.000.
.0.0.

Target1mark
#0f0 #ff0
.....
.000.
.010.
.000.
.....

PlayerTarget1
#0f0 #ff0
.000.
.000.
00100
.000.
.0.0.

Target1
#0f0 #ff0
.....
.000.
.010.
.000.
.....

Target0
#0f0 #f00
.....
.000.
.010.
.000.
.....

Target0Mark
#0f0 #f00
.....
.000.
.010.
.000.
.....

Wall0 
#884400 #f00
00000
00000
00000
00000
00000

walll0 
Brown #f00
1....
1....
1....
1....
1....

Wallr0 
Brown #f00
....1
....1
....1
....1
....1

Walld0 
Brown #f00
.....
.....
.....
.....
11111

Wallu0 
Brown #f00
11111
.....
.....
.....
.....

Black
black

wall0ghost
transparent

Wall0Mark
#884400 #ff0
00000
00000
00000
00000
00000

Wall1Mark
#884400 #ff0
00000
00000
00000
00000
00000

Wall1 
#884400 #ff0
00000
00000
00000
00000
00000

Wall2
#884400 #00f
00000
00000
00000
00000
00000

walll2
Brown #00f
1....
1....
1....
1....
1....

Wallr2 
Brown #00f
....1
....1
....1
....1
....1

Walld2 
Brown #00f
.....
.....
.....
.....
11111

Wallu2 
Brown #00f
11111
.....
.....
.....
.....

walll1 
Brown #ff0
1....
1....
1....
1....
1....

Wallr1 
Brown #ff0
....1
....1
....1
....1
....1

Walld1 
Brown #ff0
.....
.....
.....
.....
11111

Wallu1 
Brown #ff0
11111
.....
.....
.....
.....

Crate1Mark
#f80 #ff0
.....
.000.
.010.
.000.
.....

Crate1
#f80 #ff0
.....
.000.
.010.
.000.
.....

Crate01
transparent

Crate0
#f80 #f00
.....
.000.
.010.
.000.
.....

Crate0mark
#f80 #f00
.....
.000.
.010.
.000.
.....

crate0ghost
transparent

Player1Mark
#884400 #f80 White #80f #f80
.000.
.414.
33333
.333.
.3.3.

Player1
#884400 #f80 White #80f #f80
.000.
.414.
33333
.333.
.3.3.

Ded1Mark
#442200 #bb6600 White #408 Yellow
.000.
.111.
33333
.333.
.3.3.

Ded1
#442200 #bb6600 White #408 Yellow
.000.
.111.
33333
.333.
.3.3.

Crated0
#f80  #f00
.....
.....
.....
.010.
.010.

Crateu0
#f80  #f00
.010.
.010.
.....
.....
.....

Cratel0
#f80  #f00
.....
00...
11...
00...
.....

Crater0
#f80  #f00
.....
...00
...11
...00
.....

Crated1
#f80 #ff0
.....
.....
.....
.010.
.010.

Crateu1
#f80 #ff0
.010.
.010.
.....
.....
.....

Cratel1
#f80 #ff0
.....
00...
11...
00...
.....

Crater1
#f80 #ff0
.....
...00
...11
...00
.....


Pit1Mark
black #bbbb00 white
.111.
10001
10001
10001
.111.


Pit1
black #bbbb00 #eeeeee
.111.
10001
10001
10001
.111.

pit0ghost
transparent

Pit0
black #bb0000 #eeeeee
.111.
10001
10001
10001
.111.


pit0u
black #bb0000
10001
.....
.....
.....
.....

pit0d
black #bb0000
.....
.....
.....
.....
10001

pit0l
black #bb0000
1....
0....
0....
0....
1....

pit0r
black #bb0000
....1
....0
....0
....0
....1

pit1u
black #bbbb00
10001
.....
.....
.....
.....

pit1d
black #bbbb00
.....
.....
.....
.....
10001

pit1l
black #bbbb00
1....
0....
0....
0....
1....

pit1r
black #bbbb00
....1
....0
....0
....0
....1

rim0u
#b00
.000.
.....
.....
.....
.....

rim0l
#b00
.....
0....
0....
0....
.....

rim0r
#b00
.....
....0
....0
....0
.....

rim0d
#b00
.....
.....
.....
.....
.000.

rim1u
#bb0
.000.
.....
.....
.....
.....

rim1l
#bb0
.....
0....
0....
0....
.....

rim1r
#bb0
.....
....0
....0
....0
.....

rim1d
#bb0
.....
.....
.....
.....
.000.

blackul
black
0....
.....
.....
.....
.....

blackur
black
....0
.....
.....
.....
.....

blackdl
black
.....
.....
.....
.....
0....

blackdr
black
.....
.....
.....
.....
....0

fell0
#884400 #f00
.....
.000.
.010.
.000.
.....

fell1
#884400 #ff0
.....
.000.
.010.
.000.
.....

fell1mark
#884400 #ff0
.....
.000.
.010.
.000.
.....

felld1
#884400 #ff0
.....
.....
.....
.010.
.010.

fellu1
#884400 #ff0
.010.
.010.
.....
.....
.....

felll1
#884400 #ff0
.....
00...
11...
00...
.....

fellr1
#884400 #ff0
.....
...00
...11
...00
.....

fell0ghost
transparent

supported
transparent

fellcant1
transparent

cant1
transparent

cant0
transparent

fellcant0
transparent

dead0a
darkred
.....
.0.0.
.....
.0.0.
.....

cursor0a
#f00
00.00
0...0
.....
0...0
00.00

cursor0b
#f00
00.00
0...0
.....
0...0
00.00



cursor2a
#00f
0...0
.....
.....
.....
0...0

cursor2b
#00f
0...0
.....
.....
.....
0...0

one2mark
#00f
.00..
..0..
..0..
..0..
.000.

two2mark
#00f
.00..
...0.
..0..
.0...
.000.

three2mark
#00f
.00..
...0.
..00.
...0.
.00..

one2
#00f
.00..
..0..
..0..
..0..
.000.

two2
#00f
.00..
...0.
..0..
.0...
.000.

three2
#00f
.00..
...0.
..00.
...0.
.00..

one0mark
#f00
.00..
..0..
..0..
..0..
.000.

two0mark
#f00
.00..
...0.
..0..
.0...
.000.

three0mark
#f00
.00..
...0.
..00.
...0.
.00..

one0
#f00
.00..
..0..
..0..
..0..
.000.

two0
#f00
.00..
...0.
..0..
.0...
.000.

three0
#f00
.00..
...0.
..00.
...0.
.00..

four0
#f00
.0.0.
.0.0.
..00.
...0.
...0.

five0
#f00
.000.
.0...
.00..
...0.
.00..

six0
#f00
..00.
.0...
.00..
.0.0.
..0..

seven0
#f00
.000.
...0.
...0.
..0..
..0..

inf0
#f00
.....
00.00
0.0.0
00.00
.....

inf0mark
#f00
.....
00.00
0.0.0
00.00
.....


decrement
transparent

holding
transparent

activeb
#f00
.....
.0.0.
.....
.....
.....

active
#ff0
.....
.0.0.
.....
.....
.....

activefell
#ff0
.....
.0.0.
.....
.....
.....

pickeda0
transparent

pickedb0
transparent

pickeda2
transparent

pickedb2
transparent

chosepink
#ff22ff

chosecyan
#22ffff

pink
#ff88ff

cyan
#88ffff

placemark
transparent

activecurse0
transparent

partb
transparent

partc
transparent

cursor0mark
#f00
00.00
0...0
.....
0...0
00.00

wall2ghost
transparent

cant2
transparent

showhere
transparent

showthis
transparent
=======
LEGEND
=======

` = Background
& = Wall2 and wall1 and wall0
# = Wall1
X = Wall0
+ = Wall0 and Wall1
P = Player1 and active

n = crate0 and crate1 and crate01
o = Crate1
- = crate1 and crater1
: = crate1 and crateu1
L = crate1 and crater1 and crateu1
0 = pit1
! = crate0
? = pit0
/ = pit0 and pit1

@ = one0 and pit0
z = player1 and pit0

| = black

' = partb
" = partc

~ = cursor2a
_ = cursor2b

1 = one0
2 = two0
3 = three0
4 = four0
5 = five0
6 = six0
7 = seven0
8 = inf0

[ = one2
{ = two2
] = three2
> = wall2
activeab = active or activeb or activefell
partbc = partb or partc

y = target0
u = Target1 and pit1
t = Target1
q = playertarget1
r = player1
b = cursor0a
c = cursor0a and activecurse0
k = cursor0b and activecurse0
curse0a = cursor0a or dead0a
curse0 = curse0a or cursor0b
curse2 = cursor2a or cursor2b
cursor0 = cursor0a or cursor0b
 
player = curse2 or activecurse0 or active or dead0a or activefell
dead0 = dead0a
x1 = player1 or crate1 or wall1 or target1 or playertarget1 or pit1
x0 = crate0 or wall0 or pit0 or fell0
xx = x0 or x1 or curse0
x0ghost = crate0ghost or wall0ghost or pit0ghost or fell0ghost
cratex1 = cratel1 or crater1 or crated1 or crateu1
wallx2 = walll2 or wallr2 or walld2 or wallu2
wallx1 = walll1 or wallr1 or walld1 or wallu1
wallx0 = walll0 or wallr0 or walld0 or wallu0
fellx1 = felll1 or fellr1 or felld1 or fellu1
pit0x = pit0l or pit0r or pit0d or pit0u
pit1x = pit1l or pit1r or pit1d or pit1u
crateorfell1 = crate1 or fell1
playerormark1 = player1 or player1mark

x0mark =  one0mark or two0mark or three0mark or cursor0mark or wall0mark or target0mark or crate0mark or inf0mark
x1mark = player1mark or crate1mark or pit1mark or wall1mark or fell1mark or ded1mark or crate1mark or Target1mark or PlayerTarget1mark
x2mark = one2mark or two2mark or three2mark
xmark = x0mark or x1mark or x2mark
number0 = one0 or two0 or three0 or four0 or five0 or six0 or seven0 or inf0
number2 = one2 or two2 or three2
picked = pickeda0 or pickedb0 or pickeda2 or pickedb2

pickable = player1 or pit1 or crate1 or number0 or wall1 or number2 or fell1 or ded1 or target0 or PlayerTarget1 or Target1
pickable2 = curse0a or pickable or wall0 or crate0 

blackcorner = blackul or blackdl or blackur or blackdr
rim0x = rim0l or rim0d or rim0u or rim0r
rim1x = rim1l or rim1d or rim1u or rim1r 
=======
SOUNDS
=======

SFX0 51304502

sfx1 22108304

SFX2 84700307

SFX3 78571302

SFX4 25131906

SFX5 13408901

SFX6 50704903

SFX7 24622504

SFX8 29744504

SFX9 13344503


================
COLLISIONLAYERS
================

Background
pink, chosepink
cyan, chosecyan


Pit1,
pit1r
pit1l
pit1d
pit1u
pit0, pit0ghost
pit0r
pit0l
pit0d
pit0u
blackul
blackur
blackdr
blackdl
Target1, PlayerTarget1, target0

fell1, ded1
felll1
fellr1
felld1
fellu1

dead0a, fell0, fell0ghost
rim0l  
rim0d  
rim0u  
rim0r

rim1l  
rim1d  
rim1u 
rim1r 
Player1, Wall1, Crate1, 
pit1mark
fell1mark, ded1mark, target0mark
Target1mark, PlayerTarget1mark
player1mark, crate1mark
wall1mark
one0mark, two0mark, three0mark, wall0mark, inf0mark
one2mark, two2mark, three2mark
cursor0mark, crate0mark
number0, number2
cratel1, walll1, 
crater1, wallr1, 
crated1, walld1, 
crateu1, wallu1, 
cursor0, wall0, crate0, wall0ghost, crate0ghost
 walll0, cratel0
 wallr0, crater0
 walld0, crated0
 wallu0, crateu0
 crate01
 wall2, wall2ghost
 wallr2
 wallu2
 walll2
 walld2
 cursor2a, cursor2b
 
fellcant1
fellcant0
cant0
cant1
cant2
supported
activefell
active, activeb
activecurse0
holding
decrement
pickeda0, pickeda2
pickedb0, pickedb2
placemark
partb partc
showthis
showhere
black

======
RULES
======

(Deal with action-----------------------------------------------------------------------------)

[action activecurse0 curse0] -> [activecurse0 action curse0]

(pick up ready icon)
down[action curse0a no holding pickable|number0] -> [curse0a holding pickable|number0 pickeda0] SFX9
down[action cursor0b no holding pickable|number0] -> [cursor0b holding pickable|number0 pickedb0] SFX9
down[action cursor2a no holding pickable2|number2] -> [cursor2a holding pickable2|number2 pickeda2] SFX9
down[action cursor2b no holding pickable2|number2] -> [cursor2b holding pickable2|number2 pickedb2] SFX9
(switch to new ready icon)
down [action curse0a pickable| number0 no pickeda0][pickeda0] -> [curse0a pickable | number0 pickeda0][] SFX9
down [action cursor0b pickable| number0 no pickedb0][pickedb0] -> [cursor0b pickable | number0 pickedb0][] SFX9
down [action cursor2a pickable2| number2 no pickeda2][pickeda2] -> [cursor2a pickable2 | number2 pickeda2][] SFX9
down [action cursor2b pickable2| number2 no pickedb2][pickedb2] -> [cursor2b pickable2 | number2 pickedb2][] SFX9

(put down ready icon)
down [action curse0 holding pickable| picked] -> [curse0 pickable|] SFX8
down [action cursor2a holding pickable2| pickeda2] -> [cursor2a pickable2|] SFX8
down [action cursor2b holding pickable2| pickedb2] -> [cursor2b pickable2|] SFX8

(show mark)
late [xmark] -> []
late [showthis] -> []
late [showhere] -> []

late down [ | number0 pickeda0][curse0a] -> [showthis | number0 pickeda0][curse0a showhere]
late down [ | number0 pickedb0][cursor0b] -> [showthis | number0 pickedb0][cursor0b showhere]
late down [ | number2 pickeda2][cursor2a] -> [showthis | number2 pickeda2][cursor2a showhere]
late down [ | number2 pickedb2][cursor2b] -> [showthis | number2 pickedb2][cursor2b showhere]
late [showthis player1][showhere] -> [showthis player1][showhere player1mark]
late [showthis pit1][showhere] -> [showthis pit1][showhere pit1mark]
late [showthis crate1][showhere] -> [showthis crate1][showhere crate1mark]
late [showthis wall1][showhere] -> [showthis wall1][showhere wall1mark]
late [showthis one0][showhere] -> [showthis one0][showhere one0mark]
late [showthis two0][showhere] -> [showthis two0][showhere two0mark]
late [showthis three0][showhere] -> [showthis three0][showhere three0mark]
late [showthis one2][showhere] -> [showthis one2][showhere one2mark]
late [showthis two2][showhere] -> [showthis two2][showhere two2mark]
late [showthis three2][showhere] -> [showthis three2][showhere three2mark]
late [showthis wall0][showhere] -> [showthis wall0][showhere wall0mark]
late [showthis fell1][showhere] -> [showthis fell1][showhere fell1mark]
late [showthis ded1][showhere] -> [showthis ded1][showhere ded1mark]
late [showthis target0][showhere] -> [showthis target0][showhere target0mark]
late [showthis crate0][showhere] -> [showthis crate0][showhere crate0mark]
late [showthis cursor0][showhere] -> [showthis cursor0][showhere cursor0mark]
late [showthis inf0][showhere] -> [showthis inf0][showhere inf0mark]
late [showthis Target1][showhere] -> [showthis target1][showhere Target1mark]
late [showthis PlayerTarget1][showhere] -> [showthis PlayerTarget1][showhere PlayerTarget1mark]
(connect cratemark)
up [action curse0 holding crate1mark no x1  no number0| crate1] -> [action curse0 holding crate1mark crateu1 | crate1 crated1]
down [action curse0 holding crate1mark no x1  no number0| crate1] -> [action curse0 holding crate1mark crated1 | crate1 crateu1]
left [action curse0 holding crate1mark no x1  no number0| crate1] -> [action curse0 holding crate1mark cratel1 | crate1 crater1]
right [action curse0 holding crate1mark no x1  no number0| crate1] -> [action curse0 holding crate1mark crater1 | crate1 cratel1]
up [action curse0 holding fell1mark no xx no number0 no number2 | fell1] -> [action curse0 holding fell1mark crateu1 | fell1 felld1]
down [action curse0 holding fell1mark no xx no number0 no number2 | fell1] -> [action curse0 holding fell1mark crated1 | fell1 fellu1]
left [action curse0 holding fell1mark no xx no number0 no number2| fell1] -> [action curse0 holding fell1mark cratel1 | fell1 fellr1]
right [action curse0 holding fell1mark no xx no number0 no number2 | fell1] -> [action curse0 holding fell1mark crater1 | fell1 felll1]

(place mark)
[placemark] -> []

down [action curse0a holding xmark no x1 no number0][pickeda0 number0] -> [curse0a holding xmark placemark][pickeda0 decrement number0] SFX7
down [action cursor0b holding xmark no x1 no number0][pickedb0 number0] -> [cursor0b holding xmark placemark][pickedb0 decrement number0] SFX7

down [action cursor2a holding xmark no xx no number0 no number2][pickeda2 number2] -> [cursor2a holding xmark placemark][pickeda2 decrement number2] SFX7
down [action cursor2b holding xmark no xx no number0 no number2][pickedb2 number2] -> [cursor2b holding xmark placemark][pickedb2 decrement number2] SFX7



[placemark player1mark] -> [placemark player1 player1mark]
[placemark pit1mark] -> [placemark pit1 pit1mark]
[placemark crate1mark] -> [placemark crate1 crate1mark]
[placemark wall1mark] -> [placemark wall1 wall1mark]
[placemark one0mark] -> [placemark one0 one0mark]
[placemark two0mark] -> [placemark two0 two0mark]
[placemark three0mark] -> [placemark three0 three0mark]
[placemark one2mark] -> [placemark one2 one2mark]
[placemark two2mark] -> [placemark two2 two2mark]
[placemark three2mark] -> [placemark three2 three2mark]
[placemark wall0mark] -> [placemark wall0 wall0mark]
[placemark fell1mark] -> [placemark fell1 fell1mark]
[placemark ded1mark] -> [placemark ded1 ded1mark]
[placemark target0mark] -> [placemark target0 target0mark]
[placemark crate0mark] -> [placemark crate0 crate0mark]
[placemark Target1mark] -> [placemark target1 Target1mark]
[placemark inf0mark] -> [placemark inf0 inf0mark]
[placemark PlayerTarget1mark] -> [placemark playertarget1 PlayerTarget1mark]
down [placemark cursor0mark][curse0a|no one2] -> [ cursor0b cursor0mark][curse0a|]
[placemark cursor0mark] -> [placemark cursor0a cursor0mark]

(decrement number of placed item)
down [xmark holding cursor2a][pickable2|one2 decrement pickeda2] -> [xmark holding cursor2a][|one2 decrement pickeda2]
down [xmark holding cursor2b][pickable2|one2 decrement pickedb2] -> [xmark holding cursor2b][|one2 decrement pickedb2]
down [xmark holding curse0a][pickable|one0 decrement pickeda0] -> [xmark holding curse0a][|one0 decrement pickeda0]
down [xmark holding cursor0b][pickable|one0 decrement pickedb0] -> [xmark holding cursor0b][|one0 decrement pickedb0]
down [xmark holding cursor2a][|one2 decrement pickeda2] -> [cursor2a][|]
down [xmark holding cursor2b][|one2 decrement pickedb2] -> [cursor2b][|]
down [xmark holding cursor0a][|one0 decrement pickeda0] -> [cursor0a][|]
down [xmark holding cursor0b][|one0 decrement pickedb0] -> [cursor0b][|]
[one0 decrement] -> []
[two0 decrement] -> [one0]
[three0 decrement] -> [two0]
[four0 decrement] -> [three0]
[five0 decrement] -> [four0]
[six0 decrement] -> [five0]
[seven0 decrement] -> [six0]
[one2 decrement] -> []
[two2 decrement] -> [one2]
[three2 decrement] -> [two2]



(player -> cursor)
[player1 action active no wall0ghost no crate0ghost][curse0a][partbc] -> [cursor0b player1 activecurse0][curse0a][partbc] SFX5
[ded1 action activefell no wall0ghost no crate0ghost][curse0a][partbc] -> [cursor0b ded1 activecurse0][curse0a][partbc] SFX5
random [player1 action active no wall0ghost no crate0ghost][partbc] -> [cursor0a player1 activecurse0][partbc] SFX5
[player1 action active no wall0ghost no crate0ghost][curse0a][partbc] -> [cursor0b player1 activecurse0][curse0a][partbc] SFX5
[ded1 action activefell no wall0ghost no crate0ghost][curse0a][partbc] -> [cursor0b ded1 activecurse0][curse0a][partbc] SFX5
random [ded1 action activefell no wall0ghost no crate0ghost][partbc] -> [cursor0a ded1 activecurse0][partbc] SFX5
[player1 action active no wall0ghost no crate0ghost][curse0a][partbc] -> [cursor0b player1 activecurse0][curse0a][partbc] SFX5
[ded1 action activefell no wall0ghost no crate0ghost][curse0a][partbc] -> [cursor0b ded1 activecurse0][curse0a][partbc] SFX5

(cursor holding -> player)
down [action curse0a activecurse0 player1 holding xmark][| number0 pickeda0] -> [active player1][ | number0] SFX6
down [action curse0a activecurse0 ded1 holding xmark][| number0 pickeda0] -> [activefell ded1][ | number0] SFX6
down [action cursor0b activecurse0 player1 holding xmark][| number0 pickedb0] -> [active player1][ | number0] SFX6
down [action cursor0b activecurse0 ded1 holding xmark][| number0 pickedb0] -> [activefell ded1][ | number0] SFX6
(empty cursor -> player)
[action curse0 player1 activecurse0 no holding] -> [active player1] SFX6
[action curse0 ded1 activecurse0 no holding] -> [activefell ded1] SFX6
(red -> blue)
[action cursor0a activecurse0 holding xmark][partc][pickeda0] -> [cursor0a cursor2a][partc][] SFX5
[action cursor0a activecurse0 ][partc] -> [cursor0a cursor2a][partc] SFX5
[action cursor0b activecurse0 holding xmark][partc][pickedb0] -> [cursor0b cursor2b][partc][] SFX5
[action cursor0b activecurse0 ][partc] -> [cursor0b cursor2b][partc] SFX5
(blue holding -> red)
down [action curse2 curse0 holding xmark][| number2 pickeda2][cursor0a] -> [activecurse0 cursor0b][ | number2][cursor0a] SFX6
random down [action curse2 curse0 holding xmark][| number2 pickeda2] -> [activecurse0 cursor0a][ | number2] SFX6
down [action curse2 curse0 holding xmark][| number2 pickeda2][cursor0a] -> [activecurse0 cursor0b][ | number2][cursor0a] SFX6
(empty blue -> red)
[action curse2 curse0 no holding][cursor0a] -> [activecurse0 cursor0b][cursor0a] SFX6
random [action curse2 curse0 no holding] -> [activecurse0 cursor0a] SFX6
[action curse2 curse0 no holding][cursor0a] -> [activecurse0 cursor0b][cursor0a] SFX6


[action player] -> SFX0
[action curse0] -> SFX0

(Aesthetic----------------------------------------------------------------------------------------)



(ghosts)
[player1 active][pit0] -> [player1 active][pit0ghost]
[pit0ghost][cursor0] -> [pit0][cursor0]
[player1 active][wall0] -> [player1 active][wall0ghost]
[wall0ghost][cursor0] -> [wall0][cursor0]
[player1 active][crate0] -> [player1 active][crate0ghost]
[crate0ghost][cursor0] -> [crate0][cursor0]
[player1 active][fell0] -> [player1 active][fell0ghost]
[fell0ghost][cursor0] -> [fell0][cursor0]
[curse0 activecurse0][wall2] -> [curse0 activecurse0][wall2ghost]
[wall2ghost][curse2] -> [wall2][curse2]


(wall boundaries)
[wallx2] -> []
[wallx1] -> []
[wallx0] -> []
up [wall2 | no wall2] -> [wall2 wallu2 | ]
down [wall2 | no wall2] -> [wall2 walld2 | ]
left [wall2 | no wall2] -> [wall2 walll2 | ]
right [wall2 | no wall2] -> [wall2 wallr2 | ]
up [wall1 | no wall1] -> [wall1 wallu1 | ]
down [wall1 | no wall1] -> [wall1 walld1 | ]
left [wall1 | no wall1] -> [wall1 walll1 | ]
right [wall1 | no wall1] -> [wall1 wallr1 | ]
up [wall0 | no wall0] -> [wall0 wallu0 | ]
down [wall0 | no wall0] -> [wall0 walld0 | ]
left [wall0 | no wall0] -> [wall0 walll0 | ]
right [wall0 | no wall0] -> [wall0 wallr0 | ]

(crate connectors)
up [crateu0|] -> [crateu0 | crated0]
right [crater0|] -> [crater0 | cratel0]
up [crateu1|] -> [crateu1 | crated1]
right [crater1|] -> [crater1 | cratel1]
(pit connectors)
[blackcorner] -> []
[pit0x] -> [] 
[pit1x] -> [] 
up [pit1 | pit1] -> [pit1u pit1|pit1d pit1]
right [pit1 | pit1] -> [pit1r pit1|pit1l pit1]
up [pit0 | pit0] -> [pit0u pit0|pit0d pit0]
right [pit0 | pit0] -> [pit0r pit0|pit0l pit0]
[pit1u pit1r] -> [pit1u pit1r blackur]
[pit1u pit1l] -> [pit1u pit1l blackul]
[pit1d pit1r] -> [pit1d pit1r blackdr]
[pit1d pit1l] -> [pit1d pit1l blackdl]
[pit0u pit0r] -> [pit0u pit0r blackur]
[pit0u pit0l] -> [pit0u pit0l blackul]
[pit0d pit0r] -> [pit0d pit0r blackdr]
[pit0d pit0l] -> [pit0d pit0l blackdl]
[rim1x] -> []
[rim0x] -> []
up[pit0|no pit0] -> [pit0 rim0u|]
down[pit0|no pit0] -> [pit0 rim0d|]
left[pit0|no pit0] -> [pit0 rim0l|]
right[pit0|no pit0] -> [pit0 rim0r|]
up[pit1|no pit1] -> [pit1 rim1u|]
down[pit1|no pit1] -> [pit1 rim1d|]
left[pit1|no pit1] -> [pit1 rim1l|]
right[pit1|no pit1] -> [pit1 rim1r|]
late [pink] -> []
late [cyan] -> []
late [chosepink] -> []
late [chosecyan] -> []
late down [pickable |number0] -> [pickable pink | number0 pink]
late down [pickable2 |number2] -> [pickable2 cyan | number2 cyan]
late up [pink picked|pink] -> [chosepink picked|chosepink ]
late up [cyan picked|cyan ] -> [chosecyan picked|chosecyan ]

(Movement----------------------------------------------------------------------------------------)

(move attached items)
[moving activecurse0 curse0] -> [moving activecurse0 moving curse0]
[moving active player1] -> [moving active moving player1]
[moving activefell ded1] -> [moving activefell moving ded1]
[moving curse0 holding] -> [moving curse0 moving holding]
[moving curse2 holding] -> [moving curse2 moving holding]

(reset and propogate cant)
[cant0] -> []
[cant1] -> []
[cant2] -> []
[fellcant0] -> []
[fellcant1] -> []

[black no cant0 no cant1 no cant2] -> [black cant0 cant1 cant2]
[wall2 no cant2] -> [wall2 cant2]
[wall1 no cant1] -> [wall1 cant1]
[wall0 no cant0] -> [wall0 cant0]
[number0] -> [number0 cant1]
[number2] -> [number2 cant1]
[no pit1 no fellcant1] -> [fellcant1]
[no pit0 no fellcant0] -> [fellcant0]

[> ded1][fell1|fellcant1] -> [> ded1][fell1 fellcant1 | fellcant1]
+ [> ded1][ded1|fellcant1] -> [> ded1][ded1 fellcant1 | fellcant1]
+ up[fellu1|fellcant1] -> [fellu1 fellcant1 | fellcant1]
+ left[felll1|fellcant1] -> [felll1 fellcant1 | fellcant1]
+ down[felld1|fellcant1] -> [felld1 fellcant1 | fellcant1]
+ right[fellr1|fellcant1] -> [fellr1 fellcant1 | fellcant1]

+ [> player1][crate1|cant1] -> [> player1][crate1 cant1 | cant1]
+ [> player1][player1|cant1] -> [> player1][player1 cant1 | cant1]
+ up[crateu1|cant1] -> [crateu1 cant1 | cant1]
+ left[cratel1|cant1] -> [cratel1 cant1 | cant1]
+ down[crated1|cant1] -> [crated1 cant1 | cant1]
+ right[crater1|cant1] -> [crater1 cant1 | cant1]
+ [crate1 crate0 cant0] -> [crate1 crate0 cant0 cant1]
+ [crate1 crate0 cant1] -> [crate1 crate0 cant0 cant1]
+ [> dead0][dead0|fellcant0] -> [> dead0][dead0 fellcant0 | fellcant0]
+ [> cursor0][cursor0|cant0] -> [> cursor0][cursor0 cant0 | cant0]
+ [> cursor0][crate0 | number0] -> [> cursor0][crate0 cant0| number0]
+ [> cursor0][crate0|cant0] -> [> cursor0][crate0 cant0 | cant0]
+ up[crateu0|cant0] -> [crateu0 cant0 | cant0]
+ left[cratel0|cant0] -> [cratel0 cant0 | cant0]
+ down[crated0|cant0] -> [crated0 cant0 | cant0]
+ right[crater0|cant0] -> [crater0 cant0 | cant0]

[> curse2 | wall2] -> [stationary curse2 | wall2]
[> curse2 | black] -> [stationary curse2 | black]
[cursor0 cant0] -> [stationary cursor0 cant0]
[player1 cant1] -> [stationary player1 cant1]
[ded1 fellcant1] -> [stationary ded1 fellcant1]
[dead0 fellcant0] -> [stationary dead0 fellcant0]


(propagate movement)

[ >  Ded1 | Fell1 no fellcant1] -> [  >  Ded1 | > Fell1  ]

[ >  Cursor0 | Crate0 ] -> [  >  Cursor0 | > Crate0  ]
+ [ >  Cursor0 | Cursor0 ] -> [  >  Cursor0 | > Cursor0  ]
+ [ >  Crate0 | Crate0 ] -> [  >  Crate0 | > Crate0  ]

+ [ >  Player1 | Crate1 ] -> [  >  Player1 | > Crate1  ]
+ [ >  Player1 | Player1] -> [  >  Player1 | > Player1  ]
+ [ >  Crate1 | Player1 ] -> [  >  Crate1 | > Player1  ]
+ [> crate1|crate1] -> [> crate1 | > crate1]
+ up[moving crate1 crateu1|crate1] -> [moving crate1 crateu1 | moving crate1]
+ left[moving crate1 cratel1|crate1] -> [moving crate1 cratel1 | moving crate1]
+ down[moving crate1 crated1|crate1] -> [moving crate1 crated1 | moving crate1]
+ right[moving crate1 crater1|crate1] -> [moving crate1 crater1 | moving crate1]
 

[> fell1|fell1] -> [> fell1 | > fell1]
+ up[moving fell1 fellu1|fell1] -> [moving fell1 fellu1 | moving fell1]
+ left[moving fell1 felll1|fell1] -> [moving fell1 felll1 | moving fell1]
+ down[moving fell1 felld1|fell1] -> [moving fell1 felld1 | moving fell1]
+ right[moving fell1 fellr1|fell1] -> [moving fell1 fellr1 | moving fell1]

[moving crate1 cratex1] -> [moving crate1 moving cratex1]
[moving fell1 fellx1] -> [moving fell1 moving fellx1]
[moving crate1 crate0ghost] -> [moving crate1 moving crate0ghost]

[active stationary player1] -> [stationary active stationary player1]
[activecurse0 stationary curse0] -> [stationary activecurse0 stationary curse0]
[activefell stationary ded1] -> [stationary activefell stationary ded1]
[stationary cursor0 activecurse0 holding] -> [stationary cursor0 activecurse0 stationary holding]
[stationary curse2 holding] -> [stationary curse2 stationary holding]


(falling)
late [supported] -> []

late [crate1 ded1] -> [crate1 supported ded1]
late [crate1 no pit1] -> [crate1 supported]
late [crate1 fell1] -> [crate1 fell1 supported]


late up[crateu1|supported] -> [crateu1 supported | supported]
+ late left[cratel1|supported] -> [cratel1 supported | supported]
+ late down[crated1|supported] -> [crated1 supported | supported]
+ late right[crater1|supported] -> [crater1 supported | supported]


late [cursor0 pit0 no fell0] -> [pit0 dead0a]SFX3
late [player1 active pit1 no fell1 no ded1] -> [pit1 ded1 activefell]SFX3
late [player1  pit1 no fell1 no ded1] -> [pit1 ded1 ]SFX3
late [crate0 pit0 no supported] -> [pit0 fell0]SFX3
late [crate1 pit1 no supported] -> [pit1 fell1] SFX3



late [crateu1 no crate1 fell1] -> [fellu1 fell1]
late [cratel1 no crate1 fell1] -> [felll1 fell1]
late [crater1 no crate1 fell1] -> [fellr1 fell1]
late [crated1 no crate1 fell1] -> [felld1 fell1]

late [crateu1 no crate1] -> []
late [cratel1 no crate1] -> []
late [crater1 no crate1] -> []
late [crated1 no crate1] -> []
late up [crateu1 |no crate1] -> [|]
late left [cratel1 |no crate1] -> [|]
late right [crater1 |no crate1] -> [|]
late down [crated1 |no crate1] -> [|]
late [player1 ded1] -> [player1 ded1] SFX0
late [player1 ded1] -> cancel 

[moving crate0] -> SFX2
[moving crate1] -> SFX2

==============
WINCONDITIONS
==============


All target0 on crate0
All Target1 on Crateorfell1
All PlayerTarget1 on Playerormark1
no Cursor0 on target1
no cursor0 on PlayerTarget1

=======
LEVELS
=======

message Book 1: BASE

message Page 1.1: GOAL

#######
#`````#
#`p`t`#
#`````#
#`o`q`#
#`````#
#######

message Page 1.2: TOOL

#######
#`t`o`#
#t`-:`#
#``tlo#
#p````#
#######

message Page 1.3: HOLE

########
##``0`##
#```0``#
#``o0`t#
#po:0``#
#``:0`t#
#```0``#
##``0`##
########

message Page 1.4: FALL

########
#00u``0#
#00po`0#
#`000`0#
#`00```#
#`000``#
#`000``#
########

message Book 2: EDIT

message Page 2.1: MINI

||||||||||
|```````'|
|`######`|
|`#````#`|
|`#``t`#`|
|`#````#`|
|`######`|
|````````|
|`c`ro```|
|```11```|
|````````|
||||||||||

message Page 2.2: FORM

||||||||||||
|`````````'|
|`########`|
|`#qo````#`|
|`####```#`|
|`#``````#`|
|`#``````#`|
|`#``````#`|
|`#``````#`|
|`########`|
|``````````|
|``c`ro````|
|````18````|
|``````````|
||||||||||||

message Page 2.3: PLUG

|||||||||||
|````````'|
|`#######`|
|`#ro`o`#`|
|`#####`#`|
|`###`o`#`|
|`#``oLo#`|
|`#````##`|
|`#`q####`|
|`#######`|
|`````````|
|``c`0````|
|````8````|
|`````````|
|||||||||||

message Page 2.4: TRAP

||||||||||||
|`````````'|
|`########`|
|`#``````#`|
|`#``t```#`|
|`#`tqt``#`|
|`#``t```#`|
|`#``````#`|
|`#``````#`|
|`########`|
|``````````|
|``c`ro````|
|````16````|
|``````````|
||||||||||||

message Book 3: DUAL

message Page 3.1: CO-OP

#######
####qq#
#po000#
#`o000#
###o``#
###r`'#
#######

message Page 3.2: REST

|||||||||||
|````````'|
|`#######`|
|`#`r00q#`|
|`#######`|
|`````````|
|``c`o````|
|````2````|
|`````````|
|||||||||||
message Page 3.3: SAIL

|||||||||||
|````````'|
|`#######`|
|`#0000q#`|
|`#00000#`|
|`#00000#`|
|`#```00#`|
|`#```00#`|
|`#######`|
|`````````|
|``c`ro```|
|````23```|
|`````````|
|||||||||||

message Page 3.4: TWIN

|||||||||||||||||||||
|`````````+````````'|
|`#######`+`#######`|
|`#`````#`+`#`````#`|
|`#```q`#`+`#`````#`|
|`#``r``#`+`#``r``#`|
|`#`````#`+`#`q```#`|
|`#`````#`+`#`````#`|
|`#######`+`#######`|
|`````````+`````````|
|``c`o````+``k`o````|
|````5````+````6````|
|`````````+`````````|
|||||||||||||||||||||

message Book 4: WHAT

message Page 4.1: GUIS


#######
#q#``'#
#``8#`#
#`#p#`#
#`#o#`#
#`#1``#
#######

message Page 4.2: IDEA

||||||||||
|```````'|
|`######`|
|`#```t#`|
|`#```t#`|
|`######`|
|````````|
|`c`ro```|
|```31```|
|````````|
||||||||||

message Page 4.3: FILL


######
#0poq#
#0`70#
#'000#
#0000#
######

message Page 4.4: A LOT

|||||||||
|```````|
|`#####`|
|`#``t#`|
|`#####`|
|`#``t#`|
|`#####`|
|`#``t#`|
|`#####`|
|``````'|
|`c3o2r`|
|``1111`|
|```````|
|||||||||

message Book 5: UHHH

message Page 5.1: WALL

||||||||||
|```````'|
|`######`|
|`#t`#`#`|
|`#````#`|
|`##`o`#`|
|x+xxxx+x|
|`#````#`|
|`#````#`|
|`#````#`|
|`######`|
|````````|
|`c`ro```|
|```11```|
|````````|
||||||||||

message Page 5.2: SAFE

|||||||||||
|````````'|
|`#######`|
|`#xxxxx#`|
|`#x```x#`|
|`#x`t`x#`|
|`#x```x#`|
|`#xxxxx#`|
|`#######`|
|`````````|
|`c`o1r```|
|```111```|
|`````````|
|||||||||||

message Page 5.3: LOCK

|||||||||||||
|o```'`xxxcx|
|1``x```qx!x|
|`1!````qx`x|
|``1?x`xxx?x|
|r``1````!`x|
|````2`xxxxx|
|||||||||||||



message Page 5.4: POST

+++++++++
+p#```#q+
+`#``'#3+
+`#`1`#`+
+`#`2`#`+
+`#```#`+
+`#```#`+
+++++++++
message Book 6: BLUE

message Page 6.1: META

||||||||||||
|++++++++++|
|+````"```+|
|+`######`+|
|+`#````#`+|
|+`#``t`#`+|
|+`#````#`+|
|+`######`+|
|+````````+|
|+```ro```+|
|+````````+|
|+````````+|
|++++++++++|
|``````````|
|``~`1b````|
|````{[````|
|``````````|
||||||||||||

message Page 6.2: WILD

|||||||||||||
|+++++++++++|
|+``````"``+|
|+`#######`+|
|+`#0000q#`+|
|+`#00000#`+|
|+`#`0000#`+|
|+`#`0000#`+|
|+`#```00#`+|
|+`#######`+|
|+`````````+|
|+`````````+|
|+`````````+|
|+`````````+|
|+++++++++++|
|```````````|
|`~`12rbo```|
|```[{[[[```|
|```````````|
|||||||||||||

message Page 6.3: UH OH

|||||||||||
|+++++++++|
|+``````"+|
|+`+++++`+|
|+`+```+`+|
|+`+`t`+`+|
|+`+```+`+|
|+`+```+`+|
|+`+++++`+|
|+```````+|
|+```o```+|
|+```````+|
|+```````+|
|+++++++++|
|`````````|
|`````````|
|`~`1br```|
|```][[```|
|`````````|
|||||||||||

message Page 6.4: VARY

|||||||||||||||||||||||
|++++++++++&++++++++++|
|+``````"`x&x````````+|
|+`######`x&x`######`+|
|+`#````#`x&x`#````#`+|
|+`#````#`x&x`#````#`+|
|+`#`qt`#`x&x`#`tq`#`+|
|+`#````#`x&x`#````#`+|
|+`#````#`x&x`#````#`+|
|+`######`x&x`######`+|
|+````````x&x````````+|
|+````````x&x````````+|
|+````````x&x````````+|
|+````````x&x````````+|
|++++++++++&++++++++++|
|``````````&``````````|
|`~`r1ob```&`_`r1ob```|
|```[{[[```&```[[[[```|
|``````````&``````````|
|||||||||||||||||||||||

message Book 7: ?!?!

message Page 7.1: EACH

+++++++
+````'+
+`###`+
+`#q#`+
+`#0#`+
+`#`#`+
+`#`#`+
+`#`#`+
+`#`#`+
+`###`+
+`````+
+``c``+
+`r0o`+
+``4``+
+`````+
+++++++

message Page 7.2: HOLD

&&&&&&&&&
&`8```+`&
&`{po`+`&
&`"```+t&
&&&&&&&&&

message Page 7.3: TRIP

&&&&&&&
&```"`&
&`o`!`&
&`````&
&+++++&
&`r2b`&
&`[[{~&
&y```t&
&&&&&&&

message Page 7.4: PLAN


|||||||||
|```t```|
|```````|
|+++++++|
|```````|
|```````|
|>>>>>>>|
|```````|
|```````|
|+++++++|
|```~```|
|[[`2"11|
|[[`[`[[|
|&&`&&&&|
|&&bor&&|
|||||||||





message Book 8: (RE)STAR(TS)


message Page 8.1: (IN)FORM(ED)

||||||||||||
|`````````'|
|`########`|
|`#qo````#`|
|`####```#`|
|`#``````#`|
|`#``````#`|
|`#``````#`|
|`########`|
|``````````|
|``c`ro````|
|````18````|
|``````````|
||||||||||||

message Page 8.2: (EN)TWIN(ED)

|||||||||||||||||||||
|`````````+`````````|
|`#######`+`#######`|
|`#````q#`+`#`````#`|
|`#`````#`+`#`````#`|
|`#``r``#`+`#``r``#`|
|`#`````#`+`#`````#`|
|`#`````#`+`#q````#`|
|`#######`+`#######`|
|`````````+`````````|
|``c`o````+``k`o````|
|````5````+````5````|
|`````````+`````````|
|||||||||||||||||||||

message Page 8.3: (ZE)ALOT(RY)

|||||||||
|```````|
|`#####`|
|`#``t#`|
|`#####`|
|`#``t#`|
|`#####`|
|`#``t#`|
|`#####`|
|```````|
|`c2o2r`|
|``1111`|
|```````|
|||||||||

message Page 8.4: (BE)WILD(ER)

|||||||||||||
|+++++++++++|
|+``````"``+|
|+`+++++++`+|
|+`+0000q+`+|
|+`+00000+`+|
|+`+00000+`+|
|+`+`0000+`+|
|+`+```00+`+|
|+`+++++++`+|
|+`````````+|
|+`````````+|
|+++++++++++|
|```````````|
|`~`12rbo```|
|```[{[[[```|
|```````````|
|||||||||||||














